# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=smartmontools
pkgver=7.1
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
url="http://smartmontools.sourceforge.net"
license=('GPL')
arch=('x86_64')
groups=('app-utils')
depends=('gcc-libs' 'libcap-ng' 'bash')
optdepends=('s-nail: to get mail alerts to work')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'smartd.conf')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--with-libcap-ng=yes \
		--with-selinux=yes \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--with-smartdscriptdir=/usr/share/smartmontools \
		--with-smartdplugindir=/usr/share/smartmontools/smartd_warning.d
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i -e "s:sysconfig/smartmontools:conf.d/smartd:g" smartd.service
	sed -i -e "s:smartd_opts:SMARTD_ARGS:g" smartd.service

	make DESTDIR="${pkgdir}" install

	rm -rf ${pkgdir}/etc/rc.d
	install -Dm644 ${srcdir}/smartd.conf "${pkgdir}/etc/conf.d/smartd"
}
