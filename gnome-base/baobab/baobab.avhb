# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=baobab
pkgver=45.0
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/DiskUsageAnalyzer"
arch=(x86_64)
groups=('gnome-base')
license=(GPL2)
depends=(dconf gtk3 gsettings-desktop-schemas librsvg)
makedepends=(vala gobject-introspection yelp-tools appstream-glib git meson)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
    avouch-meson ${pkgname}-${pkgver} build
    ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  # https://bugs.archlinux.org/task/31861
  sed -i '/^MimeType=/d' "$pkgdir/usr/share/applications/org.gnome.baobab.desktop"
}