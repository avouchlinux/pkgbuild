# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=baobab
pkgver=47.0
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/DiskUsageAnalyzer"
arch=(x86_64)
groups=('gnome-base')
license=(GPL2)
depends=(
  cairo
  dconf
  glib2
  glibc
  graphene
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
  pango
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=(
  "git+https://gitlab.gnome.org/GNOME/baobab.git#tag=${pkgver/[a-z]/.&}"
  no-mimetype.diff
)
b2sums=('SKIP'
        '71e04f307e842fa0676f5b64f55e8be908e3075a9b948a6ab3979b51a8db08fdd07c0f214adb74d5461b4f175a14a7bf9867cf2c9b044660a5b46bc8ea296a22')

prepare() {
  cd baobab

  # https://bugs.archlinux.org/task/31861
  git apply -3 ../no-mimetype.diff
}

build() {
  avouch-meson baobab build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
