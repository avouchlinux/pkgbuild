# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=mutter
pkgname=(
  mutter
  mutter-docs
)
pkgver=45.4
pkgrel=1
pkgdesc="Window manager and compositor for GNOME"
arch=('x86_64')
groups=('gnome-base')
url="https://gitlab.gnome.org/GNOME/mutter"
license=(GPL)
depends=(
  colord
  dconf
  gnome-desktop-4
  gnome-settings-daemon
  graphene
  gsettings-desktop-schemas
  iio-sensor-proxy
  lcms2
  libcanberra
  libdisplay-info
  libei
  libgudev
  libinput
  libsm
  libsysprof-capture
  libxkbcommon-x11
  libxkbfile
  pipewire
  startup-notification
  xorg-xwayland
)
makedepends=(
  egl-wayland
  gi-docgen
  git
  gobject-introspection
  gtk3
  meson
  sysprof
  wayland-protocols
  xorg-server
  xorg-server-xvfb
)
checkdepends=(
  gnome-session
  python-dbusmock
  wireplumber
  zenity
)

install=mutter.install
source=(https://gitlab.gnome.org/GNOME/mutter/-/archive/$pkgver/mutter-$pkgver.tar.bz2)

prepare() {
  cd $pkgname-$pkgver
  # Make tests run
  sed -i '/catchsegv/d' meson.build
}

build() {
local meson_options=(
    -D docs=true
    -D egl_device=true
    -D installed_tests=false
    -D libdisplay_info=true
    -D wayland_eglstream=true
  )

  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  avouch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build

}

# check() (
#   export XDG_RUNTIME_DIR="$PWD/rdir" GSETTINGS_SCHEMA_DIR="$PWD/build/data"
#   mkdir -p -m 700 "$XDG_RUNTIME_DIR"
#   glib-compile-schemas "$GSETTINGS_SCHEMA_DIR"

#   export NO_AT_BRIDGE=1 GTK_A11Y=none
#   export MUTTER_DEBUG_DUMMY_MODE_SPECS="800x600@10.0"

#   xvfb-run -s '-nolisten local +iglx -noreset' \
#     mutter/src/tests/meta-dbus-runner.py --launch=pipewire --launch=wireplumber \
#     meson test -C build --print-errorlogs -t 5 --setup plain
# )


_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mutter() {
  provides=(libmutter-13.so)

  meson install -C build --destdir "$pkgdir"

  _pick docs "$pkgdir"/usr/share/mutter-*/doc
}

package_mutter-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv docs/* "$pkgdir"
}
