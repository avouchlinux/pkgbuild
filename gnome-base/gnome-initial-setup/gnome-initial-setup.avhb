# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnome-initial-setup
pkgver=46.0
pkgrel=1
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=('x86_64')
groups=('gnome-base')
url="https://www.gnome.org/"
license=('GPL')
license=('GPL-2.0-or-later')
depends=(
    'accountsservice'
    'cairo'
    'fontconfig'
    'gcc-libs'
    'gdk-pixbuf2'
    'gdm'
    'geoclue'
    'geocode-glib-2'
    'glib2'
    'glibc'
    'gnome-desktop-4'
    'gnome-control-center'
    'gnome-keyring'
    'gsettings-desktop-schemas'
    'gtk4'
    'harfbuzz'
    'json-glib'
    'krb5'
    'libadwaita'
    'libgdm'
    'libgweather-4'
    'libibus'
    'libmalcontent'
    'libnm'
    'libnma-gtk4'
    'libpwquality'
    'librest'
    'libsecret'
    'malcontent'
    'pango'
    'polkit'
    'tecla'
    'webkitgtk-6.0'
)
makedepends=(
    'meson'
    'git'
)
optdepends=('gdm: GDM Initial Setup mode'
            'gnome-getting-started-docs: GNOME Welcome Tour')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)

build() {
  avouch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -d -o root -g 70 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # Setup system user and group
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /run/gnome-initial-setup 0700 gnome-initial-setup gnome-initial-setup -' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
