# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=evince
pkgname=(
  evince
  evince-lib-docs
)
pkgver=48.0
pkgrel=1
pkgdesc="Document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))"
url="https://wiki.gnome.org/Apps/Evince"
arch=('x86_64')
groups=('gnome-base')
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  djvulibre
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gsettings-desktop-schemas
  gsfonts
  gspell
  gst-plugins-base-libs
  gstreamer
  gtk3
  gvfs
  hicolor-icon-theme
  libarchive
  libgxps
  libhandy
  libsecret
  libspectre
  libsynctex
  libtiff
  libxml2
  pango
  poppler-glib
)
makedepends=(
  appstream-glib
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  texlive-bin
  yelp-tools
)
source=("https://gitlab.gnome.org/GNOME/$pkgbase/-/archive/$pkgver/$pkgbase-$pkgver.tar.bz2")
b2sums=('740a48206a435bd50cd22a8afad308075a84b223d17b0f48bc38f38926645ab0428fb1cd07169e07cc9ac04b97b502126121e17331e785e758172c9c6d862633')
validpgpkeys=(
  6F3E1831D69760DC3FCE7873D6197451C129658C # Germán Poo-Caamaño <gpoo@gnome.org>
)

build() {
  local meson_options=(
    -D ps=enabled
  )

  avouch-meson $pkgbase-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_evince() {
  provides=(libev{document,view}3.so)
  optdepends=('texlive-bin: DVI support')
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  _pick lib-docs "$pkgdir"/usr/share/doc/lib*
}

package_evince-lib-docs() {
  pkgdesc+=" (library API documentation)"
  depends=()

  mv lib-docs/* "$pkgdir"
}
