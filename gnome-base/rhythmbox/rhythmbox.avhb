# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=rhythmbox
pkgver=3.4.7
pkgrel=1
pkgdesc="Music playback and management application"
arch=('x86_64')
groups=('gnome-base')
license=(GPL)
depends=(
  dconf
  grilo
  gst-plugins-base
  gst-plugins-good
  json-glib
  libgudev
  libnotify
  libpeas
  libsoup3
  media-player-info
  python-gobject
  tdb
  totem-plparser
)
makedepends=(
  brasero
  git
  gobject-introspection
  gtk-doc
  libdmapsharing
  libgpod
  libmtp
  lirc
  meson
  vala
  yelp-tools
  zeitgeist
)
checkdepends=(
  check
  xorg-server-xvfb
)
optdepends=(
  'brasero: Audio CD Recorder plugin'
  'grilo-plugins: Grilo media browser plugin'
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
  'gvfs-mtp: Portable Players - Android plugin'
  'libdmapsharing: DAAP Music Sharing plugin'
  'libgpod: Portable Players - iPod plugin'
  'libmtp: Portable Players - MTP plugin'
  'lirc: LIRC plugin'
  'zeitgeist: Zeitgeist logging plugin'
)

source=(https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2)

build() {
  local meson_options=(
    -D daap=enabled
    -D gtk_doc=true
  )

  avouch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

