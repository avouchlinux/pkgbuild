# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnome-connections
pkgver=47.0
pkgrel=1
pkgdesc="A remote desktop client for the GNOME desktop environment"
arch=('x86_64')
groups=('gnome-base')
url='https://apps.gnome.org/Connections/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gdk-pixbuf2
  glib2
  glibc
  gtk-vnc
  gtk3
  hicolor-icon-theme
  libhandy
  libsecret
  libxml2
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  itstool
  meson
  vala
)
source=(
  "git+https://gitlab.gnome.org/GNOME/connections.git?signed#tag=${pkgver/[a-z]/.&}"
  git+https://gitlab.gnome.org/GNOME/gtk-frdp.git
)
b2sums=('8e9779827d6039d50ec7f3d43b8e3a2d1ab0bded137bbcadaa8a1ddfefcf66669f691a7a4d94a9d721b420d51fa6be0d09d0d6454edc41c9b4ff4c911a706d7f'
        'SKIP')
validpgpkeys=(F55CDAB508C3ACBCB1C8B930C910F152653B1688) # Marek Kasik <mkasik@redhat.com>

prepare() {
  cd connections

  git submodule init
  git submodule set-url subprojects/gtk-frdp "$srcdir/gtk-frdp"
  git -c protocol.file.allow=always submodule update
}

build() {
  avouch-meson connections build
  export LDFLAGS=
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
