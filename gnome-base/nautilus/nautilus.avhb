# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=nautilus
pkgname=(nautilus libnautilus-extension)
pkgver=42.0
pkgrel=1
pkgdesc="Default file manager for GNOME"
url="https://wiki.gnome.org/Apps/Nautilus"
arch=(x86_64)
groups=('gnome-base')
license=(GPL)
depends=(libgexiv2 gnome-desktop gvfs dconf tracker nautilus-sendto gnome-autoar
         gst-plugins-base-libs tracker-miners)
makedepends=(gobject-introspection git gtk-doc meson appstream-glib)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
    cd ${srcdir}
    avouch-meson ${pkgbase}-${pkgver} build \
        -D docs=true \
        -D packagekit=false \
        -D tests=none
    meson compile -C build
}

package_nautilus() {
    depends+=(libnautilus-extension)
    groups=('gnome-base')

    DESTDIR="$pkgdir" meson install -C build

    ### Split libnautilus-extension

    mkdir -p libne/{lib,share}
    mv "$pkgdir"/usr/include libne
    mv "$pkgdir"/usr/lib/{girepository-1.0,libnautilus-extension*,pkgconfig} libne/lib
    mv "$pkgdir"/usr/share/{gir-1.0,gtk-doc} libne/share
}

package_libnautilus-extension() {
    pkgdesc="Library for extending the $pkgdesc"
    depends=(gtk3)
    mv libne "$pkgdir/usr"
}
