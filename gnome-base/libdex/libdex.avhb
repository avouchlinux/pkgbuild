# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libdex
pkgname=(
  libdex
  libdex-docs
)
pkgver=0.10.0
pkgrel=1
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/GNOME/libdex"
arch=(x86_64)
groups=('gnome-base')
license=(LGPL-2.1-or-later)
depends=(
  glib2
  liburing
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  libsoup3
  libsysprof-capture
  meson
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('279af19eaac5e211fb17f3ae0cf147d7a3023fe471719f1e4fe18dc4d3bcd6d8f8b75164e5fa8878aa6b892bb0fcf94cfe59bc564f87edcd9ac60224053cc5d7')

pkgver() {
  cd libdex
  git describe --tags | sed -r 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libdex
}

build() {
  local meson_options=(
    -D docs=true
    -D sysprof=true
  )

  avouch-meson libdex build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  provides=(libdex-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libdex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

