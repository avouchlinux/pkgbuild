# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnome-color-manager
pkgver=3.36.2
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
url="https://gitlab.gnome.org/GNOME/gnome-color-manager"
arch=(x86_64)
groups=('gnome-base')
license=(GPL2)
depends=(
  cairo
  colord
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  lcms2
  libcolord
  pango
)
makedepends=(
  appstream-glib
  docbook-sgml
  docbook-utils
  git
  meson
  perl-sgmls
  yelp-tools
)
checkdepends=(xorg-server-xvfb)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")
b2sums=('SKIP')

prepare() {
  cd $pkgname-$pkgver
  sed -i "/subdir('man')/d" meson.build
}

build() {
  avouch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
