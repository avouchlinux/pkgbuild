# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gcr
pkgver=4.1.0
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
arch=('x86_64')
groups=('gnome-base')
url="https://gitlab.gnome.org/GNOME/gcr"
license=('GPL2')
depends=(dconf gtk3 libgcrypt p11-kit)
makedepends=(intltool gobject-introspection python vala libxslt git gtk-doc)
source=(https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2)

build() {
    avouch-meson ${pkgname}-${pkgver} build
    meson compile -C build
}

check() {
  # Secure memory tests fail
  dbus-run-session meson test -C build --print-errorlogs || :
}

package() {
    DESTDIR="$pkgdir" meson install -C build
	
	# gcr wants to lock some memory to prevent swapping out private keys
	# https://bugzilla.gnome.org/show_bug.cgi?id=688161
	install -Dm644 /dev/stdin "$pkgdir/etc/security/limits.d/10-gcr.conf" <<END
@users - memlock 1024
END
}