# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=gcr
pkgname=(
  gcr
  gcr-docs
)
pkgver=3.41.2
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
arch=('x86_64')
groups=('gnome-base')
url="https://gitlab.gnome.org/GNOME/gcr"
license=(LGPL-2.1-or-later)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgcrypt
  libp11-kit
  libsecret
  openssh
  pango
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libxslt
  meson
  vala
)
source=(https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2)

build() {
  local meson_options=(
    # ssh-agent moved to gcr-4
    -D ssh_agent=false
  )

  avouch-meson gcr-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # Secure memory tests fail
  dbus-run-session meson test -C build --print-errorlogs || :
}

package_gcr() {
  provides=(
    libgck-1.so
    libgcr-{base,ui}-3.so
  )
  install=gcr.install

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gcr-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
