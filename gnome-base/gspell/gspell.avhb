# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gspell
pkgver=1.12.2
pkgrel=1
pkgdesc="A flexible API to implement the spell checking in a GTK+ application"
arch=('x86_64')
groups=('gnome-base')
url="https://wiki.gnome.org/Projects/gspell"
license=(LGPL)
depends=(
  enchant
  glib2
  gtk3
  icu
)
makedepends=(
  autoconf-archive
  git
  gobject-introspection
  gtk-doc
  vala
)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")
b2sums=('SKIP')


prepare() {
  cd gspell-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}


build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --enable-gtk-doc
  )

  cd gspell-$pkgver
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package(){
  depends+=(
    libenchant-2.so
    libgtk-3.so
    libg{lib,object,io}-2.0.so
    libicuuc.so
    libpango-1.0.so
  )
  provides+=("libgspell-${pkgver%%.*}.so")

  cd gspell-$pkgver
  make DESTDIR="$pkgdir" install
}

