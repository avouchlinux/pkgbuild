# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnome-autoar
pkgver=0.4.5
pkgrel=1
pkgdesc="Automatic archives creating and extracting library"
url="https://wiki.gnome.org/TingweiLan/GSoC2013Final"
arch=(x86_64)
groups=('gnome-base')
license=(LGPL)
depends=(
  gcc-libs
  glib2
  glibc
  gtk3
  libarchive
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  vala
)
provides=(libgnome-autoar{,-gtk}-${pkgver%%.*}.so)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
    local meson_options=(
    -D gtk_doc=true
    -D tests=true
    -D vapi=true
  )

  avouch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build

}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
