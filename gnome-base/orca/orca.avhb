# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=orca
pkgver=47.0
_srcname=${pkgname^^}_${pkgver/./_}
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
arch=(any)
groups=('gnome-base')
url="https://wiki.gnome.org/Projects/Orca"
arch=(any)
license=(LGPL-2.1-or-later)
depends=(
  at-spi2-core
  brltty
  glib2
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gstreamer
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
  gtk3
  hicolor-icon-theme
  liblouis
  libwnck3
  pango
  python
  python-cairo
  python-gobject
  python-psutil
  python-setproctitle
  speech-dispatcher
  xorg-xkbcomp
  xorg-xmodmap
)
makedepends=(
  git
  itstool
  meson
  yelp-tools
)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$_srcname/$pkgname-$_srcname.tar.bz2")
b2sums=('SKIP')

build() {
  local meson_options=(
    -D spiel=false
  )

  avouch-meson $pkgname-$_srcname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
