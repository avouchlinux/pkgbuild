# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=evolution-data-server
pkgname=(
  evolution-data-server
  libedataserverui4
  evolution-data-server-docs
)
pkgver=3.54.1
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
groups=('gnome-base')
license=("LGPL-2.0-only AND LGPL-2.1-or-later")
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  icu
  json-glib
  krb5
  libcanberra
  libgoa
  libgweather-4
  libical
  libldap
  libphonenumber
  libsecret
  libsoup3
  libxml2
  nspr
  nss
  pango
  perl
  sqlite
  util-linux-libs
  webkit2gtk-4.1
  zlib
)
makedepends=(
  boost
  cmake
  git
  glib2-devel
  gobject-introspection
  gperf
  gtk-doc
  gtk4
  ninja
  postfix
  python
  vala
  webkitgtk-6.0
)
checkdepends=(db)
source=("https://download.gnome.org/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz")
b2sums=('3a26bda58a1da44d763ac8a71cd5d0393c6e0ee989a183aecc478c5c660196a12d0915085a269cd9d105a8857c333bd557f7ebcca5fd9aa1379eda90acca3590')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D LIBEXEC_INSTALL_DIR=/usr/lib
    -D SYSCONF_INSTALL_DIR=/etc
    -D ENABLE_GTK_DOC=ON
    -D ENABLE_INTROSPECTION=ON
    -D ENABLE_VALA_BINDINGS=ON
    -D WITH_GWEATHER4=ON
    -D WITH_LIBDB=OFF
    -D WITH_PHONENUMBER=OFF
    -D ENABLE_OAUTH2_WEBKITGTK=OFF
    -D ENABLE_OAUTH2_WEBKITGTK4=OFF
  )

  cmake -S $pkgbase-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_evolution-data-server() {
  provides=(
    libcamel-1.2.so
    libebackend-1.2.so
    libebook-1.2.so
    libebook-contacts-1.2.so
    libecal-2.0.so
    libedata-book-1.2.so
    libedata-cal-2.0.so
    libedataserver-1.2.so
    libedataserverui-1.2.so
  )

  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgdir"

  _pick ui4 usr/include/evolution-data-server/libedataserverui4
  _pick ui4 usr/lib/libedataserverui4-1.0.so*
  _pick ui4 usr/lib/pkgconfig/libedataserverui4-1.0.pc
  _pick ui4 usr/lib/girepository-1.0/EDataServerUI4-1.0.typelib
  _pick ui4 usr/share/gir-1.0/EDataServerUI4-1.0.gir
  _pick ui4 usr/share/vala/vapi/libedataserverui4-1.0.*

  _pick docs usr/share/gtk-doc
}

package_libedataserverui4() {
  pkgdesc+=" - GTK 4 UI library"
  depends=(
    "evolution-data-server=$pkgver-$pkgrel"
    glib2
    glibc
    libgtk-4.so
    libical
    libsoup3
    libwebkitgtk-6.0.so
    nspr
    nss
    pango
  )
  provides=(libedataserverui4-1.0.so)

  mv ui4/* "$pkgdir"
}

package_evolution-data-server-docs() {
  pkgdesc+=" - documentation"
  depends=()

  mv docs/* "$pkgdir"
}
