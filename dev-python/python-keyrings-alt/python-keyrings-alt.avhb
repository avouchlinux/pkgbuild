# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python-keyrings-alt
epoch=1
pkgver=5.0.0
_commit=6c39a611b5cf2f7cb51c4684d71185ec3a970f14
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
groups=('dev-python')
url='https://github.com/jaraco/keyrings.alt'
license=('MIT')
depends=('python-keyring' 'python-pycryptodomex')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('libgnome-keyring' 'python-pycryptodome' 'python-gobject' 'python-keyring'
              'python-pytest-enabler')
optdepends=('libgnome-keyring: GNOME Integration'
            'python-gobject: GNOME Integration'
            'python-fs'
            'python-pycryptodome: Encrypted file storage')
source=("git+https://github.com/jaraco/keyrings.alt.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd keyrings.alt
  python -m build --wheel --no-isolation
}

check() {
  cd keyrings.alt
  pytest
}

package() {
  cd keyrings.alt
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
