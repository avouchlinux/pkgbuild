# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-dephell-setuptools
_realname=dephell_setuptools
pkgver=0.2.4
pkgrel=1
pkgdesc='Extract meta information from setup.py'
arch=(any)
url="https://github.com/dephell/$_realname"
license=(MIT)
depends=(python3-setuptools)
checkdepends=(python3-pytest)
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/$_realname/$_realname-$pkgver.tar.gz")
sha256sums=('663629e1ebf7b20bf7e372ee2a2e7ebf1a15aeb3bc6d46ad32e1bcb21044ca29')

prepare() {
    cd "$_realname-$pkgver"
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "$_realname-$pkgver"
    python3 setup.py build
}

check() {
    cd "$_realname-$pkgver"
    python3 -m pytest
}

package() {
    cd "$_realname-$pkgver"
    python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
