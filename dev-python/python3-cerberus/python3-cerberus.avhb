# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-cerberus
_realname=cerberus
pkgver=1.3.4
pkgrel=1
pkgdesc="Lightweight, extensible schema and data validation tool for Python dictionaries"
url="https://github.com/pyeve/cerberus"
license=('ISC')
arch=('any')
groups=('dev-python')
depends=('python3')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest-runner' 'python3-pytest-benchmark')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyeve/cerberus/archive/$pkgver.tar.gz")
sha512sums=('c816b426ed1b32367d1e8f1c26d6ddd4b49c6578540e688baf693e3094a05800f645cfe879be6378c1e9be9899cad7bc2ad1ed9844d6762d2ef6e0cc54a7476a')

build() {
  cd cerberus-$pkgver
  python3 setup.py build
}

check() {
  cd cerberus-$pkgver
  python3 setup.py pytest --addopts cerberus
}

package() {
  cd cerberus-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}