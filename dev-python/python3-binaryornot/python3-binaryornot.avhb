# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-binaryornot
_realname=binaryornot
pkgver=0.4.4
pkgrel=1
pkgdesc="Ultra-lightweight pure Python package to check if a file is binary or text"
arch=('any')
groups=('dev-python')
url="https://github.com/audreyr/binaryornot"
license=('BSD')
depends=('python-chardet')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "${pkgname}-0.4.4-hypothesis_tests.patch::https://github.com/audreyr/binaryornot/pull/52.patch")
sha512sums=('379e71b41824b9389ea02c64223e26c57694d07b749c197e3a6b9f10558c8d502e9b93a13ae6c3a4bca49064ffc650ce822073dc312fb06e50e06b8a3f04f419'
            '8a0f1066a580f08778434ed7d30c2ebf2764dbfd746b561ffce2fb8dd8d77cafaf4a58b03504cf5b1e4d37e0a6ffe3038dcaa5611cdfd7d42ada86edd1e47f3f')

prepare() {
  cd "$_realname-$pkgver"
  # fix tests using python-hypothesis:
  # https://github.com/audreyr/binaryornot/issues/46
  patch -Np1 -i "../${pkgname}-0.4.4-hypothesis_tests.patch"
}

build() {
  cd "$_realname-$pkgver"
  python setup.py build
}

check() {
  cd "$_realname-$pkgver"
# tests can only be run via setuptools:
# https://github.com/audreyr/binaryornot/issues/160
#  export PYTHONPATH="build:${PYTHONPATH}"
#  pytest -v
  python setup.py test
}

package() {
  cd "$_realname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CONTRIBUTING,HISTORY,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
