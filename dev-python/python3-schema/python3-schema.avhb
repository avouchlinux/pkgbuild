# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname="python3-schema"
_realname=schema
pkgver=0.7.5
pkgrel=1
pkgdesc='Python module to validate and convert data structures.'
arch=(any)
groups=("dev-python")
url="https://github.com/keleshev/$_realname"
depends=(python3 python3-contextlib2)
makedepends=(python3-setuptools)
checkdepends=(python3-mock python3-pytest)
license=(MIT)
source=(https://files.pythonhosted.org/packages/source/${_realname::1}/$_realname/$_realname-$pkgver.tar.gz)

prepare() {
  cd "$srcdir/$_realname-$pkgver"
  # accept arbitrary versions
  sed 's|==|>=|g' -i requirements.txt
}

build() {
  cd "$srcdir/$_realname-$pkgver"
  python3 setup.py build
}

check() {
  cd "$srcdir/$_realname-$pkgver"
  python3 setup.py test
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -D -m644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}