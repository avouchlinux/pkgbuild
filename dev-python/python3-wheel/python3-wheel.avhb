# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-wheel
_realname=wheel
pkgver=0.37.1
pkgrel=1
pkgdesc="A built-package format for Python"
arch=(any)
groups=('dev-python')
url="https://pypi.python.org/pypi/wheel"
license=('MIT')
depends=('python3-packaging')
optdepends=('python3-keyring: for wheel.signatures'
            'python3-xdg: for wheel.signatures')
makedepends=('python3-setuptools')
checkdepends=('python3-jsonschema' 'python3-pytest' 'python3-keyring' 'python3-keyrings-alt'
              'python3-xdg' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pypa/wheel/archive/$pkgver.tar.gz")
sha512sums=('9493db2f29e16d17ca06a4d47854b3df5dfd17f21c77efd42befd54470858256fd59f56aa9585cbf0a09830927b657d546324f793e1b1153665bf0b74d0c0457')

prepare() {
  # don't depend on python-coverage for tests
  sed -i 's/--cov=wheel//'  $_realname-$pkgver/setup.cfg

  # https://github.com/pypa/wheel/pull/365 but why?
  rm -r $_realname-$pkgver//src/wheel/vendored
  sed -i 's/from .vendored.packaging import tags/from packaging import tags/' $_realname-$pkgver//src/wheel/bdist_wheel.py
}

build() {
  cd $_realname-$pkgver
  python3 setup.py build
}

check() {
  # Hack entry points by installing it
  cd $_realname-$pkgver
  python3 setup.py install --root="$PWD/tmp_install" --optimize=1
  pysitedir=$(python -V | sed 's|Python |python|' | sed 's/\.[^.]*$//')
  PYTHONPATH="$PWD/tmp_install/usr/lib/$pysitedir/site-packages:$PYTHONPATH" test
}

package() {
  cd $_realname-$pkgver
  python3 setup.py install --skip-build --root="${pkgdir}" --optimize='1'
}