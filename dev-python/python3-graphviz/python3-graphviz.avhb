# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-graphviz
_realname=graphviz
pkgver=0.19
pkgrel=1
pkgdesc="Simple Python interface for Graphviz"
arch=('any')
groups=('dev-python')
url="https://github.com/xflr6/${_realname}"
license=('MIT')
depends=('graphviz' 'python')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest' 'python3-pytest-mock' 'python3-mock')
#source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.zip")
source=("https://github.com/xflr6/graphviz/archive/$pkgver/$_realname-$pkgver.tar.gz")
sha256sums=('47da628b46d5a9f8027170f0a3926ece5f9dbf0835f950d552a0547085c1f4e6')
b2sums=('d68acacaeff538c77484a8c63553095ea916c6e6dc24353351db57b6c9bd24429053643780461a44e5bb95e8d4cd76b5f3ca50279c93cd6ed26ea64a34d0709e')

prepare() {
    cd ${_realname}-${pkgver}

    # do not run python-coverage in unittests :/
    sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
    # don't add pointless dependency on python-mock needed on python 3.5
    sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
    cd ${_realname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_realname}-${pkgver}/tests

    # python -m pytest
}

package() {
    cd ${_realname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
