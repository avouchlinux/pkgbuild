# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python-virtualenv
_realname=virtualenv
pkgver=20.27.1
pkgrel=1
pkgdesc='Virtual Python Environment builder'
arch=('any')
groups=('dev-python')
url='https://virtualenv.pypa.io'
license=('MIT')
depends=(
  'python'
  'python-distlib'
  'python-filelock'
  'python-platformdirs'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
  'python-hatch-vcs'
  'python-sphinx'
  'python-sphinx-argparse'
  'python-sphinxcontrib-towncrier'
  'python-wheel'
  'towncrier'
)
checkdepends=(
  'fish'
  'python-coverage'
  'python-flaky'
  'python-pip'
  'python-pytest'
  'python-pytest-freezer'
  'python-pytest-mock'
  'python-time-machine'
  'python-setuptools'
  'tcsh'
  'xonsh'
)
replaces=('virtualenv')
conflicts=('virtualenv')
options=('!makeflags')
# source=("$pkgname::git+https://github.com/pypa/virtualenv#tag=$pkgver")
source=("$_realname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/8c/b3/7b6a79c5c8cf6d90ea681310e169cf2db2884f4d583d16c6e1d5a75a4e04/virtualenv-20.27.1.tar.gz")
sha512sums=('a147d315789a05c4fea2f87ae2c4217f4f2c77faaf130471b38921cd747467252fb526aa825e1ddb5e859a20d66d77071a22ae42fcb1e24048fd6d832629c479')
b2sums=('d61da8fa5d2c1fd1ef0a3cf5555e93cd9ad8b339c8a1a76b6292842bc901d62eb4185d904115cde9cf2be6f187e48601a1c70adf471d174a8d8fcf5e18eeb126')

build() {
  cd "$_realname-$pkgver"

  python -m build --wheel --no-isolation

  # NOTE: install to tmp dir for documentation and tests
  python -m installer --destdir=test_dir dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  # PYTHONPATH="$(pwd)/test_dir/$site_packages:$PYTHONPATH" \
  #   sphinx-build -b man docs docs/_build/man
}

check() {
  local pytest_options=(
    -vv
    # tests try to find python2
    --deselect tests/unit/create/test_creator.py::test_py_pyc_missing[True-False]
    --deselect tests/unit/create/test_creator.py::test_py_pyc_missing[False-False]
    --deselect tests/unit/discovery/py_info/test_py_info.py::test_fallback_existent_system_executable
    --deselect tests/unit/test_util.py::test_reentrant_file_lock_is_thread_safe
    ## https://github.com/pypa/setuptools_scm/issues/1036
    --deselect tests/unit/create/via_global_ref/test_build_c_ext.py::test_can_build_c_extensions
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd "$_realname-$pkgver"

  PYTHONPATH="$(pwd)/test_dir/$site_packages:$PYTHONPATH" \
    pytest "${pytest_options[@]}"
}

package() {
  cd "$_realname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" docs/_build/man/virtualenv.1

  # sort out files with suffix of 3
  ln -s virtualenv.1.gz "${pkgdir}/usr/share/man/man1/virtualenv3.1.gz"
  ln "$pkgdir/usr/bin/virtualenv" "$pkgdir/usr/bin/virtualenv3"

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/${pkgname#python-}-$pkgver.dist-info/licenses/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
