# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-cloudpickle
_realname=cloudpickle
pkgver=2.0.0
pkgrel=1
pkgdesc='Extended pickling support for Python objects'
arch=('any')
groups=('dev-python')
url='https://github.com/cloudpipe/cloudpickle'
license=('BSD')
makedepends=('python3-setuptools')
options=(!emptydirs)
source=("https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0390ecb3731ac035d74c34651460f4a683e9ef7443861712b8d56c20c2f92113')

build() {
  cd $_realname-$pkgver
  python3 setup.py build
}

check() {
  cd $_realname-$pkgver
  cd tests/cloudpickle_testpkg
  python3 setup.py build
  cd ../../
  PYTHONPATH="${PWD}"/tests/cloudpickle_testpkg/build/lib/ \
  pytest -vl -k 'not test_pickle_empty and not test_dont_steal_unknown_functions' \
         --deselect tests/cloudpickle_file_test.py::CloudPickleFileTests::test_pickling_special_file_handles \
         --deselect tests/cloudpickle_test.py::CloudPickleTest::test_pickling_file_handles \
         --deselect tests/cloudpickle_test.py::Protocol2CloudPickleTest::test_pickling_file_handles
}

package() {
  cd $_realname-$pkgver
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}