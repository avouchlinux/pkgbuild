# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_realname=installer
pkgname=python3-$_realname
pkgver=0.5.0
pkgrel=1
pkgdesc='Low-level library for installing a Python package from a wheel distribution'
arch=('any')
groups=('dev-python')
url='https://github.com/pradyunsg/installer'
license=('MIT')
depends=('python3')
makedepends=('git' 'python3-flit-core' 'python3-build'
             'python3-sphinx' 'python3-sphinx-furo' 'python3-myst-parser'
             'python3-sphinx-argparse')
checkdepends=('python3-pytest' 'python3-mock')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('e993aa80138d7aa463f1950f013a884c7bad9b1148f083753351d97f3727dc4c')
sha512sums=('8731bbf8a51b1094dc87f84575eec07a650988bdeddf205d04cf80a5088eac0d8b2927022864f4011a9562fcaae9f511747e5672f94ec3cce1dccdfa833c4fce')

build() {
  cd $_realname-$pkgver
  python3 -m build -wn
  PYTHONPATH=src sphinx-build -b dirhtml -v docs docs/build/html
}

check() {
  cd $_realname-$pkgver
  PYTHONPATH=src pytest
}

package() {
  cd $_realname-$pkgver
  PYTHONPATH=src python3 -m installer --destdir="$pkgdir" dist/*.whl
  # remove windows entrypoint scripts executables
  rm "$pkgdir"/usr/lib/python*/site-packages/installer/_scripts/*.exe

  # install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership docs/build/html "$pkgdir"/usr/share/doc/$pkgname
  rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
