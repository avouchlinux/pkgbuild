# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=python3-pytest-plugins
pkgname=('python3-pytest-fixture-config' 'python3-pytest-shutil' 'python3-pytest-virtualenv')
_realname=pytest-plugins
pkgver=1.7.0
pkgrel=1
arch=('any')
groups=('dev-python3')
license=('BSD')
url='https://github.com/manahl/pytest-plugins'
makedepends=('python3-pytest' 'python3-setuptools-git' 'python3-pypandoc' 'python3-execnet' 'python3-path.py'
             'python3-mock' 'python3-virtualenv' 'python3-termcolor')
source=("https://github.com/manahl/pytest-plugins/archive/v$pkgver.tar.gz")

prepare() {
  # New pypandoc?
  sed -i "s/'rst')/'rst', format='markdown')/" pytest-plugins-$pkgver/common_setup.py

  # Our /bin is a symlink
  sed -i "s|'/bin'|'/usr/bin'|" pytest-plugins-$pkgver/pytest-shutil/tests/integration/test_cmdline_integration.py

  # Pytest 4
  sed -i 's/< *4.0/<5/' pytest-plugins-$pkgver/pytest-*/setup.py

  (cd pytest-plugins-$pkgver; make copyfiles)

  sed -i '/contextlib2/d' pytest-plugins-$pkgver/pytest-shutil/setup.py
}

build() {
  for _pkg in pytest-fixture-config pytest-shutil pytest-virtualenv; do
    cd "$srcdir"/pytest-plugins-$pkgver/$_pkg
    python3 setup.py build
  done
}

check() {
  # Hack entry points by installing it

  for _pkg in pytest-fixture-config pytest-shutil pytest-virtualenv; do
    cd "$srcdir"/pytest-plugins-$pkgver/$_pkg
    python3 setup.py install --root="$srcdir"/tmp_install --optimize=1
    PYTHONPATH="$srcdir"/tmp_install/usr/lib/python3.10/site-packages py.test
  done
}

package_python3-pytest-fixture-config() {
  pkgdesc='Fixture configuration utils for py.test'
  depends=('python-pytest')

  cd pytest-plugins-$pkgver/pytest-fixture-config
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python3-pytest-shutil() {
  pkgdesc='A goodie-bag of unix shell and environment tools for py.test'
  depends=('python-pytest' 'python-execnet' 'python-path.py' 'python-mock' 'python-termcolor')

  cd pytest-plugins-$pkgver/pytest-shutil
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python3-pytest-virtualenv() {
  pkgdesc='Virtualenv fixture for py.test'
  depends=('python-pytest-fixture-config' 'python-pytest-shutil' 'python-virtualenv')

  cd pytest-plugins-$pkgver/pytest-virtualenv
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}