# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname='python3-boolean.py'
_realname=boolean.py
pkgver=3.8
pkgrel=1
pkgdesc='Implements boolean algebra in one module'
arch=('any')
groups=('dev-python')
url='https://github.com/bastikr/boolean.py'
license=('BSD')
depends=('python3')
makedepends=('python3-setuptools')
source=("$_realname-$pkgver.tar.gz::https://github.com/bastikr/boolean.py/archive/v$pkgver.tar.gz")
sha512sums=('d41ce9a05743342c116d68b88ea36be316ac9fc190a8451aca97b52fde60c58ec49d36d2dee071f83fb5f6ad49337da80bf9de1bf3d479b46386fd87e4cf2f14')

build() {
  cd "$_realname-$pkgver"
  python3 setup.py build
}

check() {
  cd "$_realname-$pkgver"
  python3 -m unittest discover --verbose
}

package() {
  cd "$_realname-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}