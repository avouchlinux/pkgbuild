# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-service_identity
_realname=service-identity
pkgver=21.1.0
pkgrel=1
pkgdesc='Service Identity Verification for python3'
url='https://service-identity.readthedocs.io/'
arch=('any')
groups=('dev-python3')
license=('MIT')
depends=('python3' 'python3-attrs' 'python3-pyasn1' 'python3-pyasn1-modules' 'python3-idna')
makedepends=('python3' 'python3-setuptools' 'python3-attrs' 'python3-pyasn1'
             'python3-pyasn1-modules' 'python3-idna' 'python3-sphinx')
checkdepends=('python3-pytest' 'python3-cryptography')
source=(https://github.com/pyca/service_identity/archive/${pkgver}.tar.gz)

build() {
  cd ${_realname}-${pkgver}
  python3 setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd ${_realname}-${pkgver}
  python3 setup.py test
}

package() {
  cd ${_realname}-${pkgver}
  python3 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/service_identity.1 "${pkgdir}/usr/share/man/man1/service_identity.1"
}