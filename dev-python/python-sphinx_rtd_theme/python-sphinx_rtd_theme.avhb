# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python-sphinx_rtd_theme
_name=${pkgname/python-/}
pkgver=2.0.0
pkgrel=1
pkgdesc='Read the Docs theme for Sphinx'
arch=('any')
groups=('dev-python')
url=https://github.com/readthedocs/sphinx_rtd_theme
license=('MIT')
depends=('python' 'python-sphinx' 'python-sphinxcontrib-jquery')
makedepends=('python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel')
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('40446e6789dd34deb4e9814e379bae0aa74057b6fb43de4b343a48c84fc0f8db')
b2sums=('69721e2729e8c8ff3832721255d6ccfbd78d6a87d1ca032b95185b190f265e888fbdf1b297b45aad82d45bbc8229dbe07dfd901cd7945085057321500fcb992f')

build() {
  cd "$_name"-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

package() {
  cd "$_name"-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
