# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-xmlschema
_realname=xmlschema
pkgver=1.9.2
pkgrel=1
pkgdesc="An XML Schema validator and decoder"
url="https://github.com/sissaschool/xmlschema"
license=('MIT')
arch=('any')
groups=('dev-python')
depends=('python3-elementpath')
makedepends=('python3-setuptools')
checkdepends=('python3-lxml')
source=("https://github.com/sissaschool/xmlschema/archive/v$pkgver.tar.gz")

prepare() {
  sed -i 's/~=/>=/' xmlschema-$pkgver/setup.py
}

build() {
  cd xmlschema-$pkgver
  python setup.py build
}

check() {
  cd xmlschema-$pkgver
  python -c "import xmlschema.codepoints; xmlschema.codepoints.save_unicode_categories()"
  cp xmlschema/unicode_categories.json build/lib/xmlschema/
  PYTHONPATH="$PWD/build/lib" python -m unittest
}

package() {
  cd xmlschema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}