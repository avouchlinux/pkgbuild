# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-dephell-venvs
_realname=dephell_venvs
pkgver=0.1.18
pkgrel=1
pkgdesc="Manage Python virtual environments"
arch=('any')
groups=('dev-python')
url="https://github.com/dephell/${_realname}"
license=('MIT')
depends=('python3-attrs' 'python3-dephell-pythons')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest' 'python3-requests')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c7307291b754edba325ab27edeb05d85ee4dd2f1487c48872a1ebfc372bf7a2e')
b2sums=('ca3b64a0983ef32f4aec42a26413604d241e00d2ac5d2e6c36fa58b3d354ad1a95562aa495a799245b54a7796c036fea1c3041ab087d3cb915c829caf75e02cd')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete

    # https://github.com/dephell/dephell_venvs/issues/3
    sed -e "s|dephell_venvs.ensurepip||" -e '/package_data/d' -i setup.py
}

build(){
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py build
}

check() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 -m pytest
}

package() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
