# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-dephell-markers
_realname=dephell_markers
pkgver=1.0.3
pkgrel=1
pkgdesc="Work with environment markers (PEP-496)"
arch=('any')
url="https://github.com/dephell/${_realname}"
license=('MIT')
depends=('python3-attrs' 'python3-dephell-specifier' 'python3-packaging')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('525e17914e705acf8652dd8681fccdec912432a747d8def4720f49416817f2d4')
b2sums=('e0d3f64cfb0b96a1c18c43c1d050d23477c2e7e6b00142e53d742cdfe8e9d6c8172c91a97c8607e220b34268976ecb2611aa617fbd3319117e9d3782ada2fac2')

prepare() {
    cd ${_realname}-${pkgver}
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd ${_realname}-${pkgver}
    python3 setup.py build
}

check() {
    cd ${_realname}-${pkgver}
    python3 -m pytest
}

package() {
    cd ${_realname}-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
