# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-clikit
_realname=clikit
pkgver=0.6.2
pkgrel=1
pkgdesc="clikit allows you to create beautiful and testable command-line interfaces"
arch=('any')
groups=('dev-python')
url="https://github.com/sdispater/${_pkgname}"
license=(MIT)
depends=(python3-crashtest
         python3-pastel
         python3-pylev
         python3-typing_extensions)
makedepends=(python3-{build,installer}
             python3-poetry)
checkdepends=(python3-pytest-mock)
_archive="$_realname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('4bb1d934e0c0a93f25429919242d11d60dcd1d9c65ac096033092d7973cbfc6e')
b2sums=('b72f245128c698375f97139cf245bdc8c017e3e2d142f6e3183a42620b23a024531e5b14f05163c6defa9ee54c52c19a64932c62484f2ec8da5c75ea1b7d3d27')

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	PYTHONPATH=$PWD/src python -m pytest
}

package() {
	cd "$_archive"
	python -m installer -d "${pkgdir}" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
