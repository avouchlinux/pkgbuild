# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-asgiref
_realname=asgiref
pkgver=3.5.2
pkgrel=1
pkgdesc="Reference ASGI adapters and channel layers"
arch=(any)
groups=('dev-python')
url="http://github.com/django/asgiref"
license=('BSD')
makedepends=('python3-setuptools')
checkdepends=(python3-pytest python3-pytest-asyncio)
depends=('python3')
source=("https://pypi.io/packages/source/a/asgiref/asgiref-${pkgver}.tar.gz")
sha256sums=('2f8abc20f7248433085eda803936d98992f1343ddb022065779f37c5da0181d0')

build() {
    cd "asgiref-${pkgver}"
    python3 setup.py build
}

check() {
    cd "asgiref-${pkgver}"
    PYTHONPATH=. pytest .
}

package() {
    cd "asgiref-${pkgver}"
    python3 setup.py install --root=${pkgdir} --optimize=1

    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
