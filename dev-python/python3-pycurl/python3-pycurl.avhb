# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-pycurl
_realname=pycurl
pkgver=7.44.1
_pkgver=${pkgver//./_}
pkgrel=1
pkgdesc="A python3 3.x interface to libcurl"
arch=('x86_64')
groups=('dev-python')
url="http://pycurl.sourceforge.net/"
license=('LGPL' 'MIT')
depends=('python3' 'curl')
makedepends=('python3' 'curl')
checkdepends=('python3-bottle' 'python3-nose-show-skipped' 'vsftpd' 'python3-flaky'  'python3-pyflakes')
# source=("https://github.com/pycurl/pycurl/archive/REL_$_pkgver.tar.gz")
# do not use githup achive build failed
source=("https://pypi.io/packages/source/p/$_realname/$_realname-$pkgver.tar.gz")

prepare() {
  cd ${_realname}-${pkgver}
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python3|" {} +
  sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python3-pycurl"/' setup.py
  sed -e "s/nosetests/nosetests3/" -e "s/python/python3/" -i tests/run.sh

}

build() {
  cd ${_realname}-${pkgver}
  python3 setup.py build
}

check() {
  cd ${_realname}-${pkgver}
  python3 setup.py pytest
}

package() {
  cd ${_realname}-${pkgver}
  python3 setup.py install --skip-build --root="${pkgdir}" --optimize='1'
}