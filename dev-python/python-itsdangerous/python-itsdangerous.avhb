# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python-itsdangerous
pkgver=2.1.2
pkgrel=1
pkgdesc='Various helpers to pass trusted data to untrusted environments'
arch=('any')
groups=('dev-python')
url='https://palletsprojects.com/p/itsdangerous/'
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-freezegun')
source=("https://github.com/mitsuhiko/itsdangerous/archive/${pkgver}/$pkgname-$pkgver.tar.gz")
sha256sums=('a983dd8e49b9374c942b7946f9bfbcf639ed4a22838d353e4cd30d45e20cc943')

build() {
  cd "itsdangerous-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "itsdangerous-$pkgver"
  PYTHONPATH="build/lib" pytest
}

package() {
  cd "itsdangerous-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}
