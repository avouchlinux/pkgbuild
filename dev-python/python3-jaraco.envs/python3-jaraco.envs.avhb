# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-jaraco.envs
_realname=jaraco.envs
pkgver=2.3.0
pkgrel=1
pkgdesc="Classes for orchestrating Python (virtual) environments"
url="https://github.com/jaraco/jaraco.envs"
license=('MIT')
arch=('any')
groups=('dev-python')
depends=('python3-jaraco' 'python3-path' 'python3-virtualenv' 'python3-tox')
makedepends=('python3-setuptools-scm')
checkdepends=('python3-pytest-black' 'python3-pytest-cov' 'python3-pytest-flake8')
source=("https://github.com/jaraco/jaraco.envs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a834f72ae5b3e0633f1d794a28914e653807ba27a9184724a651ed8ee7d8d0541fef6d5888a47ac53f386dcd8b701d305ae68ddcf56bec5992e49272a641ea38')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jaraco.envs-$pkgver
  sed -i '/tox-venv/d' setup.cfg
}

build() {
  cd jaraco.envs-$pkgver
  python3 setup.py build
}

check() {
  cd jaraco.envs-$pkgver
  python3 -m pytest
}

package() {
  cd jaraco.envs-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1

  rm "$pkgdir"/usr/lib/python3.10/site-packages/jaraco/__init__.py \
     "$pkgdir"/usr/lib/python3.10/site-packages/jaraco/__pycache__/__init__.cpython-310.pyc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
