# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname='python3-license-expression'
_realname=license-expression
pkgver=21.6.14
pkgrel=1
pkgdesc='Utility to parse, normalize and compare license expressions'
arch=('any')
groups=('dev-python')
url='https://github.com/nexB/license-expression'
license=('Apache')
depends=('python3' 'python-boolean.py')
makedepends=('python3-setuptools' 'python3-wheel' 'python3-pip')
checkdepends=('python3-pytest' 'python3-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('12c4333a53f30c73f123434c9e50ee50163a3bad5831e756cc1350c14daa062b939e8382f4bcf2fef11020f8bec5185f71302f3c68ae88cd59b8da9a5f2964f4')

prepare() {
  cd "$_realname-$pkgver"
  # Fix file to comply with PEP-440
  sed -i "s/^fallback_version =.*/fallback_version = \"$pkgver\"/" pyproject.toml
}

build() {
  cd "$_realname-$pkgver"
  python3 setup.py build
}

check() {
  cd "$_realname-$pkgver"
  pytest
}

package() {
  cd "$_realname-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}