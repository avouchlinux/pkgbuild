# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-tabulate
_realname=tabulate
pkgver=0.8.9
pkgrel=1
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
groups=('dev-python')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
depends=('python3')
makedepends=('python3-setuptools')
checkdepends=('python3-nose' 'python3-numpy' 'python3-pandas' 'python3-pytest' 'python3-wcwidth')
optdepends=('python3-numpy: NumPy array support'
            'python3-pandas: pandas.DataFrame support'
            'python3-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha512sums=('4170bb4ded836997ea053b039f19e084818c40e8ae2ae4f56c686ed3d33a341986bc311dd0370256b1af815db7b1ed8ba89008f87f8634b216412d0fd5fa76d9')
b2sums=('6c2a939d2e5e1c128545ee9d7c2ad6e51c2a0512b7cf97858dbd2bc9df9e58198b74441c5963bde64fa851558e1146dc4252951ed47d4c56630b89e5210293ac')

build() {
  cd "${_realname}-$pkgver"
  python3 setup.py build
}

check() {
  cd "${_realname}-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  pytest -v
}

package() {
  cd "${_realname}-$pkgver"
  python3 setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
