# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-dephell-shells
_realname=dephell_shells
pkgver=0.1.5
pkgrel=1
pkgdesc="Activate virtual environment for current shell"
arch=('any')
groups=('dev-python')
url="https://github.com/dephell/${_realname}"
license=('MIT')
depends=('python3-attrs' 'python3-pexpect' 'python3-shellingham')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('77150b732db135d436f41c2c6f12694e6058a8609214117ee80f6c40234ac2d5')
b2sums=('6cc99191ddd5ca5585aa45f8e6f3815a1f2ee2de1176e8e41f96bf65c6611fb744a368cbd727d1de2642f6f42cf6462ca364a90f76f4cec0f50059c111b6d9fd')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py build
}

check() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 -m pytest
}

package() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
