# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-typing_extensions
_realname=typing_extensions-gnupg
pkgver=4.1.1
pkgrel=1
pkgdesc='Backported and Experimental Type Hints for Python 3.5+'
arch=(any)
groups=('dev-python')
url=https://github.com/python/typing/tree/master/typing_extensions
license=(custom)
depends=(python)
makedepends=(git python3-build python3-flit-core python3-installer)
checkdepends=(python3-tests)
provides=(python3-typing-extensions)
source=("git+https://github.com/python/typing.git#tag=${pkgver}")
sha256sums=(SKIP)

build() {
  cd typing/typing_extensions-$pkgver

  python3 -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd typing/typing_extensions-$pkgver

  python3 -m venv --system-site-packages test-env
  test-env/bin/python3 -m installer dist/*.whl
  test-env/bin/python3 src/test_typing_extensions.py
}

package() {
  cd typing/typing_extensions-$pkgver

  python3 -m installer --destdir="${pkgdir}" dist/*.whl

  # Symlink license file
  local site_packages=$(python3 -c "import site; print(site.getsitepackages()[0])")
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s "${site_packages}"/typing_extensions-$pkgver.dist-info/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}