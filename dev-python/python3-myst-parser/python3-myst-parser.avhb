# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_realname=MyST-Parser
pkgname=python3-myst-parser
pkgver=0.17.0
pkgrel=1
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
groups=('dev-python')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python3-markdown-it-py' 'python3-mdit_py_plugins' 'python3-yaml' 'python3-docutils' 'python3-sphinx' 'python3-jinja')
optdepends=('python3-linkify-it-py: linkify support')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest' 'python3-pytest-regressions' 'python3-beautifulsoup4' 'python3-linkify-it-py')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2426a36672bd7ab7c956199f191f3e3b6242efcb2897ebb5dc720f8a4e7ef7a97568ac8c671d32842b91aa4bf367c08c55e26b35f32a955d2f89b15d324920aa')

build() {
  cd $_realname-$pkgver
  python3 setup.py build
}

check() {
  cd $_realname-$pkgver
  python3 -m pytest -k 'not test_sphinx_builds and not test_fixtures'
}

package() {
  cd $_realname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}