# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-platformdirs
_realname=platformdirs
pkgver=2.5.1
pkgrel=1
pkgdesc='A small Python module for determining appropriate platform-specific dirs, e.g. a "user data dir"'
arch=('any')
groups=('dev-python')
url="https://github.com/platformdirs/platformdirs"
license=('MIT')
depends=('python')
makedepends=('python3-setuptools-scm')
checkdepends=('python3-pytest-mock')
source=("https://pypi.io/packages/source/p/platformdirs/platformdirs-$pkgver.tar.gz")
sha512sums=('c2a71b2717b0d3d202be2b11a87e133c283b1cf8dc37d581a156b11c4a2376dfcf06f35965fd5bf7d0679100ef0e016d5f728e4c48a4f24374e14584c85c940f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd "$srcdir"/platformdirs-$pkgver
  python3 setup.py build
}

check() {
  cd "$srcdir"/platformdirs-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd platformdirs-$pkgver
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}