# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libindicator
pkgname=("${pkgbase}-gtk"{2,3})
pkgver=12.10.1
pkgrel=1
pkgdesc='Set of symbols and convenience functions for Ayatana indicators'
url='https://launchpad.net/libindicator'
arch=('x86_64')
groups=('dev-libs')
license=('GPL3')
makedepends=('gtk2' 'gtk3' 'pkg-config' 'python')
source=(https://launchpad.net/${pkgbase}/${pkgver%.*}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('6FC05581A37D71FCECE165DB5BE41E162CD6358E')  # Charles Kerr <charles.kerr@canonical.com>

prepare() {
  (cd ${pkgbase}-${pkgver}
    sed -i 's/-Werror//' {libindicator,tools}/Makefile.am
    autoreconf -fi
  )
  cp -ra ${pkgbase}-${pkgver}{,-gtk2}
}

build() {
  msg2 "Building gtk3..."
  (cd ${pkgbase}-${pkgver}
    ./configure \
      --prefix=/usr \
      --localstatedir=/var \
      --libexecdir=/usr/lib/libindicator \
      --sysconfdir=/etc \
      --with-gtk=3 \
      --disable-static \
      --disable-tests
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
  )

  msg2 "Building gtk2..."
  (cd ${pkgbase}-${pkgver}-gtk2
    ./configure \
      --prefix=/usr \
      --localstatedir=/var \
      --libexecdir=/usr/lib/libindicator \
      --sysconfdir=/etc \
      --with-gtk=2 \
      --disable-static \
      --disable-tests
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
  )
}

package_libindicator-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2')

  cd ${pkgbase}-${pkgver}-gtk2
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share
}

package_libindicator-gtk3() {
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share
}