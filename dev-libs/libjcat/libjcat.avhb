# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libjcat
pkgver=0.1.11
pkgrel=1
pkgdesc="Library for reading and writing Jcat files"
arch=(x86_64)
groups=('dev-libs')
url="https://github.com/hughsie/libjcat"
license=(LGPL)
depends=(gobject-introspection-runtime json-glib gnutls gpgme)
makedepends=(meson gobject-introspection gtk-doc vala help2man)
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('8b8585cc34f1b2e87d21c6eab8a1081cf6aa45294339bb5fca8a8437a17d2b73' 'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
    avouch-meson ${pkgname}-${pkgver} build -D gtkdoc=true
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
    rm -r "${pkgdir}"/usr/{lib,share}/installed-tests/
}
