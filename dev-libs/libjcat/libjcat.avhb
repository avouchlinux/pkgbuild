# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libjcat
pkgver=0.2.1
pkgrel=1
pkgdesc="Library for reading and writing Jcat files"
arch=(x86_64)
groups=('dev-libs')
url="https://github.com/hughsie/libjcat"
license=(LGPL)
depends=(json-glib gnutls gpgme)
makedepends=(meson gobject-introspection gtk-doc vala help2man)
source=("https://github.com/hughsie/libjcat/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('a6232aeca3c3fab6dbb3bed06ec3832088b49a4b278a7119558d72be60ce921f'
            'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  avouch-meson ${pkgname}-${pkgver} build -D gtkdoc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -r "${pkgdir}"/usr/{lib,share}/installed-tests/
}

