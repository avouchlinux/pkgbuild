# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=cryptoplusplus
_realname=crypto++
pkgver=8.9.0
_srcver=${pkgver//./}
_realname=cryptopp
pkgrel=1
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('x86_64')
groups=('dev-libs')
url=https://www.cryptopp.com/
license=(custom)
depends=(gcc-libs)
makedepends=(
  git
  unzip
)
_tag=843d74c7c97f9e19a615b8ff3c0ca06599ca501b
source=(git+https://github.com/weidai11/cryptopp.git#tag=${_tag})
b2sums=(SKIP)
validpgpkeys=(B8CC19802062211A508B2F5CCE0586AF1F8E37BD) # Jeffrey Walton <noloader@gmail.com>

pkgver() {
  cd cryptopp
  git describe --tags | sed 's/^CRYPTOPP_//; s/_/./g'
}

build() {
  export CXXFLAGS="$CXXFLAGS -DNDEBUG -fPIC"
  make PREFIX=/usr -C cryptopp dynamic cryptest.exe libcryptopp.pc
}

check() {
  make PREFIX=/usr -C cryptopp test
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C cryptopp install
  install -Dm 644 cryptopp/License.txt -t "${pkgdir}"/usr/share/licenses/crypto++/
  # Remove cryptest.exe and test files, only needed for check() and bloats the package
  # because cryptest.exe is linked statically.
  rm -rf "${pkgdir}"/usr/{bin,share/cryptopp}
}


