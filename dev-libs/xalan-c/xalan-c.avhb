# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xalan-c
_realname=xalan_c
pkgver=1.12
pkgrel=1
epoch=1
pkgdesc="A XSLT processor for transforming XML documents"
arch=('x86_64')
groups=('dev-libs')
url="https://xalan.apache.org/"
license=('APACHE')
makedepends=('cmake')
depends=('xerces-c')
provides=('libxalan-c.so' 'libxalanMsg.so')
validpgpkeys=('F33D281D470AB4436756147C07B3C8BC4083E800') # Roger Leigh <rleigh@debian.org>
source=("https://downloads.apache.org/xalan/xalan-c/sources/xalan_c-$pkgver.tar.gz"{,.asc})
sha256sums=('ee7d4b0b08c5676f5e586c7154d94a5b32b299ac3cbb946e24c4375a25552da7'
            'SKIP')

build() {
  cmake -B build -S xalan_c-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -Dpkgconfig-dir=/usr/lib/pkgconfig
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
