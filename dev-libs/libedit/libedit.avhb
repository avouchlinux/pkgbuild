# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libedit
_pkgver=20191231-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='A library providing generic line editing, history, and tokenization functions'
arch=('x86_64')
groups=('dev-libs')
url='https://thrysoee.dk/editline/'
license=('BSD')
depends=('ncurses')
source=("http://thrysoee.dk/editline/libedit-${_pkgver}.tar.gz")
sha256sums=('ac8f0f51c1cf65492e4d1e3ed2be360bda41e54633444666422fbf393bba1bae')

build() {
    cd "${srcdir}/${pkgname}-${_pkgver}"
    ./configure --prefix=/usr --enable-widec
    make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make prefix="${pkgdir}"/usr install

	rm "${pkgdir}"/usr/share/man/man3/history.3 # conflicts with readline
	cp "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/libedit/LICENSE
}
