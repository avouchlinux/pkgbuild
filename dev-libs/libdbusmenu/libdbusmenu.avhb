# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libdbusmenu
pkgname=("${pkgbase}-glib" "${pkgbase}-gtk"{2,3})
pkgver=16.04.0
pkgrel=1
pkgdesc='Library for passing menus over DBus'
url='https://launchpad.net/libdbusmenu'
arch=('x86_64')
groups=('dev-libs')
license=('GPL3' 'LGPL2.1' 'LGPL3')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk2' 'gtk3' 'intltool' 'vala' 'valgrind' 'pkg-config')
options=('!emptydirs')
source=(https://launchpad.net/${pkgbase}/${pkgver%.?}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('45B1103FB93ACBD90296DBCAE83D089481836EBF')  # Marco Trevisan (at 3v1n0.net) <marco.trevisan@3v1n0.net>

prepare() {
  cd ${pkgbase}-${pkgver}
  # don't treat warnings as errors
  sed -i 's/-Werror//' libdbusmenu-*/Makefile.{am,in}
  cd ..

  cp -ra ${pkgbase}-${pkgver}{,-gtk2}
}

build() {
  export HAVE_VALGRIND_TRUE='#'
  export HAVE_VALGRIND_FALSE=''

  echo "Building gtk3..."
  (cd ${pkgbase}-${pkgver}
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-{dumper,static,tests} \
      --with-gtk=3
    make ${MAKEFLAGS}
  )

  echo "Building gtk2..."
  (cd ${pkgbase}-${pkgver}-gtk2
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-{dumper,static,tests} \
      --with-gtk=2
    make ${MAKEFLAGS}
  )
}

package_libdbusmenu-glib() {
  depends=('glib2')

  cd ${pkgbase}-${pkgver}
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
}

package_libdbusmenu-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("${pkgbase}-glib" 'gtk2')

  cd ${pkgbase}-${pkgver}-gtk2
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-gtk DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" uninstall
}

package_libdbusmenu-gtk3() {
  pkgdesc+=" (GTK+ 3 library)"
  depends=("${pkgbase}-glib" 'gtk3')

  cd ${pkgbase}-${pkgver}
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-gtk DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" uninstall
}

# vim: ts=2 sw=2 et:
