# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=dia
pkgver=0.97.3
pkgrel=1
pkgdesc="A GTK+ based diagram creation program"
arch=('x86_64')
groups=('app-grafix')
license=('GPL')
url="http://live.gnome.org/Dia"
depends=('libxslt' 'gtk2')
makedepends=('intltool' 'python2' 'docbook-xsl')
optdepends=('python2')
options=('docs' '!emptydirs')
source=("ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.97/${pkgname}-${pkgver}.tar.xz")

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	for file in `find -type f -name '*.py'`; do
		sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' "$file"
	done
	sed -i 's#python2\.1#python2 python2.1#' configure
  sed -i 's#freetype-config --cflags#pkg-config --cflags freetype2#' configure
  sed -i 's#freetype-config --libs#pkg-config --libs freetype2#' configure
  
	# run in single window mode (--integrated) by default (#910275)
	sed -i 's|Exec=dia|Exec=dia --integrated|' dia.desktop.in.in
	
    patch -Np1 -i ../dia-0.97.0-gnome-doc.patch #159381 , upstream #470812 #558690
		
    patch -Np0 -i ../build_fix.patch
    
    # Skip man generation
    sed -i -e '/if HAVE_DB2MAN/,/endif/d' doc/*/Makefile.am
		
		# Upstream bug #737254
	sed -i -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.in
		
		# Upstream bug #737255
	sed -i -e "/localedir/d" configure.in
	
	autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  #export LDFLAGS="$LDFLAGS -lgmodule-2.0"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
    --with-cairo \
    --with-hardbooks \
    --enable-db2html \
    --disable-silent-rules
    sed -i 's#SUBDIRS = lib objects plug-ins shapes app bindings samples po sheets data doc tests installer#SUBDIRS = lib objects plug-ins shapes app bindings samples po sheets data tests installer#' Makefile
  make ${MAKEFLAGS}
  cd doc
  make html
  
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd doc
  make html
  make DESTDIR="${pkgdir}" install-html
  ln -sf ../doc/dia/html "${pkgdir}"/usr/share/dia/help
}
