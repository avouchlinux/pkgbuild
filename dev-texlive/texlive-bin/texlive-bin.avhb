# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=texlive
pkgname=('texlive-bin' 'libsynctex')
pkgver=20240312
_realname=texlive-$pkgver-source
pkgrel=1
url='http://tug.org/texlive/'
license=('GPL')
arch=('x86_64')
groups=('dev-texlive')
makedepends=(bash
             cairo
             clisp
             ffcall
             fontconfig
             freetype2
             gcc-libs
             gd
             git
             glibc
             gmp
             graphite
             harfbuzz
             icu
             libjpeg-turbo
             libpaper
             libpng
             libsigsegv
             libunistring
             libx11
             libxaw
             libxcrypt
             libxmu
             libxpm
             libxt
             mpfr
             ncurses
             perl
             pixman
             potrace
             readline
             subversion
             t1lib
             zlib
             zziplib)
source=(#"https://github.com/TeX-Live/texlive-source/archive/build-$_pkgver.tar.gz"
         https://ftp.math.utah.edu/pub/tex/historic/systems/texlive/${pkgver:0:4}/texlive-$pkgver-source.tar.xz)

prepare() {
   cd $pkgbase-$pkgver-source
#   bibtex-x needs kpathsea flags
  sed -i '/AC_SEARCH_LIBS/a KPSE_KPATHSEA_FLAGS' texk/bibtex-x/configure.ac
  (cd texk/bibtex-x && autoreconf)
   # t4ht expects to be un /usr/share/texmf/bin/t4ht (FS#27251)
  sed -i s/SELFAUTOPARENT/TEXMFROOT/ texk/tex4htk/t4ht.c
   # remove spurious ptex "guessed encoding" print
  patch -p1 -i ../ptex-debug-print.patch
}

build() {
   cd $pkgbase-$pkgver-source

   mkdir -p build
   cd build


   # export LDFLAGS="$LDFLAGS -lkpathsea"

   export LC_ALL=C

   CFLAGS+=" -Wno-incompatible-pointer-types" \
   ax_cv_c_float_words_bigendian=no \
   ../configure --prefix=/usr -C \
      --sysconfdir=/etc \
      --datarootdir=/usr/share \
      --datadir=/usr/share \
      --mandir=/usr/share/man \
      --with-banner-add=" - Avouch" \
      --with-system-cairo \
      --with-system-fontconfig \
      --with-system-freetype2 \
      --with-system-gmp   \
      --with-system-graphite2 \
      --with-system-harfbuzz \
      --with-system-icu   \
      --with-system-libgs \
      --with-system-libpaper \
      --with-system-libpng\
      --with-system-mpfr  \
      --with-system-pixman\
      --with-system-zlib  \
      --with-system-pixman \
      --with-system-cairo \
      --with-freetype2-libdir=/usr/lib \
      --with-freetype2-include=/usr/include/freetype2 \
      --enable-shared \
      --disable-static \
      --disable-native-texlive-build \
      --disable-multiplatform \
      --disable-dialog \
      --disable-psutils \
      --disable-t1utils \
      --disable-bibtexu \
      --disable-xz \
      --disable-aleph \
      --disable-xindy-rules \
      --disable-xindy-docs \
      --disable-dump-share \
      --enable-xindy \
      --disable-xindy-rules \
      --disable-xindy-docs


   # prevent excessive overlinking due to libtool
   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

   make
}

package_libsynctex() {
  pkgdesc='Library for synchronization between TeX files and resulting file'
  depends=(glibc zlib)
  provides=(libsynctex.so)

  cd $pkgbase-$pkgver-source/build
  make -C texk/web2c DESTDIR="$pkgdir" \
    install-data-am install-libLTLIBRARIES

# remove docs included in texlive-doc
  rm -r "$pkgdir"/usr/share/man
}

package_texlive-bin() {
  pkgdesc='TeX Live binaries'
  depends=(bash
           cairo libcairo.so
           ffcall # libffcall.so
           fontconfig libfontconfig.so
           freetype2 libfreetype.so
           gcc-libs # libgcc_s.so libstdc++.so
           gd # libgd.so
           glibc # ld-linux-x86-64.so libc.so libm.so
           gmp
           graphite libgraphite2.so
           harfbuzz libharfbuzz.so
           icu libicui18n.so libicuuc.so
           libpaper # libpaper.so
           libpng libpng16.so
           libsigsegv # libsigsegv.so
           libsynctex libsynctex.so
           libunistring libunistring.so
           libx11 # libX11.so
           libxaw # libXaw.so
           libxcrypt libcrypt.so
           libxmu # libXmu.so
           libxpm # libXpm.so
           libxt # libXt.so
           mpfr libmpfr.so
           ncurses libncursesw.so
           perl
           pixman libpixman-1.so
           potrace # libpotrace.so
           readline libreadline.so
           zlib libz.so
           zziplib) # libzzip-0.so
  provides=(lcdf-typetools
            libptexenc.so
            libtexlua53.so
            libtexluajit.so
            kpathsea
            xindy)
  optdepends=('psutils: to manipulate the output of dvips')

   cd $pkgbase-$pkgver-source
   # fixes for xindy
   find utils/xindy -name Makefile -exec sed -i -e "s|^prefix =.\+$|prefix = $pkgdir/usr|" -e "s|^mandir =.\+$|mandir = \${prefix}/share/man|" -e "s|^datadir =.\+$|datadir = \${datarootdir}/texmf|" -e "s|^docdir =.\+$|docdir = \${datadir}/doc/xindy|" '{}' \;

   cd build
   make DESTDIR="$pkgdir" texmf="$pkgdir"/usr/share/texmf install
   LD_PRELOAD="$pkgdir"/usr/lib/libkpathsea.so.6 \
   make DESTDIR="$pkgdir" texlinks

   # remove stuff included in texlive-texmf
   rm -r "$pkgdir"/usr/share/texmf-dist

   # remove docs included in texlive-doc
   rm -r "$pkgdir"/usr/share/{info,man}

   # remove links to scripts
   for _link in $(ls "$pkgdir"/usr/bin); do
      [[ $(readlink -m "$pkgdir"/usr/bin/$_link) == */scripts/* ]] && _rmlinks+="$pkgdir/usr/bin/$_link "
   done
   rm $_rmlinks

   # remove libsynctex
   rm "$pkgdir"/usr/include/synctex/*
   rm "$pkgdir"/usr/lib/libsynctex.*
   rm "$pkgdir"/usr/lib/pkgconfig/synctex.pc
}
