# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=aisleriot
pkgver=3.22.11
pkgrel=1
pkgdesc="A collection of patience games written in guile scheme"
arch=('x86_64')
groups=('gnome-games')
url="https://wiki.gnome.org/Apps/Aisleriot"
license=(GPL)
depends=(guile gtk3 librsvg libcanberra dconf)
makedepends=(appdata-tools gnome-common meson)
optdepends=('libkdegames: KDE card sets'
            'pysolfc: PySol card sets'
            'pysolfc-cardsets: PySol card sets')
options=(!emptydirs)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)

build() {
    avouch-meson ${pkgname}-${pkgver} build \
		-D theme_kde=false \
		-D theme_pysol=true \
		-D theme_pysol_path=/usr/share/PySolFC
    ninja -C build
}

check() {
    meson test -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
