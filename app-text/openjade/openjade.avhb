# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=openjade
pkgver=1.3.2
pkgrel=1
pkgdesc="James Clark's DSSSL Engine"
url='http://www.jclark.com/jade/'
arch=('x86_64')
groups=('app-text')
license=('custom')
depends=('gcc-libs')
conflicts=('jade')
provides=('jade')
source=(https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1d2d7996cc94f9b87d0c51cf0e028070ac177c4123ecbfd7ac1cb8d0b7d322d1'
            '11d90e242eae60ce06bf27fd234adbd8efd7d4a9a4a2da058faa4e8336dc423a')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    #fix build on ppc64
    patch -Np1 -i "${srcdir}"/openjade-ppc64.patch
    #do not link against -lnsl
    patch -Np1 -i "${srcdir}"/openjade-1.3.1-nsl.patch
    #Fix dependent libs for libogrove (bug #198232).
    patch -Np1 -i "${srcdir}"/openjade-deplibs.patch
    #do not require OpenSP libosp.la file for build(#485114)
    patch -Np1 -i "${srcdir}"/openjade-nola.patch
    #upstream bug tracker fix for build with gcc46
    patch -Np1 -i "${srcdir}"/openjade-1.3.2-gcc46.patch
    #use Getopt:Std to prevent build failure
    patch -Np1 -i "${srcdir}"/openjade-getoptperl.patch

}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    export CXXFLAGS+=' -fno-lifetime-dse'
    ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-static  \
        --enable-default-catalog=/etc/sgml/catalog \
        --enable-default-search-path=/usr/share/sgml \
        --datadir=/usr/share/sgml/${pkgname}-${pkgver} \
        --enable-html \
        --enable-http \
        --enable-mif
    make ${MAKEFLAGS}

}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
    ln -s openjade "$pkgdir"/usr/bin/jade

    install -d "${pkgdir}"/etc/sgml

    install -d "${pkgdir}"/usr/share/sgml/${pkgname}-${pkgver}/
    cp dsssl/catalog "$pkgdir"/usr/share/sgml/${pkgname}-${pkgver}/
    cp dsssl/{dsssl,style-sheet,fot}.dtd "$pkgdir"/usr/share/sgml/${pkgname}-${pkgver}/
    # license
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

