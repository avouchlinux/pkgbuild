# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=1
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=('any')
groups=('app-text')
url='https://docbook.org/'
arch=(any)
license=(custom)
install=$pkgname.install
source=(https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F$pkgver/docbook-xsl{,-nons}-$pkgver.tar.gz
        765567_non-recursive_string_subst.patch)

prepare() {
	bsdtar -xf $srcarchivedir/docbook-xsl-nons-1.79.2.tar.gz
	cd $pkgname-$pkgver
	patch -Np2 -i ../765567_non-recursive_string_subst.patch

	cd ../$pkgname-nons-$pkgver
	patch -Np2 -i ../765567_non-recursive_string_subst.patch
}

package() {
  depends=(libxml2 libxslt docbook-xml)

  local pkgroot ns dir
  for ns in -nons ''; do
    pkgroot="$pkgdir/usr/share/xml/docbook/xsl-stylesheets$ns-$pkgver"
    dir=$pkgname$ns-$pkgver

    install -Dt "$pkgroot" -m644 $dir/VERSION{,.xsl}

    (
      shopt -s nullglob  # ignore missing files
      for fn in assembly common eclipse epub epub3 fo highlighting html \
        htmlhelp javahelp lib manpages params profiling roundtrip template \
        website xhtml xhtml-1_1 xhtml5
      do
        install -Dt "$pkgroot/$fn" -m644 $dir/$fn/*.{xml,xsl,dtd,ent}
      done
    )
  done

  install -d "$pkgdir/etc/xml"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $dir/COPYING
}