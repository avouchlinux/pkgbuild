# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname='ocaml-findlib'
_realname='findlib'
pkgver=1.9.3
pkgrel=1
pkgdesc='OCaml package manager'
arch=('x86_64')
groups=('dev-ocaml')
url='http://projects.camlcity.org/projects/findlib.html'
license=(MIT)
depends=(ocaml-compiler-libs)
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
options=(staticlibs !strip zipman !makeflags)

build() {
  cd "findlib-$pkgver"

  ./configure \
    -sitelib /usr/lib/ocaml \
    -config /etc/findlib.conf \
    -mandir /usr/share/man

  make all opt
}

package () {
  cd "findlib-$pkgver"

  make prefix="$pkgdir" install

  install -Dm755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/ocamlfind_opt"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}