# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-keyrings-alt
_realname=keyrings.alt
epoch=1
pkgver=4.1.0
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
url='https://github.com/jaraco/keyrings.alt'
license=('MIT')
depends=('python3-keyring')
makedepends=('python3-setuptools-scm')
checkdepends=('libgnome-keyring' 'python3-pycryptodome' 'python3-gobject' 'python3-keyring'
              'python3-pytest-runner' 'python3-pytest-flake8' 'python3-pytest-black' 'python3-pytest-cov')
optdepends=('libgnome-keyring: GNOME Integration'
            'python3-gobject: GNOME Integration'
            'python3-fs'
            'python3-crypto: Encrypted file storage')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('941793b72688b182002793c14e4df84c79d8864fa3033d25326af8fd8cc90c500b10b53827916240f242490f60a9fd638873cedc3b8b04455a2621c756228bf8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd $_realname-$pkgver
  python3 setup.py build
}

check() {
  cd $_realname-$pkgver
  python3 setup.py pytest
}

package() {
  cd $_realname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}