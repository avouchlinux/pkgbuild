# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-tomli
_realname=tomli
pkgver=2.0.1
pkgrel=1
pkgdesc="A lil' TOML parser"
url="https://github.com/hukkin/tomli"
license=('MIT')
arch=('any')
groups=('dev-python')
depends=('python3')
makedepends=('python3-pyproject2setuppy')
checkdepends=('python3-pytest' 'python3-dateutil')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hukkin/tomli/archive/$pkgver.tar.gz")
sha512sums=('dc7cdfadaca74f39dd6cb00686ec2e5b47b8ee3dbec9bd0a289a6ff78619cfdbf125adb6253d838f197722f4389690872a3ca870664b7785c19f3c2bb1a18231')

build() {
  cd tomli-$pkgver
  python3 -m pyproject2setuppy.main build
}

check() {
  cd tomli-$pkgver
  python3 -m pytest
}

package() {
  cd tomli-$pkgver
  python3 -m pyproject2setuppy.main install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
