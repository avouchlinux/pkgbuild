# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-cachecontrol
_realname=cachecontrol
# Before bumping, watch out for:
# https://github.com/ionrock/cachecontrol/issues/262
pkgver=0.12.6
pkgrel=1
epoch=1
pkgdesc="httplib2 caching for requests"
arch=('any')
groups=('dev-python')
url="https://github.com/ionrock/${_realname}"
license=('Apache')
depends=('python3-msgpack' 'python3-requests')
makedepends=('python3-setuptools')
checkdepends=('python3-mock' 'python3-pytest' 'python3-lockfile' 'python3-cherrypy')
optdepends=('python3-lockfile: for the FileCache')
source=("${_realname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "0001-Remove-unnecessary-console-script.patch")
sha256sums=('2ec120838f445bff473cd8b8206ddae3650679142976927179743fabb876f3e3'
            'a2c93d4852887152027140bdd54030d5363876b02e5eabee6a018d4e946a87b1')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}
    patch -p1 -i ../0001-Remove-unnecessary-console-script.patch
}

build() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py build
}

check() {
    cd "${srcdir}"/${_realname}-${pkgver}
    # python3 -m pytest
}

package() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
