# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-distributed
_realname=distributed
pkgver=2022.2.1
pkgrel=1
pkgdesc="A python33 library for distributed computation."
arch=('any')
groups=('dev-python3')
depends=(
  'python3' 'python3-click' 'python3-cloudpickle' 'python3-dask' 'python3-msgpack'
  'python3-psutil' 'python3-sortedcontainers' 'python3-tblib' 'python3-toolz'
  'python3-tornado' 'python3-zict' 'python3-yaml'
)
optdepends=(
  # Packages suggested in dev-requirements.txt
  'python3-joblib: Joblib integration'
  'python3-pandas: Pandas integration'
  'python3-numpy: NumPy integration'
  'python3-bokeh: Interactive visualisation of scheduler tasks'
  'python3-pyzmq: Asynchronous messaging with ZeroMQ'
  'python3-ipykernel: Ipython3 integration for computing and debugging'
  'python3-prometheus_client: Prometheus integration for monitoring and alerting'

  # Packages which have optional unit tests (pytest.importorskip calls)
  'python3-keras: Support for the Keras deep learning library'
  'python3-lz4: LZ4 compression of messages'
  'python3-netcdf4: Support for netCDF4 data files'
  'python3-numba: JIT compilation of code'
  'python3-h5py: Support for the HDF5 binary data format'
  'python3-asyncssh: Computing cluster using SSH communication'
  'python3-pytorch: Support for tensors and dynamic neural networks'
  'python3-ipywidgets: Extra widgets in diagnostic pages'
)
makedepends=('python3-setuptools')
url="https://distributed.dask.org/"
license=('BSD')
source=("https://files.pythonhosted.org/packages/source/d/distributed/distributed-$pkgver.tar.gz")

build() {
  cd "$_realname-$pkgver"
  python3 setup.py build
}

package() {
  cd "$_realname-$pkgver"
  python3 setup.py install --skip-build --root="$pkgdir" --prefix=/usr --optimize=1
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname"
}