# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-pyhamcrest
_realname=PyHamcrest
pkgver=2.0.3
pkgrel=1
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
groups=("dev-python")
license=('BSD')
url="https://github.com/hamcrest/PyHamcrest"
depends=('python3')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest' 'python3-mock')
source=("https://github.com/hamcrest/PyHamcrest/archive/V$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f7e79f7d26c4c6add042052e451898bbf5adf21b05a234cc62bce890d68f5997693b042a9daf136f7026040a8ab7bf28b829288417b947973dcb959bf613a37b')

build() {
  cd PyHamcrest-$pkgver
  python3 setup.py build
}

check() {
  cd PyHamcrest-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd PyHamcrest-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
