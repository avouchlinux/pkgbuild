# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-dephell-discover
_realname=dephell_discover
pkgver=0.2.10
pkgrel=1
pkgdesc="Find project modules and data files (packages and package_data for setup.py)"
arch=('any')
groups=('dev-python')
url="https://github.com/dephell/${_realname}"
license=('MIT')
depends=('python3-attrs')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('a2ad414e5e0fe16c82c537d6a3198afd9818c0c010760eccb23e2d60e5b66df6')
b2sums=('2721bdee22c9edcdc11c7102fb034db4ec92b23bf8ac7c077c87036083cefecf34bbf1474bdb0ddfa252ca47d40181054b5eacff68daef8c4e7bad9f4426ea35')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py build
}

check() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 -m pytest
}

package() {
    cd "${srcdir}"/${_realname}-${pkgver}

    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
