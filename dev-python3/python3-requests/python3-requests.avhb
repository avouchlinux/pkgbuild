# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-requests
_realname=requests
pkgver=2.27.1
pkgrel=1
pkgdesc="python3 HTTP for Humans"
arch=('any')
groups=('dev-python')
url="http://python-requests.org"
license=('Apache')
depends=('python3-urllib3' 'python3-chardet' 'python3-idna')
optdepends=('python3-pysocks: SOCKS proxy support')
makedepends=('python3-setuptools' 'python3-chardet' 'python3-urllib3' 'python3-idna')
checkdepends=('python3-pytest-httpbin' 'python3-pytest-mock' 'python3-pysocks')
source=("https://pypi.io/packages/source/r/$_realname/$_realname-${pkgver}.tar.gz")

prepare() {
  cd requests-$pkgver
  # Stay with chardet for now: https://github.com/psf/requests/issues/5871
  sed -e '/certifi/d' \
      -e "s/,<.*'/'/" \
      -e '/charset_normalizer/d' \
      -i setup.py
  patch -p1 -i "$srcdir"/certs.patch
}

build() {
  cd requests-$pkgver
  python3 setup.py build
}

check() {
  # Seems to be a problem about pytest-httpbin:
  # pytest-httpbin server hit an exception serving request: [SSL: HTTP_REQUEST] http request (_ssl.c:1129)
  # pytest-httpbin server hit an exception serving request: [SSL: TLSV1_ALERT_UNKNOWN_CA] tlsv1 alert unknown ca (_ssl.c:1129)

  cd requests-$pkgver
  pytest tests --deselect tests/test_requests.py::TestRequests::test_pyopenssl_redirect
}

package() {
  cd requests-$pkgver
  python3 setup.py install --skip-build -O1 --root="$pkgdir"
}