# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-zope-i18nmessageid
_realname=zope.i18nmessageid
pkgver=5.0.1
pkgrel=1
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
groups=('dev-python')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
depends=('python3-setuptools')
checkdepends=('python3-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('f21fe9a9778c46411444ae34b5f631a3618045a1bd2c2e7f6909aa8cf518a47d9717f13756c9d6e472c909dd7642c3588f8473945d623e77dced28415323dce5')

build() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python3 setup.py build
}

check() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  pybasever=$(python -V | sed 's|Python ||' | sed 's/\.[^.]*$//')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-$pybasever:$PYTHONPATH" python3 -m zope.testrunner --test-path=src || echo "Tests failed"
}

package() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}