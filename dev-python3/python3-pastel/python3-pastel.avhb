# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python3-pastel
_realname=pastel
pkgver=0.2.1
pkgrel=1
pkgdesc="Bring colors to your terminal"
arch=('any')
groups=('dev-python')
url="https://github.com/sdispater/${_realname}"
license=('MIT')
depends=('python3')
makedepends=('python3-dephell' 'python3-setuptools')
checkdepends=('python3-pytest')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e6581ac04e973cac858828c6202c1e1e81fee1dc7de7683f3e1ffe0bfd8a573d')
b2sums=('064d30404600e3ad6896deb397f05f597faf550d595f7bf6f38e0e3465681ad4bf7601a85bd1e044a889fc1d32e56475a1bb16e5e81380b89238c3b70d8ad7dd')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}
    # poetry does not generate correct setup.py, nuke it and recreate from scratch
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py build
}

check() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 -m pytest
}

package() {
    cd "${srcdir}"/${_realname}-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
