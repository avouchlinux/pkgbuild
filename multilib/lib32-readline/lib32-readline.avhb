# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-readline
pkgbase=readline
_basever=8.1
_patchlevel=0
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="GNU readline library (32-bit)"
arch=('x86_64')
groups=('multilib')
repo=('extra')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('lib32-glibc' 'lib32-ncurses' $_pkgbasename)
makedepends=('gcc-multilib')
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
		source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

prepare() {
	cd ${srcdir}/readline-${_basever}
	
	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    	message "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    	patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  	done

	# Remove RPATH from shared objects (FS#14366)
	sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
	cd ${srcdir}/readline-${_basever}

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	./configure --prefix=/usr --libdir=/usr/lib32
	make ${MAKEFLAGS}
}

package() {
	cd ${srcdir}/readline-${_basever}
	make DESTDIR=${pkgdir} install
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}

