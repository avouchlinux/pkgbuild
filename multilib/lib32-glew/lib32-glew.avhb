# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_pkgbasename=glew
pkgname=lib32-$_pkgbasename
pkgver=2.2.0
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library (32 bit)"
arch=('x86_64')
groups=('multilib')
repo=('extra')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('lib32-libxmu' 'lib32-libxi' 'lib32-glu' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
source=(https://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tgz)
sha1sums=('18eca05460d0b61709fc115e632b6fe320718b44')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  sed -i 's|CC = cc|CC = gcc -m32|' config/Makefile.linux
  sed -i 's|LD = cc|LD = gcc -m32|' config/Makefile.linux
  sed -i 's|lib64|lib32|g' config/Makefile.linux

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" \
    PKGDIR="/usr/lib32/pkgconfig" install

  rm -rf "${pkgdir}"/usr/{include,bin}
}