# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-libvpx
_realname=libvpx
pkgver=1.8.2
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=('x86_64')
groups=('multilib')
repo=('extra')
url=http://www.webmproject.org/
license=(BSD)
depends=(
  lib32-gcc-libs
  libvpx
)
makedepends=(
  git
  nasm
)
provides=(libvpx.so)
source=("https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
  cd libvpx-$pkgver

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --target=x86-linux-gcc \
    --disable-install-bins \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising
  make ${MAKEFLAGS}
}

package() {
  cd libvpx-$pkgver

  make DIST_DIR="${pkgdir}/usr" install
  rm -rf "${pkgdir}"/usr/include
}