# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_pkgbasename=qt4
pkgname=lib32-$_pkgbasename
_tarname=qt-everywhere-opensource-src
pkgver=4.8.7
pkgrel=1
pkgdesc='A cross-platform application and UI framework (32-bit)'
arch=('x86_64')
groups=('multilib')
repo=('extra')
url='http://qt-project.org/'
license=('GPL3' 'LGPL')
depends=(lib32-{fontconfig,sqlite3,alsa-lib,glib2,libdbus,openssl}
         lib32-lib{png,tiff,mng,gl,sm,xrandr,xv,xi} $_pkgbasename)
optdepends=('lib32-libxinerama: Xinerama support'
            'lib32-libxcursor: Xcursor support'
            'lib32-libxfixes: Xfixes support')
makedepends=(cups gcc-multilib lib32-{mesa,libcups,libxfixes,gtk2})
options=('staticlibs') # libQtUiTools builds as static only FS#36606
provides=(lib32-qtwebkit)
replaces=(lib32-qtwebkit 'lib32-qt<=4.8.4')
conflicts=(lib32-qtwebkit lib32-qt)
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/4.8/${pkgver}/${_pkgfqn}.tar.gz")
md5sums=('1864987bdbb2f58f8ae8b350dfdbe133')

#export CC='clang'
#export CXX='clang++'
	
prepare() {
	cd "${srcdir}/${_tarname}-${pkgver}"
	
	export QT4DIR=$srcdir/$_pkgfqn
	export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    # (FS#28381) (KDEBUG#180051)
    patch -p1 -i "${srcdir}"/improve-cups-support.patch

    # QTBUG#22829
    patch -p1 -i "${srcdir}"/moc-boost-workaround.patch

    # http://blog.martin-graesslin.com/blog/2014/06/where-are-my-systray-icons/
    patch -p1 -i "${srcdir}"/kubuntu_14_systemtrayicon.diff

    # FS#45106
    patch -p0 -i "${srcdir}"/kde4-settings.patch
        
    # fixes for LibreOffice from the upstream Qt bug tracker FS#46436, FS#41648, FS#39819
    # https://bugreports.qt.io/browse/QTBUG-37380
    patch -p1 -i "${srcdir}"/glib-honor-ExcludeSocketNotifiers-flag.diff

    # https://bugreports.qt.io/browse/QTBUG-34614
    patch -p0 -i "${srcdir}"/l-qclipboard_fix_recursive.patch

    # https://bugreports.qt.io/browse/QTBUG-38585
        patch -p0 -i "${srcdir}"/l-qclipboard_delay.patch

    # React to OpenSSL's OPENSSL_NO_SSL3 define
    patch -p1 -i "${srcdir}"/disable-sslv3.patch

    # enable qtwebkit module with gcc6
    #patch -Np0 -i ../enable-qtwebkit-with-gcc6.patch

    # Fix build with GCC6 (Fedora)
    patch -p1 -i "$srcdir"/qt4-gcc6.patch

    # Fix build of Qt4 applications with glibc 2.25 (Fedora)
    patch -p1 -i "$srcdir"/qt4-glibc-2.25.patch

    # Fix build with ICU 59 (pld-linux)
    patch -p1 -i "$srcdir"/qt4-icu59.patch

    # Fix build with OpenSSL 1.1 (Debian + OpenMandriva)
    patch -p1 -i "$srcdir"/qt4-openssl-1.1.patch

    # enable also support for alsa gt 1.1.4
    patch -Np0 -i ../alsa-support-for-alsa1.1.4.patch
	  
	#fixes detection of libmng 2.0 (from BLFS)
	sed -i -e "/#if/d" -e "/#error/d" -e "/#endif/d" \
	config.tests/unix/libmng/libmng.cpp

	# some of those are likely unnecessary, but I'm too lazy to find and remove them
	sed -i "/^QMAKE_LINK\s/s|g++|g++ -m32|g" mkspecs/common/g++-base.conf
	sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/g++-base.conf
	sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/gcc-base.conf
	sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
	sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS} -m32|g" mkspecs/common/gcc-base.conf
	sed -i "s|-Wl,-O1|-m32 -Wl,-O1|" mkspecs/common/g++-unix.conf
	
	sed -e "s|-O2|$CXXFLAGS -m32|" \
	-e "/^QMAKE_RPATH/s| -Wl,-rpath,||g" \
	-e "/^QMAKE_LINK\s/s|g++|g++ -m32|g" \
	-e "/^QMAKE_LFLAGS\s/s|+=|+= $LDFLAGS|g" \
	-i mkspecs/common/g++.conf

	cp mkspecs/common/linux{,32}.conf
	sed -i "/^QMAKE_LIBDIR\s/s|=|= /usr/lib32|g" mkspecs/common/linux32.conf
	sed -i "s|common/linux.conf|common/linux32.conf|" mkspecs/linux-g++-32/qmake.conf

    #fixes detection of libmng 2.0 (from BLFS)
    sed -i -e "/#if/d" -e "/#error/d" -e "/#endif/d" \
        config.tests/unix/libmng/libmng.cpp
    
    echo "QMAKE_CXXFLAGS += -std=gnu++98" >> src/3rdparty/javascriptcore/JavaScriptCore/JavaScriptCore.pri
    echo "QMAKE_CXXFLAGS += -std=gnu++98" >> src/plugins/accessible/qaccessiblebase.pri
}

build() {
	cd "${srcdir}/${_tarname}-${pkgver}"
		
	export CXXFLAGS="$CXXFLAGS -std=gnu++98 -Wno-deprecated"

	export QT4DIR=$srcdir/$_pkgfqn
	export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}	
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	./configure -confirm-license -opensource -v -platform linux-g++-32 \
	-prefix /usr \
	-libdir /usr/lib32 \
	-plugindir /usr/lib32/qt/plugins \
	-headerdir /usr/include/qt4-32 \
	-importdir /usr/lib32/qt/imports \
	-datadir /usr/share/qt \
	-translationdir /usr/share/qt/translations \
	-sysconfdir /etc \
	-system-sqlite \
	-no-phonon \
	-no-phonon-backend \
	-webkit \
	-graphicssystem raster \
	-openssl-linked \
	-nomake demos \
	-nomake examples \
	-nomake docs \
	-optimized-qmake \
	-no-rpath \
	-dbus-linked \
	-reduce-relocations \
	-no-openvg
    make
}

package() {
	cd "${srcdir}/${_tarname}-${pkgver}"
	make INSTALL_ROOT=$pkgdir install

	# Fix wrong libs path in pkgconfig files
	find "${pkgdir}/usr/lib32/pkgconfig" -type f -name '*.pc' \
	-exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;

	# Fix wrong bins path in pkgconfig files
	find "${pkgdir}/usr/lib32/pkgconfig" -type f -name '*.pc' \
	-exec sed -i 's|/usr/bin/|/usr/lib32/qt4/bin/|g' {} \;

	# Fix wrong path in prl files
	find "${pkgdir}/usr/lib32" -type f -name '*.prl' \
	-exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

	# The TGA plugin is broken (FS#33568)
	rm "${pkgdir}"/usr/lib32/qt4/plugins/imageformats/libqtga.so

	# Remove references to the build directory from the .pc
	find "${pkgdir}" -name "*.pc" -exec perl -pi -e "s, -L$PWD/?\S+,,g" {} \;

	rm -rf "${pkgdir}"/usr/{include,share,bin,tests}
	
	mkdir -p "$pkgdir/usr/share/licenses"
	ln -s qt4 "$pkgdir/usr/share/licenses/$pkgname"
}
