# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-libnftnl
pkgbase=libnftnl
pkgver=1.1.6
pkgrel=1
pkgdesc='Netfilter library providing interface to the nf_tables subsystem'
arch=('x86_64')
groups=('multilib')
repo=('extra')
url='http://netfilter.org/projects/libnftnl/'
license=('GPL2')
depends=('libmnl')
validpgpkeys=('57FF5E9C9AA67A860B557AF7A4111F89BB5F58CC') # Netfilter Core Team
source=("http://netfilter.org/projects/libnftnl/files/libnftnl-$pkgver.tar.bz2"{,.sig})
sha1sums=('2ae3172251574ee36077af760933ec0369960a17'
          'SKIP')

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	./configure --prefix=/usr --libdir=/usr/lib32
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make ${MAKEFLAGS} check
}

package() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="$pkgdir" install
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
