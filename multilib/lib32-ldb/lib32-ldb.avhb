# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_realname=ldb
pkgname=lib32-$_realname
pkgver=2.5.0
pkgrel=1
pkgdesc="A schema-less, ldap like, API and database"
arch=('x86_64')
groups=('multilib')
repo=('extra')
url="https://ldb.samba.org/"
license=('GPL3')
depends=('lib32-talloc' 'lib32-tevent' 'lib32-tdb' 'lib32-popt' 'lib32-libbsd' 'ldb' 'python3')
makedepends=('lib32-cmocka')
source=(https://samba.org/ftp/${_realname}/${_realname}-${pkgver}.tar.gz)
md5sums=('24d9f18b085ba27f96d4dec643abea39')

build() {
  cd ${_realname}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
    --disable-rpath \
    --disable-rpath-install \
    --bundled-libraries=NONE \
    --builtin-libraries=replace \
    --libdir=/usr/lib32 \
    --with-modulesdir=/usr/lib32/ldb/modules \
    --with-privatelibdir=/usr/lib32/ldb \
    --disable-python
  
  make ${MAKEFLAGS}
}

package() {
  cd "${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
