# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-gstreamer0.10
_realname=gstreamer
pkgver=0.10.36
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
groups=('multilib')
repo=('extra')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('gstreamer0.10' 'lib32-glib2' 'lib32-libxml2')
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('9151aa108c177054387885763fa0e433e76780f7c5655c70a5390f2a6c6871da')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"

	patch -Np1 -i ../tests-remove-silly-test_fail_abstract_new-check.patch
	patch -Np1 -i ../bison3.patch
	sed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac

	#NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	# Multilib
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	# Build
	./configure --prefix=/usr --sysconfdir=/etc \
	--localstatedir=/var \
	--libdir=/usr/lib32 \
	--libexecdir=/usr/lib32 \
	--with-package-name="GStreamer (Avouch Linux Multilib)" \
	--with-package-origin="http://www.avouch.org/" \
	--disable-{examples,static,tests}
	make
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	# Install
	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{bin,include,share}
}
