# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-db
pkgbase=db
pkgver=6.2.38
pkgrel=1
pkgdesc='The Berkeley DB embedded database system'
arch=('x86_64')
groups=('multilib')
repo=('extra')
url='http://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=('custom')
depends=("${pkgname#*-}=${pkgver}" 'gcc-libs-multilib')
source=("http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz")
sha256sums=('e0a992d740709892e81f9d93f06daf305cf73fb81b545afe72478043172c3628')

build() {
	cd "${srcdir}/$pkgbase-${pkgver}/build_unix"

	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	../dist/configure --prefix='/usr' \
	--libdir='/usr/lib32' \
	--enable-{compat185,cxx,dbm,shared}
	make LIBSO_LIBS='-lpthread'

}

package() {
	cd "${srcdir}/$pkgbase-${pkgver}/build_unix"
	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{bin,docs,include,share}
}
