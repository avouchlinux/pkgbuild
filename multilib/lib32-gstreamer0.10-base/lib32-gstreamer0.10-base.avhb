# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=('lib32-gstreamer0.10-base')
pkgname=('lib32-gstreamer0.10-base' 'lib32-gstreamer0.10-base-plugins')
_realname=gst-plugins-base
pkgver=0.10.36
pkgrel=1
arch=('x86_64')
groups=('multilib')
repo=('extra')
url="http://gstreamer.freedesktop.org/"
license=('LGPL')
makedepends=('lib32-alsa-lib' 'lib32-cdparanoia' 'lib32-gstreamer0.10>=0.10.35' 'lib32-mesa-libgl' 'lib32-orc' 'lib32-pango' 'lib32-libtheora' 'lib32-libvisual' 'lib32-libvorbis' 'lib32-libxv')
options=('!libtool' '!emptydirs')
source=("http://gstreamer.freedesktop.org/src/gst-plugins-base/gst-plugins-base-${pkgver}.tar.xz")
sha256sums=('1fe45c3894903001d4d008b0713dab089f53726dcb5842d5b40c2595a984e64a')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"

	sed -i -e '/AC_PATH_XTRA/d' -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
	patch -Np1 -i ../fix-crash-0-byte-ogg.patch
	patch -Np1 -i ../colorbalance-fix-abi.patch
	patch -Np1 -i ../ayuv64-lanczos.patch
	patch -Np1 -i ../videoscale-fix-negotiation.patch
	patch -Np1 -i ../gstaudio-symbols.patch
	patch -Np1 -R -i ../revert-decodebin-playbin-removal.patch

	#NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	# Multilib
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	# Build
	./configure --prefix=/usr --sysconfdir=/etc \
	--localstatedir=/var --libdir=/usr/lib32 \
	--disable-{gnome_vfs,static} \
	--enable-experimental \
	--with-package-name="GStreamer (Avouch Linux Multilib)" \
	--with-package-origin="http://www.avouch.org/" \
	--disable-audioresample
	make
	
	sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

package_lib32-gstreamer0.10-base() {
	pkgdesc="GStreamer Multimedia Framework Base plugin libraries"
	depends=("${pkgname#*-}" 'lib32-gstreamer0.10>=0.10.35' 'lib32-orc' 'lib32-libxv')

	cd "${srcdir}/${_realname}-${pkgver}"
	# Install
	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{bin,include,share}

}

package_lib32-gstreamer0.10-base-plugins() {
	pkgdesc="GStreamer Multimedia Framework Base Plugins (gst-plugins-base)"
	depends=('lib32-alsa-lib' 'lib32-cdparanoia' "lib32-gstreamer0.10-base" 'lib32-pango' 'lib32-libtheora' 'lib32-libvisual' 'lib32-libvorbis')

	cd "${srcdir}/${_realname}-${pkgver}"

	# Install
	make -C gst-libs DESTDIR="${pkgdir}" install
	make -C ext DESTDIR="${pkgdir}" install
	make -C gst-libs DESTDIR="${pkgdir}" uninstall
	rm -rf "${pkgdir}"/usr/{bin,include,share}
}


