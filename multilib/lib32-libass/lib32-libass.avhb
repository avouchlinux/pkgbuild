# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_realname=libass
pkgname=lib32-${_realname}
pkgver=0.14.0
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering (32 bit)'
arch=('x86_64')
groups=('multilib')
url='https://github.com/libass/libass/'
license=('BSD')
depends=("${_realname}" 'lib32-fontconfig' 'lib32-fribidi' 'lib32-glib2' 'lib32-glibc'
         'libfreetype.so')
makedepends=('gcc-multilib' 'nasm')
provides=('libass.so')
source=("https://github.com/libass/libass/releases/download/${pkgver}/libass-${pkgver}.tar.xz")
sha256sums=('881f2382af48aead75b7a0e02e65d88c5ebd369fe46bc77d9270a94aa8fd38a2')

build() {
  cd "${_realname}-${pkgver}"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export CC='gcc -m32'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --host=i686-linux-gnu \
    --enable-harfbuzz \
    --enable-fontconfig
  make ${MAKEFLAGS}
}

package() {
  cd "${_realname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  rm -rf "${pkgdir}"/usr/include
}