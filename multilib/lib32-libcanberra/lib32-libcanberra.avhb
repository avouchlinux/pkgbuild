# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=lib32-libcanberra
pkgname=(lib32-libcanberra lib32-libcanberra-pulse)
_realname=libcanberra
pkgver=0.30+2+gc0620e4
pkgrel=1
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification (32-bit)"
arch=('x86_64')
groups=('multilib')
repo=('extra')
license=(LGPL)
depends=(lib32-libvorbis lib32-libltdl lib32-alsa-lib lib32-tdb libcanberra)
makedepends=(gtk-doc lib32-libpulse lib32-gtk2 git gcc-multilib libtool-multilib)
options=(!emptydirs libtool)
url=http://0pointer.de/lennart/projects/libcanberra
source=("git://git.0pointer.de/libcanberra.git#commit=c0620e4")
sha256sums=('SKIP')

_plugindir=usr/lib32/libcanberra-0.30

# getsrc(){
# 	git clone git://git.0pointer.de/libcanberra.git
# }

prepare() {
	cd "${srcdir}/${_realname}"
	
	./autogen.sh
}

build() {
	cd "${srcdir}/${_realname}"

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
	--disable-static --with-builtin=dso --enable-null --disable-oss \
	--enable-alsa --disable-gstreamer --enable-pulse \
	--disable-udev --libdir=/usr/lib32

	# Fight unused direct deps
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

	make ${MAKEFLAGS}
}

package_lib32-libcanberra() {
	optdepends=("libcanberra-pulse: PulseAudio driver")

	cd "${srcdir}/${_realname}"

	make -j1 DESTDIR="$pkgdir" install

	# Split plugins
	for _p in pulse; do
		mkdir ../$_p-plugin
		mv "$pkgdir/$_plugindir"/libcanberra-$_p.* ../$_p-plugin
	done

	rm -r "$pkgdir"/usr/{include,share,bin,lib32/gnome-settings-daemon-3.0}
}

package_lib32-libcanberra-pulse() {
	pkgdesc="PulseAudio plugin for libcanberra (32-bit)"
	depends=("lib32-libcanberra" lib32-libpulse)

	cd ${srcdir}
	install -d "$pkgdir/$_plugindir"
	mv pulse-plugin/* "$pkgdir/$_plugindir"
}
