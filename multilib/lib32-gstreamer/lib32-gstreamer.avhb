# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=gstreamer
pkgname=lib32-gstreamer
pkgver=1.18.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
groups=('multilib')
repo=('extra')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('b9f12137ab663edc6c37429b38ca7911074b9c2a829267fe855d4e57d916a0b6')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    avouch-meson $pkgbase-$pkgver build \
      --libdir='/usr/lib32' \
      --libexecdir='/usr/lib32' \
      -D doc=disabled \
      -D ptp-helper-permissions=capabilities \
      -D dbghelp=disabled \
      -D gobject-cast-checks=disabled \
      -D glib-asserts=disabled \
      -D glib-checks=disabled \
      -D package-name="GStreamer (Avouch)" \
      -D package-origin="https://www.avouch.org/" \
      -D introspection=disabled
  ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	rm -rf "${pkgdir}"/usr/{include,share}

	for _i in "${pkgdir}"/usr/bin/*; do
		mv "${_i}" "${_i}-32"
	done
}