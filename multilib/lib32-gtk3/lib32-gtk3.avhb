# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-gtk3
_realname=gtk
pkgver=3.24.33
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit (32-bit)'
arch=('x86_64')
groups=('multilib')
repo=('extra')
license=(LGPL)
url=https://www.gtk.org/
depends=(
  gtk3
  lib32-at-spi2-atk
  lib32-colord
  lib32-dbus
  lib32-mesa
  lib32-gdk-pixbuf2
  lib32-json-glib
  lib32-libcups
  lib32-libepoxy
  lib32-librsvg
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-pango
  lib32-rest
  lib32-libcanberra
  lib32-wayland
  lib32-libxdamage
)
makedepends=(
  gobject-introspection
  gtk-doc
  git
  glib2-docs
  sassc
)
install='lib32-gtk3.install'
source=(
  git+https://gitlab.gnome.org/GNOME/gtk.git#tag=${pkgver}
  gtk-query-immodules-3.0-32.hook
)
sha256sums=('SKIP'
            '4ac8112ac7e6fa879756e1eeb89b5efa0825ba00e5b05469913b256f86a37608')

prepare() {
  cd gtk-$pkgver

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtk-$pkgver

  export CC='gcc -m32'
  export CXX='/bin/false'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-schemas-compile \
    --enable-{broadway,wayland,x11}-backend \
    --disable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

package() {
  cd gtk-$pkgver

  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}"/usr/bin -type f -not -name gtk-query-immodules-3.0 -delete
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
}