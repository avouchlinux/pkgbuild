# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-colord
_realname=colord
pkgver=1.4.5
pkgrel=1
pkgdesc='System daemon for managing color devices'
arch=('x86_64')
groups=('multilib')
url=https://www.freedesktop.org/software/colord
license=(GPL2)
depends=(
  colord
  lib32-dconf
  lib32-libgudev
  lib32-libgusb
  lib32-lcms2
  lib32-polkit
  lib32-sqlite
  lib32-dbus
)
makedepends=(
  argyllcms
  bash-completion
  git
  gobject-introspection
  intltool
  meson
  python2
  sane
  vala
)
source=(git+https://github.com/hughsie/colord#tag=${pkgver})
sha256sums=(SKIP)

getsrc() {
  git clone https://github.com/hughsie/colord
}

prepare() {
  cd colord
  
  # Allow turning off introspection
  git cherry-pick -n e73723cabe4d5a2e2b281d2f1ea751b2d75bccc6
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  avouch-meson colord build \
    --libdir=/usr/lib32 \
    -D bash-completion=false \
    -D docs=false \
    -D man=false \
    -D tests=false \
    -D libcolordcompat=true \
    -D sane=false \
    -D introspection=false \
    -D vapi=false \
    -D print_profiles=false \
    -D daemon_user=colord
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -r "${pkgdir}"/{var,usr/{bin,include,lib,share}}
}
