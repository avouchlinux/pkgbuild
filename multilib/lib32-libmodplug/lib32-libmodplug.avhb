# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-libmodplug
_realname=libmodplug
pkgver=0.8.9.0
pkgrel=1
pkgdesc='A MOD playing library'
arch=('x86_64')
groups=('multilib')
repo=('extra')
url=http://modplug-xmms.sourceforge.net/
license=(custom)
depends=(
  lib32-gcc-libs
  lib32-glibc
  libmodplug
)
source=(https://downloads.sourceforge.net/project/modplug-xmms/libmodplug/${pkgver}/libmodplug-${pkgver}.tar.gz)
sha256sums=(457ca5a6c179656d66c01505c0d95fafaead4329b9dbaa0f997d00a3508ad9de)

build() {
  cd libmodplug-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32
  make ${MAKEFLAGS}
}

package() {
  cd libmodplug-${pkgver}

  make DESTDIR="${pkgdir}" install
  sed 's|includedir=${prefix}/include|includedir=${prefix}/include/libmodplug/|' -i "${pkgdir}"/usr/lib32/pkgconfig/libmodplug.pc
  rm -rf "${pkgdir}"/usr/include
}
