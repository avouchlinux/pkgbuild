# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

### !!! rebuild groff from core that picks up hardcoding the GS versioned font path !!! ###

pkgname=lib32-ghostscript
_realname=ghostpdl
pkgver=9.54.0
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
groups=('multilib')
repo=('extra')
license=('AGPL' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'jasper' 'zlib' 'libpng' 'libjpeg'
         'libtiff' 'lcms2' 'dbus' 'libpaper')
makedepends=('gtk3' 'gnutls')
optdepends=('texlive-core:      needed for dvipdf'
            'gtk3:              needed for gsx')
url="http://www.ghostscript.com/"
source=(http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2
        ghostscript-sys-zlib.patch)
#options=('!makeflags')
# http://downloads.ghostscript.com/public/SHA1SUMS
sha1sums=('19f01538d33b9623bc6653d96ea4ec891f14a9d3'
          'e054caf753df4d67221b29a2eac66130653f7556')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"

	# force it to use system-libs
	# keep heavily patched included openjpeg, leads to segfault with system openjpeg
	# https://bugs.archlinux.org/task/38226
	rm -rf jpeg libpng zlib jasper expat tiff lcms lcms2 freetype cups/libs # jbig2dec is in community

	# fix build with system zlib
	sed -i 's/ZLIBDIR=src/ZLIBDIR=$includedir/' configure.ac configure
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	./configure --prefix=/usr \
		--enable-dynamic \
		--with-ijs \
		--with-jbig2dec \
		--with-omni \
		--with-x \
		--with-drivers=ALL \
		--with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
		--enable-fontconfig \
		--enable-freetype \
		--enable-openjpeg \
		--without-luratech \
		--without-omni \
		--with-system-libtiff \
		--with-libpaper \
		--disable-compile-inits \
		--libdir=/usr/lib32 \
		--disable-gtk
	make ${MAKEFLAGS}
	make ${MAKEFLAGS} so

	# Build IJS
	cd ijs
	sed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
	./autogen.sh

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	./configure --prefix=/usr --enable-shared \
	--disable-static --libdir=/usr/lib32
    make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make DESTDIR="${pkgdir}" soinstall

	# install IJS
	cd ijs
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{bin,include,share}
}

