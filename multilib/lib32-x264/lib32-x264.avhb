# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-x264
_realname=x264
pkgver=164.rbaee400f
pkgrel=1
pkgdesc='Open Source H264/AVC video encoder (lib32)'
arch=('x86_64')
groups=('multilib')
url='https://www.videolan.org/developers/x264.html'
license=('GPL')
depends=("${_realname}")
makedepends=('git' 'l-smash' 'nasm' 'lib32-gcc-libs')
provides=('lib32-libx264' 'libx264.so')
conflicts=('lib32-libx264' 'lib32-libx264-10bit' 'lib32-libx264-all')
replaces=('lib32-libx264' 'lib32-libx264-10bit' 'lib32-libx264-all')
source=("https://git.videolan.org/git/x264.git")
sha256sums=('SKIP')

getsrc(){
    git clone https://code.videolan.org/videolan/x264.git
}

pkgver() {
  cd x264

  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _rev=$(git rev-parse --short HEAD)

  echo ${_ver}.r${_rev}
}

prepare() {
	if [[ -d build ]]; then
		rm -rf build
	fi
	mkdir build
}

build() {
	cd build
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	../x264/configure \
		--prefix='/usr' \
		--enable-shared \
		--enable-pic \
		--enable-lto \
		--libdir=/usr/lib32 \
		--host=i686-linux-gnu
	make ${MAKEFLAGS}
}

package() {
	make -C build DESTDIR="${pkgdir}" install-cli install-lib-shared
	cd "$pkgdir/usr"
	rm -rf {bin,include}/
}
