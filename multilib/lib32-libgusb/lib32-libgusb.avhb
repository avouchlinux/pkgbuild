# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lib32-libgusb
pkgbase=libgusb
pkgver=0.3.4
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
arch=('x86_64')
groups=('multilib')
repo=('extra')
url='https://github.com/hughsie/libgusb'
license=('LGPL2.1')
depends=('lib32-glib2' 'lib32-libusb' 'libgusb')
makedepends=('git' 'gobject-introspection' 'gtk-doc' 'meson' 'python2' 'vala')
source=("git+https://github.com/hughsie/libgusb#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  avouch-meson libgusb-$pkgver build --libdir='/usr/lib32' -D vapi=false -D tests=false -D docs=false -D introspection=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}