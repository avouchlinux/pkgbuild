# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=polkit
pkgname=lib32-polkit
pkgver=0.119
pkgrel=1
pkgdesc='Application development toolkit for controlling system-wide privileges'
arch=(x86_64)
groups=('multilib')
license=(LGPL)
url=https://www.freedesktop.org/wiki/Software/polkit/
depends=(
  lib32-expat
  lib32-glib2
  lib32-pam
  lib32-systemd
  polkit
)
makedepends=(
  autoconf-archive
  git
  gobject-introspection
  gtk-doc
  intltool
)
_tag=2e5348bf4eb0ef984db32f7f96ec6722d441c6ca
source=(git+https://gitlab.freedesktop.org/polkit/polkit/#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd $pkgbase-$pkgver

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase-$pkgver

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --libexecdir=/usr/lib32 \
    --disable-gtk-doc \
    --disable-static \
    --enable-libsystemd-login=yes \
    --with-os-type='redhat'

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -C src/polkit polkitenumtypes.h
  make -C src/polkit libpolkit-gobject-1.la
  make -C src/polkitagent polkitagentenumtypes.h marshal.stamp
  make -C src/polkitagent libpolkit-agent-1.la
}

package() {
  cd $pkgbase-$pkgver

  make -C src/polkit DESTDIR="${pkgdir}" lib_LTLIBRARIES=libpolkit-gobject-1.la install-libLTLIBRARIES
  make -C src/polkitagent DESTDIR="${pkgdir}" lib_LTLIBRARIES=libpolkit-agent-1.la install-libLTLIBRARIES
  make -C data DESTDIR="${pkgdir}" install-pkgconfigDATA
}