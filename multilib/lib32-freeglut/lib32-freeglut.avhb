# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

_realname=freeglut
pkgname=lib32-${_realname}
pkgver=3.2.1
pkgrel=1
pkgdesc='Provides functionality for small OpenGL programs (32-bit)'
url='http://freeglut.sourceforge.net/'
arch=('x86_64')
groups=('multilib')
repo=('extra')
license=('MIT')
depends=('lib32-libxi' 'lib32-libxrandr' 'lib32-libgl' 'freeglut')
makedepends=('lib32-mesa' 'lib32-glu' 'lib32-libxxf86vm' 'cmake')
source=(https://downloads.sourceforge.net/freeglut/${_realname}-${pkgver}.tar.gz)

prepare() {
   mkdir ${_realname}-${pkgver}/build
	cd $_realname-$pkgver
	patch -p1 -i ../freeglut-3.2.1-gcc10-fno-common.patch
}

build() {
  cd ${_realname}-${pkgver}/build
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_BUILD_TYPE=Release
  make ${MAKEFLAGS}
}

package() {
  cd ${_realname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/include"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
