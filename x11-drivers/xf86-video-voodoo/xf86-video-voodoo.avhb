# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xf86-video-voodoo
pkgver=1.2.5.r10.g6375a58
pkgrel=1
pkgdesc="X.org 3dfx Voodoo1/Voodoo2 2D video driver"
arch=('x86_64')
groups=('x11-drivers')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'xf86dgaproto')
source=(https://gitlab.freedesktop.org/xorg/driver/xf86-video-voodoo.git)

getsrc() {
	git clone https://gitlab.freedesktop.org/xorg/driver/xf86-video-voodoo.git
}

pkgver() {
  cd ${pkgname}
  git describe --long | sed 's/^xf86-video-voodoo-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


prepare() {
  cd ${pkgname}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-z now}

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}