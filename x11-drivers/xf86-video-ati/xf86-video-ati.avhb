# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xf86-video-ati
pkgver=19.1.0.r9.g5eba006e
# pkgver=19.1.0
pkgrel=1
pkgdesc="X.org ati video driver"
arch=('x86_64')
groups=('x11-drivers')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('systemd-libs' 'mesa')
makedepends=('xorg-server-devel' 'systemd' 'git')
groups=('xorg-drivers')
#source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2{,.sig})
#sha512sums=('73a81f6c492daf2e89067fb52b3033dc0fe6841f109627ddca1aee54a45a738c8c134443753a2a2aaa2c131e1d560057ebc76351ff2304c16407df3ff568fcd6'
#            'SKIP')
_commit=5eba006e4129e8015b822f9e1d2f1e613e252cda
source=(https://gitlab.freedesktop.org/xorg/driver/xf86-video-ati.git
        0001_dont_set_sourcevalidate_pointer_to_null.diff)
sha512sums=('SKIP'
            'ca7c769a20e0682ca0122a8248fa79a54d2181d9554c93174a3c49638b6810951787ed5e5e7f7e0f6d091ce2a111aee4bd03108ef74af95ebea340bc0300480f')
validpgpkeys=('B09FAF35BE914521980951145A81AF8E6ADBB200') # Michel Daenzer <michel@daenzer.net>

getsrc() {
	git clone https://gitlab.freedesktop.org/xorg/driver/xf86-video-ati.git
}

pkgver() {
  cd ${pkgname}
  # from ati-git AUR pkg
  git describe --long | sed 's/^xf86-video-ati-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


prepare() {
  cd ${pkgname}

  # fix a crash under Xorg-server 21.1.1
  # https://bugs.archlinux.org/task/72759
  # https://gitlab.freedesktop.org/xorg/driver/xf86-video-ati/-/merge_requests/9
  patch -Np1 -i ../0001_dont_set_sourcevalidate_pointer_to_null.diff

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname}

  # hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-z now}

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}