# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=pango
pkgname=(
  pango
  pango-docs
)
pkgver=1.54.0
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=('x86_64')
groups=('x11-libs')
license=(LGPL-2.1-or-later)
depends=(
  cairo
  fontconfig
  freetype2
  fribidi
  glib2
  glibc
  harfbuzz
  libsysprof-capture
  libthai
  libx11
  libxft
  libxrender
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  help2man
  meson
)
source=("https://gitlab.gnome.org/GNOME/pango/-/archive/$pkgver/pango-$pkgver.tar.bz2")
sha256sums=('SKIP')

prepare(){
  cd $pkgname-$pkgver
}

build() {
  local meson_options=(
    -D documentation=true
    -D sysprof=enabled
  )

  avouch-meson pango-$pkgver build "${meson_options[@]}"
  meson compile -C build

}

# check() {
#   meson test -C build
# }

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
