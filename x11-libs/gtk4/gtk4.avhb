# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=gtk4
pkgname=(
  gtk4
  gtk4-demos
  gtk4-docs
  gtk-update-icon-cache
)
_realname=gtk
pkgver=4.16.2
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=(x86_64)
groups=('x11-libs')
url="https://www.gtk.org/"
depends=(
  adwaita-icon-theme
  at-spi2-core
  cairo
  cantarell-fonts
  dconf
  desktop-file-utils
  fontconfig
  fribidi
  gcc-libs
  gdk-pixbuf2
  glib2
  graphene
  gst-plugins-bad-libs
  harfbuzz
  iso-codes
  libcloudproviders
  libcolord
  libcups
  libegl
  libepoxy
  libgl
  libjpeg-turbo
  libpng
  librsvg
  libtiff
  libx11
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxinerama
  libxkbcommon
  libxrandr
  libxrender
  pango
  shared-mime-info
  tinysparql
  wayland
)
makedepends=(
  docbook-xsl
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libsysprof-capture
  meson
  python-docutils
  python-gobject
  sassc
  shaderc
  vulkan-headers
  wayland-protocols
)
checkdepends=(
  gst-plugin-pipewire
  gst-plugins-base
  mutter
  noto-fonts
  pipewire
  python-pydbus
  vulkan-swrast
  weston
  wireplumber
)
license=(LGPL)
source=(https://download.gnome.org/sources/gtk/${pkgver%.*}/gtk-$pkgver.tar.xz
        settings.ini)

prepare() {
  cd gtk-$pkgver

  # Allow -fcf-protection to work
  # https://gitlab.gnome.org/GNOME/gtk/-/issues/6153
  patch -Np1 -i ../0001-HACK-Don-t-use-objcopy-for-resource-embedding.patch
}


build() {
  
    local meson_options=(
        -D broadway-backend=true
        -D cloudproviders=enabled
        -D colord=enabled
        -D documentation=true
        -D man-pages=true
        -D sysprof=enabled
        -D tracker=enabled
    )

    export CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
    avouch-meson gtk-$pkgver build "${meson_options[@]}"
    meson compile -C build
}

# check() (
#   export XDG_RUNTIME_DIR="$PWD/runtime-dir" WAYLAND_DISPLAY=wayland-5

#   mkdir -p -m 700 "$XDG_RUNTIME_DIR"
#   weston --backend=headless-backend.so --socket=$WAYLAND_DISPLAY --idle-time=0 &
#   _w=$!

#   trap "kill $_w; wait" EXIT

#   meson test -C build --print-errorlogs
# )


_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gtk4() {
  depends+=(
    bash
    glibc
    gst-plugins-base-libs
    gstreamer
    gtk-update-icon-cache
    vulkan-icd-loader
  )
  optdepends=('evince: Default print preview command')
  provides=(libgtk-4.so)

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-4.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  cd "$pkgdir"

  _pick demo usr/bin/gtk4-{demo,demo-application,icon-browser,node-editor,print-editor,widget-factory}
  _pick demo usr/share/applications/org.gtk.{Demo4,IconBrowser4,PrintEditor4,WidgetFactory4,gtk4.NodeEditor}.desktop
  _pick demo usr/share/glib-2.0/schemas/org.gtk.Demo4.gschema.xml
  _pick demo usr/share/icons/hicolor/*/apps/org.gtk.{Demo4,IconBrowser4,PrintEditor4,WidgetFactory4,gtk4.NodeEditor}[-.]*
  _pick demo usr/share/man/man1/gtk4-{demo,demo-application,icon-browser,node-editor,widget-factory}.1
  _pick demo usr/share/metainfo/org.gtk.{Demo4,IconBrowser4,PrintEditor4,WidgetFactory4,gtk4.NodeEditor}.appdata.xml

  _pick docs usr/share/doc

  # Built by GTK 4, shared with GTK 3
  _pick guic usr/bin/gtk4-update-icon-cache
  _pick guic usr/share/man/man1/gtk4-update-icon-cache.1
}

package_gtk4-demos() {
  pkgdesc+=" (demo applications)"
  depends=(
    cairo
    dconf
    gdk-pixbuf2
    glib2
    glibc
    graphene
    gtk4
    harfbuzz
    hicolor-icon-theme
    libepoxy
    librsvg
    pango
  )
  mv demo/* "$pkgdir"
}

package_gtk4-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

package_gtk-update-icon-cache() {
  pkgdesc="GTK icon cache updater"
  depends=(
    bash
    gdk-pixbuf2
    glib2
    glibc
    hicolor-icon-theme
    librsvg
  )

  mv guic/* "$pkgdir"

  ln -sr "$pkgdir"/usr/bin/gtk{4,}-update-icon-cache
  ln -sr "$pkgdir"/usr/share/man/man1/gtk{4,}-update-icon-cache.1

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-update-icon-cache.hook
  install -D gtk-update-icon-cache.script \
    "$pkgdir/usr/share/libalpm/scripts/gtk-update-icon-cache"
}
