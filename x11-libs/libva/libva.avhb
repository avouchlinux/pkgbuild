# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libva
pkgver=2.21.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
groups=('x11-libs')
url='https://01.org/linuxmedia/vaapi'
license=(MIT)
depends=(
  libdrm
  libgl
  libx11
  libxext
  libxfixes
  wayland
)
makedepends=(
  git
  libglvnd
  mesa
  meson
)
optdepends=(
  'intel-media-driver: backend for Intel GPUs (>= Broadwell)'
  'libva-intel-driver: backend for Intel GPUs (<= Haswell)'
  'libva-mesa-driver: backend for AMD and NVIDIA GPUs'
)
provides=(
  libva-drm.so
  libva-glx.so
  libva-wayland.so
  libva-x11.so
  libva.so
)
backup=(etc/libva.conf)
_tag=0b01aed44ef1a6ad660261284ff266fa812829ef
source=(git+https://github.com/intel/libva.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd libva
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
    export CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
    avouch-meson $pkgname build
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm 644 /dev/stdin "${pkgdir}"/etc/libva.conf <<END
LIBVA_MESSAGING_LEVEL=1
END
}
