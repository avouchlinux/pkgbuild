# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libva
pkgver=2.13.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
groups=('x11-libs')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('libdrm' 'libgl' 'libx11' 'libxext' 'libxfixes' 'wayland')
makedepends=('git' 'libglvnd' 'mesa' 'meson')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
provides=('libva-drm.so' 'libva-glx.so' 'libva-wayland.so' 'libva-x11.so' 'libva.so')
source=("https://github.com/intel/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
    CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
    avouch-meson $pkgname-$pkgver build
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm 644 /dev/stdin "${pkgdir}"/etc/libva.conf <<END
LIBVA_MESSAGING_LEVEL=1
END
}
