# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=upx
pkgver=3.96
pkgrel=1
pkgdesc='Ultimate executable compressor.'
arch=('x86_64')
groups=('app-archive')
url='https://github.com/upx/upx'
license=(custom GPL2)
depends=(ucl)
makedepends=(git perl)
source=("https://github.com/upx/upx/releases/download/v$pkgver/$pkgname-$pkgver-src.tar.xz")

prepare() {
  cd $pkgname-$pkgver-src
  patch -Np1 -i ../upx-3.96_CVE-2021-20285.patch
}

build() {
  make -C $pkgname-$pkgver-src \
    CHECK_WHITESPACE=/bin/true \
    UPX_LZMA_VERSION=0x465 \
    UPX_LZMADIR="$srcdir" \
    CXXFLAGS_WERROR="" \
    all
}

package() {
  cd $pkgname-$pkgver-src
  install -Dm755 src/upx.out "$pkgdir/usr/bin/upx"
  install -Dm644 doc/upx.1 "$pkgdir/usr/share/man/man1/upx.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
