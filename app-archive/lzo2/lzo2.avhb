# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lzo2
_realname=lzo
pkgver=2.10
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('x86_64')
groups=('app-archive')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz)

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
	./configure --prefix=/usr --enable-shared
	make ${MAKEFLAGS}

	# build minilzo
	gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
	gcc -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

check() {
	cd "${srcdir}/${_realname}-${pkgver}"
	#make test # Larger test
	make check
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make DESTDIR=${pkgdir} install

	# install minilzo
	install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
	install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
	cd ${pkgdir}/usr/lib
	ln -s libminilzo.so.0 libminilzo.so
}

