# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=unzip
pkgver=6.0
pkgrel=1
pkgdesc="Unpacks .zip archives such as those made by PKZIP"
arch=('x86_64')
groups=('app-archive')
url="http://www.info-zip.org/"
license=('custom')
depends=('bzip2' 'bash')
source=('http://downloads.sourceforge.net/infozip/unzip60.tar.gz')

prepare() {
	cd ${srcdir}/${pkgname}60

	# set CFLAGS -- from Debian
	export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64 -DACORN_FTYPE_NFS \
	-DWILD_STOP_AT_DIR -DLARGE_FILE_SUPPORT -DUNICODE_SUPPORT \
	-DUNICODE_WCHAR -DUTF8_MAYBE_NATIVE -DNO_LCHMOD -DDATE_FORMAT=DF_YMD \
	-DUSE_BZIP2 -DNATIVE"

	sed -i "/MANDIR =/s#)/#)/share/#" unix/Makefile
}

build() {
	cd ${srcdir}/${pkgname}${pkgver/./}
	# make -- from Debian
	make -f unix/Makefile LOCAL_UNZIP="$CFLAGS" prefix=/usr LF2="" \
	D_USE_BZ2=-DUSE_BZIP2 L_BZ2=-lbz2 unzips
}

package() {
	cd ${srcdir}/${pkgname}${pkgver/./}
	# install -- from Debian
	make -f unix/Makefile prefix=${pkgdir}/usr INSTALL_PROGRAM="install" install
	# license
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

