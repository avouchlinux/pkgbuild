# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lz4
pkgver=1.9.2
pkgrel=1
pkgdesc='Extremely fast compression algorithm'
arch=('x86_64')
groups=('app-archive')
url='http://www.lz4.org/'
license=('GPL2')
makedepends=('git')
checkdepends=('diffutils')
depends=('glibc')
source=("git+https://github.com/lz4/lz4.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
	# do not use the main makefile, it calls sub make with -e
	# exported CLFAGS by makepkg break the version.
	cd "$pkgname-$pkgver"
	make -C lib PREFIX=/usr
	make -C programs PREFIX=/usr lz4 lz4c
}

check() {
	cd "$pkgname-$pkgver"
	rm -f passwd.lz4
	programs/lz4 /etc/passwd passwd.lz4
	programs/lz4 -d passwd.lz4 passwd
	diff -q /etc/passwd passwd
	rm passwd
}

package() {
	cd "$pkgname-$pkgver"
	make install PREFIX=/usr DESTDIR="$pkgdir"
}
