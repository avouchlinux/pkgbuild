# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=unrar
pkgname=(unrar libunrar)
pkgver=5.9.2
pkgrel=1
epoch=1
url="https://www.rarlab.com/rar_add.htm"
arch=('x86_64')
groups=('app-archive')
license=(custom)
source=("https://ftp.osuosl.org/pub/blfs/conglomeration/$pkgbase/unrarsrc-$pkgver.tar.gz")

prepare() {
  sed -e '/CXXFLAGS=/d' -e '/LDFLAGS=/d' -i unrar/makefile # Use system build flags
}

build() {
  cp -a unrar libunrar
  export LDFLAGS+=' -pthread'
  make -C libunrar lib
  make -C unrar -j1
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=(gcc-libs)
  cd unrar
  install -Dm755 unrar -t "$pkgdir"/usr/bin/
  # install license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=(gcc-libs)
  cd libunrar
  install -Dm755 libunrar.so -t "$pkgdir"/usr/lib/
  install -Dm644 dll.hpp -t "$pkgdir"/usr/include/unrar/
  # install license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
