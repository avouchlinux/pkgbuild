# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=brotli
pkgname=('brotli' 'python3-brotli' 'brotli-testdata')
pkgver=1.0.9
pkgrel=1
pkgdesc='Brotli compression library'
arch=('x86_64')
groups=('app-archive')
license=('MIT')
url='https://github.com/google/brotli'
depends=('gcc-libs')
makedepends=('cmake' 'python3-setuptools')
source=("https://github.com/google/$pkgbase/archive/v$pkgver.tar.gz")

prepare() {
  mkdir -p build
}

build() {
  cd "$srcdir"/brotli-$pkgver
  python setup.py build

  cd "$srcdir"/build
  cmake ../brotli-$pkgver -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_INSTALL_LIBDIR="/usr/lib"
  make ${MAKEFLAGS}
}

check() {
  cd brotli-$pkgver
  # make test
  # python setup.py test
}

package_brotli() {
  cd build
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python3-brotli() {
  depends=('python3')

  cd brotli-$pkgver

  python setup.py install --skip-build -O1 --root="$pkgdir"
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_brotli-testdata() {
  depends=()

  cd brotli-$pkgver
  install -dm755 "$pkgdir"/usr/share/brotli
  cp -a tests/testdata "$pkgdir"/usr/share/brotli/
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
