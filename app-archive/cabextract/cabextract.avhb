# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=cabextract
pkgver=1.11
pkgrel=1
pkgdesc="A program to extract Microsoft cabinet (.CAB) files"
arch=('x86_64')
groups=('app-archive')
license=('GPL')
url=https://www.cabextract.org.uk/
depends=(glibc)
makedepends=(git)
_tag=305907723a4e7ab2018e58040059ffb5e77db837
source=(git+https://github.com/kyz/libmspack.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd libmspack/cabextract
  ./autogen.sh
}

pkgver() {
  cd libmspack
  git describe --tags | sed 's/^v//'
}

build() {
  cd libmspack/cabextract
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libmspack/cabextract install
}

