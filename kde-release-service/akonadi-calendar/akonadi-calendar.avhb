# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=akonadi-calendar
pkgver=24.12.3
pkgrel=1
pkgdesc="Akonadi calendar integration"
arch=('x86_64')
groups=('kde-release-services')
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         gpgme
         kcalendarcore
         kcalutils
         kcodecs
         kconfig
         kcontacts
         kcoreaddons
         kdbusaddons
         ki18n
         kidentitymanagement
         kio
         kitemmodels
         kmailtransport
         kmime
         knotifications
         kservice
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkleo
         messagelib
         qgpgme-qt6
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c6ad351c820e40ac69a7c5f67fdf2a7d541f7669d3d57874645f6f85f74ebf9e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	    -DCMAKE_INSTALL_PREFIX=/usr \
		    -DLIB_INSTALL_DIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
