# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=k3b
pkgver=22.04.0
pkgrel=1
epoch=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('x86_64')
groups=('kde-apps')
url="https://userbase.kde.org/K3b"
license=('GPL')
depends=('libkcddb' 'kcmutils' 'knotifyconfig' 'libmad' 'kfilemetadata' 'knewstuff'
         'libmpcdec' 'libdvdread' 'libburn' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a8cfccdf6c0bfb8b8ac1e07da56fb284ad23bfc9d18724ef917ea9adf9cb62fb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DK3B_ENABLE_PERMISSION_HELPER=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
