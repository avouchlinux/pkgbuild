# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=akregator
pkgver=24.08.2
pkgrel=1
pkgdesc="A Feed Reader by KDE"
arch=('x86_64')
groups=('kde-release-service')
url='https://apps.kde.org/akregator/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         grantleetheme
         kcmutils
         kcodecs
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         knotifications
         knotifyconfig
         kontactinterface
         kparts
         kstatusnotifieritem
         ktextaddons
         ktexttemplate
         ktextwidgets
         kuserfeedback
         kwidgetsaddons
         kxmlgui
         libkdepim
         messagelib
         pimcommon
         qt6-base
         qt6-webengine
         syndication)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('kdepim-addons: additional plugins')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('59f11f7b336e8f9a4131ccfae222e9f1bf492ef7bcc8234a9ae0d2c02c711140'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	    -DCMAKE_INSTALL_PREFIX=/usr \
		    -DLIB_INSTALL_DIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
