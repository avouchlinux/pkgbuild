# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kmail-account-wizard
pkgver=22.04.0
pkgrel=1
pkgdesc="KMail account wizard"
arch=('x86_64')
groups=('kde-apps')
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kidentitymanagement libkleo pimcommon kmailtransport kross ktexteditor knotifyconfig)
makedepends=(extra-cmake-modules boost python kdoctools qt5-tools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('fb790134522ec3f9cef39a3f296ea24d1d7c933c443a2463682a5df8fdc1eed0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make ${MAKEFLAGS}
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
