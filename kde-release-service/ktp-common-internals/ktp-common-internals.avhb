# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=ktp-common-internals
pkgver=23.08.1
pkgrel=1
pkgdesc='Common components for KDE-Telepathy'
arch=('x86_64')
groups=('kde-release-service')
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
depends=(knotifyconfig ktexteditor kpeople telepathy-logger-qt kaccounts-integration libotr hicolor-icon-theme telepathy-accounts-signon)
makedepends=(extra-cmake-modules kdoctools doxygen python)
source=("https://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('6292696ed50a8b721f1b7ec6852edb5f4f029d29e7f2a1e0144f2560826db907'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}