# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=marble
pkgname=(marble marble-data)
pkgver=22.04.0
pkgrel=1
pkgdesc="Desktop Globe"
arch=('x86_64')
groups=('kde-apps')
url="https://www.kde.org/applications/system/marble"
license=(GPL)
makedepends=(extra-cmake-modules gpsd quazip libwlocate phonon-qt5 qt5-webkit krunner
             kparts knewstuff qt5-tools kdoctools qt5-serialport plasma-workspace)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

  # only build the KDE app
  sed -e '/mobile/d' \
      -e '/qt/d'  \
      -e '/Quick/d' \
      -e '/touch/d' \
      -i $pkgname-$pkgver/src/apps/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make ${MAKEFLAGS}
}

package_marble() {
  depends=(marble-data qt5-webkit kparts knewstuff quazip)
  optdepends=('gpsd: GPS based geolocation'
            'qt5-serialport: APRS plugin'
            'libwlocate: WLAN based geolocation'
            'krunner: Krunner plugin')

  cd build
  make DESTDIR="$pkgdir" install

# provided by marble-data
  rm -r "$pkgdir"/usr/share/{icons,marble/data,mime}
}

package_marble-data() {
  pkgdesc='Data for Marble'
  depends=(hicolor-icon-theme)

  cd build/data
  make DESTDIR="$pkgdir" install
}
