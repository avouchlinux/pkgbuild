# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kdenlive
pkgver=24.12.3
pkgrel=1
pkgdesc="A non-linear video editor for Linux using the MLT video framework"
arch=('x86_64')
groups=('kde-release-services')
url='https://apps.kde.org/kdenlive/'
license=(GPL-2.0-or-later)
depends=(frei0r-plugins
         gcc-libs
         glibc
         karchive
         kbookmarks
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kfilemetadata
         kguiaddons
         ki18n
         kiconthemes
         kio
         kitemviews
         knewstuff
         knotifications
         knotifyconfig
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         mlt
         purpose
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-networkauth
         qt6-svg
         solid)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools
             qt6-tools)
optdepends=('bigsh0t: VR360 effects'
            'dvgrab: for firewire capture'
            'ffmpeg: for FFmpeg plugin'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'mediainfo: for technical clip information'
            'noise-suppression-for-voice: noise suppression effect'
            'opencv: for motion tracking'
            'opentimelineio: timeline export/import'
            'plasma-desktop: theme configuration'
            'python-openai-whisper: speech to text (Whisper engine)'
            'python-srt_equalizer: speech to text (Whisper engine)'
            'python-vosk: speech to text (VOSK engine)'
            'qt6-imageformats: support for tiff, webp, and more image formats'
            'recordmydesktop: for screen capture')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1556689ee92e769735b591bbf67b418671810feeed09ea565e9c8a00bdbf8fb7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	    -DCMAKE_INSTALL_PREFIX=/usr \
		    -DLIB_INSTALL_DIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
