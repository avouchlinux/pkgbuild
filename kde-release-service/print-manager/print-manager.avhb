# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=print-manager
pkgver=22.04.0
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('x86_64')
groups=('kde-apps')
url='https://www.kde.org/applications/utilities/'
license=('GPL' 'LGPL' 'FDL')
depends=('libcups' 'kcmutils')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'plasma-framework')
optdepends=('system-config-printer: auto-detect the printer driver')
source=("http://download.kde.org/stable/applications/${pkgver}/src/print-manager-${pkgver}.tar.xz"{,.sig})
sha1sums=('71307c7f75f311db246668a9ee404e0321a9db53'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make ${MAKEFLAGS}
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
