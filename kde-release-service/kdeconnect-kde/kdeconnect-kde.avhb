# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kdeconnect-kde
pkgver=24.08.2
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
url='https://kdeconnect.kde.org/'
arch=(x86_64)
license=(GPL)
groups=('kde-release-service')
depends=(
  kcmutils 
  kwayland 
  libfakekey 
  qca-qt6 
  kpeoplevcard 
  pulseaudio-qt 
  qqc2-desktop-style 
  hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=(
  'sshfs: remote filesystem browser' 
  'python-nautilus: Nautilus integration' 
  'qt6-tools: for some runcommand plugin actions')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kdeconnect-openssh-8.8.patch)
sha256sums=('4c70baecf581b1d4aa0569ebe2161b95f1973d6421b321b8ae87ace461b481d7'
            'SKIP'
            'e1b43f8b3e0b2a521277d70d4fdeb8dff714f6855f33ae1c3c7d8f03fe69dbb9')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < kdeconnect-openssh-8.8.patch # Fix file browsing with openssh 8.8
}

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	-DCMAKE_INSTALL_PREFIX=/usr \
		-DLIB_INSTALL_DIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
