# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kget
pkgver=22.04.0
pkgrel=1
pkgdesc='Download Manager'
url='https://kde.org/applications/internet/kget/'
arch=('x86_64')
groups=('kde-apps')
license=(GPL LGPL FDL)
depends=(kcmutils knotifyconfig qca-qt5 qgpgme kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules boost libktorrent kdoctools kdesignerplugin)
optdepends=('libktorrent: torrent support')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5ceb75ec5b760f932b345edde5ed467b8ece3c72d95dcec7fcdbeec59c48dfd8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kget-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make ${MAKEFLAGS}
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
