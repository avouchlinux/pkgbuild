# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=akonadi-mime
pkgver=24.08.2
pkgrel=1
pkgdesc="Libraries and daemons to implement basic email handling"
arch=('x86_64')
groups=('kde-apps')
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         ki18n
         kio
         kmime
         kwidgetsaddons
         kxmlgui
         libakonadi
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools5
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('fbd2e09385a60bfa409fd84f00347fac8bf8b99c690207706bad05d5154f21dd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	    -DCMAKE_INSTALL_PREFIX=/usr \
		    -DLIB_INSTALL_DIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
