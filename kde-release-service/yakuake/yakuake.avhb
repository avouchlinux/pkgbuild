# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=yakuake
pkgver=24.12.3
pkgrel=1
pkgdesc="A drop-down terminal emulator based on KDE konsole technology"
arch=('x86_64')
groups=('kde-release-services')
url='https://apps.kde.org/yakuake/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kglobalaccel
         ki18n
         kiconthemes
         kio
         knewstuff
         knotifications
         knotifyconfig
         konsole
         kparts
         kstatusnotifieritem
         kwayland
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libx11
         qt6-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('fb08a3db36484b15c3d2efb276946b9abf45a97d57eb676a1817232d4641f1a7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	  -DLIB_INSTALL_DIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

