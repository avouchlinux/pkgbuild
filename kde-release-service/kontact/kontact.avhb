# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kontact
pkgver=24.12.3
pkgrel=1
pkgdesc='Personal Information Manager'
arch=('x86_64')
groups=('kde-release-services')
url='https://apps.kde.org/kontact/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         grantleetheme
         kcmutils
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         kjobwidgets
         kontactinterface
         kmail-account-wizard
         kparts
         kservice
         kwidgetsaddons
         kxmlgui
         libkdepim
         pimcommon
         qt6-base
         qt6-webengine)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=(akregator
            kaddressbook
            kjots
            kmail
            knotes
            korganizer
            zanshin)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3abc3f9dcd526cf9fdea859ce5d7dcbc8aeb12d32f33040fe483553bc6393650'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	  -DLIB_INSTALL_DIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
