# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kwave
pkgver=24.12.3
pkgrel=1
pkgdesc="A sound editor for KDE"
arch=('x86_64')
groups=('kde-release-services')
url='https://apps.kde.org/kwave/'
license=(GPL-2.0-or-later)
depends=(alsa-lib
         audiofile
         flac
         fftw
         gcc-libs
         glibc
         id3lib
         karchive5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdbusaddons5
         ki18n5
         kiconthemes5
         kio5
         ktextwidgets5
         kwidgetsaddons5
         kxmlgui5
         libmad
         libogg
         libpulse
         libsamplerate
         libvorbis
         qt5-base
         qt5-multimedia)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools5
             librsvg
             opus)
optdepends=('opus: OGG plugin')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0104065ca5e6f00420b7bdfb0ebd92f528907c2e96c2907ad069040328a3dcfd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
# https://gitlab.archlinux.org/archlinux/packaging/packages/kwave/-/issues/1
  export CFLAGS=${CFLAGS/-fno-omit-frame-pointer/}
  export CXXFLAGS=${CXXFLAGS/-fno-omit-frame-pointer/}
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	  -DLIB_INSTALL_DIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
