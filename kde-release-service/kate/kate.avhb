# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=kate
pkgname=(kwrite kate)
pkgver=24.08.1
pkgrel=1
arch=('x86_64')
groups=('kde-apps')
license=(GPL LGPL FDL)
makedepends=(extra-cmake-modules kdoctools python plasma-framework knewstuff ktexteditor threadweaver kitemmodels kactivities)
source=("https://download.kde.org/stable/applications/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz"{,.sig})
sha256sums=('f25a39c7e272713940e82d53c1e8b266cf1d7acad3eeb7dd229f166fc8e0667d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=(kde-applications kdebase)
  url="https://www.kde.org/applications/utilities/kwrite/"
  depends=(ktexteditor kactivities hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kate*' -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/qt/plugins/ktexteditor \
        "$pkgdir"/usr/share/doc/HTML/*/{kate,katepart} \
        "$pkgdir"/usr/share/katexmltools \
        "$pkgdir"/usr/share/locale/*/LC_MESSAGES/tabswitcherplugin.mo \
        "$pkgdir"/usr/share/plasma/plasmoids
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=(kde-applications kdebase)
  url="https://www.kde.org/applications/utilities/kate/"
  depends=(knewstuff ktexteditor threadweaver kitemmodels kactivities hicolor-icon-theme)
  optdepends=('konsole: open a terminal in Kate')

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kwrite*' -exec rm {} \;
  rm -r "$pkgdir"/usr/share/doc/HTML/*/kwrite
}
