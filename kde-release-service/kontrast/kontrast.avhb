# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kontrast
pkgver=24.08.2
pkgrel=1
pkgdesc='Tool to check contrast for colors that allows verifying that your colors are correctly accessible'
arch=(x86_64)
groups=('kde-release-service')
url='https://apps.kde.org/kontrast/'
license=(GPL-2.0-or-later)
depends=(futuresql
         gcc-libs
         glibc
         kcoreaddons
         ki18n
         kirigami
         kirigami-addons
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules
             kdoctools
             python
             qcoro)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ed60b1e229fa4a2b0511c4e8a27a19479b69274f69b18f77820922cded0b5104'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver -G Ninja \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	  -DLIB_INSTALL_DIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}


