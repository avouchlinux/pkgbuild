# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kdevelop
pkgver=23.08.1
_clangver=13.0.0
pkgrel=1
pkgdesc='C++ IDE using KDE technologies'
arch=('x86_64')
groups=('kde-apps')
url='http://www.kdevelop.org/'
license=(GPL)
depends=(clang=$_clangver libksysguard kitemmodels knotifyconfig ktexteditor grantlee libkomparediff2 qt5-webengine
         kcmutils threadweaver knewstuff)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt llvm qt5-tools plasma-framework krunner okteta boost purpose subversion)
optdepends=('konsole: embedded terminal'
            'git: Git support' 'subversion: SVN support'
            'gdb: GNU debugger support' 'lldb: LLDB debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration'
            'qt5-tools: qthelp plugin'
            'okteta: GNU debugger support'
            'plasma-framework: for the plasma addons'
            'purpose: patch review plugin'
            'cppcheck: code analyzer' 'heaptrack: memory profiler'
            'kdevelop-clang-tidy: clang-tidy analyzer plugin' 'clazy: clazy analyzer plugin')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('cedd1c7ba8e352a76ec021218cf2e35a6c060875ef3d27964246c48d0f2ae81c'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D'  # Kevin Funk <kfunk@kde.org>
              'E191FD5BE6F46870F09E82B2024E7FB43D015474') # Friedrich W. H. Kossebau <kossebau@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
