# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=pinentry
pkgver=1.2.0
pkgrel=1
pkgdesc='Collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol'
url='https://gnupg.org/related_software/pinentry/'
arch=('x86_64')
groups=('app-crypt')
license=('GPL')
depends=(
  'glibc' 'ncurses' 'libcap' 'libassuan' 'libsecret' 'glib2'
  libsecret-1.so libcap.so libncursesw.so libassuan.so libglib-2.0.so)
makedepends=('gtk2' 'qt5-base' 'gcr')
optdepends=(
  'gtk2: gtk2 backend'
  'qt5-base: qt backend'
  'gcr: gnome3 backend'
)
backup=('etc/pinentry/preexec')
source=("https://www.gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        'pinentry'
        'preexec')
sha256sums=('10072045a3e043d0581f91cd5676fcac7ffee957a16636adedaa4f583a616470'
            'SKIP'
            'af7c27139b20a7f1b9a227f3663abb21bb2b954523352b7844da27db1b0c1c86'
            'f428f08d77f1f7830eadb8435af282a833fc13d8b623b122eefd9d3b628bef90')
validpgpkeys=(
  'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6' # Werner Koch (dist sig)
  '6DAA6E64A76D2840571B4902528897B826403ADA' # Werner Koch (dist signing 2020)
)

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../pinentry-1.0.0-make-icon-work-under-Plasma-Wayland.patch
  patch -Np1 -i ../pinentry-0.8.2-ncurses.patch
  patch -Np1 -i ../pinentry-1.0.0-AR.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --enable-pinentry-tty \
    --enable-pinentry-curses \
    --enable-fallback-curses \
    --enable-pinentry-emacs \
    --enable-pinentry-gtk2 \
    --enable-pinentry-gnome3 \
    --enable-pinentry-qt \
    --enable-libsecret

  # fix build
  sed -i "s|-I/usr/include/KF5|-I/usr/include/KF5/KWayland|g" qt/Makefile
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm 755 ../pinentry -t "${pkgdir}/usr/bin"
  install -Dm 755 ../preexec -t "${pkgdir}/etc/pinentry"
}