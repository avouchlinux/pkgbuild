# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xxhash
_realname=xxHash
pkgver=0.8.2
pkgrel=1
pkgdesc='Extremely fast non-cryptographic hash algorithm'
arch=('x86_64')
groups=('app-crypt')
url=https://cyan4973.github.io/xxHash/
license=(
  GPL2
  BSD
)
depends=(glibc)
makedepends=(git)
provides=(libxxhash.so)
_tag=bbb27a5efb85b92a0486cf361a8635715a53f6ba
source=(git+https://github.com/Cyan4973/xxHash.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd xxHash
  git describe --tags | sed 's/^v//'
}

build() {
  make PREFIX=/usr DISPATCH=1 -C xxHash
}

package() {
  make PREFIX=/usr DISPATCH=1 DESTDIR="${pkgdir}" -C xxHash install
  install -Dm 644 xxHash/LICENSE -t "${pkgdir}"/usr/share/licenses/xxhash
}

