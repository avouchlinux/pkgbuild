# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=shared-color-targets
pkgver=0.1.7
pkgrel=1
pkgdesc="Shared color targets for creating color profiles"
arch=('any')
groups=('media-grafix')
url="https://github.com/hughsie/shared-color-targets"
license=(GPL CCPL:by-sa)
makedepends=(git)
_commit=b6ea84551a9d51f3b07de9a2863b113ec8dc7303  # tags/SHARED_COLOR_TARGETS_0_1_7^0
source=("git+https://github.com/hughsie/shared-color-targets#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^SHARED_COLOR_TARGETS_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

