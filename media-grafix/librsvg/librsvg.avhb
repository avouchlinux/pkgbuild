# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=librsvg
pkgname=(librsvg librsvg-docs)
pkgver=2.54.0
pkgrel=1
epoch=2
pkgdesc="SVG rendering library"
arch=('x86_64')
groups=('media-grafix')
url="https://wiki.gnome.org/Projects/LibRsvg"
license=(LGPL)
depends=(gdk-pixbuf2 pango libcroco)
makedepends=(gobject-introspection vala gtk-doc git rust)
source=(#"https://gitlab.gnome.org/GNOME/librsvg/-/archive/$pkgver/librsvg-$pkgver.tar.bz2"
        https://ftp.gnome.org/pub/GNOME/sources/librsvg/${pkgver%??}/librsvg-$pkgver.tar.xz)

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  ./configure --prefix=/usr --disable-static --enable-vala
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make ${MAKEFLAGS}
}

check() {
  # Test suite is very dependent on the versions of
  # Cairo, Pango, FreeType and HarfBuzz
  make -C $pkgname-$pkgver check || :
}

package_librsvg() {
  provides=(librsvg-${pkgver%%.*}.so)

  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_librsvg-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}