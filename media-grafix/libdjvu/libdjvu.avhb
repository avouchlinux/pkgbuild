# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libdjvu
_realname=djvulibre
pkgver=3.5.27
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
url='http://djvu.sourceforge.net/'
arch=('x86_64')
groups=('media-grafix')
license=('GPL2')
makedepends=('librsvg')
depends=('libtiff' 'hicolor-icon-theme')
source=("https://downloads.sourceforge.net/project/djvu/DjVuLibre/${pkgver}/djvulibre-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
	./autogen.sh
	./configure --prefix=/usr --disable-desktopfiles
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	for sz in 22 32 48 64; do
		install -Dm644 desktopfiles/prebuilt-hi${sz}-djvu.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
	done
}
