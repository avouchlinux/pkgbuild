# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=babl
pkgver=0.1.108
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('x86_64')
groups=('media-grafix')
url='https://gegl.org/babl/'
license=('LGPL-3.0-or-later')
depends=('glibc' 'lcms2')
makedepends=(
    'git'
    'meson'
    'gobject-introspection'
    'vala'
    'gi-docgen'
)
source=("git+https://gitlab.gnome.org/GNOME/babl.git#tag=BABL_${pkgver//./_}")
sha256sums=('82e14ab79ee5b13a10aadd7a3e66517ce802f6bfeb132e75ffb008121090c9f1')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^BABL_//;s/_$//;s/_/./g;s/-/+/g'
}

build() {
  avouch-meson "${pkgname}" build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

