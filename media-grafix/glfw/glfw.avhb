# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=glfw
pkgname=('glfw-x11' 'glfw-wayland' 'glfw-doc')
pkgver=3.3.2
pkgrel=1
arch=('x86_64')
groups=('media-grafix')
url="http://www.glfw.org/"
license=('custom:ZLIB')
makedepends=('mesa' 'cmake' 'doxygen' 'vulkan-headers' 'vulkan-icd-loader'
             'extra-cmake-modules' 'wayland-protocols' 'libxi' 'libxrandr'
             'libxcursor' 'libxkbcommon' 'libxinerama')
source=("https://github.com/glfw/glfw/archive/${pkgver}.tar.gz")

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  mkdir build-x11 build-wayland
}

build() {
  cd "$srcdir/$pkgbase-$pkgver/build-x11"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON
  make ${MAKEFLAGS}
  make docs

  cd ../build-wayland

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON \
      -DGLFW_USE_WAYLAND=ON
  make ${MAKEFLAGS}
}

package_glfw-x11() {
  pkgdesc="A free, open source, portable framework for graphical application development (x11)"
  depends=('libxi' 'libxrandr' 'libxinerama' 'libxcursor' 'libgl')
  replaces=('glfw')
  conflicts=('glfw')
  provides=('glfw')

  cd "$srcdir/$pkgbase-$pkgver"/build-x11

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_glfw-wayland() {
  pkgdesc="A free, open source, portable framework for graphical application development (wayland)"
  depends=('wayland' 'libxkbcommon' 'libgl')
  conflicts=('glfw')
  provides=('glfw')

  cd "$srcdir/$pkgbase-$pkgver"/build-wayland

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_glfw-doc() {
  pkgdesc="Set of HTML documentation for GLFW"

  mkdir -p "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/build-x11/docs/html" "${pkgdir}/usr/share/doc/glfw"
}
