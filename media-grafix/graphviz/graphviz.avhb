# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=graphviz
pkgver=3.0.0
pkgrel=1
pkgdesc='Graph visualization software'
arch=('x86_64')
groups=('media-grafix')
url='https://www.graphviz.org/'
license=('EPL')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts' 'gsfonts' 'libnsl' 'libpng')
makedepends=('swig' 'mono' 'guile' 'lua' 'ocaml' 'perl' 'python3' 'r' 'tk' 'qt5-base' 'gtk2')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'python: python bindings'
            'r: r bindings'
            'tcl: tcl bindings'
            'qt5-base: gvedit'
            'gtk2: gtk output plugin'
            'xterm: vimdot')
source=("https://gitlab.com/graphviz/graphviz/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        'ghostscript918.patch')
install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../ghostscript918.patch
	./autogen.sh NOCONFIG
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBPOSTFIX=/
    export CXXFLAGS+=' -fPIC -fpermissive'

	./configure --prefix=/usr \
           --enable-python3=yes \
		   --disable-python \
		   --enable-lefty \
		   --enable-php=no
		   # requires php7 our default is php8
        sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# fix symlink to symlink that doesn't get picked up by makepkg's zipman
	ln -s gv2gxl.1.gz "${pkgdir}"/usr/share/man/man1/dot2gxl.1.gz
	rm "${pkgdir}"/usr/share/man/man1/dot2gxl.1

	# Deduplicates TCL libraries
	cd "${pkgdir}/usr/lib/tcl8.6"
	rm -fr graphviz
	ln -s ../graphviz/tcl graphviz

	# note
	# fix an isuue with the installer
	# must run this command
	# /usr/bin/dot -c
}