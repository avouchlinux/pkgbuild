# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=graphicsmagick
_realname=GraphicsMagick
pkgver=1.3.35
pkgrel=1
pkgdesc='Image processing system'
arch=('x86_64')
groups=('media-grafix')
url='http://www.graphicsmagick.org/'
license=('MIT')
optdepends=('jasper: jp2 module'
            'libwmf: wmf module'
            'libxml2: msl, svg, url modules'
            'ghostscript: pdf, ps modules')
makedepends=('perl' 'jasper' 'libwmf' 'libxml2')
depends=('freetype2' 'libxext' 'libsm' 'lcms2' 'libltdl' 'bzip2' 'xz'
         'libpng' 'libtiff' 'libwebp')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/GraphicsMagick-${pkgver}.tar.xz")
options=('!emptydirs' 'libtool')

prepare() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"
	sed -e "s:freetype_config='':freetype_config='/usr/bin/pkg-config freetype2':g" -i configure
}

build() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"
	./configure \
		--prefix=/usr \
		--enable-shared \
		--with-modules \
		--with-perl \
		--with-gs-font-dir=/usr/share/fonts/Type1 \
		--with-quantum-depth=16 \
		--with-threads
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# Install MIT license
	install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/Copyright.txt"

	# Install perl bindings
	# The patching was introduced in order to build perl module without installing package itself and
	# not to introduce unnecessary path into LD_RUN_PATH
	cd PerlMagick
	sed -i -e "s:'LDDLFLAGS'  => \"\(.*\)\":'LDDLFLAGS'  => \"-L${pkgdir}/usr/lib \1\":" Makefile.PL
	perl Makefile.PL INSTALLDIRS=vendor PREFIX=/usr DESTDIR="${pkgdir}"
	sed -i -e "s/LDLOADLIBS =/LDLOADLIBS = -lGraphicsMagick/" Makefile
	make
	make install
}
