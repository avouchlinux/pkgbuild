# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libgphoto2
pkgver=2.5.24
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs."
url="http://www.gphoto.org/"
arch=('x86_64')
groups=('media-grafix')
license=(LGPL)
depends=(libexif libjpeg gd libltdl libusb libxml2)
makedepends=(autoconf-archive git)
source=("https://sourceforge.net/projects/gphoto/files/libgphoto/$pkgver/libgphoto2-$pkgver.tar.bz2")
validpgpkeys=('7C4AFD61D8AAE7570796A5172209D6902F969C95') # Marcus Meissner

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-rpath
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make ${MAKEFLAGS}
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove unused udev helper
  rm -r "$pkgdir/usr/lib/udev"

  _genudev
}

_genudev() (
  cd "$pkgdir/usr/lib/libgphoto2"

  export LD_LIBRARY_PATH="$pkgdir/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH"
  export CAMLIBS="$PWD/$pkgver"

  ./print-camera-list hwdb \
    | install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/hwdb.d/20-gphoto.hwdb"
  ./print-camera-list udev-rules version 201 \
    | install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/rules.d/40-gphoto.rules"
)