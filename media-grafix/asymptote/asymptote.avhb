# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=asymptote
pkgver=2.92
pkgrel=1
pkgdesc="A vector graphics language (like metapost)"
arch=('x86_64')
groups=('media-grafix')
url='http://asymptote.sourceforge.net/'
license=(GPL-3.0-only)
depends=(boost-libs
         curl
         fftw
         freeglut
         gc
         gcc-libs
         glibc
         gsl
         libsigsegv
         libtirpc
         texlive-basic
         zlib)
makedepends=(boost
             cmake
             doxygen
             ghostscript
             glm
             glu
             imagemagick
             python-pyqt5
             texlive-binextra texlive-meta
             texlive-fontutils
             texlive-latexextra
             texlive-plaingeneric)
optdepends=('python-pyqt5: for the xasy GUI'
            'python-cson: for the xasy GUI'
            'ghostscript: PDF output')
source=(https://github.com/vectorgraphics/asymptote/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('41124c7d71b1d0457776be729ba6917690c808656defd75cae8b3bed5dccd769')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS+=" -I$srcdir/$pkgname-$pkgver" \
  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context
  make asy asy-keywords.el
}

check() {
  cd $pkgname-$pkgver
  make check-all
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install-asy
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf "$pkgdir"/usr/share/info/asymptote

  # copy some data files to their correct location
  mkdir -p "$pkgdir"/usr/share/emacs/site-lisp
  cp "$pkgdir"/usr/share/asymptote/*.el "$pkgdir"/usr/share/emacs/site-lisp
  mkdir -p "$pkgdir"/usr/share/vim/vimfiles/syntax
  cp "$pkgdir"/usr/share/asymptote/*.vim "$pkgdir"/usr/share/vim/vimfiles/syntax
  mkdir -p "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
  cd "$pkgdir"/usr/share/asymptote/
  sh asy-kate.sh
  mv asymptote.xml "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
}

