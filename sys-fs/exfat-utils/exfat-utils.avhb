# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=exfat-utils
_pkgname=${pkgname%-utils}
pkgver=1.4.0
pkgrel=1
pkgdesc="Utilities for exFAT file system"
arch=('x86_64')
groups=('sys-fs')
url='https://github.com/relan/exfat'
license=(GPL2)
depends=(glibc
         fuse3)
conflicts=(fuse-exfat)
provides=(fuse-exfat)
replaces=(fuse-exfat)
_archive="${_pkgname}-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('af560659f55dca0270f94809530f359d6bdad5d313133c68b458fb0a8eeab1c8')

prepare() {
	cd "$_archive"
	autoreconf -fiv
}

build() {
	cd "$_archive"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin
	make CCFLAGS="$CFLAGS $CPPFLAGS -std=c99" LINKFLAGS="$LDFLAGS"
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	install -Dm0644 -t "$pkgdir/usr/share/man/man8" */*.8
}
