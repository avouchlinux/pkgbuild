# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=mtools
pkgver=4.0.24
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
groups=('sys-fs')
url="https://www.gnu.org/software/mtools/"
license=('GPL')
depends=('sh')
makedepends=('texinfo' 'libx11' 'libsm')
optdepends=('libx11: required by floppyd'
	    'libsm:  required by floppyd')
backup=('etc/mtools.conf')
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2"
        "https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2.sig")
validpgpkeys=('E151F8F54AE4F4E9019F037BC80631B26F431961')
md5sums=('1d17b58c53a46b29c7f521d4a55ccef1'
         'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i '/^SAMPLE FILE$/s:^:# :' mtools.conf
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--mandir=/usr/share/man \
	--infodir=/usr/share/info \
	--sysconfdir=/etc
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -j 1 DESTDIR="${pkgdir}" install
	install -Dm644 mtools.conf "${pkgdir}/etc/mtools.conf"
}
