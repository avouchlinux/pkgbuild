# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=e2fsprogs
pkgver=1.45.6
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
groups=('sys-fs')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')
depends=('sh' 'libutil-linux')
makedepends=('util-linux')
backup=('etc/mke2fs.conf')
options=('staticlibs')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0') # Theodore Ts'o <tytso@mit.edu>
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar."{xz,sign}
        'MIT-LICENSE')
prepare() {	
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    # Remove unnecessary init.d directory
    sed -i '/init\.d/s|^|#|' misc/Makefile.in
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
        --with-root-prefix="" \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --enable-elf-shlibs \
        --disable-fsck \
        --disable-uuidd \
        --disable-libuuid \
        --disable-libblkid

    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install install-libs

    sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

    # remove references to build directory
    sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
    sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"

    # remove static libraries with a shared counterpart
    rm ${pkgdir}/usr/lib/lib{com_err,e2p,ext2fs,ss}.a

    # license
    install -Dm644 "${srcdir}/MIT-LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
