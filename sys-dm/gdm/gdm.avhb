# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=46.0
pkgrel=1
pkgdesc="Display manager and login screen"
url="https://wiki.gnome.org/Projects/GDM"
arch=(x86_64)
groups=('sys-dm')
license=(GPL)
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-xhost)
makedepends=(yelp-tools gobject-introspection git docbook-xsl)
checkdepends=(check)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")
patches=(0001-gdm-3.36.2-logo.patch
        0002-Add-Avouch-Linux-PAM-config-files.patch
        0003-avouch-pam-config-for-mason.patch
        0004-avouch-pam-config-for-mason.patch
        0007-system-dconf.patch)

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}

    # Avouch specific patches
    patch -Np0 -i ../0001-gdm-3.36.2-logo.patch
    patch -Np1 -i ../0002-Add-Avouch-Linux-PAM-config-files.patch
    patch -Np0 -i ../0003-avouch-pam-config-for-mason.patch
    cd data
    patch -Np0 -i ../../0004-avouch-pam-config-for-mason.patch

    cd ..
    # From Koji Fedora
    patch -Np1 -i ../0007-system-dconf.patch
}

build() {
  local meson_options=(
    -D dbus-sys="/usr/share/dbus-1/system.d"
    -D default-pam-config=arch
    -D default-path="/usr/local/bin:/usr/local/sbin:/usr/bin"
    -D gdm-xsession=true
    -D ipv6=true
    -D run-dir=/run/gdm
  )

  avouch-meson ${pkgbase}-${pkgver} build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)
  install=gdm.install

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  mkdir -p "$pkgdir"/var/lib/gdm/{.config/pulse,.local/share/applications}
  chown 80:80 -Rf "$pkgdir"/var/lib/gdm
  chmod 1770 "$pkgdir"/var/lib/gdm
  chmod 0700 "$pkgdir"/var/lib/gdm/{.config,.config/pulse,.local}
  chmod 0755 "$pkgdir"/var/lib/gdm/{.local/share,.local/share/applications}

  # install -d -o   0 -g   0 -m 0755 "$pkgdir"/var
  # install -d -o   0 -g   0 -m 0755 "$pkgdir"/var/lib
  # install -d -o 80 -g 80 -m 1770 "$pkgdir"/var/lib/gdm
  # install -d -o 80 -g 80 -m 0700 "$pkgdir"/var/lib/gdm/.config
  # install -d -o 80 -g 80 -m 0700 "$pkgdir"/var/lib/gdm/.config/pulse
  # install -d -o 80 -g 80 -m 0700 "$pkgdir"/var/lib/gdm/.local
  # install -d -o 80 -g 80 -m 0755 "$pkgdir"/var/lib/gdm/.local/share
  # install -d -o 80 -g 80 -m 0755 "$pkgdir"/var/lib/gdm/.local/share/applications

  # https://src.fedoraproject.org/rpms/gdm/blob/master/f/default.pa-for-gdm
  install -o80 -g80 -m644 /dev/stdin "$pkgdir"/var/lib/gdm/.config/pulse/default.pa <<END
load-module module-device-restore
load-module module-card-restore
load-module module-udev-detect
load-module module-native-protocol-unix
load-module module-default-device-restore
load-module module-always-sink
load-module module-intended-roles
load-module module-suspend-on-idle
load-module module-systemd-login
load-module module-position-event-sounds
END

  install -Dm644 /dev/stdin usr/lib/sysusers.d/gdm.conf <<END
g gdm 80 -
u gdm 80 "Gnome Display Manager" /var/lib/gdm
END

  _pick libgdm usr/include
  _pick libgdm usr/lib/{girepository-1.0,libgdm*,pkgconfig}
  _pick libgdm usr/share/{gir-1.0,glib-2.0}
}

package_libgdm() {
  pkgdesc+=" - support library"
  depends=(libsystemd.so libg{lib,object,io}-2.0.so)
  provides=(libgdm.so)

  mv libgdm/* "$pkgdir"
}
