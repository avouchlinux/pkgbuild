# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=openbox
pkgver=3.6.1
pkgrel=1
pkgdesc='Highly configurable and lightweight X11 window manager'
arch=('x86_64')
groups=('x11-wm')
repo=('core')
url='http://openbox.org'
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
         'libxcursor' 'pango' 'imlib2' 'librsvg')
optdepends=('kdebase-workspace: for the KDE/Openbox xsession'
            'python2-xdg: for the openbox-xdg-autostart script')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml'
        'etc/xdg/openbox/autostart' 'etc/xdg/openbox/environment')
source=(http://www.icculus.org/openbox/releases/$pkgname-$pkgver.tar.gz
        openbox-3.5.0-title-matching.patch
        openbox-3.5.0-which-2.20.patch)

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    patch -Np1 -i "$srcdir"/openbox-3.5.0-title-matching.patch  # OB#5277
    patch -Np1 -i "$srcdir"/openbox-3.5.0-which-2.20.patch # FS#11455

    sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    data/autostart/openbox-xdg-autostart
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr \
        --with-x \
        --enable-startup-notification \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/openbox \
        --disable-static
    make ${MAKEFLAGS}
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install

    # GNOME Panel is no longer available in the official repositories
    rm -r "$pkgdir"/usr/bin/{gdm-control,gnome-panel-control,openbox-gnome-session} \
        "$pkgdir"/usr/share/gnome{,-session} \
        "$pkgdir"/usr/share/man/man1/openbox-gnome-session.1 \
        "$pkgdir"/usr/share/xsessions/openbox-gnome.desktop

    sed -i 's:startkde:/usr/bin/\0:' \
        "$pkgdir"/usr/share/xsessions/openbox-kde.desktop

    # license
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
