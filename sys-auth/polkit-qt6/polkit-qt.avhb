# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=polkit-qt
pkgname=('polkit-qt4' 'polkit-qt5')
pkgver=0.113.0
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('x86_64')
groups=('sys-auth')
url='https://www.kde.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'polkit' 'qt4' 'qt5-base')
#source=("http://download.kde.org/stable/apps/KDE4.x/admin/${pkgbase}-1-${pkgver}.tar.bz2")
source=("http://quickgit.kde.org/?p=polkit-qt-1.git&a=snapshot&h=15c7867e88c5278f61896b5531ac2b544add8220&fmt=tgz")
md5sums=('e74c39223328edf89cdce4e037ef3c38')

prepare() {		
	mkdir "${srcdir}"/build{-qt4,-qt5}
}

build() {
  cd ${srcdir}/build-qt4
  cmake ../${pkgbase}-1-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DUSE_QT4=ON
  make ${MAKEFLAGS}

  cd ${srcdir}/build-qt5
  cmake ../${pkgbase}-1-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib
  make ${MAKEFLAGS}
}

package_polkit-qt4() {
  depends=('polkit' 'qt4')
  replaces=('polkit-qt')
  provides=('polkit-qt')
  conflicts=('polkit-qt')

  cd ${srcdir}/build-qt4
  make DESTDIR="${pkgdir}" install
}

package_polkit-qt5() {
  depends=('polkit' 'qt5-base')

  cd ${srcdir}/build-qt5
  make DESTDIR="${pkgdir}" install
}
