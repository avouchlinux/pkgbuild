# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=oath-toolkit
pkgver=2.6.2
pkgrel=1
pkgdesc='OATH one-time password toolkit'
arch=('x86_64')
groups=('sys-auth')
url='https://www.nongnu.org/oath-toolkit/'
license=('GPL3')
depends=('glibc' 'pam' 'xmlsec')
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C') # Simon Josefsson
source=("https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        'oath-toolkit-update-gnulib.patch::https://gitlab.com/oath-toolkit/oath-toolkit/commit/2fffce2a471f74a585939c84cce16ef3015e5d3d.diff'
        'fseeko.patch')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 < "${srcdir}"/oath-toolkit-update-gnulib.patch
  patch -Np1 < "${srcdir}"/fseeko.patch
  
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make ${MAKEFLAGS}
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Install basic docs too
  install -D -m 0644 pam_oath/README "$pkgdir/usr/share/doc/$pkgname/PAM_README"
  install -D -m 0644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

