# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=rtkit
pkgver=0.13+1+gc295fa8
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('x86_64')
groups=('sys-auth')
url="https://github.com/heftig/rtkit"
license=(GPL3 'custom:BSD')
depends=(dbus polkit systemd)
makedepends=(git)
_commit=4c7e5ca63b1278b41c31b63a4ca5cce331d9db2e  # tags/v0.12^0
source=("git+https://github.com/heftig/rtkit#commit=$_commit")
sha256sums=('SKIP')

# getsrc() {
# 	git clone https://github.com/heftig/rtkit.git
# }

pkgver() {
  cd $pkgname
  #git checkout $_commit
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgname \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make

  #./rtkit-daemon --introspect > org.freedesktop.RealtimeKit1.xml
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/share/dbus-1/interfaces" -m644 org.freedesktop.RealtimeKit1.xml

  echo 'u rtkit 71 "RealtimeKit" /proc' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  sed -ne '4,25p' rtkit.c |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
