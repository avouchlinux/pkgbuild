# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=polkit
pkgver=0.220
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
groups=('sys-auth')
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat systemd mozjs78)
makedepends=(meson gtk-doc gobject-introspection git)
checkdepends=(python3-dbusmock)
provides=(libpolkit-{agent,gobject}-1.so)
backup=(etc/pam.d/polkit-1)
# source=("https://gitlab.freedesktop.org/polkit/polkit/-/archive/$pkgver/polkit-$pkgver.tar.bz2")
source=(https://gitlab.freedesktop.org/polkit/polkit.git)

getsrc(){
  if [[ ! -d $pkgname ]]; then
    git clone $source
  fi
}

pkgver() {
  cd polkit
  git checkout tags/$pkgver
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd polkit

  # CVE-2021-4034
  git cherry-pick -n a2bf5c9c83b6ae46cbd5c779d3055bff81ded683

  # CVE-2021-4115
  git cherry-pick -n 41cb093f554da8772362654a128a84dd8a5542a7

  # Fix build with Meson 0.61.0
  git apply -3 ../meson-0.61.diff
}

build() {
  avouch-meson polkit build \
    -D session_tracking=libsystemd-login \
    -D os_type=redhat \
    -D examples=true \
    -D tests=true \
    -D gtk_doc=true \
    -D man=true
  meson compile -C build
}

# check() {
#   meson test -C build --print-errorlogs -t 3
# }

package() {
  meson install -C build --destdir "$pkgdir"

  install -d -o root -g 102 -m 750 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  mv "$pkgdir"/{etc,usr/share}/polkit-1/rules.d/50-default.rules

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}