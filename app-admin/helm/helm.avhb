# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=helm
pkgver=3.9.0
pkgrel=1
pkgdesc="The Kubernetes Package Manager"
arch=("x86_64")
groups=('app-admin')
url="https://helm.sh/"
license=("Apache")
depends=('glibc')
makedepends=("go" "git")
options=("!lto")
source=(https://github.com/helm/helm/archive/refs/tags/v$pkgver.tar.gz)
validpgpkeys=('672C657BE06B4B30969C4A57461449C25E36B98E'
              'CABAA8D44DFACA14791FBE9892C44A3D421FF7F9'
              '967F8AC5E2216F9F4FD270AD92AA783CBAAE8E3B')
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  go mod tidy
}

build() {
    cd "${pkgname}-${pkgver}"
    export CGO_LDFLAGS="$LDFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_CPPFLAGS="$CPPFLAGS"
    make EXT_LDFLAGS="-linkmode external" GOFLAGS="-buildmode=pie -trimpath"
}

check(){
    cd "${pkgname}-${pkgver}"
    export CGO_LDFLAGS="$LDFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_CPPFLAGS="$CPPFLAGS"
    make LDFLAGS="-s -w -linkmode external" GOFLAGS="-buildmode=pie -trimpath" test-unit
}

package(){
    cd "${pkgname}-${pkgver}"
    install -Dm755 bin/helm -t "$pkgdir/usr/bin"
    bin/helm completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/helm"
    bin/helm completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_helm"
}
