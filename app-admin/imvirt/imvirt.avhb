# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=imvirt
pkgver=0.9.6
pkgrel=1
pkgdesc='Detect the virtualization technology of a machine, it is run in'
url='http://micky.ibh.net/~liske/imvirt.html'
arch=('x86_64')
groups=('app-admin')
license=('GPL2')
depends=('perl' 'dmidecode' 'perl-module-find' 'perl-file-slurp' 'perl-data-dumper')
options=('!emptydirs')
source=(https://github.com/DE-IBH/${pkgname}/archive/v${pkgver}.tar.gz)

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|/usr/sbin|/usr/bin|g' -i perl/lib/ImVirt/Utils/dmidecode/pipe.pm
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}