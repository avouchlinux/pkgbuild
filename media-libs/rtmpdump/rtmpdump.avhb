# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=rtmpdump
pkgver=2.4
pkgrel=1
epoch=1
pkgdesc='Tool to download rtmp streams'
arch=('x86_64')
groups=('media-libs')
url='https://rtmpdump.mplayerhq.hu/'
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'gnutls' 'zlib')
makedepends=('git')
provides=('librtmp.so')
options=('!makeflags')
_commit='fa8646d'
source=("git+https://git.ffmpeg.org/rtmpdump#commit=${_commit}")

# getsrc() {
# 	git clone https://git.ffmpeg.org/rtmpdump.git
# }

pkgver() {
  cd rtmpdump

  _ver_name='2.4'
  _ver_commit='c28f1bab7822de97353849e7787b59e50bbb1428'

  echo "${_ver_name}.r$(git rev-list --count ${_ver_commit}..HEAD).${_commit}"
}

build() {
  cd rtmpdump
  sed -e 's/^CRYPTO=OPENSSL/#CRYPTO=OPENSSL/' -e 's/#CRYPTO=GNUTLS/CRYPTO=GNUTLS/' -i Makefile -i librtmp/Makefile

  make \
    OPT="$CFLAGS" \
    XLDFLAGS="$LDFLAGS"
}

package() {
  cd rtmpdump

  make \
    prefix='/usr' \
    sbindir='/usr/bin' \
    mandir='/usr/share/man' \
    DESTDIR="${pkgdir}" \
    install
}
