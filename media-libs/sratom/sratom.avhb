# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=sratom
pkgver=0.6.4
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=('x86_64')
groups=('media-libs')
url="https://drobilla.net/software/sratom/"
license=('custom:ISC')
depends=('lv2' 'sord')
makedepends=('python2')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local ldconfig call
  sed -i '/ldconfig/d' wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python3 waf configure --prefix=/usr --test
  python3 waf build
}

check() {
  cd "${pkgname}-${pkgver}"
  python3 waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  python3 waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 NEWS
}