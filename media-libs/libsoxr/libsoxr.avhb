# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libsoxr
_realname=soxr
pkgver=0.1.3
pkgrel=1
pkgdesc='The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio'
arch=('x86_64')
groups=('media-libs')
url='http://sourceforge.net/p/soxr/wiki/Home/'
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake)
options=(!emptydirs)
source=(http://downloads.sourceforge.net/project/soxr/soxr-$pkgver-Source.tar.xz)

build() {
  cd soxr-$pkgver-Source
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DVISIBILITY_HIDDENL=ON \
    -DWITH_AVFFT=ON \
    -DWITH_DOUBLE_PRECISION=ON \
    -DWITH_LSR_BINDINGS=ON \
    -DWITH_OPENMP=ON \
    -DWITH_PFFFT=ON \
    -DWITH_SIMD=ON \
    .
  make
}

check() {
  cd soxr-$pkgver-Source
  make test
}

package() {
  cd soxr-$pkgver-Source
  
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -r "$pkgdir"/usr/share/doc/$pkgname/
}
