# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=alsa-firmware
pkgver=1.2.1
pkgrel=1
pkgdesc="ALSA firmware package"
arch=('any')
groups=('media-libs')
url="http://alsa-project.org/"
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/firmware/$pkgname-$pkgver.tar.bz2)
options=(!strip)

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --enable-buildfw --with-hotplug-dir=/usr/lib/firmware
	make ${MAKEFLAGS}
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR="$pkgdir" install
	# remove files which conflicts with linux-firmware
	rm -rf "${pkgdir}"/usr/lib/firmware/{ct{efx,speq}.bin,ess,korg,sb16,yamaha}
	# remove broken symlinks (broken upstream)
	rm -rf "${pkgdir}"/usr/lib/firmware/turtlebeach
	install -vDm 644 aica/license.txt \
		-t "${pkgdir}/usr/share/licenses/${pkgname}/aica/"
	install -vDm 644 ca0132/creative.txt \
		-t "${pkgdir}/usr/share/licenses/${pkgname}/ca0132/"
}

