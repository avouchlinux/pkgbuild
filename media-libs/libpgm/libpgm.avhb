# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libpgm
_realname=openpgm-release
pkgver=5.3.128
_pkgver="${pkgver//./-}"
pkgrel=1
pkgdesc="OpenPGM: implementation of the Pragmatic General Multicast (PGM, RFC3208)"
arch=('x86_64')
groups=('media-libs')
url='https://github.com/steve-o/openpgm'
license=('LGPL-2.1-or-later')
depends=('glibc')
makedepends=('python')
source=("https://github.com/steve-o/openpgm/archive/release-${pkgver//./-}/openpgm-$pkgver.tar.gz")
sha256sums=('8d707ef8dda45f4a7bc91016d7f2fed6a418637185d76c7ab30b306499c6d393')

prepare() {
  cd openpgm-release-${pkgver//./-}/openpgm/pgm

  # https://github.com/steve-o/openpgm/pull/66
  mv openpgm-5.2.pc.in openpgm-5.3.pc.in

  ./bootstrap.sh
}

build() {
  cd openpgm-release-${pkgver//./-}/openpgm/pgm
  ./configure --prefix=/usr
  make
}

package() {
  cd openpgm-release-${pkgver//./-}/openpgm/pgm
  make DESTDIR="$pkgdir" install
}
