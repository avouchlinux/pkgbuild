# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

ppkgbase=speech-dispatcher
pkgname=(speech-dispatcher libspeechd)
pkgver=0.8.8
pkgrel=1
arch=('x86_64')
groups=('media-libs')
pkgdesc="High-level device independent layer for speech synthesis interface"
url="http://www.freebsoft.org/speechd"
license=('GPL2' 'FDL')
makedepends=('intltool' 'espeak' 'libltdl' 'python-xdg' 'dotconf' 'libpulse' 'libao')
source=("https://www.freebsoft.org/pub/projects/speechd/$pkgname-$pkgver.tar.gz"
        'speech-dispatcherd.service')

prepare() {
  cd ${pkgname}-${pkgver}

  # Disabled due to bug: https://bugs.archlinux.org/task/35700
  sed -i "s/cicero //g" configure.ac
  sed -i "s/sd_cicero//g" src/modules/Makefile.am

  autoreconf -vif
}

build() {
  cd ${pkgname}-${pkgver}

  export CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr \
    --enable-shared \
    --sysconfdir=/etc \
    --without-flite
  make ${MAKEFLAGS}
}

package_speech-dispatcher() {
  depends=("libspeechd=$pkgver-$pkgrel" 'libltdl' 'python-xdg' 'dotconf' 'libpulse' 'libao')
  optdepends=('festival: Speech output using Festival'
              'espeak: Speech output using ESpeak'
              'pulseaudio: PulseAudio support')
  backup=(etc/speech-dispatcher/clients/emacs.conf
  	      etc/speech-dispatcher/modules/ibmtts.conf
  	      etc/speech-dispatcher/modules/espeak-generic.conf
  	      etc/speech-dispatcher/modules/espeak.conf
  	      etc/speech-dispatcher/modules/swift-generic.conf
  	      etc/speech-dispatcher/modules/festival.conf
  	      etc/speech-dispatcher/modules/cicero.conf
  	      etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
  	      etc/speech-dispatcher/modules/dtk-generic.conf
  	      etc/speech-dispatcher/modules/llia_phon-generic.conf
  	      etc/speech-dispatcher/modules/ivona.conf
  	      etc/speech-dispatcher/modules/epos-generic.conf
          etc/speech-dispatcher/modules/flite.conf
          etc/speech-dispatcher/modules/pico-generic.conf
          etc/speech-dispatcher/speechd.conf)

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/speech-dispatcherd.service "${pkgdir}/usr/lib/systemd/system/speech-dispatcherd.service"
  install -d "${pkgdir}/var/log/speech-dispatcher"

  sed -i 's|includedir=.*|includedir=${prefix}/include/speech-dispatcher|g' "${pkgdir}/usr/lib/pkgconfig/speech-dispatcher.pc"

  mkdir -p ${srcdir}/libspeechd/usr/lib
  mv "${pkgdir}"/usr/lib/libspeechd*so* ${srcdir}/libspeechd/usr/lib
}

package_libspeechd() {
  mkdir -p "${pkgdir}"/usr/lib

  mv ${srcdir}/libspeechd/usr/lib/* "${pkgdir}"/usr/lib
}
