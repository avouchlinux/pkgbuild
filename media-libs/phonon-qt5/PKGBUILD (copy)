# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=phonon
pkgname=('phonon-qt4' 'phonon-qt5')
pkgver=4.9.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
license=('LGPL')
makedepends=('extra-cmake-modules' 'automoc4' 'libpulse' 'libqzeitgeist' 'qt5-base' 'qt5-tools')
options=('!emptydirs')
source=("http://download.kde.org/stable/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('8abeb7b1eaf4935668c7fb441fc2491a')

prepare() {
  mkdir build-qt4
  mkdir build-qt5
}

build() {
  cd build-qt4
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib
  make ${MAKEFLAGS}

  cd ../build-qt5
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON \
    -DPHONON_BUILD_PHONON4QT5=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib
  make ${MAKEFLAGS}
}

package_phonon-qt4(){
  pkgdesc="The multimedia framework for KDE4"
  depends=('phonon-qt4-backend' 'libpulse' 'libqzeitgeist')
  optdepends=('pulseaudio: PulseAudio support')
  replaces=('phonon')
  provides=('phonon')
  conflicts=('phonon')

  cd build-qt4
  make DESTDIR="${pkgdir}" install

  # Install headers into the Qt4 dir
  install -d "${pkgdir}"/usr/include/qt4
  mv "${pkgdir}"/usr/include/{phonon,KDE} "${pkgdir}"/usr/include/qt4/

  sed -i 's#includedir=/usr/include#includedir=/usr/include/qt4#' \
    "${pkgdir}/usr/lib/pkgconfig/phonon.pc"
}

package_phonon-qt5(){
  pkgdesc="The multimedia framework for KF5"
  depends=('phonon-qt5-backend' 'libpulse' 'qt5-base')
  optdepends=('pulseaudio: PulseAudio support')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
