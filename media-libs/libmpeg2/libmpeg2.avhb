# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libmpeg2
pkgver=0.5.1
pkgrel=1
pkgdesc="Library for decoding MPEG-1 and MPEG-2 video streams."
arch=('x86_64')
groups=('media-libs')
url='https://libmpeg2.sourceforge.io/'
depends=('glibc')
license=('GPL2')
provides=('mpeg2dec')
makedepends=('sdl' 'libxv')
optdepends=('sdl: required for mpeg2dec'
            'libxv: required for mpeg2dec')
source=("https://libmpeg2.sourceforge.io/files/$pkgname-$pkgver.tar.gz"
        libmpeg2-0.5.1-gcc4.6.patch)


prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libmpeg2-0.5.1-gcc4.6.patch"

  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-shared --disable-static
  make ${MAKEFLAGS}	OPT_CFLAGS="${CFLAGS}" \
	MPEG2DEC_CFLAGS="${CFLAGS}" \
	LIBMPEG2_CFLAGS=""
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
