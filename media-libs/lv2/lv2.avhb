# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lv2
pkgver=1.16.0
pkgrel=1
pkgdesc="Plugin standard for audio systems"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('x86_64')
groups=('media-libs')
makedepends=('asciidoc' 'doxygen' 'gtk2' 'libsndfile' 'pygmentize'
'python-pygments' 'python-rdflib')
optdepends=('libsndfile: Example sampler'
            'gtk2: Example scope and sampler'
            'python-pygments: Documentation generator script'
            'python-rdflib: Documentation generator script')
source=("http://lv2plug.in/spec/${pkgname}-${pkgver}.tar.bz2"{,.sig})
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd "${pkgname}-${pkgver}"
  # let wscript(s) find the custom waf scripts
  mkdir -pv tools
  touch __init__.py
  cp -v waflib/extras/{autowaf,lv2}.py tools/
  mkdir -pv plugins/tools/
  cp -v waflib/extras/{autowaf,lv2}.py plugins/tools/
  rm -rv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/g" \
      -i {,plugins/,plugins/*/}wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  # --docs is currently broken: https://gitlab.com/lv2/lv2/issues/28
  waf -vv configure --prefix=/usr \
                --libdir=/usr/lib \
                --test
  waf -vv build $MAKEFLAGS
}

check() {
  cd "${pkgname}-${pkgver}"
  waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}