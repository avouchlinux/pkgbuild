# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Maintainer: Qurban Ullah <qurbanullah@gmail.com>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@gmail.com>

pkgname=a52dec
pkgver=0.7.4
pkgrel=1
pkgdesc="A free library for decoding ATSC A/52 streams"
url="http://liba52.sourceforge.net/"
arch=('x86_64')
groups=('media-libs')
license=('GPL2')
depends=('glibc')
source=("http://liba52.sourceforge.net/files/${pkgname}-${pkgver}.tar.gz"
        'a52dec-0.7.4-build.patch'
        'stderr.patch')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../a52dec-0.7.4-build.patch
  patch -Np1 -i ../stderr.patch
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in
  mv configure.in configure.ac
  ./bootstrap
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --enable-shared
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -m644 liba52/a52_internal.h "${pkgdir}/usr/include/a52dec/"
}

