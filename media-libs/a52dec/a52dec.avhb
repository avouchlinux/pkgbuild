# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=a52dec
pkgver=0.8.0
pkgrel=1
pkgdesc="A free library for decoding ATSC A/52 streams"
arch=('x86_64')
groups=('media-libs')
url='https://git.adelielinux.org/community/a52dec/'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(git)
source=("git+https://git.adelielinux.org/community/a52dec.git#tag=v$pkgver")
b2sums=(3d40824f3aaf949facd428105a6f2213e8f1663a10bd4251a15791e0bbca2b10cbccae1baeff24fd6bc03d5b2b2a6798da5311da796becda4f8d88c4dc7b00b9)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-shared
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/include/a52dec/" liba52/a52_internal.h
}

