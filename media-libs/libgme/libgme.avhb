# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libgme
_realname=game-music-emu
pkgver=0.6.3
pkgrel=1
pkgdesc="Video game music file emulation/playback library"
url="https://bitbucket.org/mpyne/game-music-emu"
license=('LGPL')
arch=('x86_64')
groups=('media-libs')
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  zlib
)
makedepends=(
  cmake
  git
  ninja
  sdl2
)
source=("git+https://github.com/libgme/game-music-emu.git?signed#tag=$pkgver")
b2sums=(e83e52c259d50e360c2b2878e581410346fc84d5e9479875acdb657fdbe1a3c592672c8237c34dfebb2f960ab57d0d4596fe16655cb96b1d9fc8ddd9a7c39f51)
validpgpkeys=(5406ECE83665DA9D201D35720BAF0C9C7B6AE9F2) # Michael Pyne <mpyne@kde.org>

prepare() {
  cd game-music-emu

  # Fix assertion at negative count value
  git cherry-pick -n b667f79dcee5c380541dc90630bb33931be378c7
}

build() {
  cmake -S game-music-emu -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
