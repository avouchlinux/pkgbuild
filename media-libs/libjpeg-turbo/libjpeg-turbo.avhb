# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libjpeg-turbo
pkgver=2.0.4
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
url='http://libjpeg-turbo.virtualgl.org/'
arch=('x86_64')
groups=('media-libs')
license=('custom')
makedepends=('cmake' 'nasm' 'jdk8-openjdk')
provides=('libjpeg=8.2.2')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('7D6293CC6378786E1B5C496885C7044E033FDE16')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/Szathm√°ry/Szathmary/g' -i java/org/libjpegturbo/turbojpeg/*.java
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cmake \
		-DWITH_JAVA=1 \
		-DWITH_JPEG8=1 \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		.

	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS} test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make \
		DESTDIR="${pkgdir}" \
		docdir="/usr/share/doc/${pkgname}" \
		exampledir="/usr/share/doc/${pkgname}" \
		install

	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	ln -s ../../doc/libjpeg-turbo/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m 644 jpegint.h "${pkgdir}/usr/include" # required by other software
}