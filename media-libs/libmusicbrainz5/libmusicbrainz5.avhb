# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libmusicbrainz5
_realname=libmusicbrainz
pkgver=5.1.0
pkgrel=1
pkgdesc="Library That Provides Access to the MusicBrainz Server"
arch=('x86_64')
groups=('media-libs')
url='http://musicbrainz.org/doc/libmusicbrainz'
license=(LGPL-2.1-or-later)
depends=(gcc-libs glibc neon libxml2)
makedepends=(cmake doxygen ninja)
provides=(libmusicbrainz5.so)
source=("https://github.com/metabrainz/libmusicbrainz/releases/download/release-${pkgver}/libmusicbrainz-$pkgver.tar.gz"
        libmusicbrainz5-5.patch
        libmusicbrainz5-16.patch
        libmusicbrainz5-19.patch)
sha256sums=('6749259e89bbb273f3f5ad7acdffb7c47a2cf8fcaeab4c4695484cef5f4c6b46'
            '1e412f8e89183f1a70be530809b34ce3f3cb880ce731d8c7253f6bfe9cc8b7b6'
            '5ad7e45b191cceca3bb71c92adb6c767ae8079bf2814354341fae829d4328ae9'
            'c8defc41681b1ba7c93af93f73207bf1666c78e98d22a1a5f16c9e20b947f787')

prepare() {
  cd libmusicbrainz-$pkgver

  # https://github.com/metabrainz/libmusicbrainz/pull/5
  patch -Np1 -i ../libmusicbrainz5-5.patch
  # https://github.com/metabrainz/libmusicbrainz/pull/16
  patch -Np1 -i ../libmusicbrainz5-16.patch
  # https://github.com/metabrainz/libmusicbrainz/pull/19
  patch -Np1 -i ../libmusicbrainz5-19.patch
}

build() {
  cmake -S libmusicbrainz-$pkgver -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
  cmake --build build --target docs
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp -r build/docs "$pkgdir/usr/share/doc/$pkgname/html"
}
