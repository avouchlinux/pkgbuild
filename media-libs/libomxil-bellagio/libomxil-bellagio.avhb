# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libomxil-bellagio
pkgver=0.9.3
pkgrel=1
pkgdesc="An opensource implementation of the OpenMAX Integration Layer API"
arch=('x86_64')
groups=('media-libs')
url="http://omxil.sourceforge.net"
license=('LGPL')
depends=('glibc')
source=("https://downloads.sourceforge.net/project/omxil/omxil/Bellagio%200.9.3/${pkgname}-${pkgver}.tar.gz"
        fedora-fixes.patch)

prepare() {
  cd ${srcdir}/${pkgname}-$pkgver

  # Fixes from fedora repo
  patch -Np1 -i ../fedora-fixes.patch
  sed -e 's/-Werror//' -i configure.ac
  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # GCC 10
  export CFLAGS+=' -fcommon'
  ./configure --prefix=/usr --disable-static --docdir=/usr/share/doc/${pkgname}
  make ${MAKEFLAGS}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
