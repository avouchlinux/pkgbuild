# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libcdio
pkgver=2.1.0
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library"
url="https://www.gnu.org/software/libcdio/"
arch=('x86_64')
groups=('media-libs')
license=(GPL3)
depends=(libcddb ncurses gcc-libs)
source=(https://ftp.gnu.org/gnu/libcdio/$pkgname-$pkgver.tar.bz2{,.sig})
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-vcd-info --enable-cpp-progs --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    # Configure screipt wrongly append hadend flags
  # temprary fix to correct hardening flags
  # with out correction build fails as
  # gcc: error: relro: No such file or directory
  sed -i 's/relro/-z relro/g' Makefile
  sed -i 's/-z -z relro/-z relro/g' Makefile
  sed -i 's/relro/-z relro/g' src/Makefile
  sed -i 's/-z -z relro/-z relro/g' src/Makefile

  make ${MAKEFLAGS}
}

check() {
  cd $pkgname-$pkgver
  make ${MAKEFLAGS} -C test check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -e "/define CDIO_LIBCDIO_SOURCE_PATH/s|.*|/* #undef CDIO_LIBCDIO_SOURCE_PATH */|" \
      -i "$pkgdir/usr/include/cdio/cdio_config.h"
}