# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=opencolorio
_realname=OpenColorIO
pkgver=1.1.1
pkgrel=1
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
groups=('media-libs')
url="http://opencolorio.org"
license=('BSD')
depends=('lcms2' 'tinyxml' 'yaml-cpp')
makedepends=('boost' 'cmake' 'python')
optdepends=('python: python bindings')
source=(https://github.com/imageworks/OpenColorIO/tarball/v$pkgver
        8d48ee8da42de2d878db7b42586db8b3c67f83e1.patch)

prepare() {
  cd $_realname-$pkgver
  sed -i "s/-Werror//g" src/core/CMakeLists.txt
  sed -i "s/-Werror//g" src/pyglue/CMakeLists.txt
  sed -i "s/push(hidden)/push(default)/g" src/core/OCIOYaml.cpp

  patch -Np1 -i "$srcdir"/8d48ee8da42de2d878db7b42586db8b3c67f83e1.patch
}

build() {
  cmake -S $_realname-$pkgver -B build -G Ninja  \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_EXTERNAL_YAML=ON \
    -DUSE_EXTERNAL_TINYXML=ON \
    -DUSE_EXTERNAL_LCMS=ON
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  mv "$pkgdir"/usr/lib/static/libOpenColorIO.a "$pkgdir"/usr/lib/libOpenColorIO.a
  rmdir "$pkgdir"/usr/lib/static
}