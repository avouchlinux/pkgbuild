# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=frei0r-plugins
pkgver=2.3.3
pkgrel=1
pkgdesc="A minimalistic plugin API for video sources and filters"
arch=('x86_64')
groups=('media-libs')
url='https://frei0r.dyne.org/'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cairo
  cmake
  gavl
  git
  ninja
  opencv
)
optdepends=(
  'cairo: cairoaffineblend, cairoblend, cairogradient and cairoimagegrid plugins'
  'gavl: rgbparade, scale0tilt and vectorscope plugins'
  'opencv: facebl0r and facedetect plugins'
)
source=("git+https://github.com/dyne/frei0r#tag=v$pkgver")
b2sums=(543c512c8ba74098c1a78e827f82284170a555037e1b259af69062497f78881bed152dee4b1f1261096b0b0c23bb42c2e04288526c53a92e262de3526df43d3d)
validpgpkeys=(6113D89CA825C5CEDD02C87273B35DA54ACB7D10) # Denis Roio (Jaromil)

build() {
  cmake -S frei0r -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

