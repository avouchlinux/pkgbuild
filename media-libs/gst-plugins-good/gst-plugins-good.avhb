# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gst-plugins-good
pkgver=1.18.4
pkgrel=1
pkgdesc="GStreamer open-source multimedia framework good plugins"
url="https://gstreamer.freedesktop.org/"
arch=('x86_64')
groups=('media-libs')
license=(LGPL)
depends=(libpulse libsoup gst-plugins-base-libs wavpack aalib taglib libdv libshout libvpx
         gdk-pixbuf2 libcaca libavc1394 libiec61883 libxdamage v4l-utils cairo libgudev speex flac
         libraw1394 lame mpg123 twolame)
makedepends=(python gtk-doc git gtk3 qt5-declarative qt5-x11extras qt5-wayland meson jack2)
checkdepends=(gst-plugins-base)
source=("https://gitlab.freedesktop.org/gstreamer/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
  avouch-meson $pkgname-$pkgver build \
    -D gobject-cast-checks=disabled \
    -D doc=disabled \
    -D rpicamsrc=disabled \
    -D package-name="GStreamer Good Plugins (Avouch)" \
    -D package-origin="https://www.avouch.org/" \
    -D qt5=disabled
    # break build temporary disable qt5
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}