# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=sox
pkgver=14.4.2
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('x86_64')
groups=('media-libs')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
makedepends=('libao' 'libmad' 'libid3tag' 'wavpack' 'libpulse' 'opusfile' 'twolame')
checkdepends=('time')
optdepends=('libao: for ao plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin'
            'libpulse: for pulse plugin'
            'opusfile: for opus plugin'
            'twolame: for mp3 plugin')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        CVE-2017-15371.patch::https://github.com/mansr/sox/commit/818bdd0.patch
        CVE-2017-11358.patch::https://github.com/mansr/sox/commit/6cb44a4.patch
        CVE-2017-15370.patch::https://github.com/mansr/sox/commit/ef3d8be.patch
        CVE-2017-11332.patch::https://github.com/mansr/sox/commit/7405bca.patch
        CVE-2017-11359.patch::https://github.com/mansr/sox/commit/8b590b3.patch
        CVE-2017-15372.patch::https://github.com/mansr/sox/commit/001c337.patch
        CVE-2017-15642.patch::https://github.com/mansr/sox/commit/0be259e.patch
        CVE-2017-18189.patch
        update-exported-symbol-list.patch
        add-destdir.patch
        cleanup-lsx_malloc.patch
        CVE-2019-8354.patch
        CVE-2019-8355.patch
        CVE-2019-8356.patch
        CVE-2019-8357.patch)

prepare() {
  cd ${pkgname}-${pkgver}
  # fix man page installation
  sed -e 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' \
      -i Makefile.in

  # patch all (known) CVEs from 2017
  for _cve in 15371 11358 15370 11332 11359 15372 15642 18189; do
    patch -Np1 -i ../CVE-2017-$_cve.patch
  done

  # applying patches required to apply CVE patches cleanly
  patch -Np1 -i ../add-destdir.patch
  patch -Np1 -i ../update-exported-symbol-list.patch
  patch -Np1 -i ../cleanup-lsx_malloc.patch

  # patch all (known) CVEs from 2019
  for _cve in 8354 8355 8356 8357; do
    patch -p1 -i ../CVE-2019-$_cve.patch
  done

	# bug #386027 frpm Gentoo
	sed -i -e 's:CFLAGS="-g":CFLAGS="$CFLAGS -g":' configure.ac

  # test fails with CVE-2017-11359.patch
  sed -e '/hcom/d' -i src/tests.sh
  autoreconf -vfi
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc \
	--with-dyn-default --with-distro="Avouch Linux"
	make ${MAKEFLAGS}
}

check() {		
	cd ${srcdir}/${pkgname}-${pkgver}
	make bindir=. installcheck
}

package() {	
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install

	install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"

	  # license
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
