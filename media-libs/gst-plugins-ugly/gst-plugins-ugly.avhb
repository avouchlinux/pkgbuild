# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gst-plugins-ugly
pkgver=1.18.4
pkgrel=1
pkgdesc="GStreamer open-source multimedia framework ugly plugins"
url="https://gstreamer.freedesktop.org/"
arch=('x86_64')
groups=('media-libs')
license=(LGPL)
depends=(gst-plugins-base-libs libdvdread libmpeg2 a52dec libsidplay libcdio
         x264 opencore-amr)
makedepends=(python gtk-doc git meson)
source=("https://gitlab.freedesktop.org/gstreamer/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
  avouch-meson $pkgname-$pkgver build \
    -D gobject-cast-checks=disabled \
    -D doc=disabled \
    -D package-name="GStreamer Ugly Plugins (Avouch)" \
    -D package-origin="https://www.avouch.org/"
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
