# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libbluray
pkgver=1.3.4
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=('x86_64')
groups=('media-libs')
url=https://www.videolan.org/developers/libbluray.html
license=(LGPL-2.1-only)
depends=(
  fontconfig
  glibc
  libfreetype.so
  libxml2
)
makedepends=(
  apache-ant
  git
  java-environment=17
)
optdepends=('java-runtime: BD-J library')
provides=(libbluray.so)
_tag=bb5bc108ec695889855f06df338958004ff289ef
source=(
  git+https://code.videolan.org/videolan/libbluray.git#tag=${_tag}
  git+https://code.videolan.org/videolan/libudfread.git
)
b2sums=(
  SKIP
  SKIP
)

pkgver() {
  cd libbluray
  git describe --tags
}

prepare() {
  cd libbluray
  for submodule in contrib/libudfread; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
  autoreconf -fiv
}

build() {
  cd libbluray
  ./configure \
    --prefix=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libbluray install
}

