# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=celt
pkgver=0.11.3
pkgrel=1
pkgdesc="Low-latency audio communication codec"
arch=('x86_64')
groups=('media-libs')
url="http://www.celt-codec.org/"
license=(BSD)
depends=(libogg)
makedepends=(git)
provides=(libcelt0.so)
_commit=6c28805c56d562bec56cc0b31c0d5905ebb5a6ac  # tags/v0.11.3
source=("git+$url.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd celt
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd celt
  ./autogen.sh
}

build() {
  cd celt
  ./configure --prefix=/usr --enable-custom-modes --disable-static
  make
}

package() {
  cd celt
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
