# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=cdrtools
pkgver=3.02a09
pkgrel=1
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('x86_64')
groups=('media-libs')
url="http://cdrecord.org/"
license=('CDDL')
depends=('acl' 'libcap')
options=(!makeflags)
backup=('etc/default/cdrecord' 'etc/default/rscsi')
source=(https://downloads.sourceforge.net/cdrtools/cdrtools-$pkgver.tar.bz2)

build() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}
  sed -i 's|/opt/schily|/usr|g' DEFAULTS/Defaults.linux
  sed -i 's|DEFINSGRP=.*|DEFINSGRP=root|' DEFAULTS/Defaults.linux
  # install rscsi to /usr/bin instead of /usr/sbin
  sed -i 's|INSDIR=.*|INSDIR=bin|' rscsi/Makefile

  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/
}

check() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}
  make tests
}

package() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}

  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/ DESTDIR="$pkgdir" install

  # remove unneeded files that may cause conflicts
  rm -rf "$pkgdir"/usr/include
  rm -rf "$pkgdir"/usr/lib/{*.a,profiled}
  rm -rf "$pkgdir"/usr/share/man/man3

  # autoload 'sg' module needed by cdrecord
  install -d -m755 "$pkgdir"/usr/lib/modules-load.d/
  echo sg > "$pkgdir"/usr/lib/modules-load.d/cdrecord.conf

  # create symlinks for cdrkit compatibility
  cd "$pkgdir"/usr/bin
  ln -s cdrecord wodim
  ln -s readcd readom
  ln -s mkisofs genisoimage
  ln -s cdda2wav icedax
}