# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=zimg
pkgver=3.0.5
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=(x86_64)
groups=('media-libs')
url=https://github.com/sekrit-twc/zimg
license=(custom:WTFPL)
depends=(
  gcc-libs
  glibc
)
makedepends=(git)
provides=(libzimg.so)
options=(!emptydirs)
_tag=e5b0de6bebbcbc66732ed5afaafef6b2c7dfef87
source=(git+https://github.com/sekrit-twc/zimg.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd zimg
  ./autogen.sh
}

pkgver() {
  cd zimg
  git describe --tags | sed 's/^release-//'
}

build() {
  cd zimg
  ./configure \
    --prefix=/usr \
    --enable-x86simd \
    --disable-static
  make
}

package(){
  make DESTDIR="${pkgdir}" -C zimg install
  install -Dm 644 zimg/COPYING -t "${pkgdir}"/usr/share/licenses/zimg/
}

