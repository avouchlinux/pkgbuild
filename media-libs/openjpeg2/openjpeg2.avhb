# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=openjpeg2
_realname=openjpeg
pkgver=2.3.1
pkgrel=1
pkgdesc="An open source JPEG 2000 codec, version ${pkgver}"
arch=('x86_64')
groups=('media-libs')
license=('custom: BSD')
url="https://github.com/uclouvain/openjpeg"
makedepends=('cmake' 'doxygen')
depends=('zlib' 'libpng' 'libtiff' 'lcms2')
source=("https://github.com/uclouvain/openjpeg/archive/v$pkgver.tar.gz")

prepare() {
  cd ${srcdir}
  mkdir build

  # Install doxygen docs to the right directory
  sed -i -e "s:DESTINATION\ share/doc:DESTINATION\ share/doc/${pkgname}:" openjpeg-${pkgver}/doc/CMakeLists.txt
}

build() {
  cd "${srcdir}/build"

  cmake "../openjpeg-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_DOC=on

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg2"
  mv ${pkgdir}/usr/share/doc/openjpeg-*/LICENSE ${pkgdir}/usr/share/licenses/openjpeg2
  rmdir ${pkgdir}/usr/share/doc/openjpeg-*
}
