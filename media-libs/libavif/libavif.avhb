# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libavif
pkgver=1.1.1
pkgrel=2
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64)
groups=("media-libs")
url="https://github.com/AOMediaCodec/libavif"
license=(LicenseRef-libavif)
depends=(glibc aom dav1d librav1e.so svt-av1 libpng libjpeg-turbo libyuv libwebp)
makedepends=(cmake
             gdk-pixbuf2
             git
             nasm
             pandoc-cli)
source=($pkgname-$pkgver.tar.gz::https://github.com/AOMediaCodec/libavif/archive/refs/tags/v1.1.1.tar.gz)
sha256sums=('2868a336117886bd3b4094e8c48589e67152ae8f39b06c067f8c51ec2709f577')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_BUILD_MAN_PAGES=OFF \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=ON \
    -DAVIF_LIBSHARPYUV=SYSTEM \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install
  install -Dm644 $pkgname-$pkgver/LICENSE -t "${pkgdir}"/usr/share/licenses/$pkgname
}
