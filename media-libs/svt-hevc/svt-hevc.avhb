# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=svt-hevc
_realname=SVT-HEVC
pkgver=1.5.0
pkgrel=1
pkgdesc='Scalable Video Technology HEVC encoder'
arch=(x86_64)
url='https://github.com/OpenVisualCloud/SVT-HEVC'
license=(BSD)
makedepends=(cmake  git  nasm)
source=(https://github.com/OpenVisualCloud/SVT-HEVC/archive/v$pkgver.tar.gz)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  sed '/CMAKE_BUILD_TYPE Release/d' -i SVT-HEVC-$pkgver/CMakeLists.txt
}

build() {
  cd build

  export LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

  cmake ../SVT-HEVC-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DNATIVE=OFF
  make ${MAKEFLAGS}
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../SVT-HEVC-$pkgver/LICENSE.md -t "${pkgdir}"/usr/share/licenses/svt-hevc/
}