# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=svt-hevc
_realname=SVT-HEVC
pkgver=1.5.1
pkgrel=1
pkgdesc='Scalable Video Technology HEVC encoder'
arch=(x86_64)
groups=('media-libs')
url=https://github.com/OpenVisualCloud/SVT-HEVC
license=(BSD-2-Clause-Patent)
depends=(
  glibc
)
makedepends=(
  cmake
  git
  nasm
  ninja
)
_tag=b65eba07e6dee37407631cc441561960838b0333
source=(git+https://github.com/OpenVisualCloud/SVT-HEVC.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd SVT-HEVC
  git describe --tags | sed 's/^v//'
}

prepare() {
  sed '/CMAKE_BUILD_TYPE Release/d' -i SVT-HEVC/CMakeLists.txt
}

build() {
  cmake -S SVT-HEVC -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DNATIVE=OFF
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 SVT-HEVC/LICENSE.md -t "${pkgdir}"/usr/share/licenses/svt-hevc/
}
