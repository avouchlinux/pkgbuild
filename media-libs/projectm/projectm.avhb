# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=projectm
pkgname=('projectm' 'projectm-sdl' 'projectm-pulseaudio')
_realname=projectM
pkgver=3.1.3
pkgrel=1
url='https://github.com/projectM-visualizer/projectm'
arch=('x86_64')
groups=('media-libs')
license=('LGPL')
makedepends=('mesa-libgl' 'glm' 'qt5-base' 'ftgl' 'glew' 'sdl2' 'libpulse')
source=("https://github.com/projectM-visualizer/$pkgbase/releases/download/v$pkgver/projectM-$pkgver.tar.gz")

build() {
    cd "projectM-$pkgver"
    ./configure --prefix=/usr --enable-gles --enable-sdl --enable-threading --enable-qt
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
    make ${MAKEFLAGS}
}

package_projectm() {
    pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering'
    depends=('libgl' 'glm' 'ftgl' 'glew')


    cd "$srcdir/projectM-$pkgver"
    make DESTDIR="$pkgdir" install-data-am
    cd "$srcdir/projectM-$pkgver/src"
    make DESTDIR="$pkgdir" install-am
    cd "$srcdir/projectM-$pkgver/src/libprojectM"
    make DESTDIR="$pkgdir" install
    cd "$srcdir/projectM-$pkgver/src/NativePresets"
    make DESTDIR="$pkgdir" install

    find "$pkgdir" -iname "\.*" -delete
}

package_projectm-sdl() {
    pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering (sdl)'
    depends=('projectm' 'sdl2')

    cd "$srcdir/projectM-$pkgver/src/projectM-sdl"
    make DESTDIR="$pkgdir" install
}

package_projectm-pulseaudio() {
    pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering (pulseaudio)'
    depends=('projectm' 'qt5-base' 'libpulse')

    cd "$srcdir/projectM-$pkgver/src/projectM-pulseaudio"
    make DESTDIR="$pkgdir" install
}