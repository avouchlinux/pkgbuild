# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=x264
pkgver=164.rbaee400f
pkgrel=1
epoch=2
pkgdesc='Open Source H264/AVC video encoder'
arch=('x86_64')
groups=('media-libs')
url=https://www.videolan.org/developers/x264.html
license=(GPL)
depends=(libavcodec.so libavformat.so libavutil.so liblsmash.so libswscale.so)
makedepends=(git ffmpeg l-smash nasm)
provides=(libx264 libx264.so)
source=(git+https://code.videolan.org/videolan/x264.git)
sha256sums=(SKIP)

getsrc(){
    git clone https://code.videolan.org/videolan/x264.git
}

pkgver() {
  cd x264

  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _rev=$(git rev-parse --short HEAD)

  echo ${_ver}.r${_rev}
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  ../x264/configure \
    --prefix=/usr \
    --enable-shared \
    --enable-pic \
    --enable-lto \
    --disable-avs
  make ${MAKEFLAGS}
}

package() {
  make -C build DESTDIR="${pkgdir}" install-cli install-lib-shared
}