# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=x264
pkgver=0.164.r3108.31e19f9
pkgrel=1
epoch=2
pkgdesc='Open Source H264/AVC video encoder'
arch=('x86_64')
groups=('media-libs')
url=https://www.videolan.org/developers/x264.html
license=(GPL-2.0-only)
depends=(
  glibc
  liblsmash.so
)
makedepends=(
  git
  nasm
)
provides=(
  libx264
  libx264.so
)
conflicts=(
  libx264
  libx264-10bit
  libx264-all
)
replaces=(
  libx264
  libx264-10bit
  libx264-all
)
_commit=31e19f92f00c7003fa115047ce50978bc98c3a0d
source=(git+https://code.videolan.org/videolan/x264.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd x264
  ./version.sh | grep X264_POINTVER | sed -r 's/^#define X264_POINTVER "([0-9]+\.[0-9]+)\.([0-9]+) (.*)"$/\1.r\2.\3/'
}

build() {
  cd x264
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-pic \
    --enable-lto \
    --disable-avs
  make
}

package() {
  make -C x264 DESTDIR="${pkgdir}" BASHCOMPLETIONSDIR="/usr/share/bash-completion/completions" install-cli install-lib-shared install-bashcompletion
}

