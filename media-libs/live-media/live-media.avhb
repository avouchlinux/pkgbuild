# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=live-media
_realname=live
pkgver=2024.11.28
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('x86_64')
groups=('media-libs')
url='http://live555.com/liveMedia'
license=('LGPL')
depends=('gcc-libs' 'openssl')
source=(http://live555.com/liveMedia/public/live.$pkgver.tar.gz)
sha256sums=('e6c2ebfec6772c1f37084271e4e5236abc7f0e1fc4489f9869763440200ddcc2')
sha512sums=('62aeacea8f987731237c5924097a2aa4f650629f44d190637b2c960425f295b1360fa27ecdfe96636e7f29a23385ed872f54b66b402c38e3e76f6564f3cdc38f')

prepare() {
  cd live
  find . -name Makefile.head | xargs sed -e 's|/local||g' -i # Fix prefix
}

build() {
  cd live
  sed -E 's|(-DSOCKLEN_T=socklen_t)|\1 -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1|g' -i config.linux-with-shared-libraries
  ./genMakefiles linux-with-shared-libraries
  CXXFLAGS+=' -std=c++20' \
  make
}

package() {
  cd live
  make DESTDIR="$pkgdir" install
}
