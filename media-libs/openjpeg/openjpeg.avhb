# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=openjpeg
pkgver=1.5.1
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=('x86_64')
groups=('media-libs')
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib')
makedepends=('libtiff' 'lcms2' 'libpng' 'doxygen')
optdepends=('lcms2: j2k_to_image and image_to_j2k programs'
            'libpng: j2k_to_image and image_to_j2k programs')
source=(http://downloads.sourceforge.net/openjpeg.mirror/${pkgname}-${pkgver}.tar.gz)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	### patches from fedora
	## upstreamable patches
	patch -Np1 -i ../openjpeg-1.5.1-cmake_libdir.patch
	patch -Np1 -i ../openjpeg-1.5.1-doxygen_timestamp.patch
	# Only compile with -ffast-math, linking with it causes problem for library users
	# https://bugzilla.redhat.com/show_bug.cgi?id=1127544
	patch -Np1 -i ../openjpeg-fast-math.patch

	## upstream patches:
	# http://code.google.com/p/openjpeg/issues/detail?id=155
	patch -Np0 -i ../openjpeg-1.5-r2029.patch
	# http://code.google.com/p/openjpeg/issues/detail?id=152
	patch -Np0 -i ../openjpeg-1.5-r2031.patch
	# http://code.google.com/p/openjpeg/issues/detail?id=169
	patch -Np0 -i ../openjpeg-1.5-r2032.patch
	# http://code.google.com/p/openjpeg/issues/detail?id=166
	patch -Np0 -i ../openjpeg-1.5-r2033.patch

	## security patches
	# https://bugzilla.redhat.com/show_bug.cgi?id=1036491
	patch -Np1 -i ../openjpeg-1.5.1-CVE-2013-6052.patch
	# https://bugzilla.redhat.com/show_bug.cgi?id=1036493
	patch -Np1 -i ../openjpeg-1.5.1-CVE-2013-6053.patch
	# https://bugzilla.redhat.com/show_bug.cgi?id=1036495
	# omitted due to regression https://bugzilla.redhat.com/show_bug.cgi?id=1047494
	patch -Np1 -i ../openjpeg-1.5.1-CVE-2013-6045.patch
	# https://bugzilla.redhat.com/show_bug.cgi?id=1037945
	patch -Np1 -i ../openjpeg-1.5.1-CVE-2013-1447.patch
	# https://bugzilla.redhat.com/show_bug.cgi?id=1037948
	patch -Np1 -i ../openjpeg-1.5.1-CVE-2013-6887.patch
	patch -Np1 -i ../openjpeg-c99.patch


	autoreconf -fi
	# make sure we use system libs
	rm -rf thirdparty
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-shared --disable-silent-rules
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
