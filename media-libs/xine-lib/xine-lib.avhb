# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xine-lib
pkgver=1.2.13
pkgrel=1
_hgver=hg20240723
pkgdesc="A multimedia playback engine"
arch=('x86_64')
groups=('media-libs')
url='https://www.xine-project.org'
license=('GPL-2.0-or-later' 'LGPL-2.0-or-later')
depends=('ffmpeg' 'libjpeg-turbo' 'libpng' 'libx11' 'libxcb' 'libxext'
         'libxinerama' 'libxv' 'libxvmc' 'zlib')
optdepends=('libdvdnav: for dvd plugin'
            'libdvdread: for spu and dxr3 plugins'
            'vcdimager: for vcd plugin'
            'glu: for opengl and vaapi plugins'
            'sdl: for sdl plugin'
            'wayland: for egl_wl plugin'
            'aalib: for aalib plugin'
            'a52dec: for a52 plugin'
            'faad2: for faad plugin'
            'flac: for flac plugin'
            'libdca: for dts (dca) plugin'
            'libmad: for mad (mp3) plugin'
            'libmpcdec: for mpc (musepack) plugin'
            'libmodplug: for modplug plugin'
            'wavpack: for wavpack plugin'
            'gdk-pixbuf2: for gdk_pixbuf plugin'
            'imagemagick: for imagemagick plugin'
            'libcaca: for caca plugin'
            'libmng: for mng plugin'
            'libnfs: for nfs plugin'
            'libssh2: for ssh plugin'
            'smbclient: for samba plugin')
makedepends=('mercurial' 'mesa' 'libdvdnav' 'libdvdread' 'vcdimager' 'glu' 'sdl'
             'wayland' 'aalib' 'a52dec' 'faad2' 'flac' 'libdca' 'libmad'
             'libmodplug'  'libmpcdec' 'wavpack' 'gdk-pixbuf2' 'imagemagick'
             'libcaca' 'libmng' 'libnfs' 'libnsl' 'smbclient')
source=(# https://downloads.sourceforge.net/project/xine/xine-lib/$pkgver/xine-lib-$pkgver.tar.gz
        http://deb.debian.org/debian/pool/main/x/xine-lib-1.2/xine-lib-1.2_1.2.13+hg20240723.orig.tar.gz
        xine-lib-imagemagick7.patch)

prepare() {
    cd "${srcdir}/${pkgname}-1.2"
    ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-1.2"
    export CFLAGS+=' -fomit-frame-pointer -fcommon'

    # hack for recursive mutex support is needed, configure failes
    export CPPFLAGS+='-D_GNU_Source'
    ./configure \
        --prefix=/usr \
        --with-wavpack \
        --enable-vdpau \
        --with-external-dvdnav
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "${srcdir}/${pkgname}-1.2"
    make DESTDIR="${pkgdir}" install
}
