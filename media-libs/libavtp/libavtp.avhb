# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libavtp
pkgver=0.1.0
pkgrel=1
pkgdesc="Open source implementation of Audio Video Transport Protocol"
arch=('x86_64')
groups=('media-libs')
url="https://github.com/Avnu/libavtp"
license=('BSD')
depends=('glibc')
makedepends=('cmocka' 'meson')
provides=('libavtp.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Avnu/${pkgname}/archive/v${pkgver}.tar.gz")

build() {
  avouch-meson $pkgname-$pkgver build \
    --prefix=/usr
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 $pkgname-$pkgver/{CONTRIBUTING,HACKING,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
