# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libebml
pkgver=1.4.5
pkgrel=1
pkgdesc='Extensible Binary Meta Language library'
arch=('x86_64')
groups=('media-libs')
url=https://github.com/Matroska-Org/libebml
license=(LGPL2.1)
depends=(gcc-libs)
makedepends=(
  cmake
  git
  ninja
)
provides=(libebml.so)
_tag=1878e784321673561039a6a37076b2736f4dc98f
source=(git+https://github.com/Matroska-Org/libebml.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd libebml
  git describe --tags | sed 's/^release-//'
}

build() {
  cmake -S libebml -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
