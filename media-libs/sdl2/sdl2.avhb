# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=sdl2
_realname=SDL2
pkgver=2.0.20
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('x86_64')
groups=('media-libs')
url="http://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxext' 'libxrender' 'libx11' 'libgl' 'libxcursor')
makedepends=('alsa-lib' 'mesa' 'libpulse' 'libxrandr' 'libxinerama' 'wayland' 'libxkbcommon')
optdepends=('alsa-lib: ALSA audio driver'
            'libpulse: PulseAudio audio driver')
source=("http://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")

prepare() {
	mkdir ${srcdir}/build
}

build() {
	cd ${srcdir}/build
	../${_realname}-${pkgver}/configure --prefix=/usr \
		--enable-sdl-dlopen \
		--disable-arts --disable-esd --disable-nas \
		--enable-alsa --enable-pulseaudio-shared \
		--enable-video-wayland \
		--enable-ibus --enable-fcitx \
		--disable-rpath
	make ${MAKEFLAGS}
}

package() {
	cd ${srcdir}/build
	make DESTDIR="${pkgdir}" install

	# license
	install -Dm644 ../SDL2-$pkgver/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
