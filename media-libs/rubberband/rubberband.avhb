# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=rubberband
pkgver=1.9
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=('x86_64')
groups=('media-libs')
url="http://www.breakfastquay.com/rubberband/"
license=('GPL')
depends=('libsamplerate' 'fftw' 'vamp-plugin-sdk')
makedepends=('ladspa')
source=("http://code.breakfastquay.com/attachments/download/34/$pkgname-$pkgver.tar.bz2")

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  export JAVA_HOME="/usr/lib/jvm/java-${java_major}-openjdk"
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
  make jni
}

package() {
  depends+=('libfftw3.so' 'libsamplerate.so' 'libsndfile.so' 'libvamp-sdk.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # docs
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}