# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xfce4-session
pkgver=4.16.0
pkgrel=1
pkgdesc="A session manager for Xfce"
arch=('x86_64')
groups=('xfce4')
url="http://www.xfce.org/"
license=('GPL2')
depends=('libxfce4ui' 'libwnck3' 'xfconf' 'libsm' 'polkit' 'xorg-iceauth'
         'xorg-xinit' 'xorg-xrdb' 'polkit-gnome' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xfce4-screensaver: for locking screen with xflock4'
            'xscreensaver: for locking screen with xflock4'
            'gnome-screensaver: for locking screen with xflock4'
            'xlockmore: for locking screen with xflock4'
            'slock: for locking screen with xflock4')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        source-system-xinitrc-scripts.patch
        xfce-polkit-gnome-authentication-agent-1.desktop)


prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=15440
  patch -Np1 -i ../source-system-xinitrc-scripts.patch
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/xfce4 \
        --localstatedir=/var \
        --disable-static \
        --disable-debug
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Provide a default PolicyKit Authentication Agent (FS#42569)
    install -Dm644 "$srcdir/xfce-polkit-gnome-authentication-agent-1.desktop" \
       "$pkgdir/etc/xdg/autostart/xfce-polkit-gnome-authentication-agent-1.desktop"
}
