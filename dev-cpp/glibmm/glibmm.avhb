# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=glibmm
pkgname=(
  glibmm
  glibmm-docs
)
pkgver=2.84.0
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="http://www.gtkmm.org/"
arch=('x86_64')
groups=('dev-cpp')
license=(LGPL-2.1-or-later)
depends=(
  glib2
  libsigc++
)
makedepends=(
  clang
  git
  meson
  mm-common
  perl-xml-parser
)
checkdepends=(glib-networking)

source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
  avouch-meson $pkgbase-$pkgver build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm() {
  DESTDIR="$pkgdir" meson install -C build

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_glibmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}