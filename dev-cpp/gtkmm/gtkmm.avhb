# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=gtkmm
pkgname=('gtkmm' 'gtkmm-docs')
pkgver=2.24.5
pkgrel=1
arch=('x86_64')
groups=('dev-cpp')
makedepends=('gtk2' 'pangomm' 'atkmm' 'glibmm-docs')
license=('LGPL')
options=('!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.24/${pkgbase}-${pkgver}.tar.xz)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# glibmm 2.46 uses C++11 features
	CXXFLAGS+=' -std=c++11' ./configure --prefix=/usr
	make ${MAKEFLAGS}
}

package_gtkmm() {
	pkgdesc="C++ bindings for gtk2"
	depends=('gtk2' 'pangomm' 'atkmm')

	cd "${srcdir}/${pkgbase}-${pkgver}"

	sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

	make DESTDIR="${pkgdir}" install
}

package_gtkmm-docs() {
	pkgdesc="Developer documentation for gtkmm v2"

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C docs DESTDIR="${pkgdir}" install

}
