# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=yaml-cpp
pkgver=0.6.3
pkgrel=1
pkgdesc="YAML parser and emitter in C++, written around the YAML 1.2 spec"
arch=('x86_64')
groups=('dev-cpp')
url="https://github.com/jbeder/yaml-cpp"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
source=(https://github.com/jbeder/yaml-cpp/archive/yaml-cpp-${pkgver}.tar.gz)

prepare() {
    mkdir build
}

build() {
    cd build
    cmake ../$pkgname-$pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DYAML_BUILD_SHARED_LIBS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make ${MAKEFLAGS}
}

check() {
    cd build
    make test
    test/run-tests
}

package() {
    make -C build DESTDIR="$pkgdir" install
    # avoid conflicting gtest/gmock files
    rm -rf "$pkgdir"/usr/include/{gmock,gtest} "$pkgdir"/usr/lib/lib{gmock,gtest}*.so
}
