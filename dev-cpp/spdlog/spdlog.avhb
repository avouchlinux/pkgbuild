# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=spdlog
pkgver=1.5.0
pkgrel=1
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('x86_64')
groups=('dev-cpp')
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=('fmt')
makedepends=('cmake')
source=("https://github.com/gabime/spdlog/archive/v$pkgver.tar.gz")
provides=('libspdlog.so')

build() {
    mkdir build
    cd build
    cmake ../"$pkgname-$pkgver" \
        -DSPDLOG_BUILD_BENCH=OFF \
        -DSPDLOG_BUILD_EXAMPLES=OFF \
        -DSPDLOG_FMT_EXTERNAL=ON \
        -DSPDLOG_BUILD_SHARED=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make ${MAKEFLAGS}
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 ../"$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
