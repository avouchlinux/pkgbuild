# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=cairomm-1.16
pkgname=(
  cairomm-1.16
  cairomm-1.16-docs
)
_realname=cairomm
pkgver=1.18.0
pkgrel=1
pkgdesc="C++ bindings for Cairo"
url="https://www.cairographics.org/cairomm/"
arch=(x86_64)
groups=('dev-cpp')
license=(
  LGPL
  MPL
)
depends=(
  cairo
  libsigc++-3.0
)
makedepends=(
  git
  meson
  mm-common
)
options=(!emptydirs)
source=("https://gitlab.freedesktop.org/cairo/$_realname/-/archive/$pkgver/$_realname-$pkgver.tar.bz2")
b2sums=('SKIP')

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  avouch-meson $_realname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_cairomm-1.16() {
  depends+=(libsigc-3.0.so)
  provides=(libcairomm-1.16.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_cairomm-1.16-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}
