# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=gtkmm4
pkgname=(gtkmm4 gtkmm4-docs)
_realname=gtkmm
pkgver=4.16.0
pkgrel=1
pkgdesc="C++ bindings for GTK+ 3"
url="http://www.gtkmm.org/"
arch=('x86_64')
groups=('dev-cpp')
license=(LGPL-2.1-or-later)
depends=(
  cairo
  cairomm-1.16
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm-2.68
  graphene
  gtk4
  pangomm-2.48
  vulkan-icd-loader
)
makedepends=(
  cairomm-1.16-docs
  git
  glibmm-2.68-docs
  meson
  mm-common
  pangomm-2.48-docs
)
source=("https://gitlab.gnome.org/GNOME/$_realname/-/archive/$pkgver/$_realname-$pkgver.tar.bz2")
sha256sums=('SKIP')

build() {
  avouch-meson $_realname-$pkgver build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gtkmm4() {
  DESTDIR="$pkgdir" meson install -C build

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_gtkmm4-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
