# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=bonnie++
pkgver=2.00a
pkgrel=1
pkgdesc="Based on the Bonnie hard drive benchmark by Tim Bray"
arch=('x86_64')
groups=('app-benchmark')
url="https://www.coker.com.au/bonnie++/"
license=('GPL')
depends=('perl')
options=('debug')
source=(https://www.coker.com.au/$pkgname/$pkgname-$pkgver.tgz)
sha256sums=('a8d33bbd81bc7eb559ce5bf6e584b9b53faea39ccfb4ae92e58f27257e468f0e')
build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  sed \
    -e "s:CFLAGS=-O2 @debug@ -DNDEBUG \$(WFLAGS) \$(MORECFLAGS):CFLAGS=${CFLAGS} -std=c++14:" \
    -e "s:LINK=@CXX@:LINK=@CXX@ ${LDFLAGS}:" \
    -i Makefile.in
  ./configure --prefix=/usr
  make -Otarget V=1
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # https://www.archlinux.org/todo/move-files-to-usrbin-if-possible/
  sed -i 's/sbin/bin/' Makefile
  make prefix="$pkgdir"/usr install

  install -d "$pkgdir"/usr/share/doc/bonnie++
  install -Dm644 "$srcdir"/$pkgname-$pkgver/readme.html "$pkgdir"/usr/share/doc/bonnie++/readme.html
}

