# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt6-svg
pkgver=6.7.3
pkgrel=1
_realname="${pkgname/6-/}-everywhere-src-${pkgver}"
arch=('x86_64')
groups=('dev-qt6')
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for displaying the contents of SVG files'
depends=(gcc-libs
         glibc
         qt6-base
         zlib)
makedepends=(cmake
             git
             ninja)
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/$_realname.tar.xz")
sha256sums=('3fe64ec5653668e990bfa6d1b57e5f5e6d9227113c83e3d7d4e744d9e69181a5')

build() {
  cmake -B build -S $_realname -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
