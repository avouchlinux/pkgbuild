# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt6-imageformats
pkgver=6.8.0
pkgrel=1
_realname="${pkgname/6-/}-everywhere-src-${pkgver}"
arch=('x86_64')
groups=('dev-qt6')
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=(gcc-libs
         glibc
         jasper
         libmng
         libtiff
         libwebp
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/$_realname.tar.xz")
sha256sums=('b0379ba6bbefbc48ed3ef8a1d8812531bd671362f74e0cffa6adf67bb1139206')

build() {
    cmake -B build -S $_realname -G Ninja \
        -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
    cmake --build build

}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -d "$pkgdir"/usr/share/licenses
    ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
