# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt6-languageserver
pkgver=6.8.2
pkgrel=1
_realname="${pkgname/6-/}"
_tarname="${pkgname/6-/}-everywhere-src-${pkgver}"
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='An implementation of the Language Server Protocol'
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${pkgver}/submodules/${_tarname}.tar.xz")
sha256sums=('d1af92272405814f3f26d35e48e52a27a39eb83c97f8e9b7abeff6bafe69151a')

build() {
  cmake -B build -S $_tarname -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
