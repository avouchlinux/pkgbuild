# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt6-wayland
_pkgver=6.8.2
pkgver=6.8.2
pkgrel=1
_realname=${pkgname/6-/}
_tarname="${pkgname/6-/}-everywhere-src-${pkgver}"
arch=('x86_64')
groups=('dev-qt6')
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides APIs for Wayland'
depends=(gcc-libs
         glibc
         libglvnd
         libxkbcommon
         qt6-base
         qt6-declarative
         qt6-svg
         wayland)
makedepends=(cmake
             git
             ninja)
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/$_tarname.tar.xz")
sha256sums=('09927060c5595c33176d7a9828b427a0934484c0741d910437a49c6cad5165d4')

build() {
  cmake -B build -S $_tarname -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
