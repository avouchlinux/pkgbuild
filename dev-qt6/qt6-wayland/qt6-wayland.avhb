# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt6-wayland
_qtver=6.0.2
pkgver=${_qtver/-/}
pkgrel=1
_realname="${pkgname/6-/}-everywhere-src-${_qtver}"
arch=('x86_64')
groups=('dev-qt6')
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides APIs for Wayland'
depends=(qt6-declarative libxcomposite)
makedepends=(cmake vulkan-headers)
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_realname.tar.xz")
sha256sums=('c911e0dcff8c0e9c425df0f4db968bf4159444571deb38f6920dd6f448e985e9')

build() {
  cmake -B build -S $_realname -G Ninja
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}