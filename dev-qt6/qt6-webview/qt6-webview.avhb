# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt6-webview
pkgver=6.8.0
pkgrel=1
_realname="${pkgname/6-/}"
_tarname="${pkgname/6-/}-everywhere-src-${pkgver}"
arch=(x86_64)
groups=(dev-qt6)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides a way to display web content in a QML application'
depends=(gcc-libs
         glibc
         qt6-base
         qt6-declarative
         qt6-webengine)
makedepends=(cmake
             git
             ninja)
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${pkgver}/submodules/${_tarname}.tar.xz")
sha256sums=('7af65542b90fc6d970ba2d888d5b779d9ff4b126475e87fa5f2e165f9f96dcf6')

build() {
  cmake -B build -S $_tarname -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
