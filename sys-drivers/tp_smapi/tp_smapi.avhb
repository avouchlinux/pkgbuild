# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=tp_smapi
pkgver=0.43
pkgrel=1
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('x86_64')
groups=('sys-drivers')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
makedepends=('linux-headers' 'git')
_commit=a63729ab30d85430048f65c37f29188ab484cd52  # tags/tp-smapi/0.43
source=("git+https://github.com/evgeni/tp_smapi#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^tp-smapi\///;s/-/+/g'
}

build() {
  cd $pkgname

  # https://bugs.archlinux.org/task/54975 (kernel has no _GLOBAL_OFFSET_TABLE_):
  # Clear EXTRA_CFLAGS since it defaults to injecting CFLAGS and -fno-plt breaks the modules

  make HDAPS=1 KVER="$(</usr/src/linux/version)" EXTRA_CFLAGS=
}

package() {
  depends=('linux')

  local extradir=/usr/lib/modules/$(</usr/src/linux/version)/extramodules
  cd $pkgname

  find . -name "*.ko" -exec install -Dt "$pkgdir$extradir" {} +
  find "$pkgdir" -name "*.ko" -exec strip --strip-debug {} +
  find "$pkgdir" -name "*.ko" -exec xz {} +

  # load module on startup
  echo tp_smapi | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}
