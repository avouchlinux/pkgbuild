# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lynx
pkgver=2.9.2
pkgrel=1
pkgdesc="A text browser for the World Wide Web"
url="https://lynx.browser.org/"
arch=('x86_64')
groups=('www-apps')
license=(GPL-2.0-only)
depends=(brotli
         bzip2
         glibc
         libidn2
         ncurses
         openssl
         zlib)
backup=(etc/lynx.cfg)
options=(!lto)
_archive="$pkgname$pkgver"
source=("https://invisible-mirror.net/archives/lynx/tarballs/$_archive.tar.bz2"{,.asc})
sha256sums=('7374b89936d991669e101f4e97f2c9592036e1e8cdaa7bafc259a77ab6fb07ce'
            'SKIP')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # Thomas Dickey <dickey@invisible-island.net>

build() {
	cd "$_archive"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-ssl=/usr \
		--enable-externs \
		--enable-nls \
		--enable-ipv6 \
		--mandir=/usr/share/man
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	# FS#20404 - points to local help
	sed -i -e "s|^HELPFILE.*$|HELPFILE:file:///usr/share/doc/lynx/lynx_help/lynx_help_main.html|" "${pkgdir}/etc/lynx.cfg"
	install -d "$pkgdir/usr/share/doc/lynx"
	cp -rf lynx_help "$pkgdir/usr/share/doc/lynx"
}
