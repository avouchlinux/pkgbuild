# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xorg-font-cyrillic
pkgver=1.0.4
pkgrel=1
pkgdesc="X.org cyrillic fonts"
arch=(any)
groups=('media-fonts')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(xorg-fonts-encodings xorg-fonts-alias xorg-font-utils fontconfig)
install=xfonts.install
source=(${url}/releases/individual/font/font-cronyx-cyrillic-1.0.4.tar.xz
        ${url}/releases/individual/font/font-misc-cyrillic-1.0.4.tar.xz
        ${url}/releases/individual/font/font-screen-cyrillic-1.0.5.tar.xz
        ${url}/releases/individual/font/font-winitzki-cyrillic-1.0.4.tar.xz)

build() {

cd "${srcdir}"
for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/cyrillic
      make
      popd
    fi
done

}

package() {

cd "${srcdir}"
install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-cyrillic-*}"
      popd
    fi
done
rm -f "${pkgdir}"/usr/share/fonts/type1/fonts.*

}

