# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xorg-fonts-encodings
_realname=encodings
pkgver=1.0.5
pkgrel=1
pkgdesc="X.org font encoding files"
arch=(any)
groups=('media-fonts')
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-mkfontscale' 'xorg-util-macros' 'xorg-font-util')
options=(!makeflags)
source=(${url}/releases/individual/font/encodings-${pkgver}.tar.bz2)
sha1sums=('24ace2470403f85a0d393769204029bd8247992a')

build() {
	cd "${srcdir}/encodings-${pkgver}"
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/encodings-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	# regenerate encodngs file not to include non-compressed versions
	cd $pkgdir/usr/share/fonts/encodings/large
	mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings/large -e . .
	cd $pkgdir/usr/share/fonts/encodings/
	mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings -e . -e large .
}
