# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xorg-fonts-100dpi
pkgver=1.0.3
pkgrel=1
pkgdesc="X.org 100dpi fonts"
arch=('any')
groups=('media-fonts')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
source=(${url}/releases/individual/font/font-adobe-100dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-adobe-utopia-100dpi-1.0.4.tar.bz2
        ${url}/releases/individual/font/font-bh-100dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bh-lucidatypewriter-100dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bitstream-100dpi-1.0.3.tar.bz2)

prepare() {

	tar -jxf "${srcarchivedir}/font-adobe-100dpi-1.0.3.tar.bz2"
	tar -jxf "${srcarchivedir}/font-adobe-utopia-100dpi-1.0.4.tar.bz2"
	tar -jxf "${srcarchivedir}/font-bh-100dpi-1.0.3.tar.bz2"
	tar -jxf "${srcarchivedir}/font-bh-lucidatypewriter-100dpi-1.0.3.tar.bz2"
	tar -jxf "${srcarchivedir}/font-bitstream-100dpi-1.0.3.tar.bz2"
}

build() {

cd "${srcdir}"
for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/100dpi
      make
      popd
    fi
done

}

package() {

cd "${srcdir}"
install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-100dpi-*}"
      popd
    fi
done
rm -f "${pkgdir}"/usr/share/fonts/100dpi/fonts.*

}

