# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=perl-xml-parser
_realname=XML-Parser
pkgver=2.46
pkgrel=1
pkgdesc="Expat-based XML parser module for perl"
arch=('x86_64')
groups=('dev-perl')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/XML-Parser/"
depends=('perl' 'expat')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/XML-Parser-${pkgver}.tar.gz")

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
	perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# remove perllocal.pod and .packlist
	find "${pkgdir}" -name perllocal.pod -delete
	find "${pkgdir}" -name .packlist -delete
}
