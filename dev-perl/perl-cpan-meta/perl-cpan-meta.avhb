# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=perl-cpan-meta
_realname=CPAN-Meta
pkgver=2.150010
pkgrel=1
pkgdesc='the distribution metadata for a CPAN dist'
arch=('any')
groups=('dev-perl')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-cpan-meta-requirements'
         'perl-cpan-meta-yaml' 'perl-parse-cpan-meta')
url='https://metacpan.org/release/CPAN-Meta'
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/CPAN-Meta-$pkgver.tar.gz")

build() (
	cd "${srcdir}/${_realname}-${pkgver}"
	export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
	unset PERL5LIB PERL_MM_OPT
	/usr/bin/perl Makefile.PL
	make
)

check() (
	cd "${srcdir}/${_realname}-${pkgver}"
	export PERL_MM_USE_DEFAULT=1
	unset PERL5LIB
	make test
)

package() (
	cd "${srcdir}/${_realname}-${pkgver}"
	make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)
