# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=perl-digest-hmac
_realname=Digest-HMAC
pkgver=1.04
pkgrel=1
pkgdesc="Perl Module: Keyed-Hashing for Message Authentication."
arch=('any')
groups=('dev-perl')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-digest-sha1' 'perl>=5.10.0')
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz)
md5sums=('e6a5d6f552da16eacb5157ea4369ff9d')

build() {
	cd "${srcdir}/Digest-HMAC-${pkgver}"
	# install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${srcdir}/Digest-HMAC-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/Digest-HMAC-${pkgver}"
	make install DESTDIR=${pkgdir}

	find $pkgdir -name '.packlist' -delete
	find $pkgdir -name '*.pod' -delete
}

