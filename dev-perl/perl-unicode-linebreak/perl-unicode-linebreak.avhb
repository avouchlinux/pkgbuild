# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=perl-unicode-linebreak
_realname=Unicode-LineBreak
pkgver=2019.001
pkgrel=1
pkgdesc="UAX #14 Unicode Line Breaking Algorithm"
arch=('x86_64')
url="https://metacpan.org/release/${_realname}"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-mime-charset' 'libthai')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/N/NE/NEZUMI/${_realname}-${pkgver}.tar.gz
        LineBreakTest-8.0.0.txt::https://www.unicode.org/Public/8.0.0/ucd/auxiliary/LineBreakTest.txt
        GraphemeBreakTest-8.0.0.txt::https://www.unicode.org/Public/8.0.0/ucd/auxiliary/GraphemeBreakTest.txt)
# Checksums from https://cpan.metacpan.org/authors/id/N/NE/NEZUMI/CHECKSUMS


prepare() {
    # Copy Unicode 8.0 test files. Later versions do not pass.
    cp -av "${srcdir}/LineBreakTest-8.0.0.txt" "${srcdir}/${_realname}-${pkgver}/test-data/LineBreakTest.txt"
    cp -av "${srcdir}/GraphemeBreakTest-8.0.0.txt" "${srcdir}/${_realname}-${pkgver}/test-data/GraphemeBreakTest.txt"
}

build() {
    cd "${srcdir}/${_realname}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_realname}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
