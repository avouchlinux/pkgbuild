# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=perl-path-class
_realname=Path-Class
pkgver=0.37
pkgrel=1
pkgdesc="Cross-platform path specification manipulation"
arch=(any)
groups=('dev-perl')
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url="http://search.cpan.org/dist/Path-Class"
source=("http://search.cpan.org/CPAN/authors/id/K/KW/KWILLIAMS/Path-Class-${pkgver}.tar.gz")
md5sums=('575b60a5c5e22e259c1df62a59fdfe85')

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
	PERL_AUTOINSTALL=--skipdeps \
	PERL_MM_OPT="INSTALLDIRS=vendor" \
	PERL_MB_OPT="--installdirs vendor" \
	MODULEBUILDRC=/dev/null

	/usr/bin/perl Build.PL
	./Build
}

check(){
	cd "${srcdir}/${_realname}-${pkgver}"

	export PERL_MM_USE_DEFAULT=1 PERL5LIB=""   
	./Build test
}
package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	export PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR=$pkgdir" \
	PERL_MB_OPT="--installdirs vendor --destdir $pkgdir" 

	./Build install

	find $pkgdir -name '.packlist' -delete
	find $pkgdir -name '*.pod' -delete
}
