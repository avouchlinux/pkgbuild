# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=snowball
pkgname=(snowball libstemmer)
pkgver=2.0.0
pkgrel=1
pkgdesc="String processing language for creating stemming algorithms"
arch=('x86_64')
groups=('sys-libs')
url="http://snowballstem.org/"
license=(BSD)
depends=(glibc)
makedepends=(git)
source=("https://github.com/snowballstem/snowball/archive/v$pkgver.tar.gz")

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../dynamiclib.patch
}

build() {
  cd $pkgbase-$pkgver
  make
}

check() {
  cd $pkgbase-$pkgver
  # Disabled - needs external test data
  #make check
}

package_snowball() {
  cd $pkgbase-$pkgver
  install -d "$pkgdir/usr/bin"
  install -t "$pkgdir/usr/bin" snowball stemwords
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/snowball/LICENSE"
}

package_libstemmer() {
  pkgdesc="Stemming library supporting several languages"
  cd $pkgbase-$pkgver
  install -Dm644 {.,"$pkgdir"/usr}/include/libstemmer.h
  install -D {.,"$pkgdir"/usr/lib}/libstemmer.so.0.0.0
  ln -s libstemmer.so.0.0.0 "$pkgdir/usr/lib/libstemmer.so.0"
  ln -s libstemmer.so.0 "$pkgdir/usr/lib/libstemmer.so"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libstemmer/LICENSE"
}
