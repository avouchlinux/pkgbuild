# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=dbus-glib
pkgver=0.112
pkgrel=1
pkgdesc="GLib bindings for DBUS"
arch=('x86_64')
groups=('sys-libs')
url='https://www.freedesktop.org/wiki/Software/dbus/'
license=('AFL-2.1 OR GPL-2.0-or-later')
depends=('dbus' 'expat' 'glib2' 'glibc')
makedepends=('python')
source=("https://dbus.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc}
        'dbus-glib-bash-completion.patch::https://gitlab.freedesktop.org/dbus/dbus-glib/-/merge_requests/5.patch')
sha256sums=('7d550dccdfcd286e33895501829ed971eeb65c614e73aadb4a08aeef719b143a'
            'SKIP'
            'edbdd4fb5fce358b427da1a1c68347ce09ca444da6ac67ff1fcd6cc67fc92f99')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90')


prepare() {
  patch -d $pkgname-$pkgver -Np1 -i ../dbus-glib-bash-completion.patch # Fix bash completion
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--enable-tests
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR="$pkgdir" install completiondir=/usr/share/bash-completion/completions
	mv "$pkgdir"/usr/share/bash-completion/completions/{dbus-bash-completion.sh,dbus-send}
}