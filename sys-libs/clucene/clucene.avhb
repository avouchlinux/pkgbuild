# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=clucene
pkgver=2.3.3.4
pkgrel=1
pkgdesc="C++ port of the high-performance text search engine Lucene"
arch=('x86_64')
groups=('sys-libs')
url="http://clucene.sourceforge.net/"
license=('APACHE' 'LGPL')
depends=('gcc-libs' 'zlib' 'boost-libs')
makedepends=('cmake' 'boost')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-core-$pkgver.tar.gz
		# Fedora patches
		clucene-core-2.3.3.4-pkgconfig.patch
        clucene-core-2.3.3.4-install_contribs_lib.patch
        # upstream patch
        fix_zlib_detections.diff
        # LibreOffice patches
        clucene-warnings.patch
        clucene-gcc-atomics.patch
        clucene-debug.patch
        clucene-narrowing-conversions.patch
        clucene-multimap-put.patch)

prepare() {
	if [ ! -d  build ]; then
		mkdir build
	fi
	cd ${srcdir}/${pkgname}-core-${pkgver}

	# add missing contrib-libs needed by LibO 3.6, patch by FC
	patch -Np1 -i ${srcdir}/clucene-core-2.3.3.4-install_contribs_lib.patch
	# pkgconfig file is missing clucene-shared (upstream ID: 3461512), patch by FC
	patch -Np1 -i ${srcdir}/clucene-core-2.3.3.4-pkgconfig.patch
	# one upstream postrelease commit for proper zlib detection
	patch -Np1 -i ${srcdir}/fix_zlib_detections.diff
	# LibreOffice patches http://cgit.freedesktop.org/libreoffice/core/tree/clucene/patches
	patch -Np0 -i ${srcdir}/clucene-warnings.patch
	patch -Np0 -i ${srcdir}/clucene-gcc-atomics.patch
	patch -Np0 -i ${srcdir}/clucene-debug.patch
	patch -Np0 -i ${srcdir}/clucene-narrowing-conversions.patch
	patch -Np0 -i ${srcdir}/clucene-multimap-put.patch

	# FS#77036 / https://sourceforge.net/p/clucene/bugs/235/ 
  	patch -Np1 -i ../0001-Fix-missing-include-time.h.patch

  	# nuke bundled code
  	rm -rfv src/ext/{boost/,zlib/}
}

build() {
	cd ${srcdir}/build
	cmake ${srcdir}/${pkgname}-core-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_ASCII_MODE=OFF \
		-DENABLE_PACKAGING=OFF \
		-DBUILD_CONTRIBS_LIB:BOOL=ON \
		-DLIB_DESTINATION:PATH=/usr/lib \
		-DLUCENE_SYS_INCLUDES:PATH=/usr/lib \
		-DDISABLE_MULTITHREADING=OFF
	make ${MAKEFLAGS}
}

#check() {
	#  cd $srcdir/$pkgname-core-$pkgver
	#  make cl_test -C build
	#  make test -C build || /bin/true # currently fails the tests as expected (see FC comment)
#}

package() {
	cd ${srcdir}/build
	make DESTDIR="$pkgdir" install
	rm -rfv $pkgdir/usr/lib/CLuceneConfig.cmake
	  
	cd $pkgdir/usr/include//CLucene
	ln -s /usr/lib/CLucene/clucene-config.h .

	# license
	install -D -m644 ${srcdir}/${pkgname}-core-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

