# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libgee
pkgver=0.20.6
pkgrel=1
pkgdesc="A collection library providing GObject-based interfaces and classes for commonly used data structures"
arch=('x86_64')
groups=('sys-libs')
url="https://git.gnome.org/browse/libgee"
license=(LGPL2.1)
depends=(glib2)
makedepends=(gobject-introspection vala autoconf-archive git valadoc)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --disable-static
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	  # generates a bazillion traps - make sure systemd's core_pattern
	  # is deactivated, or you'll DoS the journal and the system
	  #make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install
}
