# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libinih
pkgver=57
pkgrel=1
pkgdesc='A simple .INI file parser written in C'
groups=('sys-libs')
arch=(x86_64)
url=https://github.com/benhoyt/inih
license=(BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libinih.so
  libINIReader.so
)
_tag=9cecf0643da0846e77f64d10a126d9f48b9e05e8
source=(git+https://github.com/benhoyt/inih.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd inih
  git describe --tags | sed 's/^r//'
}

build() {
  avouch-meson inih build \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm 644 inih/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libinih/
}
