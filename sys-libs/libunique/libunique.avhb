# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libunique
pkgver=1.1.6
pkgrel=1
pkgdesc="Library for writing single instance applications"
arch=('x86_64')
groups=('sys-libs')
license=('LGPL')
depends=('gtk2')
makedepends=('gtk-doc' 'gobject-introspection')
url="http://live.gnome.org/LibUnique"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.1/${pkgname}-${pkgver}.tar.bz2
        libunique-1.1.6-upstream_fixes-1.patch
)
sha256sums=('e5c8041cef8e33c55732f06a292381cb345db946cf792a4ae18aa5c66cdd4fbb'
            'bf99ddbf9104502ea498a6812ec2d700f61f4dbbaa2471cd52174b0b27070769')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -Np1 -i "${srcdir}/libunique-1.1.6-upstream_fixes-1.patch"
	sed -e 's/-Werror//' -i configure.ac
	autoreconf -fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc \
	--localstatedir=/var --disable-dbus
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Remove documentation, provided by libunique3
	rm -r "$pkgdir/usr/share/gtk-doc"
}
