# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=zeitgeist
pkgver=1.0.3
pkgrel=1
pkgdesc="Service logging user activities and events"
arch=('x86_64')
groups=('sys-libs')
url="https://launchpad.net/zeitgeist/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3 xapian-core sqlite)
makedepends=(gobject-introspection vala raptor python3-rdflib git)
source=("https://launchpad.net/zeitgeist/1.0/$pkgver/+download/zeitgeist-$pkgver.tar.xz")
validpgpkeys=('E4884AEEDE4CC02043C3D8045DECDBA89270E723') #Rico Tzschichholz <ricotz@ubuntu.com>

prepare() {
	cd $pkgname
	autoreconf -vif
}

build() {
	cd $pkgname
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib \
		--enable-fts
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make ${MAKEFLAGS}
}

check() {
	cd $pkgname
	# Log test fails in chroot, as we have no X and no proper dbus
	make ${MAKEFLAGS} -k check || :
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
