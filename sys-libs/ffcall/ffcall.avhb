# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=ffcall
_realname=libffcall
pkgver=2.4
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('x86_64')
groups=('sys-libs')
url="https://www.gnu.org/software/libffcall/"
license=('GPL2')
depends=('glibc')
options=('!makeflags' 'staticlibs')
source=(https://ftp.gnu.org/gnu/libffcall/libffcall-${pkgver}.tar.gz)
sha256sums=('8ef69921dbdc06bc5bb90513622637a7b83a71f31f5ba377be9d8fd8f57912c2')

build() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  [ "$CARCH" = "x86_64" ] && CONFIGFLAG="--with-pic"
  CFLAGS+=" -ffat-lto-objects" \
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  # no multi threading
  make -j1
}

check() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
