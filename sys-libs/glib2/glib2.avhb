# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=glib2
pkgname=('glib2' 'glib2-docs')
_realname=glib
pkgver=2.80.0
pkgrel=1
pkgdesc="Low level core library"
arch=('x86_64')
groups=('sys-libs')
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL2.1)
depends=(pcre libffi libutil-linux zlib)
makedepends=(gettext gtk-doc shared-mime-info python libelf git util-linux meson dbus)
checkdepends=(desktop-file-utils)
optdepends=('python: gdbus-codegen, glib-genmarshal, glib-mkenums, gtester-report'
            'libelf: gresource inspection tool')
options=(!strip)
source=("https://download.gnome.org/sources/glib/2.78/glib-$pkgver.tar.xz"
        noisy-glib-compile-schemas.diff
        glib-compile-schemas.hook gio-querymodules.hook)

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  # Suppress noise from glib-compile-schemas.hook
  patch -Np1 -i ../noisy-glib-compile-schemas.diff
}

build() {
  local meson_options=(
    --default-library both
    -D glib_debug=disabled
    -D documentation=true
    -D introspection=disabled
    -D man-pages=enabled
    # -D selinux=disabled
    -D sysprof=enabled
  )

  # Produce more debug info: GLib has a lot of useful macros
  export CFLAGS+=" -DG_DISABLE_CAST_CHECKS -g3"
  export CXXFLAGS+=" -g3"

  # use fat LTO objects for static libraries
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"

  avouch-meson ${_realname}-${pkgver} build "${meson_options[@]}"
  meson compile -C build

}

# check() {
#   meson test -C build --no-suite flaky --no-suite slow --print-errorlogs
# }

package_glib2() {
  depends+=(libmount.so)
  provides+=(libgio-2.0.so libglib-2.0.so libgmodule-2.0.so libgobject-2.0.so
             libgthread-2.0.so)
  optdepends=('python: gdbus-codegen, glib-genmarshal, glib-mkenums, gtester-report'
              'libelf: gresource inspection tool')

  DESTDIR="$pkgdir" meson install -C build

  install -D gio-querymodules.script "$pkgdir/usr/share/libalpm/scripts/gio-querymodules"

  export PYTHONHASHSEED=0
  python -m compileall -d /usr/share/glib-2.0/codegen \
    "$pkgdir/usr/share/glib-2.0/codegen"
  python -O -m compileall -d /usr/share/glib-2.0/codegen \
    "$pkgdir/usr/share/glib-2.0/codegen"

  # Split docs
  mkdir -p docs/usr/share
  # mv {"$pkgdir",docs}/usr/share/gtk-doc
}

package_glib2-docs() {
  pkgdesc="Documentation for GLib"
  depends=()
  license+=(custom)

  mv -t "$pkgdir" docs/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${_realname}-${pkgver}/docs/reference/COPYING
}
