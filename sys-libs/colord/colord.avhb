# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=colord
pkgname=(colord colord-sane)
pkgver=1.4.6
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=('x86_64')
groups=('sys-libs')
url="https://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lcms2 libgusb polkit sqlite dconf dbus libgudev shared-mime-info systemd-libs udev)
makedepends=(gobject-introspection vala sane bash-completion argyllcms git meson gtk-doc systemd
             docbook-xsl)
source=("https://github.com/hughsie/colord/archive/$pkgver.tar.gz"
        0001-trivial-Fix-error-checking-for-sd_pid_get_session-an.patch)
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')  # Richard Hughes

build() {
  avouch-meson $pkgbase-$pkgver build \
    -D libcolordcompat=true \
    -D sane=true \
    -D vapi=true \
    -D print_profiles=true \
    -D daemon_user=colord \
    -D docs=false \
    -D man=false
  meson compile -C build
}

# check() {
#   meson test -C build
# }

package_colord() {
  optdepends=('argyllcms: color profiling'
              'colord-sane: SANE support')
  replaces=(shared-color-profiles)

  DESTDIR="$pkgdir" meson install -C build

  echo 'u colord - "Color management daemon" /var/lib/colord' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/colord.conf"

### Split colord-sane
  mkdir -p colord-sane/usr/lib/colord-plugins
  mv {"$pkgdir",colord-sane}/usr/lib/colord-sane
  mv {"$pkgdir",colord-sane}/usr/lib/colord-plugins/libcolord_sensor_sane.so
}

package_colord-sane() {
  pkgdesc+=" (SANE support)"
  depends=("colord=$pkgver-$pkgrel" sane)
  mv colord-sane/* "$pkgdir"
}