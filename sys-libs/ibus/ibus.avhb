# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.29-rc2
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('x86_64')
groups=('sys-libs')
url="https://github.com/ibus/ibus/wiki"
license=('LGPL')
depends=('dconf' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject'
         'iso-codes' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'gnome-common' 'gtk-doc' 'gtk4' 'gtk2'
             'qt5-base' 'unicode-cldr' 'unicode-character-database' 'unicode-emoji' 'git')
options=('!emptydirs' 'debug')
source=("https://github.com/ibus/ibus/archive/$pkgver.tar.gz")

# pkgver() {
#   cd ibus
#   git describe --tags | sed 's/-/+/g'
# }

prepare() {
  cd $pkgbase-$pkgver
  sed -i 's|$(libibus) $(libibus_emoji_dialog)|$(libibus_emoji_dialog) $(libibus)|' ui/gtk3/Makefile.am
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --enable-dconf \
    --enable-wayland \
    --enable-gtk-doc \
    --enable-gtk4 \
    --disable-memconf \
    --enable-ui \
    --disable-python2 \
    --with-python=python3 \
    --with-ucd-dir=/usr/share/unicode/
    sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

package_ibus() {
  depends+=("libibus=$pkgver")

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  make -C src DESTDIR="$pkgdir" uninstall
  make -C bindings DESTDIR="$pkgdir" uninstall
  make DESTDIR="$pkgdir" uninstall-pkgconfigDATA
}

package_libibus() {
  pkgdesc="IBus support library"
  depends=(libg{lib,object,io}-2.0.so)
  provides=('libibus-1.0.so')

  cd $pkgbase-$pkgver
  make -C src DESTDIR="$pkgdir" install
  make -C bindings DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA
}