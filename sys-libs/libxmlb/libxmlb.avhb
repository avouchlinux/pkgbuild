# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libxmlb
pkgver=0.3.15
pkgrel=1
pkgdesc="A library to help create and query binary XML blobs"
arch=(x86_64)
groups=('sys-libs')
url="https://github.com/hughsie/libxmlb"
license=(LGPL)
depends=(glib2 libstemmer xz zstd)
makedepends=(meson gobject-introspection gtk-doc)
checkdepends=(shared-mime-info)
source=("https://github.com/hughsie/libxmlb/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('4cf605965d0e669db737da6443314664ea471807f2719a84550f2490670beea9'
            'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  avouch-meson ${pkgname}-${pkgver} build -D stemmer=true
  ninja -v -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}"/usr/{lib,share}/installed-tests/
}
