# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gmime2
_realname=gmime
pkgver=2.6.23
pkgrel=1
pkgdesc="Glorious MIME Utility Library"
arch=('x86_64')
groups=('sys-libs')
license=(GPL)
url="https://github.com/jstedfast/gmime"
depends=(glib2 gpgme zlib)
makedepends=(gtk-sharp-2 gobject-introspection gtk-doc git vala docbook-utils)
_commit=91dcee38ea301463fb9c6bc936f08b9fd1c0969f  # gmime-2-6
source=("git+https://github.com/jstedfast/gmime#commit=$_commit"
        0001-Avoid-non-UTF-8-code.patch)

prepare() {
  cd "$_realname-$pkgver"
  # Fixup tags for pkgver()
  #git tag -f 2.6.23 2e404fd8aad343aff427df02680c0c1ca39c1cfe
  patch -Np1 -i ../0001-Avoid-non-UTF-8-code.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="$srcdir/weird"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$_realname-$pkgver"

  ./configure --prefix=/usr \
    --program-prefix=$pkgname \
    --disable-static \
    --enable-smime \
    --enable-mono=no
    
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make ${MAKEFLAGS}
}

package() {
  cd "$_realname-$pkgver"
	make DESTDIR="$pkgdir" install
}
