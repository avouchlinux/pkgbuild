# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=hunspell
pkgver=1.7.0
pkgrel=1
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('x86_64')
groups=('sys-libs')
url="https://hunspell.github.io/"
license=('GPL' 'LGPL' 'MPL')
depends=('gcc-libs' 'readline')
optdepends=('perl: for ispellaff2myspell')
source=(https://github.com/hunspell/hunspell/archive/v${pkgver}.tar.gz)

prepare() {
	cd hunspell-$pkgver
	autoreconf -vfi
}

build() {
	cd hunspell-$pkgver
	./configure --prefix=/usr --disable-static \
	--with-ui --with-readline # --with-experimental breaks build in this release
	make ${MAKEFLAGS}
}

check() {
	cd hunspell-$pkgver
	make ${MAKEFLAGS} check
}

package() {
	cd hunspell-$pkgver
	make DESTDIR="$pkgdir" install
  
	# add generic hunspell.so for development and projects not using pkgconfig flags - FS#30592
	pushd $pkgdir/usr/lib
		ln -s libhunspell-?.?.so libhunspell.so
	popd
}
