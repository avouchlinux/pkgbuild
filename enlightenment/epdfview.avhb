# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=epdfview
pkgver=0.1.8
pkgrel=1
pkgdesc='Lightweight PDF document viewer'
url='http://freecode.com/projects/epdfview'
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk2')
makedepends=('pkgconfig')
install=epdfview.install
source=(ftp://ftp.slackware.com/.1/blfs/conglomeration/epdfview/$pkgname-$pkgver.tar.bz2
        epdfview-0.1.8-swap-the-blue-and-red-channel.patch
        epdfview-0.1.8-glib2-headers.patch
        epdfview-0.1.8-modern-cups.patch
        epdfview-0.1.8-pictures.patch)
md5sums=('e50285b01612169b2594fea375f53ae4'
         '7f9ea101a41f5b4e999fd024f423d41f'
         '2fffa9c7cd4c5f0744803591c2f162a3'
         '5c2cf5612d2a7dfe6cf005b94aeb5ada'
         '60064c976f273d86d6254c71b583cf4d')

prepare() {
	cd "${srcdir}/$pkgname-$pkgver"
	sed -i 's/icon_epdfview-48/epdfview/' data/epdfview.desktop

	patch -p1 -i ../epdfview-0.1.8-swap-the-blue-and-red-channel.patch
	patch -p1 -i ../epdfview-0.1.8-glib2-headers.patch # FS#30116
	patch -p1 -i ../epdfview-0.1.8-modern-cups.patch   # FS#32511
	patch -p1 -i ../epdfview-0.1.8-pictures.patch      # FS#44936
}

build() {
	cd "${srcdir}/$pkgname-$pkgver"
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
	make
}

package() {
	cd "${srcdir}/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	for size in 24 32 48; do
		install -Dm644 data/icon_$pkgname-$size.png \
		"$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps/$pkgname.png
	done
}
