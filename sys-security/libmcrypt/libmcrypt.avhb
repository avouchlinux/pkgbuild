# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libmcrypt
pkgver=2.5.8
pkgrel=1
pkgdesc="A library which provides a uniform interface to several symmetric encryption algorithms"
arch=('x86_64')
groups=('sys-security')
url="http://mcrypt.sourceforge.net/"
license=('GPL')
depends=('glibc')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/mcrypt/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('6c05c42767401d66af67b0922b207d17cdb1c3efdbfdfb5b0c2e651821c48a8a6c6749debfa0206091b8a801f538fabe9f7d95ebc86d82c6b84c8001031d50fe')

build() {
  cd ${pkgname}-${pkgver}
  # gcc-14
  export CFLAGS+=" -Wno-incompatible-pointer-types -Wno-implicit-function-declaration -Wno-implicit-int"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
