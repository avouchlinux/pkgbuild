# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=liboauth
pkgver=1.0.3
pkgrel=1
pkgdesc="C library implementing OAuth Core RFC 5849"
arch=('x86_64')
groups=('sys-security')
url="https://github.com/x42/liboauth"
license=(MIT)
depends=(curl nss)
makedepends=(git)
source=("https://github.com/x42/liboauth/archive/refs/tags/v$pkgver.tar.gz")


prepare() {
	cd $pkgname-$pkgver
	autoreconf -fvi
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --disable-static --enable-nss
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm644 COPYING.MIT "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
