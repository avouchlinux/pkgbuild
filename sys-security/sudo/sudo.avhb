# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=sudo
_sudover=1.8.31p1
pkgver=${_sudover/p/.p}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64')
groups=('sys-security')
url="https://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'libgcrypt' 'pam' 'libldap')
backup=('etc/sudoers' 'etc/pam.d/sudo')
install=$pkgname.install
source=(https://www.sudo.ws/sudo/dist/$pkgname-$_sudover.tar.gz{,.sig}
        sudo.pam)
sha256sums=('7beb68b94471ef56d8a1036dbcdc09a7b58a949a68ffce48b83f837dd33e2ec0'
            'SKIP'
            'd1738818070684a5d2c9b26224906aad69a4fea77aabd960fc2675aee2df1fa2')
validpgpkeys=('59D1E9CCBA2B376704FDD35BA9F4C021CEA470FB')

prepare() {
  cd "$srcdir/$pkgname-$_sudover"
}

build() {
	cd "$srcdir/$pkgname-$_sudover"
	./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-rundir=/run/sudo \
    --with-vardir=/var/db/sudo \
    --with-logfac=auth \
    --enable-tmpfiles.d \
    --with-pam \
    --with-sssd \
    --with-ldap \
    --with-ldap-conf-file=/etc/openldap/ldap.conf \
    --with-env-editor \
    --with-passprompt="[sudo] password for %p: " \
    --with-all-insults \
    --with-selinux
	make ${MAKEFLAGS}
}

check() {
	cd "$srcdir/$pkgname-$_sudover"
	make ${MAKEFLAGS} check
}

package() {
	cd "$srcdir/$pkgname-$_sudover"
	make DESTDIR="$pkgdir" install

	# Remove /run/sudo directory from the package; we create it using tmpfiles.d
	rmdir "$pkgdir/run/sudo"
	rmdir "$pkgdir/run"
  
	install -Dm644 "$srcdir/sudo.pam" "$pkgdir/etc/pam.d/sudo"
	install -p -c -m 0440 "$srcdir/sudoers" "$pkgdir/etc/sudoers"

	# license
	install -Dm644 doc/LICENSE "$pkgdir/usr/share/licenses/sudo/LICENSE"
}

