# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lua
pkgver=5.3.5
_majorver=${pkgver%.*}
pkgrel=1
pkgdesc='Powerful light-weight programming language designed for extending applications'
arch=('x86_64')
groups=('dev-lang')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=(http://www.lua.org/ftp/$pkgname-$pkgver.tar.gz
        liblua.so.patch
        lua.pc
        LICENSE)

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -p1 -i ../liblua.so.patch

	sed "s/%VER%/$_majorver/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc

	# Lua 5.3.4 has wrong release version in its Makefile. Fix it.
	sed "s/^R= \$V.4/R= \$V.5/" -i Makefile
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# quite a lot of packages still use lua 5.1 API, enable 5.1 compat mode
	make MYCFLAGS="$CFLAGS -fPIC -DLUA_COMPAT_5_2 -DLUA_COMPAT_5_1" MYLDFLAGS="$LDFLAGS" linux
}

package() {	
	cd ${srcdir}/${pkgname}-${pkgver}
	make \
	TO_LIB="liblua.a liblua.so liblua.so.$_majorver liblua.so.$pkgver" \
	INSTALL_DATA='cp -d' \
	INSTALL_TOP="$pkgdir"/usr \
	INSTALL_MAN="$pkgdir"/usr/share/man/man1 \
	install

	ln -sf /usr/bin/lua "$pkgdir"/usr/bin/lua$_majorver
	ln -sf /usr/bin/luac "$pkgdir"/usr/bin/luac$_majorver
	ln -sf /usr/lib/liblua.so.$pkgver "$pkgdir"/usr/lib/liblua$_majorver.so

	install -Dm644 lua.pc "$pkgdir"/usr/lib/pkgconfig/${pkgname}53.pc
	ln -sf ${pkgname}53.pc "$pkgdir"/usr/lib/pkgconfig/${pkgname}.pc
	ln -sf ${pkgname}53.pc "$pkgdir"/usr/lib/pkgconfig/${pkgname}5.3.pc
	ln -sf ${pkgname}53.pc "$pkgdir"/usr/lib/pkgconfig/${pkgname}-5.3.pc

	install -d "$pkgdir"/usr/share/doc/$pkgname
	install -m644 doc/*.{gif,png,css,html} "$pkgdir"/usr/share/doc/$pkgname
	install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
