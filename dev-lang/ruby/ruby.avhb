# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=ruby
pkgname=('ruby' 'ruby-docs')
pkgver=2.7.1
pkgrel=1
arch=('x86_64')
groups=('dev-lang')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom')
makedepends=('gdbm' 'openssl' 'libffi' 'doxygen' 'graphviz' 'libyaml' 'ttf-dejavu') # 'tk'
options=('!emptydirs' '!makeflags' 'staticlibs')
source=("http://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/ruby-${pkgver}.tar.xz"
        'gemrc'
        'disable_sse2_on_i686.patch')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	PKG_CONFIG=/usr/bin/pkg-config ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sharedstatedir=/var/lib \
		--libexecdir=/usr/lib/ruby \
		--enable-shared \
		--disable-rpath \
		--with-dbm-type=gdbm_compat
	make ${MAKEFLAGS}
}

check() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	#make test
}

package_ruby() {
	pkgdesc='An object-oriented language for quick and easy programming'
	depends=('gdbm' 'openssl' 'libffi' 'libyaml')
	optdepends=('ruby-docs: Ruby documentation') # 'tk: for Ruby/TK'
	provides=('rubygems' 'rake')
	conflicts=('rake')
	backup=('etc/gemrc')
	install='ruby.install'

	cd ${srcdir}/${pkgbase}-${pkgver}
	make DESTDIR="${pkgdir}" install-nodoc

	install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
	install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

package_ruby-docs() {
	pkgdesc='Documentation files for ruby'

	cd ${srcdir}/${pkgbase}-${pkgver}
	make DESTDIR="${pkgdir}" install-doc install-capi

	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
	install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
}
