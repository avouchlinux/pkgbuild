# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=bc
pkgver=1.07.1
pkgrel=1
pkgdesc="An arbitrary precision calculator language"
arch=('x86_64')
groups=('dev-lang')
url="https://www.gnu.org/software/bc/"
license=('GPL')
depends=('readline')
makedepends=('ed')
replaces=('bc-readline')
conflicts=('bc-readline')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('62adfca89b0a1c0164c2cdca59ca210c1d44c3ffc46daf9931cf4942664cb02a'
            'SKIP')
validpgpkeys=('00E426232F384BF6D32D8B1881C24FF12FB7B14B') # Phil Nelson <philnelson@acm.org>

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	export CFLAGS="${CFLAGS/-O?/-O3}"
	./configure --prefix=/usr \
	--mandir=/usr/share/man \
	--infodir=/usr/share/info \
	--with-readline

	make ${MAKEFLAGS}
}

check () {
	cd "${pkgname}-${pkgver}"
	echo "quit" | ./bc/bc -l Test/checklib.b
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
