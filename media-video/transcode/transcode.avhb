# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=transcode
_sripver=0.3-4
pkgver=1.1.7
pkgrel=1
pkgdesc="A video/DVD ripper and encoder for the terminal/console"
arch=('x86_64')
groups=('media-video')
url="http://www.transcoding.org/"
license=(GPL)
depends=(gawk imagemagick lzo libdvdread ffmpeg mjpegtools libmpeg2 libxaw a52dec)
makedepends=(nasm x264 libquicktime)
optdepends=('libquicktime: Quicktime format support')
#source=(https://bitbucket.org/france/transcode-tcforge/downloads/$pkgname-$pkgver.tar.bz2
source=(https://sources.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.bz2
        transcode-imagemagick7.patch
        transcode-ffmpeg3.patch
        transcode-ffmpeg4.patch
        transcode-swresample.patch
        transcode-gcc10.patch
        transcode-glibc-2.32.patch)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../transcode-ffmpeg3.patch # LFS patch
  patch -p1 -i ../transcode-ffmpeg4.patch # Gentoo patch
  patch -p1 -i ../transcode-imagemagick7.patch # Gentoo patch
  sed -e 's|freetype/ftglyph.h|freetype2/freetype/ftglyph.h|' -i filter/subtitler/load_font.c
  patch -p1 -i ../transcode-swresample.patch # port away from avresample
  patch -p1 -i ../transcode-gcc10.patch # Fix build with GCC 10
  patch -p1 -i ../transcode-glibc-2.32.patch # Fix build with glibc 2.32
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-sse --disable-sse2 --disable-altivec --enable-mmx \
    --enable-lame --enable-ogg --enable-vorbis --enable-theora \
    --enable-libdv --enable-libxml2 --enable-v4l \
    --enable-imagemagick --enable-libjpeg --enable-lzo --enable-mjpegtools \
    --enable-sdl --enable-freetype2 --enable-a52 --enable-libpostproc \
    --enable-xvid --enable-x264 --enable-alsa --enable-libmpeg2 \
    --enable-libmpeg2convert --enable-libquicktime

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
