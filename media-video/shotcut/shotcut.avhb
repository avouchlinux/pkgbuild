# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=shotcut
pkgdesc='Cross-platform Qt based Video Editor'
pkgver=24.10.29
pkgrel=1
arch=('x86_64')
groups=('media-video')
url='https://www.shotcut.org'
license=('GPL3')
depends=('qt6-base' 'qt6-declarative' 'qt6-imageformats' 'qt6-multimedia' 'qt6-translations'
         'mlt' 'movit' 'ffmpeg' 'libx264' 'libvpx' 'lame' 'frei0r-plugins' 'ladspa' 'qt6-charts')
optdepends=('swh-plugins: Several audio filters'
            'opencv: For motion tracking')
makedepends=('qt6-tools' 'git' 'cmake' 'ninja' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mltframework/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('ab3d0679cf5a54d66ff086e1e2d69f9c8928777727131d185bf8b68e480ca23dbe5227ab8bc60b998a5557eca76795176fe0d27a09f87406f36509d87468ed36')

prepare() {
  cd "$pkgname-$pkgver"

  sed -e 's|${Qt6_LUPDATE_EXECUTABLE}|/usr/lib/qt6/bin/lupdate|' -i translations/CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  # https://github.com/mltframework/shotcut/issues/1275
  export CXXFLAGS+=" -DSHOTCUT_NOUPGRADE"

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSHOTCUT_VERSION=${pkgver}
  cmake --build build --verbose
}

package() {
  cd "$pkgname-$pkgver"

  DESTDIR="${pkgdir}" cmake --install build
}

