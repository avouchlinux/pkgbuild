# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=v4l2loopback
pkgname=(v4l2loopback-dkms v4l2loopback-utils)
pkgver=0.13.2
pkgrel=1
pkgdesc="v4l2-loopback device"
arch=(any)
groups=('media-video')
url="https://github.com/umlaeute/v4l2loopback"
license=(GPL2)
makedepends=(help2man)
source=(${url}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('1e57e1e382d7451aa2a88d63cc9f146eab1f425b90e76104d4c3d73127e34771')

package_v4l2loopback-dkms() {
  pkgdesc+=" – module sources"
  depends=(dkms)
  provides=(V4L2LOOPBACK-MODULE)
  cd ${pkgbase}-${pkgver}
  install -Dm644 v4l2loopback.h v4l2loopback.c v4l2loopback_formats.h dkms.conf Kbuild Makefile -t "${pkgdir}"/usr/src/${pkgbase}-${pkgver}/
}
package_v4l2loopback-utils() {
  pkgdesc+=" – utilities only"
  depends=(V4L2LOOPBACK-MODULE)
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install-utils install-man
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgbase}
}
