# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gptfdisk
pkgver=1.0.4
pkgrel=1
pkgdesc="A text-mode partitioning tool that works on GUID GPT disks"
arch=('x86_64')
groups=('sys-block')
url="https://www.rodsbooks.com/gdisk/"
license=('GPL2')
depends=('gcc-libs' 'popt' 'libuuid.so' 'libncursesw.so')
provides=("gdisk=$pkgver")
conflicts=('gdisk')
replaces=('gdisk')
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make ${MAKEFLAGS}
}

# check() {
#   cd "$srcdir/$pkgname-$pkgver"
#   make test
# }

package () {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir"/usr/{bin,share/{doc/gdisk,man/man8}}
  install -t "$pkgdir/usr/bin" {,c,s}gdisk fixparts
  install -m644 -t "$pkgdir/usr/share/man/man8" {{,c,s}gdisk,fixparts}.8
  install -m644 -t "$pkgdir/usr/share/doc/gdisk" README NEWS
}
