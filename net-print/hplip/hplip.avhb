# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=hplip
pkgver=3.20.3
pkgrel=1
epoch=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('x86_64')
groups=('net-print')
url="https://hplipopensource.com"
license=('GPL2' 'custom')
depends=('python-dbus' 'ghostscript' 'net-snmp' 'foomatic-db-engine' 'python-gobject')
makedepends=('python-pyqt5' 'sane' 'rpcbind' 'cups' 'libusb')
optdepends=('cups: for printing support'
            'sane: for scanner support'
            'xsane: sane scanner frontend'
            'python-pillow: for commandline scanning support'
            'python-reportlab: for pdf output in hp-scan'
            'rpcbind: for network support'
            'python-pyqt5: for running GUI and hp-toolbox'
            'libusb: for advanced usb support'
            'wget: for network support')
# 'hplip-plugin: binary blobs for specific devices (AUR) or run hp-setup to download the plugin'
backup=('etc/hp/hplip.conf' 'etc/sane.d/dll.d/hpaio')
source=(https://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz{,.asc}
        disable_upgrade.patch
        0022-Add-include-cups-ppd.h-in-various-places-as-CUPS-2.2.patch
        0023-Fix-handling-of-unicode-filenames-in-sixext.py.patch
        0025-Remove-all-ImageProcessor-functionality-which-is-clo.patch)
validpgpkeys=('4ABA2F66DBD5A95894910E0673D770CDA59047B9') # HPLIP (HP Linux Imaging and Printing) <hplip@hp.com>

prepare() {
        cd $pkgname-$pkgver 
        # disable insecure update - https://bugs.archlinux.org/task/38083
        patch -Np0 -i ${srcdir}/disable_upgrade.patch 
        # add missing 'include <cups/ppd.h>' at various places
        patch -Np1 -i ${srcdir}/0022-Add-include-cups-ppd.h-in-various-places-as-CUPS-2.2.patch
        # fix some handling unicode file names FS#58412
        patch -Np1 -i ${srcdir}/0023-Fix-handling-of-unicode-filenames-in-sixext.py.patch
        # remove binary blob libImageProcessor and all linking - FS#59681
        patch -Np1 -i ${srcdir}/0025-Remove-all-ImageProcessor-functionality-which-is-clo.patch

        # Workaround patch for missing Python3 transition of the old
        # (pre-USB-storage) photo memory card support (pcardext) - Debian patch
        patch -Np1 -i "${srcdir}"/python3.diff

        # Work-around Makefile.am imperfections.
        #export AUTOMAKE='automake --foreign'
        sed -i 's|^AM_INIT_AUTOMAKE|AM_INIT_AUTOMAKE([foreign])|g' configure.in
        autoreconf --force --install
}

build() {
        cd $pkgname-$pkgver
        # hplip used improper way of checking python usability which worked
        # until python38 - start to include python in spec file
        # https://bugzilla.redhat.com/show_bug.cgi?id=1706233
        # thanks to koji fedora
        export CFLAGS="$CFLAGS $(python3-config --includes)"
        export CXXFLAGS="$CFLAGS $(python3-config --includes)"

        ./configure --prefix=/usr \
                --enable-qt5 \
                --disable-qt4 \
                --enable-hpcups-install \
                --enable-cups-drv-install \
                --enable-pp-build #--help
        make ${MAKEFLAGS}
}

package() {
        cd "$pkgname"-$pkgver
        make -j1 rulesdir=/usr/lib/udev/rules.d DESTDIR="$pkgdir/" install
        
        # remove config provided by sane and autostart of hp-daemon
        rm -rf "$pkgdir"/etc/{sane.d,xdg}
        install -dm755 "${pkgdir}"/etc/sane.d/dll.d
        echo hpaio > "${pkgdir}"/etc/sane.d/dll.d/hpaio
        
        # remove HAL .fdi file because HAL is no longer used
        rm -vrf "$pkgdir"/usr/share/hal
        
        # remove rc script
        rm -vrf "$pkgdir"/etc/init.d

        # add mixed license file
        install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 COPYING

        # Compile Python bytecode:
        python -m compileall -d /usr/share "$pkgdir/usr/share"
        python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}