# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kmarkdownwebview
pkgver=0.5.5
pkgrel=1
pkgdesc='KPart for rendering Markdown content'
arch=('x86_64')
groups=('kde-apps')
url='https://www.kde.org'
license=(LGPL)
depends=(kparts qt5-webengine)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3a506f5ccb6e9982b6e56f8cffbab1f5ba9f0a84dede6bd6a3a625d6b66831c4'
            'SKIP')
validpgpkeys=(E191FD5BE6F46870F09E82B2024E7FB43D015474) # Friedrich W. H. Kossebau <kossebau@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make ${MAKEFLAGS}
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
