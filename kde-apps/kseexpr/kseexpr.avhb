# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kseexpr
pkgver=4.0.3.0
pkgrel=1
pkgdesc="An embeddable expression evaluation engine (Krita fork)"
arch=(x86_64)
groups=('kde-apps')
url="https://krita.org"
license=(Apache)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/kseexpr/${pkgver%.*}/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=(4894424D2412FEE5176732A3FC00108CFD9DBF1E) # L. E. Segovia (GitHub signing key) <amy@amyspark.me>

build() {
  cmake -S $pkgname-$pkgver -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

