# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>/

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.2.1
pkgrel=1
arch=('x86_64')
groups=('kde-apps')
url='https://www.calligra-suite.org/'
license=(FDL1.2 GPL2 LGPL)
depends=(kcmutils knotifyconfig kross kactivities kdiagram libspnav kdelibs4support
         gsl openexr kcontacts qca-qt5 poppler-qt5 libodfgen qt5-webkit cauchy khtml)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin boost openjpeg akonadi-contacts
             libwpg okular eigen marble-common pstoedit libvisio libetonyek libwps vc libgit2)
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'openjpeg: PDF to SVG filer'
            'libgit2: Calligra Gemini git plugin'
            'kirigami2: for Calligra Gemini'
            'qt5-quickcontrols: for Calligra Gemini'
            'qt5-webengine: for Calligra Gemini')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD  # Boudewijn Rempt <foundation@krita.org>
              42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>

build() {
	cmake -S ${pkgname}-${pkgver} -B build -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_TESTING=OFF
	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
  # Remove utterly broken thumbnailers
  rm "$pkgdir"/usr/lib/qt/plugins/calligra*thumbnail.so
}
