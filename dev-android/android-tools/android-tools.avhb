# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=android-tools
pkgver=9.0.0_r35
pkgrel=1
pkgdesc='Android platform tools'
arch=('x86_64')
groups=('dev-android')
url='http://tools.android.com/'
license=(Apache MIT)
depends=(pcre2 libusb)
optdepends=('python: for mkbootimg script')
makedepends=(git clang gtest ruby cmake ninja go-pie)
# keep the boringssl commit in sync with android tree https://android.googlesource.com/platform/external/boringssl/+/$pkgver/BORINGSSL_REVISION
_boringssl_commit=45210dd4e21ace9d28cb76b3f83303fcdd2efcce
source=(git+https://android.googlesource.com/platform/system/core#tag=android-$pkgver
        git+https://android.googlesource.com/platform/system/extras#tag=android-$pkgver
        git+https://android.googlesource.com/platform/external/selinux#tag=android-$pkgver
        git+https://android.googlesource.com/platform/external/f2fs-tools#tag=android-$pkgver
        git+https://android.googlesource.com/platform/external/e2fsprogs#tag=android-$pkgver
        git+https://android.googlesource.com/platform/external/avb#tag=android-$pkgver
        git+https://boringssl.googlesource.com/boringssl#commit=$_boringssl_commit
        generate_build.rb
        fix_build_core.patch
        fix_build_selinux.patch
        fix_build_e2fsprogs.patch
        bash_completion.fastboot)
        # Bash completion file was taken from https://github.com/mbrubeck/android-completion


prepare() {
  chmod +x generate_build.rb
  PKGVER=$pkgver LDFLAGS='-z relro -z now -O2' ./generate_build.rb > build.ninja

  bsdtar -xf $srcarchivedir/avb-9.0.0_r35.tar.xz
  bsdtar -xf $srcarchivedir/boringssl-9.0.0_r35.tar.xz
  bsdtar -xf $srcarchivedir/core-9.0.0_r35.tar.xz
  bsdtar -xf $srcarchivedir/e2fsprogs-9.0.0_r35.tar.xz
  bsdtar -xf $srcarchivedir/f2fs-tools-9.0.0_r35.tar.xz
  bsdtar -xf $srcarchivedir/extras-9.0.0_r35.tar.xz
  bsdtar -xf $srcarchivedir/selinux-9.0.0_r35.tar.xz

  # add thir party library
  bsdtar -xf $srcarchivedir/googletest-1.8.1.tar.xz
  mv -f googletest boringssl/src/third_party/


  cd $srcdir/core
  patch -p1 < ../fix_build_core.patch

  cd $srcdir/selinux
  patch -p1 < ../fix_build_selinux.patch

  cd $srcdir/e2fsprogs
  patch -p1 < ../fix_build_e2fsprogs.patch

  cd $srcdir/avb
  sed -i 's|/usr/bin/env python$|/usr/bin/env python2|g' avbtool

  cd "$srcdir"/boringssl
  #patch -p1 < ../boringssl-disable-thirdpartydeps.patch

}

build() {
  cd $srcdir
  (mkdir -p $srcdir/boringssl/build && cd $srcdir/boringssl/build && cmake -GNinja ../src; ninja)
  ninja
}

package(){
  install -m755 -d "$pkgdir"/usr/bin
  install -m755 -t "$pkgdir"/usr/bin fastboot adb mke2fs.android e2fsdroid ext2simg core/mkbootimg/mkbootimg avb/avbtool
  install -Dm 644 bash_completion.fastboot "$pkgdir"/usr/share/bash-completion/completions/fastboot
}
