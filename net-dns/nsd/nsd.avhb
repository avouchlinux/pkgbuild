# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=nsd
pkgver=4.3.1
pkgrel=1
pkgdesc='Authoritative only, high performance and simple DNS server'
url='https://www.nlnetlabs.nl/nsd/'
arch=('x86_64')
groups=('net-dns')
license=('BSD')
depends=('openssl' 'libevent')
makedepends=('flex')
source=("https://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D'
              'C3E356788FAD0179D872D092BA811E62E7194568')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/ \
		--sbindir=/usr/bin \
		--datarootdir=/usr/share \
		--with-pidfile=/run/nsd/nsd.pid \
		--enable-ratelimit \
		--enable-relro-now \
		--enable-pie

	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}"/{tmp,run/{nsd,}}

	rm doc/differences.pdf
	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	install -m644 doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
	# systemd stub
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/nsd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/nsd.conf
	install -Dm644 ../sysusers.d "${pkgdir}"/usr/lib/sysusers.d/nsd.conf

}
