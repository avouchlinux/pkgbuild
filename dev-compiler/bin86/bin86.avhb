# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=bin86
pkgver=0.16.21
pkgrel=1
pkgdesc="A complete 8086 assembler and loader"
arch=('x86_64')
groups=('dev-compiler')
license=('GPL')
url="http://www.debath.co.uk/"
depends=('glibc')
source=("http://www.debath.co.uk/dev86/$pkgname-$pkgver.tar.gz"
        bin86-0.16.17-x86_64-1.patch)
md5sums=('0a20d79765a6855ce7c8c2d02b23a0f0'
         '92bdce7b0655cd2e9f83c83fc56d128e')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	if [[ $CARCH = x86_64 ]]; then
		 patch -Np1 < "$srcdir/bin86-0.16.17-x86_64-1.patch"
	fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
	make "PREFIX=$pkgdir/usr" "MANDIR=$pkgdir/usr/share/man/man1" install
}

