# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=ldc
pkgname=('ldc' 'liblphobos')
pkgver=1.29.0
_dversion=2.099.1
_clangversion=14.0.0 # related to where ldc2 looks for compiler-rt sanitizers
epoch=2
pkgrel=1
_pkgcommit=34afd6a43abf62b2050a5b0b2c20202080b1163b
pkgdesc="A D Compiler based on the LLVM Compiler Infrastructure including D runtime and libphobos2"
arch=('x86_64')
groups=('dev-compiler')
url="https://github.com/ldc-developers/ldc"
license=('BSD')
makedepends=('git' 'cmake' 'llvm' 'ldc' 'ninja')
source=(
    "git+https://github.com/ldc-developers/ldc"
    "git+https://github.com/ldc-developers/druntime.git"
    "git+https://github.com/ldc-developers/phobos.git"
    "git+https://github.com/ldc-developers/dmd-testsuite.git"
)

getsrc() {
    git clone https://github.com/ldc-developers/ldc
    git clone https://github.com/ldc-developers/druntime.git
    git clone https://github.com/ldc-developers/phobos.git
    git clone https://github.com/ldc-developers/dmd-testsuite.git
}

prepare() {
    cd $pkgbase
    git checkout $_pkgcommit

    git submodule init
    git config submodule.druntime.url "$srcdir/druntime"
    git config submodule.phobos.url "$srcdir/phobos"
    git config submodule.tests/d2/dmd-testsuite.url "$srcdir/dmd-testsuite"
    git submodule update

    # Set version used for path construction in getFullClangCompilerRTLibPath()
    sed -i "s/ldc::llvm_version_base/\"$_clangversion\"/" driver/linker-gcc.cpp
}

build() {
     cmake -S $pkgbase -B build -G Ninja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DINCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
        -DBUILD_SHARED_LIBS=BOTH \
        -DBUILD_LTO_LIBS=ON \
        -DLDC_WITH_LLD=OFF \
        -DD_COMPILER_FLAGS="-link-defaultlib-shared=false -linker=gold --flto=thin" \
        -DADDITIONAL_DEFAULT_LDC_SWITCHES="\"-link-defaultlib-shared\""
    ninja -C build
}

# check() {
#     ninja -C build all-test-runners
# }

package_ldc() {
    depends=('liblphobos' 'llvm-libs' 'gcc' 'compiler-rt')
    backup=('etc/ldc2.conf')
    provides=("d-compiler=$_dversion")

    DESTDIR="$pkgdir" ninja -C build install

    # remove liblphobos files
    rm -rf "$pkgdir/usr/include"
    rm -rf "$pkgdir/usr/lib"

    # symlinks
    ln -sf ldc2 "$pkgdir/usr/share/bash-completion/completions/ldc"
    ln -s /usr/bin/ldc2 "$pkgdir/usr/bin/ldc"
    ln -s /usr/bin/ldmd2 "$pkgdir/usr/bin/ldmd"

    # licenses
    install -D -m644 "$srcdir/$pkgbase/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_liblphobos() {
    provides=('d-runtime' 'd-stdlib')
    depends=('curl')
    options=('staticlibs')

    DESTDIR="$pkgdir" ninja -C build install

    # remove ldc files
    rm -rf "$pkgdir/usr/bin/"
    rm -rf "$pkgdir/etc/"
    rm -rf "$pkgdir/usr/share/bash-completion"

    # licenses
    install -D -m644 "$srcdir/$pkgbase/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}