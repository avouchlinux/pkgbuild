# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=icedtea-web
pkgname=('icedtea-web' 'icedtea-web-doc')
_realname=IcedTea-Web-icedtea-web
pkgver=1.8.3
pkgrel=1
arch=('x86_64')
groups=('dev-java')
url='https://github.com/AdoptOpenJDK/IcedTea-Web'
arch=('x86_64')
license=('GPL2')
makedepends=('java-environment=8' 'zip' 'libxtst' 'npapi-sdk' 'rhino' 'junit'
             'firefox' 'epiphany' 'rust' 'bc')
optdepends=('rhino: for using proxy auto config files')
source=(https://github.com/AdoptOpenJDK/IcedTea-Web/archive/${pkgbase}-${pkgver}.tar.gz
        java13-SecurityManager.patch
        compiler-flags.patch)
        
prepare() {
  cd ${_realname}-${pkgver}
  patch -Np0 < "${srcdir}/compiler-flags.patch"
  patch -Np1 < "${srcdir}/java13-SecurityManager.patch"
  autoreconf -fiv
}

build() {
  cd ${_realname}-${pkgver}
  ./configure \
      --prefix=/usr/share/${pkgbase} \
      --datarootdir=/usr/share \
      --with-jdk-home=/usr/lib/jvm/default \
      --with-jre-home=/usr/lib/jvm/default-runtime \
      --with-java=/usr/bin/java \
      --with-browser-tests \
      --with-firefox=/usr/bin/firefox \
      --with-epiphany=/usr/bin/epiphany \
      --with-itw-libs=DISTRIBUTION \
      --with-java=1.11
   make ${MAKEFLAGS}
}

check() {
  cd ${_realname}-${pkgver}
  make -k check
}


package_icedtea-web() {
  depends=('java-runtime-openjdk' 'desktop-file-utils')
  provides=('java-web-start')

  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # Install desktop files
  install -Dm 644 javaws.png -t "${pkgdir}"/usr/share/pixmaps
  install -Dm 644 *.desktop -t "${pkgdir}"/usr/share/applications

  ## link binaries into /usr/bin + jre/bin
  install -m 755 -d "${pkgdir}"/usr/bin
  for file in "${pkgdir}/usr/share/${pkgbase}/bin/"*; do
    ln -sf "/usr/share/${pkgbase}/bin/$(basename "${file}")" "${pkgdir}"/usr/bin
  done

  ## link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m 755 -d "${pkgdir}"/usr/lib/mozilla/plugins/
  ln -sf /usr/share/${pkgbase}/lib/IcedTeaPlugin.so "${pkgdir}"/usr/lib/mozilla/plugins/

  # remove splitted doc files
  rm -rf "${pkgdir}"/usr/share/doc
}

package_icedtea-web-doc() {
  pkgdesc+=' (documentation)'

  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install-data-local
  # remove javaws about and man page
  rm -rf "${pkgdir}"/usr/lib
  rm -rf "${pkgdir}"/usr/share/man
  rm -rf "${pkgdir}"/usr/share/icedtea-web
}