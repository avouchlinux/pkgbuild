# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=java-commons-daemon
pkgname=(
  java-commons-daemon
  java-jsvc
)
_realname=commons-daemon
pkgver=1.3.1
pkgrel=1
arch=(x86_64)
groups=('dev-java')
url=https://commons.apache.org/proper/commons-daemon/
license=(APACHE)
_tag=466cc9a2442412dbb25dc5dad803982a7f864264
source=(https://github.com/apache/commons-daemon/archive/refs/tags/commons-daemon-$pkgver.tar.gz)
b2sums=(SKIP)
makedepends=(
  docbook2x
  docbook-utils
  docbook-xsl
  git
  java-environment
  maven
)

prepare() {
  cd $_realname-$_realname-$pkgver/src/native/unix
  support/buildconf.sh
}

build() {
  cd $_realname-$_realname-$pkgver
  mvn clean package
  cd src/native/unix
  ./configure \
    --with-java=/usr/lib/jvm/default
  make
  db2x_docbook2man man/jsvc.1.xml
}

package_java-commons-daemon() {
  pkgdesc='Alternative invocation mechanism for unix-daemon-like java code'
  depends=(java-runtime)

  cd $_realname-$_realname-$pkgver
  install -Dm 644 target/commons-daemon-${pkgver}.jar -t "${pkgdir}"/usr/share/java/
  ln -s commons-daemon-${pkgver}.jar "${pkgdir}"/usr/share/java/commons-daemon.jar
}

package_java-jsvc() {
  pkgdesc='Apache application for making Java applications run on UNIX more easily'
  depends=(
    java-commons-daemon
    libcap
  )

  cd $_realname-$_realname-$pkgver/src/native/unix
  install -Dm 755 jsvc -t "${pkgdir}"/usr/bin/
  install -Dm 644 JSVC.1 -t "${pkgdir}"/usr/share/man/man1/
}