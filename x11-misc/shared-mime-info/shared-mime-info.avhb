# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=shared-mime-info
pkgver=1.15
_pkgver=${pkgver/./-}
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64')
groups=('x11-misc')
license=('GPL2')
depends=('libxml2' 'glib2')
makedepends=('intltool' 'pkgconfig')
install=shared-mime-info.install
url="https://www.freedesktop.org/wiki/Specifications/shared-mime-info-spec/"
source=(https://gitlab.freedesktop.org/xdg/$pkgname/-/archive/Release-${pkgver/./-}/$pkgname-Release-${pkgver/./-}.tar.bz2)

prepare() {
  cd ${pkgname}-Release-${_pkgver}
  ./autogen.sh
}

build() {
  cd ${pkgname}-Release-${_pkgver}
  # https://bugs.freedesktop.org/show_bug.cgi?id=70366
  export ac_cv_func_fdatasync=no
  ./configure --prefix=/usr --disable-update-mimedb
  make ${MAKEFLAGS}
}

check() {
  cd ${pkgname}-Release-${_pkgver}
  make -k check
}

package() {
  cd ${pkgname}-Release-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
