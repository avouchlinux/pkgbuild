# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xbindkeys
pkgver=1.8.7
pkgrel=1
pkgdesc="Launch shell commands with your keyboard or your mouse under X"
arch=('x86_64')
groups=('x11-misc')
url="https://www.nongnu.org/xbindkeys/xbindkeys.html"
license=('GPL')
depends=('libx11' 'guile')
optdepends=('tk: xbindkeys_show')
source=(https://www.nongnu.org/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a29b86a8ec91d4abc83b420e547da27470847d0efe808aa6e75147aa0adb82f2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # pick up new guile versions
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

