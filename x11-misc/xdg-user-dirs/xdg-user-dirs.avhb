# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xdg-user-dirs
pkgver=0.17
pkgrel=1
pkgdesc="Manage user directories like ~/Desktop and ~/Music"
arch=('x86_64')
groups=('x11-misc')
url="https://www.freedesktop.org/wiki/Software/xdg-user-dirs"
license=(GPL)
depends=(sh systemd)
makedepends=(docbook-xsl git)
backup=(etc/xdg/user-dirs.conf etc/xdg/user-dirs.defaults)
install=xdg-user-dirs.install
options=(!emptydirs)
source=("https://gitlab.freedesktop.org/xdg/xdg-user-dirs/-/archive/$pkgver/xdg-user-dirs-$pkgver.tar.bz2"
        xdg-user-dirs-update.service)

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install 

  # We use a service
  rm "$pkgdir/etc/xdg/autostart/xdg-user-dirs.desktop"
  install -Dt "$pkgdir/usr/lib/systemd/user" -m644 ../xdg-user-dirs-update.service
}