# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=xorg-luit
_realname=luit
pkgver=1.1.1
pkgrel=1
pkgdesc="Filter that can be run between an arbitrary application and a UTF-8 terminal emulator"
arch=('x86_64')
groups=('x11-apps')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libfontenc')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/luit-${pkgver}.tar.bz2
        git_fixes.diff)

prepare() {
	cd ${srcdir}/${_realname}-${pkgver}

	patch -Np1 -i $srcdir/git_fixes.diff
	autoreconf -vfi
}

build() {
	cd ${srcdir}/${_realname}-${pkgver}
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

package() {
	cd ${srcdir}/${_realname}-${pkgver}
	make DESTDIR="${pkgdir}" install
	# license
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
