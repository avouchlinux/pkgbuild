# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=netcdf
_realname=netcdf-c
pkgver=4.7.4
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=('x86_64')
groups=('net-libs')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=(hdf5 curl)
makedepends=(cmake)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=(!makeflags)
license=(custom)
source=(https://github.com/Unidata/netcdf-c/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('99930ad7b3c4c1a8e8831fb061cb02b2170fc8e5ccaeda733bd99c3b9d31666b')

build() {
    cmake -B build -S ${pkgname}-c-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_CDF5=ON \
        -DENABLE_DAP_LONG_TESTS=ON \
        -DENABLE_EXAMPLE_TESTS=ON \
        -DENABLE_EXTRA_TESTS=ON \
        -DENABLE_FAILING_TESTS=ON \
        -DENABLE_FILTER_TESTING=ON \
        -DENABLE_LARGE_FILE_TESTS=ON
    make ${MAKEFLAGS} -C build
}

# check() {
#     make -C build test
# }

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${pkgname}-c-${pkgver}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
