# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libsrtp
pkgname=(
  libsrtp
  libsrtp-docs
)
pkgver=2.6.0
pkgrel=1
epoch=1
pkgdesc="Library for SRTP (Secure Realtime Transport Protocol)"
url="https://github.com/cisco/libsrtp"
arch=('x86_64')
groups=('net-libs')
license=(BSD-3-Clause)
depends=(nss)
makedepends=(
  doxygen
  git
  libpcap
  meson
)
checkdepends=(procps-ng)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cisco/$pkgbase/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('SKIP')

build() {
  local meson_options=(
    --buildtype release
    -D crypto-library=nss
    -D crypto-library-kdf=disabled
  )

  avouch-meson $pkgbase-$pkgver build "${meson_options[@]}"
  meson compile -C build
  meson compile -C build doc
}

check() {
  meson test -C build --print-errorlogs
}

package_libsrtp() {
  provides=("libsrtp${pkgver%%.*}.so")

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase-$pkgver/LICENSE
}

package_libsrtp-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a build/html "$pkgdir/usr/share/doc/libsrtp"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase-$pkgver/LICENSE
}
