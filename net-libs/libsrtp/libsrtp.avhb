# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libsrtp
pkgver=2.3.0
pkgrel=1
epoch=1
pkgdesc="Library for SRTP (Secure Realtime Transport Protocol)"
url="https://github.com/cisco/libsrtp"
arch=('x86_64')
groups=('net-libs')
license=(BSD)
depends=(openssl)
makedepends=(git)
checkdepends=(libpcap procps-ng)
options=(staticlibs)
source=("https://github.com/cisco/libsrtp/archive/v$pkgver.tar.gz")

# getsrc() {
#   git clone https://github.com/cisco/libsrtp
# }

prepare() {
  cd $pkgname-$pkgver

  # Fixup pkgver: There are tags like v1.5.4 but also "moving" tags like v1 and v1.5
  #git tag | grep -Pv '^v\d+.\d+.\d+$' | xargs git tag -d

  # Fix shared lib so ldconfig doesn't complain
  patch -Np1 -i ../libsrtp-2.3.0-shared-fix.patch
  # Fix namespace issue in test/util.c
  patch -Np1 -i ../libsrtp-2.3.0-test-util.patch
  # Link test binaries against shared lib
  patch -Np1 -i ../libsrtp-2.3.0-shared-test-fix.patch

  #autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-openssl
  make ${MAKEFLAGS} all
  make shared_library  # Keep separate to link test apps against static lib
}

check() {
  cd $pkgname-$pkgver
  make ${MAKEFLAGS} runtest
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}