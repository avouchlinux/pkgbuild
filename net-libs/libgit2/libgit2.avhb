# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libgit2
pkgver=1.0.1
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
groups=('net-libs')
url="https://libgit2.github.com/"
depends=('curl' 'http-parser' 'libssh2' 'openssl' 'zlib')
makedepends=('cmake' 'python')
license=('GPL2')
source=("https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz")

prepare() {
  cd "$pkgname-$pkgver"
  # removing tests that are only compatible with the (modified) vendored
  # version of http-parser, but not with upstream http-parser
  patch -Np1 -i "../${pkgname}-0.99.0-remove_http-parse_incompatible_tests.patch"
}

build() {
	cmake -S ${pkgname}-${pkgver} -B build -G Ninja \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_HTTP_PARSER=system \
		-DTHREADSAFE:BOOL=ON \
		-Wno-dev
	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
	install -vDm 644 ${pkgname}-${pkgver}/{AUTHORS,README.md} \
    	-t "${pkgdir}/usr/share/doc/${pkgname}"
}
