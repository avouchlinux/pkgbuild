# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libteam
pkgname=(libteam libteam-docs)
pkgver=1.32
pkgrel=1
pkgdesc="Library for controlling team network device"
arch=('x86_64')
groups=('net-libs')
url="http://libteam.org/"
arch=(x86_64)
license=(LGPL)
depends=(
  bash
  jansson
  libdaemon
  libdbus
  libnl
  zeromq
)
makedepends=(
  doxygen
  git
  graphviz
)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jpirko/$pkgbase/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('SKIP')

prepare() {
  cd $pkgbase-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
  )

  cd $pkgbase-$pkgver
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make all html
}

check() {
  cd $pkgbase-$pkgver
  make check
}

package_libteam() {
  provides=(libteam.so libteamdctl.so)

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/lib/systemd/system" -m644 teamd/redhat/systemd/teamd@.service
  install -Dt "$pkgdir/usr/share/dbus-1/system.d" -m644 teamd/dbus/teamd.conf
}

package_libteam-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir/usr/share/doc/$pkgbase"
  cp -a $pkgbase-$pkgver/doc/api "$pkgdir/usr/share/doc/$pkgbase"
}
