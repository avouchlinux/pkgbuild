# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=srt
pkgver=1.5.3
pkgrel=1
pkgdesc="Secure Reliable Transport library"
url="https://www.srtalliance.org/"
arch=('x86_64')
groups=('net-libs')
license=(MPL2)
depends=(
  gcc-libs
  openssl
)
makedepends=(
  cmake
  git
  ninja
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Haivision/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('44aed3d87f693244dbaa145f3987ccf5d3294145b1eedaafccf9d88eecdea628e1abd19fbec4588cef0b1390e18aa51dc606cbc9b8c7f7cf09405eea1d6c8f95')

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DENABLE_STATIC=ON
    -DENABLE_TESTING=ON
  )

  cmake -S $pkgname-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  cd build
  ./uriparser-test
  ./utility-test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
