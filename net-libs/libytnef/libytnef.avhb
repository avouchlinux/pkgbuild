# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libytnef
_realname=ytnef
pkgver=2.1.2
pkgrel=1
pkgdesc="Yerase's TNEF Stream Reader library (decode winmail.dat)"
arch=('x86_64')
groups=('net-libs')
url='https://github.com/Yeraze/ytnef'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(git)
optdepends=('perl: ytnefprocess command')
source=("git+https://github.com/Yeraze/ytnef.git#tag=v$pkgver")
b2sums=(0e8fd7cd258371c99b8b3cb94246175dae418094c255194d75ec8835db97f3d23ff6ffcfb813c60c29298af409204d6ae3cba87de8c27bdbfc49824f50bd7204)

prepare() {
  cd ytnef
  autoreconf -fi
}

build() {
  cd ytnef
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd ytnef
  make DESTDIR="$pkgdir" install
}
