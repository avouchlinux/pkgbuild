# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libsoup2
pkgname=(libsoup2 libsoup2-docs)
_realname=libsoup
pkgver=2.74.3
pkgrel=1
pkgdesc="GNOME HTTP Library"
arch=('x86_64')
groups=('net-libs')
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=(
  brotli
  glib-networking
  glib2
  krb5
  libpsl
  libsysprof-capture
  libxml2
  sqlite
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  samba
  vala
)
checkdepends=(
  apache
  php-apache
)
source=("https://gitlab.gnome.org/GNOME/$_realname/-/archive/2.74.3/$_realname-$pkgver.tar.bz2"
        disable-flaky-test.diff)
b2sums=('SKIP'
        '4d71171b26ed107884a22696f9df1609c45c377e27baaaeb4ec5d890f9fbd62f09607ba3da0acdb42b4f86b9e828014471e6731437ec80de3b6ad98d7f0a2f60')

prepare() {
    cd $_realname-$pkgver

    # https://gitlab.gnome.org/GNOME/libsoup/-/issues/120
    patch -Np1 -i ../disable-flaky-test.diff
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )
  avouch-meson $_realname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libsoup2() {
  depends+=(
    libbrotlidec.so
    libgssapi_krb5.so
    libg{lib,object,io}-2.0.so
    libpsl.so
  )
  optdepends=('samba: Windows Domain SSO')
  provides+=(libsoup{,-gnome}-2.4.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_libsoup2-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

