# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libmbim
pkgver=1.24.2
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=('x86_64')
groups=('net-libs')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd libgudev)
makedepends=(gtk-doc python git help2man)
source=("https://gitlab.freedesktop.org/mobile-broadband/libmbim/-/archive/$pkgver/libmbim-$pkgver.tar.bz2")
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {	
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --enable-gtk-doc
    make ${MAKEFLAGS}
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make ${MAKEFLAGS} check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
}
