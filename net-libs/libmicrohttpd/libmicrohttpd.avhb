# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libmicrohttpd
pkgver=1.0.1
pkgrel=1
pkgdesc="A small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('x86_64')
groups=('net-libs')
url='https://www.gnu.org/software/libmicrohttpd/'
license=('LGPL')
depends=('gnutls')
checkdepends=('curl')
provides=('libmicrohttpd.so')
validpgpkeys=('EA812DBEFA5A7EF17DA8F2C1460A317C3326D2AE'  # Evgeny Grin (Karlson2k) <k2k@yandex.ru>
              'D8423BCB326C7907033929C7939E6BE1E29FC3CC') # Christian Grothoff <christian.grothoff@bfh.ch>
source=("https://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('a89e09fc9b4de34dde19f4fcb4faaa1ce10299b9908db1132bbfa1de47882b94'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --disable-dependency-tracking \
    --disable-examples \
    --enable-curl \
    --enable-https \
    --enable-largefile \
    --enable-messages \
    --with-pic
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install
}
