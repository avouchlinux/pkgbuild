# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libiscsi
pkgver=1.19.0
pkgrel=1
pkgdesc="Client side library to implement the iSCSI protocol"
arch=('x86_64')
groups=('net-libs')
url="https://github.com/sahlberg/libiscsi"
depends=('glibc' 'popt' 'libgcrypt')
license=('LGPL')
source=(https://github.com/sahlberg/libiscsi/archive/$pkgver.tar.gz)

prepare() {
	cd $pkgname-$pkgver
	./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# Disabled werror due to GCC 8 warnings: https://github.com/sahlberg/libiscsi/issues/266
	./configure --prefix=/usr --libdir=/usr/lib --disable-static --disable-werror 
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS} check
}

package () {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
