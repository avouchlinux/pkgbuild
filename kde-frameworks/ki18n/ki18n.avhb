# Copyright 2010-2025 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=ki18n
pkgver=6.11.0
pkgrel=1
pkgdesc='Advanced internationalization framework'
arch=('x86_64')
groups=('kde-frameworks')
url='https://projects.kde.org/projects/frameworks/ki18n'
license=('LGPL')
depends=(gcc-libs
         glibc
         iso-codes
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             python
             qt6-declarative
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('582b4c58bb69803412322d3db656fd4f5fde154eac1ac89b2c6bc905c5b61e6e'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB # David Faure <faure@kde.org>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D # Jonathan Esk-Riddell <jr@jriddell.org>
              90A968ACA84537CC27B99EAF2C8DF587A6D4AAC1 # Nicolas Fella <nicolas.fella@kde.org>
              )

build() {
	cmake -S ${pkgname}-${pkgver} -B build -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DLIB_INSTALL_DIR=lib \
		-DBUILD_TESTING=OFF \
		-DBUILD_QCH=ON
	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
}