# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=mozjs78
pkgver=78.9.0
_pkgver=78.9.0esr
_realname=firefox
pkgrel=1
pkgdesc="JavaScript interpreter and libraries - Version 78"
arch=('x86_64')
groups=('net-misc')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey"
license=(MPL)
depends=(gcc-libs readline zlib sh)
makedepends=(python2 zip autoconf2.13 python rust llvm clang)
_relver=${pkgver}esr
source=(https://archive.mozilla.org/pub/firefox/releases/$_relver/source/firefox-$_relver.source.tar.xz)
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla Software Releases <release@mozilla.com>

# Make sure the duplication between bin and lib is found
COMPRESSZST+=(--long)

prepare() {
  cd firefox-$pkgver
  mkdir obj
}

build() {
  local configure_args=(
    --prefix=/usr
    --disable-debug
    --disable-debug-symbols
    --disable-jemalloc
    --disable-strip
    --enable-hardening
    --enable-lto
    --enable-linker=gold
    --enable-optimize
    --enable-readline
    --enable-release
    --enable-shared-js
    --enable-tests
    --with-intl-api
    --with-system-zlib
    --without-system-icu
  )

  export AR=llvm-ar
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  cd firefox-$pkgver/obj

  sh ../js/src/configure "${configure_args[@]}"
  make ${MAKEFLAGS}
}

# check() {
#   local jstests_extra_args=(
#     --format=none
#     --exclude-random
#   ) jittest_extra_args=(
#     --format=none
#     --timeout 300
#   ) jittest_test_args=(
#     basic
#   )

#   cd firefox-$pkgver/obj
#   make ${MAKEFLAGS} -C js/src check-jstests check-jit-test \
#     JSTESTS_EXTRA_ARGS="${jstests_extra_args[*]}" \
#     JITTEST_EXTRA_ARGS="${jittest_extra_args[*]}" \
#     JITTEST_TEST_ARGS="${jittest_test_args[*]}"
# }

package() {
  cd firefox-$pkgver/obj
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/*.ajs
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -c a-x {} +
}