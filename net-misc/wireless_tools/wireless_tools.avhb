# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=wireless_tools
pkgver=30.pre9
_pkgver=${pkgver%.pre*}
pkgrel=1
pkgdesc='Tools allowing to manipulate the Wireless Extensions'
arch=('x86_64')
groups=('net-misc')
url='https://hewlettpackard.github.io/wireless-tools/Tools.html'
license=('GPL')
source=("https://hewlettpackard.github.io/wireless-tools/${pkgname}.${pkgver}.tar.gz")

prepare() {
	cd "${srcdir}/${pkgname}.${_pkgver}"

	#patch -p1 -i ../dense.patch # FS#15363
	sed '/BUILD_STATIC =/d' -i Makefile
}

build() {
	cd "${srcdir}/${pkgname}.${_pkgver}"
	make CFLAGS="${CFLAGS} -I." LDFLAGS="${LDFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}.${_pkgver}"
	make \
	INSTALL_DIR="${pkgdir}/usr/bin" \
	INSTALL_LIB="${pkgdir}/usr/lib" \
	INSTALL_INC="${pkgdir}/usr/include" \
	INSTALL_MAN="${pkgdir}/usr/share/man" \
	install
}
