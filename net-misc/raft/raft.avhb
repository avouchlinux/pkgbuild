# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=raft
pkgver=0.22.1
pkgrel=1
pkgdesc="C implementation of the Raft consensus protocol"
arch=('x86_64')
groups=('net-misc')
url="https://raft.readthedocs.io/"
license=('LGPL-3.0-only')
depends=('libuv' 'lz4')
options=(strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/cowsql/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('385f91a0b542ebe8b81c8f8500310dcd575fd028ea0cd2ede8807fa920dcf604')

build() {
	cd "${pkgname}-${pkgver}"
	autoreconf -i
	./configure --prefix=/usr
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	make check || true
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir/" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm -rf "${pkgdir}/usr/bin"
}

