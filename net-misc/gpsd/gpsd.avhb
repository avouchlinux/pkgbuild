# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gpsd
pkgver=3.22
pkgrel=1
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('x86_64')
groups=('net-misc')
url="http://catb.org/gpsd/"
license=('BSD')
depends=('python' 'libusb' 'bluez-libs')
optdepends=('gtk3: GUI frontends'
            'php: generate a PHP status page for your GPS'
            'php-gd: image support for the PHP status page'
            'python-cairo: GUI frontends'
            'python-gobject: GUI frontends'
            'qt5-base: for Qgpsmm')
makedepends=('scons' 'docbook-xsl' 'qt5-base' 'python-gobject' 'python-cairo')
backup=('etc/default/gpsd')
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig}
        $pkgname.conf fix-python-libdir.patch)
validpgpkeys=('EED4A0893DCC705DB309E202CCF29C7238522905') # Gary E. Miller <gem@rellim.com>
# validpgpkeys=('41876B2F579463A499843D1DECC8208F8C6C738D') # Eric S. Raymond

build() {
  cd $pkgname-$pkgver
 
  export LDFLAGS=''
  export LINKFLAGS=''
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  scons prefix=/usr \
        systemd=yes \
        gpsd_group=uucp \
        python_libdir=$_pythonpath \
        sbindir=/usr/bin \
        qt_versioned=5
  scons build
}

package() {
  cd $pkgname-$pkgver

  export DESTDIR="${pkgdir}"
  scons install
  install -Dm755 gpsinit -t "${pkgdir}"/usr/bin

  install -Dm644 "$srcdir/gpsd.conf" "${pkgdir}"/etc/default/gpsd

  install -Dm644 gpsd-$pkgver/gpsd.rules "${pkgdir}"/usr/lib/udev/rules.d/25-gpsd-usb.rules

  install -Dm755 gpsd.hotplug -t "${pkgdir}"/usr/lib/udev/

  install -Dm644 packaging/X11/*.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm644 packaging/X11/gpsd-logo.png -t "${pkgdir}"/usr/share/gpsd/

  install -Dm644 gpsd-$pkgver/systemd/{gpsd.service,gpsd.socket,gpsdctl@.service} -t "${pkgdir}"/usr/lib/systemd/system/

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}