# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=netctl
pkgver=1.29
pkgrel=1
pkgdesc='Profile based systemd network management'
url='https://projects.archlinux.org/netctl.git/'
license=('GPL')
arch=('x86_64')
groups=('net-misc')
depends=('coreutils' 'iproute2' 'resolvconf' 'systemd>=233')
# The source tarball includes pre-built (using asciidoc) documentation.
makedepends=('pkg-config')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'ppp: for PPP connections'
            'openvswitch: for Open vSwitch connections'
            'wireguard-tools: for WireGuard connections')
install=netctl.install
source=(https://sources.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz{,.sig})
arch=('any')
sha256sums=('27f91ad21a8fe8cb1e14b5a816580167da36e8ef056de40c87b3975d86b76932'
            'SKIP')
validpgpkeys=('CFA6AF15E5C74149FC1D8C086D1655C14CE1C13E')  # Florian Pritz

package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  ln -s netctl "$pkgdir/usr/share/bash-completion/completions/netctl-auto"
  ln -s netctl "$pkgdir/usr/share/bash-completion/completions/wifi-menu"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

