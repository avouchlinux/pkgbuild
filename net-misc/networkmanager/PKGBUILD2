# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=networkmanager
pkgname=('networkmanager' 'libnm' 'libnm-glib')
_realname=NetworkManager
pkgver=1.14.0
pkgrel=1
_pppver=2.4.7
pkgdesc="Network Management daemon"
arch=('x86_64')
groups=('net')
repo=('core')
license=(GPL2 LGPL2.1)
url="http://www.gnome.org/projects/NetworkManager/"
makedepends=(intltool dhclient iptables gobject-introspection gtk-doc "ppp=$_pppver" modemmanager
             dbus-glib iproute2 nss polkit wpa_supplicant libsoup systemd libgudev libmm-glib
             libnewt libndp libteam vala perl-yaml python-gobject git vala jansson bluez-libs
             glib2-docs)
checkdepends=(libx11 python-dbus)
groups=('networking')
repo=('core')
#source=(git://anongit.freedesktop.org/NetworkManager/NetworkManager#commit=93c1041
source=(http://ftp.gnome.org/pub/gnome/sources/NetworkManager/${pkgver:0:3}/NetworkManager-$pkgver.tar.xz
        NetworkManager.conf disable_set_hostname.patch git-fixes.patch
        https://gitlab.freedesktop.org/NetworkManager/NetworkManager/commit/0a3755c179.patch#/0001-version-fix-compile-error-due-to-NM_AVAILABLE_IN_1_1.patch)
sha256sums=('66a88346bb04d4f402540281181340313b2ec433e75aa9d9ea13f31697f9487e'
            '759db295ddae7a6dc6b29211fc0ec08695f875584d456dd146d3679e2c33e2e3'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460'
            '854b5f06fed30cbab2d71544197d53a8aacdeee12ec78a7f48acb9ff31b40889')

prepare() {
    cd "${srcdir}/${_realname}-${pkgver}"

    #NOCONFIGURE=1 ./autogen.sh
}

build() {
    avouch-meson ${_realname}-${pkgver} build \
        -D more_logging=false \
        -D session_tracking_consolekit=false \
        -D netconfig=false \
        -D ofono=false \
        -D docs=true \
        -D bluez5_dun=true \
        -D concheck=true \
        -D ibft=true \
        -D introspection=true \
        -D json_validation=true \
        -D ld_gc=true \
        -D modify_system=true \
        -D polkit=yes \
        -D polkit_agent=true \
        -D teamdctl=true \
        -D wifi=true \
        -D libnm_glib=true \
        -D nmcli=true \
        -D nmtui=true \
        -D wext=true \
        -D systemd_journal=true \
        -D session_tracking=systemd \
        -D suspend_resume=systemd \
        -D config_dhcp_default=dhclient \
        -D config_dns_rc_manager_default=symlink \
        -D config_logging_backend_default=journal \
        -D config_plugins_default=keyfile,ibft \
        -D crypto=nss \
        -D dbus_sys_dir=/usr/share/dbus-1/system.d \
        -D dhclient=/usr/bin/dhclient \
        -D dist_version="$pkgver-$pkgrel" \
        -D dnsmasq=/usr/bin/dnsmasq \
        -D dnssec_trigger=/usr/lib/dnssec-trigger/dnssec-trigger-script \
        -D hostname_persist=default \
        -D iptables=/usr/bin/iptables \
        -D kernel_firmware_dir=/usr/lib/firmware \
        -D pppd_plugin_dir=/usr/lib/pppd/$_pppver \
        -D pppd=/usr/bin/pppd \
        -D resolvconf=/usr/bin/resolvconf \
        -D system_ca_path=/etc/ssl/certs \
        -D systemdsystemunitdir=/usr/lib/systemd/system \
        -D udev_dir=/usr/lib/udev

    ninja -C build
}

check() {
    meson test -C build
}


_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_networkmanager() {
    depends=(libnm-glib iproute2 polkit wpa_supplicant libmm-glib libnewt libndp libteam curl
           bluez-libs libpsl)
    optdepends=('dnsmasq: connection sharing'
              'bluez: Bluetooth support'
              'ppp: dialup connection support'
              'modemmanager: cellular network support'
              'iwd: wpa_supplicant alternative')
    backup=('etc/NetworkManager/NetworkManager.conf')

    DESTDIR="$pkgdir" meson install -C build

    # packaged configuration
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/NetworkManager/conf.d/20-connectivity.conf" <<END
[connectivity]
uri=http://www.avouch.org/check_network_status.txt
END

    # /etc/NetworkManager
    install -d "$pkgdir"/etc/NetworkManager/{conf,dnsmasq}.d
    install -dm700 "$pkgdir/etc/NetworkManager/system-connections"
    install -m644 /dev/stdin "$pkgdir/etc/NetworkManager/NetworkManager.conf" <<END
# Configuration file for NetworkManager.
# See "man 5 NetworkManager.conf" for details.
END

    ### Split libnm
    _pick libnm "$pkgdir"/usr/include/libnm
    _pick libnm "$pkgdir"/usr/lib/girepository-1.0/NM-*
    _pick libnm "$pkgdir"/usr/lib/libnm.*
    _pick libnm "$pkgdir"/usr/lib/pkgconfig/libnm.pc
    _pick libnm "$pkgdir"/usr/share/gir-1.0/NM-*
    _pick libnm "$pkgdir"/usr/share/gtk-doc/html/libnm
    _pick libnm "$pkgdir"/usr/share/vala/vapi/libnm.*

    ### Split libnm-glib
    _pick libnm-glib "$pkgdir"/usr/include/*
    _pick libnm-glib "$pkgdir"/usr/lib/girepository-1.0/*
    _pick libnm-glib "$pkgdir"/usr/lib/libnm-*
    _pick libnm-glib "$pkgdir"/usr/lib/pkgconfig/*
    _pick libnm-glib "$pkgdir"/usr/share/gir-1.0/*
    _pick libnm-glib "$pkgdir"/usr/share/gtk-doc/html/libnm-*
    _pick libnm-glib "$pkgdir"/usr/share/vala/vapi/*
}

package_libnm() {
    pkgdesc="NetworkManager client library"
    depends=(glib2 nss libutil-linux jansson libsystemd)
    mv libnm/* "$pkgdir"
}

package_libnm-glib() {
    pkgdesc="NetworkManager client library (legacy)"
    depends=(libnm dbus-glib)
    mv libnm-glib/* "$pkgdir"
}
