# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=dleyna-core
pkgver=0.6.0
pkgrel=1
pkgdesc="Library of utility functions that are used by the higher level dLeyna"
arch=('x86_64')
groups=('net-misc')
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(gupnp)
makedepends=(git)
source=("https://github.com/intel/dleyna-core/archive/v$pkgver.tar.gz")

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/gupnp-1\.0/gupnp-1.2/g' configure.ac *.pc.in
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

check() {
  cd $pkgname-$pkgver
  make ${MAKEFLAGS} check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
