# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=arpwatch
pkgver=2.1a15
pkgrel=1
pkgdesc='Ethernet/FDDI station activity monitor'
arch=('x86_64')
groups=('net-misc')
url='ftp://ftp.ee.lbl.gov/'
license=('BSD')
depends=('libpcap' 'smtp-forwarder')
source=("ftp://ftp.ee.lbl.gov/$pkgname-$pkgver.tar.gz"
        'ethercodes.dat.xz'
        'LICENSE'
        "$pkgname.service")

prepare() {
  cd $pkgname-$pkgver
  # move arp database in /var/lib/arpwatch
  # /var is used because arpwatch write its learned mac into this folder
  sed -i 's|ARPDIR = $(prefix)/arpwatch|ARPDIR = /var/lib/arpwatch|' Makefile.in
  # binary ownership to root
  sed -i 's/-\(o\|g\) bin/-\1 root/g' Makefile.in
  # Update ethercodes with recent OUI. See gen_ethercodes.sh
  cp -f "$srcdir/ethercodes.dat" ethercodes.dat
  # Do not spam root user
  sed -i 's|root|arpwatch|' addresses.h.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin
  make ${MAKEFLAGS} SENDMAIL=/usr/bin/sendmail
}

package() {
  cd $pkgname-$pkgver
  install -d -m 0755 "$pkgdir"/usr/{bin,share/man/man8}
  make DESTDIR="$pkgdir" install install-man
  # install ether prefix database
  install -Dm644 ethercodes.dat "$pkgdir/var/lib/$pkgname/ethercodes.dat"
  cd "$srcdir"
  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # systemd
  install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
}