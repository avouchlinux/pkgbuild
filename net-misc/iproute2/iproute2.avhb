# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=iproute2
pkgver=5.6.0
pkgrel=1
pkgdesc='IP Routing Utilities'
arch=('x86_64')
groups=('net-misc')
license=('GPL2')
url='https://git.kernel.org/pub/scm/network/iproute2/iproute2.git'
depends=('glibc' 'iptables' 'libelf')
optdepends=('linux-atm: ATM support')
provides=('iproute')
backup=('etc/iproute2/ematch_map'
        'etc/iproute2/rt_dsfield'
        'etc/iproute2/rt_protos'
        'etc/iproute2/rt_realms'
        'etc/iproute2/rt_scopes'
        'etc/iproute2/rt_tables')
makedepends=('linux-atm')
options=('staticlibs')
source=("https://www.kernel.org/pub/linux/utils/net/${pkgname}/${pkgname}-${pkgver}.tar."{xz,sign}
        '0001-make-iproute2-fhs-compliant.patch')
validpgpkeys=('9F6FC345B05BE7E766B83C8F80A77F6095CDE47E') # Stephen Hemminger

prepare() {
        cd "${srcdir}/${pkgname}-${pkgver}"

	# set correct fhs structure
	patch -Np1 -i "${srcdir}/0001-make-iproute2-fhs-compliant.patch"

	# do not treat warnings as errors
	sed -i 's/-Werror//' Makefile

}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --with-selinux
	make ${MAKEFLAGS}
}

package() {
        cd "${srcdir}/${pkgname}-${pkgver}"

        make DESTDIR="${pkgdir}" SBINDIR="/usr/bin" install

        # libnetlink isn't installed, install it FS#19385
        install -Dm0644 include/libnetlink.h "${pkgdir}/usr/include/libnetlink.h"
        install -Dm0644 lib/libnetlink.a "${pkgdir}/usr/lib/libnetlink.a"
}
