# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=network-manager-sstp
pkgver=1.2.6
pkgrel=1
pkgdesc="SSTP support for NetworkManager"
arch=('x86_64')
groups=('net-misc')
url="http://sstp-client.sourceforge.net/#Network_Manager_Plugin"
license=('GPL2')
depends=('gtk3' 'libnma' 'libsecret' 'sstp-client')
optdepends=('ppp')
makedepends=('intltool' 'ppp' 'python')
source=("https://github.com/enaess/network-manager-sstp/archive/release-${pkgver}.tar.gz")

build() {
  pppd_version=(`pppd --version 2>&1 | awk '{print $3}'`)
  cd network-manager-sstp-release-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$pppd_version \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib/NetworkManager \
    --with-libnm-glib=no \
    --enable-more-warnings=yes
  # libnm-glib disabled due to missing libnm-gtk package
  # set_more_warnings being error by default, which adds -Werror

  make ${MAKEFLAGS}
}

package() {
  cd network-manager-sstp-release-$pkgver

  make DESTDIR="$pkgdir" dbusservicedir=/usr/share/dbus-1/system.d install
}