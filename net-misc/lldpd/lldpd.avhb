# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=lldpd
pkgver=1.0.11
pkgrel=1
pkgdesc='802.1ab implementation (LLDP) to help you locate neighbors'
arch=('x86_64')
groups=('net-misc')
url='https://vincentbernat.github.io/lldpd/'
license=('custom:ISC' 'GPL')
depends=('glibc' 'libevent' 'libbsd' 'zlib' 'openssl' 'pciutils' 'perl'
         'libxml2' 'net-snmp' 'jansson')
backup=('etc/lldpd.conf')
validpgpkeys=('AEF2348766F371C689A7360095A42FE8353525F9') # Vincent Bernat <bernat@luffy.cx>
source=("https://media.luffy.cx/files/lldpd/lldpd-${pkgver}.tar.gz"
        "https://media.luffy.cx/files/lldpd/lldpd-${pkgver}.tar.gz.gpg"
        'lldpd.tmpfiles.conf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-lldpd-ctl-socket=/run/lldpd/socket \
    --with-lldpd-pid-file=/run/lldpd/pid \
    --with-privsep-chroot=/run/lldpd/chroot \
    --with-privsep-group=lldpd \
    --with-privsep-user=lldpd \
    --with-readline \
    --with-snmp \
    --with-xml
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  
  make DESTDIR="${pkgdir}" install

  # re-introduce tmpfiles file untill resolved upstream...
  # https://github.com/vincentbernat/lldpd/pull/311
  install -D -m 0644 "${srcdir}/lldpd.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/lldpd.conf"

  # config stuff
  install -D -m 0644 /dev/null "${pkgdir}/etc/lldpd.conf"
  install -d -m 0755 "${pkgdir}/etc/lldpd.d"

  # license
  install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
