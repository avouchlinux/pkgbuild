# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=networkmanager-vpnc
_realname=NetworkManager-vpnc
pkgver=1.2.8
pkgrel=1
pkgdesc="NetworkManager VPN plugin for VPNC"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=('x86_64')
groups=('net-misc')
license=(GPL)
depends=(
  libnm
  libsecret
  vpnc
)
makedepends=(
  git
  intltool
  libnma
  libnma-gtk4
  python
)
optdepends=(
  'libnma-gtk4: GUI support (GTK 4)'
  'libnma: GUI support (GTK 3)'
)
source=("https://gitlab.gnome.org/GNOME/$_realname/-/archive/$pkgver/$_realname-$pkgver.tar.bz2")


prepare() {
  cd NetworkManager-vpnc-$pkgver

  intltoolize --automake --copy
  autoreconf -fvi
}

build() {
  cd NetworkManager-vpnc-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/NetworkManager --disable-static \
    --with-libnm-gtk=no
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-vpnc-$pkgver
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d
}