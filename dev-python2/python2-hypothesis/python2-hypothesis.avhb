# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python-hypothesis
_realname=hypothesis
pkgver=5.8.0
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
groups=('dev-python')
license=('MPL')
url="https://hypothesis.readthedocs.org"
depends=('python-attrs' 'python-sortedcontainers')
optdepends=('python-pytz: for datetime and django module'
            'python-faker: for fakefactory and django module'
            'python-django: for django module'
            'python-numpy: for numpy module'
            'python-pytest: for pytest module'
            'python-lark-parser: for lark module')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'flake8' 'python-pytz' 'python-numpy' 'python-faker'
              'python-flaky' 'python-django' 'python-mock' 'python-pandas' 'python-dpcontracts'
              'python-pytest-xdist' 'python-lark-parser' 'python-pexpect' 'python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('60e355e2f3a03264fff852f422fcceec6ca621e28ea63e100f85ab0144a8e291e7fada78e82ad55623c0bfba971008a059bc125eff89c82d5ddfbfca88e4d43c')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver
}

build() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py build
}

check() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py pytest
}

package() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1
}
