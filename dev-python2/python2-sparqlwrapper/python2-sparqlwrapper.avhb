# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=python-sparqlwrapper
pkgname=("python-sparqlwrapper" "python2-sparqlwrapper")
_realname=sparqlwrapper
pkgver=1.8.2
pkgrel=1
pkgdesc="SPARQL Endpoint interface to Python"
arch=('any')
groups=('dev-python')
url="http://rdflib.github.io/sparqlwrapper/"
license=('W3C')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/RDFLib/sparqlwrapper/archive/$pkgver.tar.gz")
sha512sums=('a98ff3276c56d464775f2db032eeb14cd67eca447fe85c48086e7ee2895529c53cbd3b9f11464ff3918ad8f8e9d00729c26ef0c55ac7350cabc05edb68e3af93')
md5sums=('SKIP')

prepare() {
	cp -a ${_realname}-${pkgver} python-build
	cp -a ${_realname}-${pkgver} python2-build
}

build() {
	cd "$srcdir/python-build"
	python setup.py build

	cd "$srcdir/python2-build"
	python2 setup.py build
}

check() {
	cd "$srcdir/python-build"
	./run_tests_py3.sh

	cd "$srcdir/python2-build"
	nosetests2
}

package_python-sparqlwrapper() {
	depends=('python')

	cd "$srcdir/python-build"
	LC_CTYPE=en_US.UTF-8 python setup.py install --root="${pkgdir}" --optimize=1
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-sparqlwrapper() {
	depends=('python2')

	cd "$srcdir/python2-build"
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
