# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=pep8
_realname=pycodestyle
pkgname=("$pkgbase" "python2-$pkgbase")
pkgver=1.7.1
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/jcrocholl/pep8/"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/jcrocholl/${pkgbase}.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
	cp -a pycodestyle-${pkgver} python-build
	cp -a pycodestyle-${pkgver} python2-build

	sed -i '1s/env python$/python2/' ${srcdir}/python2-build/pep8.py
}

build() {
	cd "${srcdir}/python-build"
	python setup.py build

	cd "${srcdir}/python2-build"
	python2 setup.py build
}

check() {
	cd "${srcdir}/python-build"
	python setup.py test

	cd "${srcdir}/python2-build"
	python2 setup.py test
}

package_pep8() {
	depends=('python' 'python-setuptools')
	provides=('pep8-python3')
	conflicts=('pep8-python3')
	replaces=('pep8-python3')

	cd "${srcdir}/python-build"
	python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pep8() {
	depends=('python2' 'python2-setuptools')
	provides=('pep8-python2')
	conflicts=('pep8-python2')
	replaces=('pep8-python2')

	cd "${srcdir}/python2-build"
	python2 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
	mv "$pkgdir/usr/bin/pep8" "$pkgdir/usr/bin/pep8-python2"

	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
