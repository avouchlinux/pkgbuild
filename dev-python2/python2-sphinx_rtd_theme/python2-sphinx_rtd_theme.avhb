# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=python-sphinx_rtd_theme
pkgname=('python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
_realname=sphinx_rtd_theme
pkgver=0.4.3
pkgrel=1
pkgdesc="Python Sphinx Read The Docs Theme"
url='https://github.com/rtfd/sphinx_rtd_theme/'
arch=('any')
groups=('dev-python')
license=('MIT')

makedepends=('python-setuptools' 'python2-setuptools')

source=("https://pypi.org/packages/source/s/sphinx_rtd_theme/sphinx_rtd_theme-$pkgver.tar.gz")

sha256sums=('2df74b8ff6fae6965c527e97cca6c6c944886aae474b490e17f92adfbe843417')

prepare() {
	cp -a ${_realname}-${pkgver} python-build
	cp -a ${_realname}-${pkgver} python2-build
}

build() {
	cd ${srcdir}/python-build
	python setup.py build

	cd ${srcdir}/python2-build
	python2 setup.py build	
}

package_python-sphinx_rtd_theme() {
	cd ${srcdir}/python-build
	python setup.py install --root="$pkgdir" --optimize=1
	# License
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx_rtd_theme() {
	cd ${srcdir}/python2-build
	python2 setup.py install --root="$pkgdir" --optimize=1
	# License
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

