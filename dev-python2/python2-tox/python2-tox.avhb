# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=python-tox
pkgname=(python-tox python2-tox)
_realname=tox
pkgver=3.13.2
pkgrel=1
pkgdesc='Python virtualenv management and testing tool'
arch=('any')
groups=('dev-python')
url='https://tox.readthedocs.io'
license=('GPL2')
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-py' 'python2-py' 'python-virtualenv'
             'python2-virtualenv' 'python-pluggy' 'python2-pluggy' 'python-toml' 'python2-toml'
             'python-filelock' 'python2-filelock' 'python-freezegun' 'python2-freezegun')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-mock'
              'python2-pytest-mock' 'python2-pathlib2' 'python-flaky' 'python2-flaky'
              'python-pathlib2' 'python2-pathlib2')
source=("https://pypi.io/packages/source/t/tox/tox-$pkgver.tar.gz")
sha512sums=('06c92c803bef7c2dd3796ecc869d78dc4d3044e62688bcf529b7c6b8330532aab30dcc44425fc226886a35d8227225db5e3f2435a469e90af7ad440231abbc35')

prepare() {
  find tox-$pkgver -name "*.pyc" -delete

  cp -a tox-$pkgver{,-py2}

  sed -i 's|pathlib2|pathlib|' tox-$pkgver/tests/integration/*.py
}

build() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/tox-$pkgver
  python setup.py build

  cd "$srcdir"/tox-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points

  (
    cd "$srcdir"/tox-$pkgver
    virtualenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  )

  (
    cd "$srcdir"/tox-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
    . "$srcdir/pyvenv-py2/bin/activate"
    python setup.py install
    python setup.py pytest
  )
}

package_python-tox() {
  depends=('python-py' 'python-virtualenv' 'python-setuptools' 'python-pluggy' 'python-toml'
           'python-filelock')

  cd "$srcdir"/tox-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  ln -s tox "$pkgdir"/usr/bin/tox3
}

package_python2-tox() {
  depends=('python2-py' 'python2-virtualenv' 'python2-setuptools' 'python2-pluggy' 'python2-toml'
           'python2-filelock')

  cd "$srcdir"/tox-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/tox{,2}
  mv "$pkgdir"/usr/bin/{tox-quickstart,tox2-quickstart}
}
