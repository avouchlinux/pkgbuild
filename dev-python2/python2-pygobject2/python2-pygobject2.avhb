# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=pygobject2
pkgname=(python-gobject2 python2-gobject2 pygobject2-devel)
_realname=pygobject
pkgver=2.28.7
pkgrel=1
pkgdesc="Python bindings for GObject (legacy)"
url="http://www.pygtk.org/"
arch=(x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(python python2 git)
options=(!emptydirs)
source=("https://download.gnome.org/sources/pygobject/2.28/pygobject-$pkgver.tar.xz")
sha256sums=('SKIP')

prepare() {
  mkdir build-py{2,3} devel
  cd pygobject-$pkgver
  find . \( -name '*.py' -o -name '*.py.in' \) -exec sed -i '1s|python$|&2|' {} +
  autoreconf -fvi
}

_build() (
  cd build-py$1
  ../pygobject-$pkgver/configure --prefix=/usr --disable-introspection PYTHON=/usr/bin/python$1
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
)

build() {
  _build 2
  _build 3
}

package_python-gobject2() {
  depends=("pygobject2-devel=$pkgver" python)

  cd build-py3
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,lib/pkgconfig,share/pygobject/xsl}
  rm -r "$pkgdir"/usr/share/gtk-doc
}

package_python2-gobject2() {
  pkgdesc="${pkgdesc/Python/Python2}"
  depends=("pygobject2-devel=$pkgver" python2)

  cd build-py2
  make DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/{include,lib/pkgconfig,share/pygobject/xsl} "$srcdir/devel"
  rm -r "$pkgdir"/usr/share/gtk-doc
}

package_pygobject2-devel() {
  pkgdesc="Common development files for pygobject2"

  cd devel
  mkdir -p "$pkgdir"/usr/{lib,share/pygobject}
  mv include "$pkgdir/usr/"
  mv pkgconfig "$pkgdir/usr/lib/"
  mv xsl "$pkgdir/usr/share/pygobject/"
}