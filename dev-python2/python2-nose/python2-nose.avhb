# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python2-nose
_realname=nose
pkgver=1.3.7
pkgrel=1
pkgdesc="A discovery-based unittest extension"
arch=('any')
groups=('dev-python2')
url='http://readthedocs.org/docs/nose/'
license=('LGPL2.1')
depends=('python2' 'python2-setuptools')
makedepends=('python2' 'python2-setuptools' 'python2-sphinx')
source=("http://pypi.python.org/packages/source/n/nose/nose-${pkgver}.tar.gz")

prepare() {
	cd ${_realname}-${pkgver}
	sed -i -e "s:man/man1:share/man/man1:g" setup.py
}

build() { 
	cd ${_realname}-${pkgver}
	python2 setup.py build
}

check() {	
	cd ${_realname}-${pkgver}
	python2 setup.py test
}

package() {	
	cd ${_realname}-${pkgver}
	python2 setup.py install --root "${pkgdir}" --optimize=1
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	mv "$pkgdir/usr/bin/nosetests" "$pkgdir/usr/bin/nosetests2"
	# documents available in python3-nose
	rm -rf "$pkgdir/usr/share"
}