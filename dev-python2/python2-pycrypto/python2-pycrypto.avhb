# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=pycrypto
pkgname=('python2-crypto' 'python-crypto')
pkgver=2.6.1
pkgrel=1
arch=('x86_64')
groups=('dev-python')
makedepends=('python2' 'python')
url="http://www.dlitz.net/software/pycrypto/"
license=('custom')
source=(http://ftp.dlitz.net/pub/dlitz/crypto/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.asc})
sha256sums=('f2ce1e989b272cfcb677616763e0a2e7ec659effa67a88aa92b3a65528f60a3c'
            'c2ab0516cc55321e6543ae75e2aa6f6e56e97432870f32a7799f3b89f467dc1b')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	find LEGAL -type f -exec chmod 644 {} \;
	find LEGAL -type d -exec chmod 755 {} \;

	cd ${srcdir}
	cp -a ${pkgbase}-${pkgver} python-build
	cp -a ${pkgbase}-${pkgver} python2-build
}

build() {
	# python2 build
	cd ${srcdir}/python2-build
	python2 setup.py build

	# python3 build
	cd ${srcdir}/python-build
	python setup.py build
}

package_python2-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 2."
  depends=('python2')
  replaces=('pycrypto')
  conflicts=('pycrypto')
  provides=("pycrypto=${pkgver}")

	cd ${srcdir}/python2-build
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
	cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_python-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 3."
  depends=('python')

	cd ${srcdir}/python-build
	python setup.py install --root="${pkgdir}" --optimize=1
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
	cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}
