# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=python2-sphinx
_realname=Sphinx
pkgver=1.8.5
pkgrel=1
pkgdesc='Python2 documentation generator'
arch=('any')
url='https://www.sphinx-doc.org/'
license=('BSD')
depends=('python2-babel'
         'python2-docutils'
         'python2-imagesize'
         'python2-jinja'
         'python2-pygments'
         'python2-requests'
         'python2-setuptools'
         'python2-six'
         'python2-snowballstemmer'
         'python2-sphinx-alabaster-theme'
         'python2-sphinxcontrib-websupport'
         'python2-typing')
makedepends=(python-setuptools)
optdepends=('imagemagick: for ext.imgconverter'
            'texlive-latexextra: for generation of PDF documentation')
source=("https://pypi.org/packages/source/S/Sphinx/Sphinx-$pkgver.tar.gz"
         python2-lexer.patch)
validpgpkeys=('8A11B79A5D0D749A66F0A030102C2C17498D6B9E'  # Takeshi KOMIYA
              'E9BEABB07E7B9CC3F56E62C91425F8CE5EBA0E07') # Takayuki Shimizukawa

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ../python2-lexer.patch # Use proper python lexer with new pygments (NixOS)

  # change python2 interpreter
  find -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  # change sphinx-binaries name in source code
  find -type f -name '*.py' -exec \
    sed -ri 's,(sphinx-(:?build|apidoc|autogen|quickstart)),\12,' {} \;
}

build() {
  cd ${_realname}-${pkgver}
  make PYTHON=python2 build
}

package() {
  cd ${_realname}-${pkgver}
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}