# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=mingw-w64-winpthreads
_realname=mingw-w64
pkgver=9.0.0
pkgrel=1
pkgdesc='MinGW-w64 winpthreads library'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=('dev-mingw-w64')
makedepends=('mingw-w64-gcc' 'mingw-w64-binutils' "mingw-w64-crt=${pkgver}")
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
source=(https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2{,.sig})

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  export LDFLAGS=" -O0"
  for _target in ${_targets}; do
    message "Building ${_target} winpthreads..."
    mkdir -p "$srcdir"/winpthreads-build-${_target} && cd "$srcdir"/winpthreads-build-${_target}
    "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-libraries/winpthreads/configure --prefix=/usr/${_target} \
        --host=${_target} --enable-static --enable-shared
    make ${MAKEFLAGS}
  done
}

package() {
  for _target in ${_targets}; do
    cd "$srcdir"/winpthreads-build-${_target}
    make DESTDIR="$pkgdir" install
    ${_target}-strip --strip-unneeded "$pkgdir"/usr/${_target}/bin/*.dll
  done
}