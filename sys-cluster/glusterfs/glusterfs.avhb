# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=glusterfs
epoch=1
pkgver=9.4
pkgrel=1
pkgdesc='a cluster file-system capable of scaling to several peta-bytes.'
arch=('x86_64')
groups=('sys-cluster')
url='https://www.gluster.org/'
license=(GPL2 LGPL3)
install=glusterfs.install
backup=('etc/glusterfs/glusterd.vol'
        'etc/glusterfs/eventsconfig.json'
        'etc/glusterfs/gluster-rsyslog-5.8.conf'
        'etc/glusterfs/gluster-rsyslog-7.2.conf'
        'etc/glusterfs/glusterd.vol'
        'etc/glusterfs/glusterfs-georep-logrotate'
        'etc/glusterfs/glusterfs-logrotate')
depends=(fuse python libxml2 libaio liburcu attr rpcbind)
makedepends=(rpcsvc-proto)
optdepends=('glib2: qemu-block'
	    'python-prettytable: gluster-georep-sshkey')
#source=($pkgname-$pkgver.tar.gz::https://github.com/gluster/glusterfs/archive/v$pkgver.tar.gz)
source=(https://download.gluster.org/pub/gluster/glusterfs/LATEST/glusterfs-$pkgver.tar.gz
        glusterfs.sysusers)

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-mountutildir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --libexecdir=/usr/lib/$pkgname \
    --with-systemddir=/usr/lib/systemd/system \
    --with-tmpfilesdir=/usr/lib/tmpfiles.d \
    --enable-gnfs \
    LEXLIB=
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make -j1 DESTDIR="$pkgdir" install

  # https://bugzilla.redhat.com/show_bug.cgi?id=1598900
  install -Dm644 "$srcdir"/glusterfs.sysusers \
    "$pkgdir"/usr/lib/sysusers.d/glusterfs.conf

  install -D -m 644 \
    "$srcdir"/$pkgname-$pkgver/{README.md,INSTALL,COPYING*} \
    "$pkgdir"/usr/share/doc/glusterfs/

  cp -rf \
    "$srcdir"/$pkgname-$pkgver/doc/* \
    "$pkgdir"/usr/share/doc/glusterfs/
  rm -rf "$pkgdir"/var/run
}