# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=hdf5
pkgver=1.12.0
pkgrel=1
pkgdesc="General purpose library and file format for storing scientific data"
arch=('x86_64')
groups=('sci-libs')
url="https://www.hdfgroup.org/hdf5/"
license=('custom')
depends=('zlib' 'bash')
makedepends=('time' 'gcc-fortran')
source=("https://support.hdfgroup.org/ftp/HDF5/releases/${pkgname}-${pkgver:0:4}/${pkgname}-${pkgver/_/-}/src/${pkgname}-${pkgver/_/-}.tar.bz2")
# https://support.hdfgroup.org/ftp/HDF5/releases/${pkgname}-${pkgver:0:4}/${pkgname}-${pkgver/_/-}/src/${pkgname}-${pkgver/_/-}.md5

build() {
    cd ${pkgname}-${pkgver/_/-}

    ./configure \
        --prefix=/usr \
        --disable-static \
        --enable-hl \
        --enable-build-mode=production \
        --with-pic \
        --docdir=/usr/share/doc/hdf5/ \
        --disable-sharedlib-rpath \
        --enable-cxx \
        --enable-fortran \
        --with-zlib
    make ${MAKEFLAGS}
}

package() {
    cd ${pkgname}-${pkgver/_/-}

    make -j1 DESTDIR="${pkgdir}" install

    rm -rf "${pkgdir}"/usr/lib/libdynlib*.so

    install -dm755 "${pkgdir}"/usr/share/${pkgname}

    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
