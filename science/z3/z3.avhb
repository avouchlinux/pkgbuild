# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=z3
pkgname=('z3' 'python3-z3')
pkgver=4.8.10
pkgrel=1
pkgdesc='High-performance theorem prover'
arch=('x86_64')
groups=('science')
url='https://github.com/Z3Prover/z3'
license=('MIT')
 # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib'
makedepends=('cmake' 'python3' 'python3-setuptools' 'gcc-libs' 'gmp')
source=(https://github.com/Z3Prover/z3/archive/Z3-${pkgver}.tar.gz)

build() {
  cmake -S ${pkgbase}-${pkgbase}-${pkgver} -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_LINK_TIME_OPTIMIZATION=1 \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DZ3_USE_LIB_GMP=1
  make ${MAKEFLAGS} -C build
}

package_z3() {
  depends=('gcc-libs' 'gmp')
  provides=('libz3' 'libz3.so')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/lib/python*
}

package_python3-z3() {
  depends=('python3' 'libz3')
  cd build
  make DESTDIR="${pkgdir}" install
  cd "${pkgdir}"
  rm -r usr/include usr/bin usr/lib/cmake usr/lib/lib*
  python -m compileall .
  python -O -m compileall .
}
