# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=libtool
pkgver=2.4.7
_gccver=11.3.0
pkgrel=1
pkgdesc="A generic library support script"
arch=('x86_64')
groups=('dev-utils')
url="http://www.gnu.org/software/libtool"
license=('GPL')
depends=(sh tar glibc)
makedepends=("gcc=$_gccver" git help2man)
checkdepends=(gcc-fortran)
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver")
install=libtool.install
_commit=6d7ce133ce54898cf28abd89d167cccfbc3c9b2b
source=(https://git.savannah.gnu.org/git/libtool.git
        https://git.savannah.gnu.org/git/gnulib.git
        gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git)
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan

getsrc() {
	git clone https://git.savannah.gnu.org/git/libtool.git
	# git clone https://git.savannah.gnu.org/git/gnulib.git
	# git clone https://github.com/gnulib-modules/bootstrap.git gnulib-bootstrap
}
pkgver() {
	cd libtool
	git checkout $_commit
	git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
	bsdtar -xf $srcarchivedir/gnulib-git20220422.tar.xz
	bsdtar -xf $srcarchivedir/gnulib-bootstrap-git20220422.tar.xz
  	cd libtool

	patch -Np1 -i ${srcdir}/no_hostname.patch

	# test 67 is broken with lto
	# this patch removes the -flto flag for this very test
	# adapt when -ffat-lto-objects is enabled by Arch
	patch -Np1 -i ${srcdir}/disable-lto-link-order2.patch

	git submodule init
	git config --local submodule.gnulib.url "$srcdir/gnulib"
	git config --local submodule.gl-mod/bootstrap.url "$srcdir/gnulib-bootstrap"
	git submodule update

	./bootstrap
}

build() {
	cd libtool
	./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
	make
}

check() {
	cd libtool
	make ${MAKEFLAGS} check gl_public_submodule_commit=
}

package() {
	cd libtool
	make DESTDIR="$pkgdir" install
}