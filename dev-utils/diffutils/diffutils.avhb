# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=diffutils
pkgver=3.8
pkgrel=1
pkgdesc="Utility programs used for creating patch files"
arch=('x86_64')
groups=('dev-utils')
url="http://www.gnu.org/software/diffutils"
license=('GPL3')
depends=('glibc' 'bash')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('4824adc0e95dbbf11dfbdfaad6a1e461'
         'SKIP')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    message "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS} check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR=${pkgdir} install
}
