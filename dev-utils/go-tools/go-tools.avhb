# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=go-tools
epoch=4
pkgver=0.26.0
pkgrel=1
pkgdesc='Developer tools for the Go programming language'
arch=('x86_64')
groups=('dev-utils')
url='https://golang.org/'
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(git go)
source=(go-tools::git+https://go.googlesource.com/tools#tag=v$pkgver)
sha256sums=('a1713523619638ad9dc651f07df8b0a6d97dc0579af427ff497789439bdc2f2b')

_tools=(benchcmp callgraph compilebench digraph eg fiximports
        go-contrib-init godex godoc goimports gomvpkg gotype 
        goyacc html2article present ssadump stringer toolstash)

prepare() {
    cd tools
    mkdir -p bin/
}

build() {
  cd tools
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  go build -ldflags "-compressdwarf=false -linkmode external" -o bin/ ./cmd/...
}

check() {
  cd tools
  # Needs to be updated and godoc is failing the test suite
  # See https://github.com/golang/go/issues/35690
  go test ./cmd/...  || true
}

package() {
  cd tools
  for tool in ${_tools[@]}; do
    install -Dm755 bin/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

