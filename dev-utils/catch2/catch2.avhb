# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=catch2
_realname=Catch2
pkgver=2.13.8
pkgrel=1
pkgdesc="Modern, C++-native, header-only, test framework for unit-tests, TDD and BDD"
arch=('any')
groups=('dev-utils')
url="https://github.com/catchorg/catch2"
license=('Boost')
makedepends=('git' 'cmake' 'python') # python seems to be necessary for building tests (FS#60273)
source=(https://github.com/catchorg/Catch2/archive/v$pkgver.tar.gz)
validpgpkeys=(
  E29C46F3B8A7502860793B7DECC9C20E314B2360 # Martin Hořeňovský
  81E70B717FFB27AFDB45F52090BBFF120F9C087B # Jozef Grajciar
)

build() {
  cd ${_realname}-${pkgver}
  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCATCH_USE_VALGRIND=OFF \
    -DCATCH_BUILD_EXAMPLES=OFF \
    -DCATCH_ENABLE_COVERAGE=OFF \
    -DCATCH_ENABLE_WERROR=OFF \
    -DBUILD_TESTING=ON
  make ${MAKEFLAGS} -C build
}

check() {
  cd ${_realname}-${pkgver}
  make -C build test
}

package() {
  cd ${_realname}-${pkgver}
  make -C build DESTDIR="$pkgdir" install
}