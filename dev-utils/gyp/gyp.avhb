# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gyp
pkgver=20191203.e87d37d6
pkgrel=1
pkgdesc='"Generate Your Projects" Meta-Build system'
arch=(any)
groups=('dev-utils')
url="https://gyp.gsrc.io/"
license=(custom:BSD)
depends=(python ninja)
makedepends=(git python-setuptools)
source=("git+https://chromium.googlesource.com/external/gyp#commit=$_commit")

# getsrc(){	
# 	git clone https://chromium.googlesource.com/external/gyp
# }

pkgver() {
  cd $pkgname

  # Commit date + short rev
  echo $(TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  # Python 3 fixes from Fedora
  patch -Np1 -i ../gyp-fips.patch
  patch -Np1 -i ../gyp-fix-cmake.patch
  patch -Np1 -i ../gyp-python38.patch
  patch -Np1 -i ../gyp-python39.patch
}

build() {
  cd $pkgname
  python3 setup.py build
}

package() {
  cd $pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}