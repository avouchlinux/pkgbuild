# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=osinfo-db-tools
pkgver=1.11.0
pkgrel=1
pkgdesc="Tools for managing the osinfo database"
arch=('x86_64')
groups=('dev-utils')
url='https://libosinfo.org/'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  json-glib
  libarchive
  libsoup3
  libxml2
)
makedepends=(
  git
  meson
)
checkdepends=(
  python-pytest
  python-requests
)
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=(7f0fae00fd7ab032cc030f7e40c6028ef7b97ae2bc9f5bb8d5514935d24edaf807586ab94bdb82ecd17ddd5933ccbe12379000d370ce1c8a34c6d4320026868c)
validpgpkeys=(
  DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF # Daniel P. Berrange
  09B9C8FF223EF113AFA06A39EE926C2BDACC177B # Fabiano FidÃªncio
  206D3B352F566F3B0E6572E997D9123DE37A484F # Victor Toso de Carvalho <me@victortoso.com>
)

prepare() {
  cd $pkgname

  # Fix build with libxml2 2.12
  # https://gitlab.com/libosinfo/osinfo-db-tools/-/merge_requests/80
  git cherry-pick -n 34378a4ac257f2f5fcf364786d1634a8c36b304f
  git cherry-pick -n 019487cbc79925e49988789bf533c78dab7e1842

  # Fix build warnings
  git cherry-pick -n 9000d9e1c1b1d0dc6e43772aeb32d0a2433298ec

  sed -i 's/-Werror//' meson.build
}

build() {
  avouch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

