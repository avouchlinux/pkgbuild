# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=cmake
pkgver=3.23.0
pkgrel=1
pkgdesc='A cross-platform open-source make system'
arch=('x86_64')
groups=('dev-utils')
url="https://www.cmake.org/"
license=('custom')
depends=('curl' 'libarchive' 'shared-mime-info' 'jsoncpp' 'libjsoncpp.so' 'libuv' 'rhash')
makedepends=('qt6-base' 'python-sphinx' 'emacs')
optdepends=('qt6-base: cmake-gui')
source=("https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")

prepare(){
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../cmake-cppflags.patch # Honor CPPFLAGS https://gitlab.kitware.com/cmake/cmake/issues/12928

  # set correct default system directories compatible with Avouch
  sed -i 's|/usr/local|/usr|g' Modules/CMakeGenericSystem.cmake
  sed -i  \
    -e 's|"lib64"|"lib"|g' \
    -e 's|``libexec``|``lib``|g' \
    -e 's|"libexec"|"lib"|g' \
    -e 's|(libexec)|(lib)|g' \
    -e 's|``sbin``|``bin``|g' \
    -e 's|"sbin"|"bin"|g' Modules/GNUInstallDirs.cmake
  sed -i \
    -e 's|/usr/include/X11|/usr/include|g' \
    -e 's|/usr/X11R6|/usr|g' \
    -e 's|/usr/lib/X11|/usr/lib|g' Modules/Platform/UnixPaths.cmake

}

build() {
  cd ${pkgname}-${pkgver}

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --datadir=/share/cmake \
    --sphinx-man \
    --sphinx-html \
    --system-libs \
    --qt-gui \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make ${MAKEFLAGS}
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  emacs -batch -f batch-byte-compile \
    "${pkgdir}"/usr/share/emacs/site-lisp/cmake-mode.el

  install -Dm644 Copyright.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}