# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=flatpak
pkgver=1.13.2
pkgrel=1
pkgdesc="Linux application sandboxing and distribution framework (formerly xdg-app)"
url="http://flatpak.org"
arch=('x86_64')
groups=('dev-utils')
license=(LGPL2.1)
depends=(dbus systemd glib2 libsoup polkit libxau ostree json-glib libseccomp libarchive
         python bubblewrap appstream-glib xdg-dbus-proxy)
makedepends=(intltool gobject-introspection gtk-doc git docbook-xsl xmlto)
checkdepends=(valgrind socat)
source=("https://github.com/flatpak/flatpak/releases/download/$pkgver/flatpak-$pkgver.tar.xz"
        flatpak-bindir.sh)

prepare() {
  cd $pkgname-$pkgver
  # https://github.com/flatpak/flatpak/issues/267
  sed -i '/locale\/C\./d' tests/make-test-runtime.sh
}

build() {
  cd $pkgname-$pkgver
  # export SOUP_CFLAGS="-I/usr/include/libsoup-3.0"
  # export SOUP_LIBS="-L/usr/lib -lsoup-3.0"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-static \
    --with-system-bubblewrap \
    --with-system-dbus-proxy \
    --with-dbus-config-dir=/usr/share/dbus-1/system.d

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

check() {
  cd $pkgname-$pkgver
  # Xattr tests fail on tmpfs
  # make ${MAKEFLAGS} -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # rm -rf "$pkgdir/usr/share/selinux"

  install -Dm644 "$srcdir/flatpak-bindir.sh" "$pkgdir/etc/profile.d/flatpak-bindir.sh"
  install -Dm644 "$srcdir/flathub.flatpakrepo" "$pkgdir/etc/flatpak/remotes.d/flathub.flatpakrepo"

  echo 'u flatpak - "Flatpak system helper"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/flatpak.conf"

  # Fixup mode to match polkit
  install -d -o root -g 70 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}