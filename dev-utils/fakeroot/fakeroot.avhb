# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=fakeroot
pkgver=1.26
pkgrel=1
pkgdesc='Tool for simulating superuser privileges'
arch=(x86_64)
groups=("dev-utils")
license=(GPL)
url='https://tracker.debian.org/pkg/fakeroot'
groups=(base-devel)
install=fakeroot.install
depends=(glibc filesystem sed util-linux sh)
makedepends=(po4a)
checkdepends=(sharutils)
source=("https://deb.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.gz"
        'silence-dlerror.patch')

prepare() {
  cd $pkgname-$pkgver
  ./bootstrap
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv

  make

  cd doc
  po4a -k 0 --rm-backups --variable 'srcdir=../doc/' po4a/po4a.cfg
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/ld.so.conf.d/"
  echo '/usr/lib/libfakeroot' > "$pkgdir/etc/ld.so.conf.d/fakeroot.conf"

  # install README for sysv/tcp usage
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
