# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libplist
pkgver=2.2.0
pkgrel=1
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
arch=('x86_64')
groups=('app-mobile')
url="http://libimobiledevice.org/"
license=('GPL2' 'LGPL2.1')
depends=('glib2')
makedepends=('python3' 'cython' 'autoconf-archive' 'git' 'python-setuptools')
options=('!makeflags')
source=("https://github.com/libimobiledevice/libplist/archive/refs/tags/$pkgver.tar.gz")

prepare() {
  cd ${pkgname}-${pkgver}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {    
  cd ${pkgname}-${pkgver}
  make check  
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 cython/plist.pxd "${pkgdir}"/usr/include/plist/cython/plist.pxd
}