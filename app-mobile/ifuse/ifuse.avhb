# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=ifuse
pkgver=1.1.4
pkgrel=1
pkgdesc="A fuse filesystem implementation to access the contents of iOS devices."
arch=('x86_64')
groups=('app-mobile')
url='https://libimobiledevice.org/'
license=(LGPL2.1)
depends=(
  fuse2
  libimobiledevice
  libplist
  usbmuxd
)
makedepends=(git)
_commit=33434dec21198de11cea78325321d55ebb7bd71f  # tags/1.1.4
source=("git+https://github.com/libimobiledevice/ifuse#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd ifuse
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  cd ifuse
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd ifuse
  make DESTDIR="$pkgdir" install
}


