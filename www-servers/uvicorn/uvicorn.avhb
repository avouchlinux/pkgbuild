# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=uvicorn
pkgver=0.31.0
pkgrel=1
pkgdesc='The lightning-fast ASGI server'
arch=(any)
groups=('dev-python')
url=https://github.com/encode/uvicorn
license=(BSD-3-Clause)
depends=(
  python
  python-asgiref
  python-click
  python-h11
  python-httptools
  python-uvloop
  python-websockets
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
)
_tag=a50753268a9033dcdc57d6dee45f3829cdedf8f0
source=(git+https://github.com/encode/uvicorn#tag=${_tag})
b2sums=('42f97d901233b4256abb5fde4fbb5a345c6735884b4005697ba089cb9e889d6d9ee76e21a239681d28d2d3994f806c5b19a439d1f1686b3ee872a95a058266c2')

pkgver() {
  cd uvicorn
  git describe --tags
}

build() {
  cd uvicorn
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" uvicorn/dist/*.whl
  install -Dm 644 uvicorn/LICENSE.md -t "${pkgdir}"/usr/share/licenses/uvicorn/
}

# vim:set ts=2 sw=2 et:
