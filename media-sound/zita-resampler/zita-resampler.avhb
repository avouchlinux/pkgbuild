# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=zita-resampler
pkgver=1.6.2
pkgrel=1
pkgdesc="A C++ library for resampling audio signals"
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
arch=('x86_64')
groups=('media-sound')
license=('GPL3')
depends=('gcc-libs' 'glibc')
makedepends=('libsndfile')
optdepends=('libsndfile: for zresample and zretune')
provides=('libzita-resampler.so')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")

prepare() {
  cd "${pkgname}-${pkgver}"
  # FS#36248 - strip march=native
  sed -e '/native/d' \
      -i {source,apps}/Makefile
  # removing local call to ldconfig
  sed -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
      -i source/Makefile
  # fix use of BINDIR without preceeding DESTDIR
  # fix use of MANDIR without preceeding DESTDIR
  sed -e 's/\ $(BINDIR)/\ $(DESTDIR)$(BINDIR)/' \
      -e 's/\ $(MANDIR)/\ $(DESTDIR)$(MANDIR)/' \
      -i apps/Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  # libs
  make -C source
  (
    cd source
    # create lib link for building apps
    ln -sv "lib${pkgname}.so.${pkgver}" "lib${pkgname}.so"
  )
  # apps
  make LDFLAGS+=" -L../source" CXXFLAGS+=" -I../source" -C apps
  # cleanup link
  rm -rv "source/lib${pkgname}.so"
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C source DESTDIR="$pkgdir/" PREFIX=/usr SUFFIX="" install
  make -C apps DESTDIR="$pkgdir/" PREFIX=/usr install
  # docs
  install -vDm 644 {AUTHORS,README} docs/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}