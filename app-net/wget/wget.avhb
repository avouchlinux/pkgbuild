# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=wget
pkgver=1.20.3
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('x86_64')
groups=('app-net')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('gnutls' 'libidn' 'libutil-linux' 'libpsl' 'pcre')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA'
              '7845120B07CBD8D6ECE5FF2B2A1743EDA91A35B6'
              '1CB27DBC98614B2D5841646D08302DB6A2670428') # Tim RÃ¼hsen <tim.ruehsen@gmx.de>

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-rpath \
        --enable-nls \
        --with-ssl=gnutls
    make ${MAKEFLAGS}
}

# check() {
#     cd "${srcdir}"/${pkgname}-${pkgver}
#      make check < /dev/null
# }

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
