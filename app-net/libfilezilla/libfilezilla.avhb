# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libfilezilla
pkgver=0.22.0
pkgrel=1
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('x86_64')
groups=('net-libs')
url="https://lib.filezilla-project.org/"
license=('GPL')
depends=('gcc-libs' 'nettle')
makedepends=(clang)
checkdepends=('cppunit')
source=("https://download.filezilla-project.org/libfilezilla/libfilezilla-$pkgver.tar.bz2")

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  export CXX=clang++
  export CC=clang
  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
