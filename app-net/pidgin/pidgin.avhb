# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=pidgin
pkgname=('pidgin' 'libpurple' 'finch')
pkgver=2.13.0
pkgrel=1
arch=('x86_64')
groups=('app-net')
url="http://pidgin.im/"
license=('GPL')
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'libsm'
             'libidn' 'libgadu' 'python' 'hicolor-icon-theme' 'farstream'
             'avahi' 'tk' 'ca-certificates' 'intltool' 'networkmanager')
source=(https://bitbucket.org/pidgin/main/downloads/$pkgname-$pkgver.tar.bz2{,.asc}
        pidgin-py3-fixes.patch)
validpgpkeys=('364E2EB38EA6A8D61FB963AD75FE259AA8AC8032')

prepare() {
  cd $pkgbase-$pkgver

  # https://developer.pidgin.im/ticket/17065
  patch -Np1 -i ../purple-remote-python3.patch
  # Fix networkmanager 1.0 detection (Fedora)
  patch -p1 -i ../pidgin-nm-1.0.patch
  # Fix build with python 3.8
  patch -p1 -i ../pidgin-python-3.8.patch

  autoreconf -vif
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-schemas-install \
		--disable-meanwhile \
		--disable-gnutls \
		--enable-cyrus-sasl \
		--disable-doxygen \
		--enable-nm \
		--with-system-ssl-certs=/etc/ssl/certs
	make ${MAKEFLAGS}
}

package_pidgin(){
  pkgdesc="Multi-protocol instant messaging client"
  depends=('libpurple' 'startup-notification' 'gtkspell' 'libxss' 'libsm'
           'gst-plugins-base' 'gst-plugins-good' 'hicolor-icon-theme')
  optdepends=('aspell: for spelling correction')

  cd $pkgbase-$pkgver

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C pidgin DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES

  rm "$pkgdir/usr/share/man/man1/finch.1"
}

package_libpurple(){
  pkgdesc="IM library extracted from Pidgin"
  depends=('farstream' 'libsasl' 'libidn' 'libnsl' 'libgadu' 'dbus-glib' 'nss'
           'libnm')
  optdepends=('avahi: Bonjour protocol support'
              'ca-certificates: SSL CA certificates'
              'python-dbus: for purple-remote and purple-url-handler'
              'tk: Tcl/Tk scripting support')

  cd $pkgbase-$pkgver

  for _dir in libpurple share/sounds share/ca-certs m4macros po; do
    make -C "$_dir" DESTDIR="$pkgdir" install
  done
}

package_finch(){
  pkgdesc="A ncurses-based messaging client"
  depends=('libpurple' 'libx11' 'python')

  cd $pkgbase-$pkgver

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C finch DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES

  rm "$pkgdir"/usr/share/man/man1/pidgin.1
}