# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=glabels
pkgver=3.4.1
pkgrel=1
pkgdesc="Creating labels and business cards the very easy way"
arch=('x86_64')
groups=('gnome-extra')
url="https://glabels.org/"
license=('GPL' 'LGPL')
depends=('evolution-data-server' 'iec16022' 'qrencode')
makedepends=('intltool' 'itstool' 'python')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)

prepare() {
  cd $pkgname-$pkgver

  # Don't use legacy path for AppStream metainfo file
  # https://gitlab.gnome.org/GNOME/glabels/merge_requests/2
  sed -i 's|appdatadir = $(datadir)/appdata|appdatadir = $(datadir)/metainfo|' data/appdata/Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=" -fcommon"
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
