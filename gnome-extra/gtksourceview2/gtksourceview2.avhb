# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gtksourceview2
_realname=gtksourceview
pkgver=2.10.5
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('x86_64')
groups=('gnome-extra')
license=('GPL')
depends=('gtk2' 'libxml2')
makedepends=('intltool' 'pkg-config' 'python')
url="http://www.gnome.org"
source=(https://download.gnome.org/sources/gtksourceview/2.10/gtksourceview-${pkgver}.tar.bz2)

prepare() {
    cd "${srcdir}/gtksourceview-${pkgver}"
    sed -i 's#python#python2#' gtksourceview/language-specs/convert.py
}
 
build() {
    cd "${srcdir}/gtksourceview-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static

    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/gtksourceview-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
