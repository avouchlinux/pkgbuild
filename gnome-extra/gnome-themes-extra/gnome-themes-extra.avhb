# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnome-themes-extra
pkgver=3.28+r6+g45b1d457
pkgrel=2
pkgdesc="Extra Themes for GNOME Applications"
url="https://gitlab.gnome.org/GNOME/gnome-themes-extra"
arch=(x86_64)
groups=('gnome-extra')
license=(LGPL2.1)
depends=(cantarell-fonts)
makedepends=(intltool gtk2 gtk3 git)
optdepends=('gtk-engines: HighContrast GTK2 theme')
provides=("gnome-themes-standard=$pkgver")
conflicts=(gnome-themes-standard)
replaces=('gnome-themes-standard<3.27.92')
options=('!emptydirs')
_commit=45b1d457c63b1c7f6c8dcb7fe29b23ec7cd63a14  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-themes-extra.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
