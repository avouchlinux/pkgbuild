# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=phodav
pkgver=2.5
pkgrel=1
pkgdesc="WebDav server implementation using libsoup (RFC 4918)"
arch=('x86_64')
groups=('gnome-extra')
url="https://wiki.gnome.org/phodav"
license=('LGPL2.1')
depends=('avahi' 'libsoup')
makedepends=('asciidoc' 'gtk-doc' 'meson' 'xmlto')
backup=('etc/conf.d/spice-webdavd')
source=("https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")

prepare() {
	cd $pkgname-$pkgver
	sed -i 's|/etc/sysconfig/spice-webdavd|/etc/conf.d/spice-webdavd|
	        s|/usr/sbin/spice-webdavd|/usr/bin/spice-webdavd|' data/spice-webdavd.service
}

build() {
	avouch-meson $pkgname-$pkgver build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	install -dm755 "$pkgdir/etc/conf.d/"
	echo 'SPICE_WEBDAVD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-webdavd"
}
