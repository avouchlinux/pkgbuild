# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnome-dictionary
pkgver=40.0
pkgrel=1
pkgdesc="Look up words in dictionary sources"
url="https://wiki.gnome.org/Apps/Dictionary"
arch=(x86_64)
groups=(gnome-extra)
license=(GPL2)
depends=(gtk3)
makedepends=(
  appstream-glib
  git
  meson
  yelp-tools
)
_commit=c37edfc2cbf91d6bae6d5264d0dcfcb5dd34b313  # tags/40.0^0
source=("https://gitlab.gnome.org/GNOME/gnome-dictionary.git")
b2sums=('SKIP')

getsrc() {
    git clone ${source[0]}
    cd $pkgname
    git checkout $_commit
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with newer meson
  git cherry-pick -n 87c026cfe4acbcfc62d15950f88a71d8d9678c7e \
                     cf3f8a67cd6f3059c555ed9cf0f5fba10abb7f68
}

build() {
  avouch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
