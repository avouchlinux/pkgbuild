# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

### we build without packagekit support

pkgbase=gnome-software
pkgname=(gnome-software)
pkgver=42.2
pkgrel=1
pkgdesc="GNOME Software Tools"
url="https://wiki.gnome.org/Apps/Software/"
arch=('x86_64')
groups=('gnome-extra')
license=(GPL2)
makedepends=(appstream gsettings-desktop-schemas flatpak
             fwupd docbook-xsl git gobject-introspection gspell gtk-doc meson
             valgrind gnome-online-accounts libxmlb malcontent libhandy
             libsysprof-capture)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2")

build() {
  # Ensure static library is non-LTO compatible
  # flatpack is built with libsoup2
  # build with libsoup2 unitil flatpack add support for libsoup3
  export CFLAGS+=" -ffat-lto-objects"
	avouch-meson ${pkgname}-${pkgver} build \
    -D packagekit=false \
    -D soup2=true
	ninja -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package() {
  depends=(libxmlb gnome-desktop gsettings-desktop-schemas gspell libpackagekit-glib
           gnome-online-accounts appstream-glib)
  optdepends=('flatpak: Flatpak support plugin'
              'fwupd: fwupd support plugin'
              'ostree: OSTree support plugin')

  DESTDIR="$pkgdir" meson install -C build
}