# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=goocanvas
pkgver=3.0.0
pkgrel=1
pkgdesc="A cairo canvas widget for GTK+"
arch=('x86_64')
groups=('gnome-extra')
url="https://wiki.gnome.org/Projects/GooCanvas"
license=('LGPL')
depends=('gtk3')
makedepends=('gobject-introspection' 'python-gobject' 'python2-gobject')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)

build() {
  cp -r $pkgname-$pkgver python2

  cd $pkgname-$pkgver
  ./configure --prefix=/usr PYTHON=python3
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  cd "$srcdir/python2"
  ./configure --prefix=/usr PYTHON=python2
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd "$srcdir/python2"
  make -C bindings/python DESTDIR="$pkgdir" install
}