# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=brasero
pkgver=3.12.3
pkgrel=1
pkgdesc="A disc burning application for Gnome"
arch=('x86_64')
groups=('gnome-extra')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!emptydirs')
depends=('gtk3' 'gst-plugins-good' 'totem-plparser' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'  'libcanberra' 'dvd+rw-tools' 'dconf' 'libsm' 'libtracker-sparql' 'libnotify' 'gvfs')
makedepends=('gtk-doc' 'yelp-tools' 'gnome-common' 'intltool' 'libburn' 'libisofs' 'libnautilus-extension' 'gobject-introspection' 'gconf' 'itstool' 'docbook-xsl')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	autoreconf -fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc \
	--localstatedir=/var --disable-caches \
	--disable-schemas-compile \
	--enable-compile-warnings=minimum
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install
}

