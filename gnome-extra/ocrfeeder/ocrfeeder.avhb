# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=ocrfeeder
pkgver=0.8.5
pkgrel=1
pkgdesc="GTK+ document layout analysis and optical character recognition application"
arch=('any')
groups=('gnome-extra')
url="https://wiki.gnome.org/Apps/OCRFeeder"
license=('GPL')
depends=('ghostscript' 'goocanvas' 'gtk3' 'gtkspell3' 'iso-codes' 'python-gobject' 'python-odfpy'
         'python-pillow' 'python-pyenchant' 'python-reportlab' 'python-sane' 'unpaper')
makedepends=('git' 'gnome-common')
optdepends=('tesseract: character recognition'
            'ocrad: character recognition'
            'gocr: character recognition'
            'cuneiform: character recognition')
source=("https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 resources/icons/org.gnome.OCRFeeder.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/org.gnome.OCRFeeder.svg"
}
