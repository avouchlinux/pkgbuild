# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=emacs-nox
_realname=emacs
pkgver=28.1
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor, without X11 support"
arch=('x86_64')
groups=('app-editors')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'libxml2' 'glib2' 'gnutls')
provides=('emacs')
conflicts=('emacs')
changelog=$pkgname.changelog
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.xz{,.sig})
validpgpkeys=('B29426DEFB07724C3C35E5D36592E9A3A0B0F199' '28D3BED851FDF3AB57FEF93C233587A47C207910')

build() {
  cd "${srcdir}"/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound --with-modules
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}"/emacs-$pkgver

  make DESTDIR="${pkgdir}" install

  # remove conflict with ctags package
  mv "${pkgdir}"/usr/bin/{ctags,ctags.emacs}
  mv "${pkgdir}"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

  # fix user/root permissions on usr/share files
  find "${pkgdir}"/usr/share/emacs/$pkgver -exec chown root.root {} \;
  
  # remove .desktop file and icons
  rm -rf "${pkgdir}"/usr/share/{applications,icons}
  
  # fix perms on /var/games
  # chmod 775 "${pkgdir}"/var/games
  # chmod 775 "${pkgdir}"/var/games/emacs
  # chmod 664 "${pkgdir}"/var/games/emacs/*
  # chown -R root:games "${pkgdir}"/var/games
}
