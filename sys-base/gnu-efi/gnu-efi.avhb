# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=gnu-efi
pkgver=3.0.18
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="https://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64')
groups=('sys-base')
options=('!strip' '!makeflags' '!buildflags')
makedepends=('pciutils')
source=("https://download.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")
sha512sums=('0893ca234272584f889b1ae1c75341a9ceee60acfd32765daa5d704191ba00450536a287b949304c6d055d1bf125cc29e24fc41df8e5230e0da4f9d944876512')
b2sums=('27f8171b411a6a8a138d44d91c7e4e4291aa399562825d51a398913572119482ffeb303d7508ae13eacd2cd10b8f5098405ab16eb56243587efe93235f661285')


prepare() {
  cd "$pkgname-$pkgver"
  # -Werror, not even once
  sed -e 's/-Werror//g' -i Make.defaults
}

build() {
	use_linker "ld"
  cd "$pkgname-$pkgver"
  # NOTE: apply only minimal CFLAGS, as gnu-efi does not provide userspace
  # libs, but may be used in unitialized machine state and should therefore not
  # be architecture optmized
  CFLAGS="-O2"
  # upstream provides LDFLAGS directly to ld: https://sourceforge.net/p/gnu-efi/bugs/33/
  LDFLAGS="${LDFLAGS//-Wl/}"
  LDFLAGS="${LDFLAGS//,/ }"

  make
}

package() {
  cd "$pkgname-$pkgver"
  make INSTALLROOT="$pkgdir/" PREFIX='/usr' install
  install -vDm 644 apps/*.efi -t "${pkgdir}/usr/share/${pkgname}/apps/$CARCH"
  install -vDm 644 README.efilib -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {ChangeLog,README.{gnuefi,git,elilo}} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
