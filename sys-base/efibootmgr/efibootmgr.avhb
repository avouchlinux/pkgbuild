# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname="efibootmgr"
pkgver=18
_commit=712aeb81311de28a3fcfea7465dcb93743f07a53  # refs/tags/18
pkgrel=1
pkgdesc="Tool to modify UEFI Firmware Boot Manager Variables"
arch=('x86_64')
groups=('sys-base')
url="https://github.com/rhboot/efibootmgr"
license=(GPL2)
depends=(glibc popt)
makedepends=(efivar git)
source=($pkgname::git+$url#tag=$_commit?signed)
sha512sums=('SKIP')
validpgpkeys=(
  'B00B48BC731AA8840FED9FB0EED266B70F4FEF10'  # Peter Jones <pjones@redhat.com>
  '039A9CEA19DE9508C36875AA2532F9176A95A442'  # Robbie Harwood (work) <rharwood@redhat.com>
)

build() {
  make libdir=/usr/lib sbindir=/usr/bin EFIDIR=arch -C $pkgname
}

package() {
  depends+=(libefiboot.so libefivar.so)

  make DESTDIR="$pkgdir" libdir=/usr/lib sbindir=/usr/bin EFIDIR=arch install -C $pkgname
  install -vDm 644 $pkgname/{AUTHORS,README,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
