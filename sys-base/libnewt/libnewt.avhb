# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libnewt
_realname=newt
pkgver=0.52.24
pkgrel=1
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('x86_64')
groups=('sys-base')
url='https://pagure.io/newt'
license=(GPL)
depends=(slang popt gpm)
makedepends=("tcl>=$_tclver" 'python' 'python2')
optdepends=('tcl: whiptcl support'
            'python: libnewt support with the _snack module'
            'python2: libnewt support with the _snack module')
options=(!makeflags)
source=("https://releases.pagure.org/newt/newt-$pkgver.tar.gz")
sha256sums=('265eb46b55d7eaeb887fca7a1d51fe115658882dfe148164b6c49fccac5abb31')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"

	sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
	echo '#define USE_INTERP_RESULT 1' >> config.h
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
	./configure --prefix=/usr --with-gpm-support
	make ${MAKEFLAGS}
}

package() {
	make -C newt-$pkgver prefix="$pkgdir/usr" install
}

