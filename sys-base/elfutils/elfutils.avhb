# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=elfutils
pkgname=(elfutils libelf)
pkgver=0.183
pkgrel=1
pkgdesc="Utilities to handle ELF object files and DWARF debugging information"
arch=('x86_64')
groups=('sys-base')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
options=('staticlibs')
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>
validpgpkeys+=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mjw@gnu.org>

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	CFLAGS+=" -g"  # required for test-suite success
	./configure --prefix=/usr --program-prefix="eu-" --enable-deterministic-archives
    make ${MAKEFLAGS}
}

# check() {
# 	cd "${srcdir}/${pkgbase}-${pkgver}"
# 	make ${MAKEFLAGS} check
# }

package_elfutils() {
	depends=("libelf=$pkgver-$pkgrel")

	cd "${srcdir}/${pkgbase}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	mkdir "${srcdir}"/libelf
	mv "${pkgdir}"/usr/{lib,include} "${srcdir}/libelf"
}

package_libelf() {
	pkgdesc="Libraries to handle ELF object files and DWARF debugging information"
	
	cd "${srcdir}"
	mv libelf "${pkgdir}/usr"
}
