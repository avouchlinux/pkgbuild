# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libbsd
pkgver=0.10.0
pkgrel=1
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy()"
arch=('x86_64')
groups=('sys-base')
url="https://libbsd.freedesktop.org"
license=('custom')
depends=('glibc')
options=('staticlibs')
source=("https://libbsd.freedesktop.org/releases/$pkgname-$pkgver.tar.xz"{,.asc})
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS} check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install
	rm "${pkgdir}"/usr/lib/libbsd.a
	# included in man-pages
	rm -f "${pkgdir}"/usr/share/man/man3/explicit_bzero.3
}

