# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=findutils
pkgver=4.10.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('x86_64')
groups=('sys-base')
url='https://www.gnu.org/software/findutils/'
license=('GPL-3.0-or-later')
depends=('glibc' 'sh')
makedepends=('git' 'wget' 'python')
source=("https://ftp.gnu.org/gnu/findutils/$pkgname-$pkgver.tar.xz")
b2sums=('a6d99d922df4c6895d9956a6902518c5f911e6ad1fdcbfc99bb083ce0a725fa4e87bb83a1a2d16e6d900755da9e9094b20f56f971c8e6a6008572cd417fe3e95')
validpgpkeys=('A5189DB69C1164D33002936646502EF796917195') # Bernhard Voelker <mail@bernhard-voelker.de>

prepare() {
  cd "$pkgname-$pkgver"
#   ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  sed -e '/^SUBDIRS/s/locate//' -e 's/frcode locate updatedb//' -i Makefile.in

  ./configure --prefix=/usr
  # don't build locate, but the docs want a file in there.
  make -C locate dblocation.texi
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
