# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=readline
_basever=8.1
_patchlevel=0
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="GNU readline library"
arch=('x86_64')
groups=('sys-base')
url='https://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL')
backup=('etc/inputrc')
depends=('glibc' 'ncurses' 'libncursesw.so')
provides=('libhistory.so' 'libreadline.so')
options=('!emptydirs')
source=(https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [ $_patchlevel -gt 0 ]; then
    for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p){,.sig})
    done
fi

prepare() {
	cd "${srcdir}/${pkgname}-${_basever}"
	
	for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    	message "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    	patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  	done

	# Remove RPATH from shared objects (FS#14366)
	sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
	cd "${srcdir}/${pkgname}-${_basever}"

	# build with -fPIC for x86_64 (FS#15634)
	[[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

	./configure --prefix=/usr

	make ${MAKEFLAGS} SHLIB_LIBS=-lncurses
}

package() {
	make -C $pkgname-$_basever DESTDIR="$pkgdir" install
	install -Dm644 inputrc "$pkgdir"/etc/inputrc
}
