# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=man-db
pkgver=2.9.1
pkgrel=1
pkgdesc="A utility for reading man pages"
arch=('x86_64')
groups=('sys-base')
url="https://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
depends=( 'bash' 'gdbm' 'zlib' 'groff' 'libpipeline' 'less' 'libseccomp')
makedepends=('po4a')
optdepends=('gzip')
backup=('etc/man_db.conf')
conflicts=('man')
provides=('man')
replaces=('man')
install=${pkgname}.install
source=(#https://download-mirror.savannah.gnu.org/releases/man-db/$pkgname-$pkgver.tar.xz{,.sig}
        https://savannah.nongnu.org/download/man-db/$pkgname-$pkgver.tar.xz{,.asc}
        convert-mans
)
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B') # Colin Watson <cjwatson@debian.org>
sha512sums=('ae2d1e9f293795c63f5a9a1a765478a9a59cbe5fe6f759647be5057c1ae53f90baee8d5467921f3d0102300f2111a5026eeb25f78401bcb16ce45ad790634977'
            'SKIP'
            '0b159285da20008f0fc0afb21f1eaebd39e8df5b0594880aa0e8a913b656608b8d16bb8d279d9e62d7aae52f62cb9b2fc49e237c6711f4a5170972b38d345535')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sbindir=/usr/bin \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--with-db=gdbm \
	--disable-setuid \
	--enable-cache-owner=root \
	--enable-mandirs=GNU \
	--with-sections="1 n l 8 3 0 2 5 4 9 6 7"
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS} -k check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR=${pkgdir} install

	# part of groff pkg
	rm -f ${pkgdir}/usr/bin/zsoelim

	# script from LFS to convert manpages, see
	# http://www.linuxfromscratch.org/lfs/view/6.4/chapter06/man-db.html
	install -D -m755 ${srcdir}/convert-mans  ${pkgdir}/usr/bin/convert-mans 

	# install man-db update timer
	install -d -m755 ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants
	ln -s ../man-db.timer ${pkgdir}//usr/lib/systemd/system/multi-user.target.wants/man-db.timer
}
