# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=bash
_basever=5.1
_patchlevel=008
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64')
groups=('sys-base')
license=(GPL)
url='https://www.gnu.org/software/bash/bash.html'
groups=(base)
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline>=7.0' glibc ncurses)
optdepends=('bash-completion: for tab completion')
provides=('sh')
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
  done
fi

prepare() {
	cd "${srcdir}/${pkgname}-${_basever}"

	for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
		message "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
		patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
	done
}

build() {
	cd "${srcdir}/${pkgname}-${_basever}"
	_bashconfig=(-DDEFAULT_PATH_VALUE='\"/usr/local/bin:/usr/bin\"'
			-DSTANDARD_UTILS_PATH='\"/usr/bin\"'
			-DSYS_BASHRC='\"/etc/bashrc\"'
			-DSYS_BASH_LOGOUT='\"/etc/bash_logout\"'
			-DNON_INTERACTIVE_LOGIN_SHELLS)
	export CFLAGS="${CFLAGS} -fPIC ${_bashconfig[@]}"

	./configure --prefix=/usr \
	--with-curses \
	--enable-readline \
	--without-bash-malloc \
	--with-installed-readline
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgname}-${_basever}"
	make ${MAKEFLAGS} check
}

package() {
	cd "${srcdir}/${pkgname}-${_basever}"
	make DESTDIR=${pkgdir} install

	ln -s bash ${pkgdir}/usr/bin/sh

	# user configuration file skeletons
	install -dm755 ${pkgdir}/etc/skel/
	install -m644 ${srcdir}/dot.bashrc ${pkgdir}/etc/skel/.bashrc
	install -m644 ${srcdir}/dot.bash_profile ${pkgdir}/etc/skel/.bash_profile
	install -m644 ${srcdir}/dot.bash_logout ${pkgdir}/etc/skel/.bash_logout

	# system-wide configuration files
	install -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bashrc
	install -m644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash_logout
}

