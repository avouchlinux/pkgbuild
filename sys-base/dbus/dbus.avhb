# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=dbus
pkgname=(dbus dbus-docs)
_realname=dbus-dbus
pkgver=1.12.20
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
arch=('x86_64')
groups=('sys-base')
url="http://www.freedesktop.org/Software/dbus"
license=(GPL custom)
depends=(systemd-libs expat audit)
makedepends=(systemd xmlto docbook-xsl python yelp-tools doxygen git autoconf-archive)
source=(https://gitlab.freedesktop.org/dbus/dbus/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.bz2)
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>
prepare() {
    cd ${pkgbase}-${pkgbase}-${pkgver}
      # Allow us to enable checks without them being fatal
    patch -Np1 -i ../no-fatal-warnings.diff

    NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd ${pkgbase}-${pkgbase}-${pkgver}
	./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/dbus-1.0 \
    runstatedir=/run \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=messagebus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-inotify \
    --enable-systemd \
    --enable-user-session \
    --enable-selinux \
    --enable-libaudit \
    --disable-static \
    --disable-xml-docs \
    --disable-doxygen-docs \
    --disable-ducktype-docs \
    --without-x

	make ${MAKEFLAGS}
}

check() {
	cd ${pkgbase}-${pkgbase}-${pkgver}
    DBUS_VERBOSE=1 make ${MAKEFLAGS} check
}

package_dbus(){
	provides=(libdbus)
	conflicts=(libdbus)
	replaces=(libdbus)

	cd ${pkgbase}-${pkgbase}-${pkgver}
	make DESTDIR="$pkgdir" install
	
	rm -r "$pkgdir"/{etc,var}
	
    # We have a pre-assigned uid (18)
	echo 'u messagebus 18 "System Message Bus"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/dbus.conf"

	# Split docs
	mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_dbus-docs() {
	pkgdesc+=" (documentation)"
	depends=(dbus)
    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase}-${pkgbase}-${pkgver}/COPYING
    mv doc "$pkgdir/usr/share"
}

