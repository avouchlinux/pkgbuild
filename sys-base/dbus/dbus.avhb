# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=dbus
pkgname=(
  dbus
  dbus-daemon-units
  dbus-docs
)
_realname=dbus-dbus
pkgver=1.14.10
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
arch=('x86_64')
groups=('sys-base')
url="http://www.freedesktop.org/Software/dbus"
license=(
  GPL
  custom
)
depends=(
  audit
  expat
  systemd-libs
)
makedepends=(
  autoconf-archive
  docbook-xsl
  doxygen
  git
  python
  python-mallard
  python-mallard.ducktype
  systemd
  xmlto
  yelp-tools
)
source=(https://gitlab.freedesktop.org/dbus/dbus/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.bz2)
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>
prepare() {
    cd ${pkgbase}-${pkgbase}-${pkgver}
    # Allow us to enable checks without them being fatal
    patch -Np1 -i ../no-fatal-warnings.diff

    NOCONFIGURE=1 ./autogen.sh
}

build() {	
	local configure_options=(
        --prefix=/usr
        --sysconfdir=/etc
        --localstatedir=/var
        --libexecdir=/usr/lib/dbus-1.0
        --runstatedir=/run
        --with-console-auth-dir=/run/console/
        --with-dbus-user=dbus
        --with-system-pid-file=/run/dbus/pid
        --with-system-socket=/run/dbus/system_bus_socket
        --with-systemdsystemunitdir=/usr/lib/systemd/system
        --enable-inotify
        --enable-libaudit
        --enable-systemd
        --enable-user-session
        --enable-xml-docs
        --enable-doxygen-docs
        --enable-ducktype-docs
        --disable-static
        --without-x
    )

    cd ${pkgbase}-${pkgbase}-${pkgver}
    ./configure "${configure_options[@]}"
    make -j1
}

check() {
  make -C ${pkgbase}-${pkgbase}-$pkgver -j1 check
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_dbus() {
  depends+=(
    libaudit.so
    libcap-ng.so
    libexpat.so
    libsystemd.so
  )
  provides=(
    libdbus
    libdbus-1.so
  )
  conflicts=(libdbus)
  replaces=(libdbus)

  cd ${pkgbase}-${pkgbase}-$pkgver
  DESTDIR="$pkgdir" make install

  rm -r "$pkgdir"/{etc,var}

  _pick unit "$pkgdir"/usr/lib/systemd/{system,user}/dbus.service
  _pick docs "$pkgdir"/usr/share/doc

  # We have a pre-assigned uid (81)
  echo 'u dbus 18 "System Message Bus"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/dbus.conf"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

package_dbus-daemon-units() {
  pkgdesc+=" - Service units"
  depends=(dbus)
  provides=(dbus-units)
  conflicts=(dbus-broker-units)

  mv unit/* "$pkgdir"
}

package_dbus-docs() {
  pkgdesc+=" - Documentation"
  depends=()

  mv docs/* "$pkgdir"
}
