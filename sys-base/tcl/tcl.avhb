# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=tcl
pkgver=8.6.10
_majorver=8.6
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('x86_64')
groups=('sys-base')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('zlib')
# do not strip static libraries
options=('!strip' 'staticlibs')
source=(https://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)

prepare() {
	cd "${srcdir}/tcl${pkgver}"	
    # we build the tcl sqlite interface in sqlite-tcl package
	rm -rf pkgs/sqlite3*
}

build() {
	cd "${srcdir}/tcl${pkgver}/unix"
	./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --enable-64bit
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/tcl${pkgver}/unix"
	make ${MAKEFLAGS} test
}

package() {
	cd "${srcdir}/tcl${pkgver}/unix"
	make INSTALL_ROOT="${pkgdir}" install install-private-headers
	ln -sf tclsh${pkgver%.*} "${pkgdir}/usr/bin/tclsh"
	ln -sf libtcl${pkgver%.*}.so "${pkgdir}/usr/lib/libtcl.so"
	install -Dm644 ../license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 tcl.m4 -t "$pkgdir"/usr/share/aclocal

	# remove buildroot traces
	sed -e "s#${srcdir}/tcl${pkgver}/unix#/usr/lib#" \
	-e "s#${srcdir}/tcl${pkgver}#/usr/include#" \
	-i "${pkgdir}/usr/lib/tclConfig.sh"

	tdbcver=tdbc1.1.1
	sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$tdbcver#/usr/lib/$tdbcver#" \
	-e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/generic#/usr/include#" \
	-e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/library#/usr/lib/tcl${pkgver%.*}#" \
	-e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver#/usr/include#" \
	-i "${pkgdir}/usr/lib/$tdbcver/tdbcConfig.sh"

	itclver=itcl4.2.0
	sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$itclver#/usr/lib/$itclver#" \
	-e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver/generic#/usr/include#" \
	-e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver#/usr/include#" \
	-i "${pkgdir}/usr/lib/$itclver/itclConfig.sh"

    # do not strip static libraries, only strip shared librries
	# do not strip all debug info 
	# expect and vim build failed 
    find "$pkgdir"/usr/lib -name '*.so' -type f -exec strip $STRIP_SHARED {} + 2> /dev/null || true
}

