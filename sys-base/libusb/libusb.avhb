# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libusb
pkgver=1.0.26
pkgrel=1
depends=('glibc')
pkgdesc="Library to enable user space application programs to communicate with USB devices."
arch=('x86_64')
groups=('sys-base')
url='https://libusb.info/'
license=(LGPL)
depends=(glibc systemd-libs)
makedepends=(systemd)
replaces=(libusb1 libusbx)
provides=("libusbx=$pkgver" libusb-1.0.so)
conflicts=(libusbx)
source=(https://github.com/libusb/libusb/releases/download/v$pkgver/libusb-$pkgver.tar.bz2)
md5sums=('466267889daead47674df933cea9cacb')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr
	
	# prevent excessive overlinking due to libtool
  	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make ${MAKEFLAGS}
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make ${MAKEFLAGS} check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="${pkgdir}" install
}

