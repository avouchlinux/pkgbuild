# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=audit
pkgname=audit
# build failed with python3 disable as an interim
# pkgname=('audit' 'python3-audit')
pkgver=3.0.8
pkgrel=1
pkgdesc='Userspace components of the audit framework'
url='http://people.redhat.com/sgrubb/audit'
arch=('x86_64')
groups=('sys-base')
url='https://people.redhat.com/sgrubb/audit'
makedepends=('glibc' 'krb5' 'libcap-ng' 'libldap' 'swig' 'linux-headers' 'python')
license=('GPL')
options=('emptydirs')
source=(https://people.redhat.com/sgrubb/audit/${pkgbase}-${pkgver}.tar.gz)

prepare() {
  cd ${pkgbase}-${pkgver}
  sed 's|/var/run/auditd.pid|/run/auditd.pid|' -i init.d/auditd.service
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/audit \
    --enable-gssapi-krb5=yes \
    --enable-systemd=yes \
    --with-libcap-ng=yes \
    --with-python=no \
	  --with-python3=no
  make
}

package() {
  depends=('krb5' 'libcap-ng')
  provides=('libaudit.so' 'libauparse.so')
  backup=(
    etc/libaudit.conf
    etc/audit/audit-stop.rules
    etc/audit/auditd.conf
    etc/audisp/audispd.conf
    etc/audisp/audisp-remote.conf
    etc/audisp/zos-remote.conf
    etc/audisp/plugins.d/af_unix.conf
    etc/audisp/plugins.d/audispd-zos-remote.conf
    etc/audisp/plugins.d/au-remote.conf
    etc/audisp/plugins.d/syslog.conf
  )

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" INSTALL='install -p' install

  cd "${pkgdir}"
  install -d -m 0700 var/log/audit
  rm -rf etc/rc.d \
      etc/sysconfig \
      usr/lib/audit \
      usr/lib/python*

  sed -ri 's|/sbin|/usr/bin|' \
    etc/audit/*.conf \
    etc/audit/plugins.d/*.conf \
    usr/lib/systemd/system/auditd.service

  chmod 644 usr/lib/systemd/system/auditd.service
}

# package_python3-audit() {
#   depends=('python3' 'audit')
#   pkgdesc+=' (python3 bindings)'
#   cd ${pkgbase}-${pkgver}
#   make -C bindings DESTDIR="${pkgdir}" INSTALL='install -p' install
#   rm -rf "${pkgdir}"/usr/lib/python2*
# }