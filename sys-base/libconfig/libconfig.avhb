# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libconfig
pkgver=1.7.3
pkgrel=1
pkgdesc="C/C++ Configuration File Library"
arch=('x86_64')
groups=('sys-base')
url="https://github.com/hyperrealm/libconfig"
license=('LGPL')
depends=('gcc-libs')
makedepends=('texinfo')
options=('!emptydirs' 'zipman')
source=($pkgname-$pkgver.tar.gz::"https://github.com/hyperrealm/libconfig/archive/v$pkgver.tar.gz")
md5sums=('d666f0ca093906d34d6e1e4890053e62')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  rm "$pkgdir/usr/share/info/dir"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
