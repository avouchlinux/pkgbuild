# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=isomd5sum
pkgname=('isomd5sum' 'python2-isomd5sum')
pkgver=1.2.3
pkgrel=1
arch=('x86_64')
groups=('sys-base')
license=('GPL')
makedepends=('popt' 'python')
url='https://github.com/rhinstaller/isomd5sum'
source=("$pkgbase-$pkgver.tar.gz::https://github.com/rhinstaller/isomd5sum/archive/$pkgver.tar.gz")
options=('!emptydirs')
sha512sums=('a500faa368377c8fb5dfc311bc3a6dbadd253e6af7928b691d1fc11e25b69819a611f72fa201e70f5df257cdc491c97d1f612bb179fa49de1fb0a638d6ac7972')

prepare() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	sed -i -e 's/^\(LIBDIR = \)lib64/\1lib/' Makefile
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make all
}

package_isomd5sum() {
	depends=('popt')
	pkgdesc="Utilities for working with md5sum implanted in ISO images"

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" install-bin install-devel
}

package_python2-isomd5sum() {
	depends=('python2')
	pkgdesc="Python2 bindings for isomd5sum"

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" install-python
}
