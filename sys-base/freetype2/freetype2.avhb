# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=freetype2
pkgname=('freetype2' 'freetype2-demos' 'freetype2-docs')
_realname=freetype
pkgver=2.10.1
pkgrel=1
arch=('x86_64')
groups=('sys-base')
license=('GPL')
url="https://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
makedepends=('libx11')
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-infinality-subpixel-hinting.patch
        0003-Enable-long-PCF-family-names.patch
        0004-Properly-handle-phantom-points-for-variation-fonts-5.patch
        0005-freetype-2.5.2-more-demos.patch
        freetype2.sh)
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
	
	bsdtar -xf $srcarchivedir/freetype-doc-$pkgver.tar.xz
	bsdtar -xf $srcarchivedir/ft2demos-$pkgver.tar.xz
	
	mv "${_realname}-${pkgver}" "${pkgbase}"
	mv ft2demos-${pkgver} freetype2-demos
	cd "${pkgbase}"

	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-infinality-subpixel-hinting.patch
	patch -Np1 -i ../0003-Enable-long-PCF-family-names.patch

	# https://bugs.archlinux.org/task/62395
	patch -Np1 -i ../0004-Properly-handle-phantom-points-for-variation-fonts-5.patch

	cd ../freetype2-demos
	# enable more demos
	patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch

	# Suppress RPATH
	sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {

	cd "${srcdir}/${pkgbase}"
	./configure --prefix=/usr --disable-static
	make ${MAKEFLAGS}

	# Build demos
	cd "${srcdir}/freetype2-demos"
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${pkgbase}"
	make ${MAKEFLAGS} -k check
}

package_freetype2() {
  provides=('libfreetype.so')
  install=freetype2.install
  backup=('etc/profile.d/freetype2.sh')

  cd freetype2
  make DESTDIR="${pkgdir}" install
  install -Dt "${pkgdir}/etc/profile.d" -m644 ../freetype2.sh
}

package_freetype2-demos() {
  pkgdesc="Freetype tools and demos"
  depends=('freetype2' 'libx11')

  cd freetype2-demos
  install -d "${pkgdir}/usr/bin"
  for _i in bin/{f,t}t*; do
    libtool --mode=install install $_i "${pkgdir}/usr/bin"
  done
}

package_freetype2-docs() {
  pkgdesc="Freetype documentation"
  depends=('freetype2')

  cd freetype2
  install -d "${pkgdir}/usr/share/doc"
  cp -a docs "${pkgdir}/usr/share/doc/freetype2"
}
