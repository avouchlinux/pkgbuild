# Copyright 2010-2024 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=multipath-tools
pkgver=0.9.7
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
groups=('sys-base')
url='http://christophe.varoqui.free.fr/'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git' 'systemd')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${_tag}")
sha256sums=('SKIP')

_make_opts=(
  prefix='/usr/'
  bindir='/usr/bin'
  etc_prefix=""
  configfile='/etc/multipath.conf'
  statedir='/etc/multipath'
  LIB='lib'
)


build() {
  cd "${pkgname}"

  make \
    "${_make_opts[@]}" \
    FORTIFY_OPT=''
}

package() {
  cd "${pkgname}"

  make -j1 \
    "${_make_opts[@]}" \
    DESTDIR="${pkgdir}" \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
