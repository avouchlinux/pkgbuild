# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=fuse2
_realname=fuse
pkgver=2.9.9
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('x86_64')
groups=('sys-base')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
provides=(fuse=$pkgver) # TODO: remove it once all packages start to use 'fuse2' name
replaces=('fuse')
conflicts=('fuse')
depends=('glibc' 'fuse-common')
makedepends=('pkg-config')
options=(!emptydirs)
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc})
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

prepare(){
	cd "${srcdir}/fuse-${pkgver}"

	patch -Np1 -i ../fuse2-0001-More-parentheses.patch
	# https://bugzilla.redhat.com/show_bug.cgi?id=970768
	patch -Np1 -i ../fuse2-0002-add-fix-for-namespace-conflict-in-fuse_kernel.h.patch
	# https://github.com/libfuse/libfuse/commit/4f8f034a8969a48f210bf00be78a67cfb6964c72
	# backported for fuse2
	patch -Np1 -i ../fuse2-0003-make-buffer-size-match-kernel-max-transfer-size.patch
	# https://bugzilla.redhat.com/1694552#c7
	# https://github.com/libfuse/libfuse/pull/392
	# backported for fuse2
	patch -Np1 -i ../fuse2-0004-Whitelist-SMB2-found-on-some-NAS-devices.patch
	# cherry-picked from upstream
	patch -Np1 -i ../fuse2-0005-Whitelist-UFSD-backport-to-2.9-branch-452.patch
	# cherry-picked from upstream
	patch -Np1 -i ../fuse2-0006-Correct-errno-comparison-571.patch
	# cherry-picked from upstream
	# https://bugzilla.redhat.com/show_bug.cgi?id=1984776
	patch -Np1 -i ../fuse2-0007-util-ulockmgr_server.c-conditionally-define-closefro.patch

	autoreconf -ivf
}

build() {
	cd "${srcdir}/fuse-${pkgver}"
	
	UDEV_RULES_PATH=/usr/lib/udev/rules.d MOUNT_FUSE_PATH=/usr/bin ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  	make
}

package() {
	cd "${srcdir}/fuse-${pkgver}"
	make DESTDIR=${pkgdir} install

	# Remove init script in wrong path
	# Don't add our own for now, as fusectl fs oopses on 2.6.18
	rm -rf ${pkgdir}/etc

	# static device nodes are handled by udev
	rm -rf ${pkgdir}/dev
	}

