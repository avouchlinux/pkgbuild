# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=fontconfig
pkgver=2.15.0
pkgrel=1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
groups=('sys-base')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom')
depends=('expat' 'freetype2')
depends=(
  expat
  freetype2
)
makedepends=(
  docbook-sgml
  docbook-utils
  git
  gperf
  meson
  perl-sgmls
)
install=fontconfig.install
source=(https://www.freedesktop.org/software/$pkgname/release/$pkgname-$pkgver.tar.bz2)
sha256sums=('b449a3e10c47e1d1c7a6ec6e2016cca73d3bd68fbbd4f0ae5cc6b573f7d6c7f3')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

build() {
  local meson_options=(
    -D default-hinting=slight
    -D default-sub-pixel-rendering=rgb
	-D doc=disabled
    # -D doc-html=enabled
    # -D doc-pdf=disabled
    # -D doc-txt=disabled
  )
  avouch-meson fontconfig-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libexpat.so
    libfreetype.so
  )
  provides+=(libfontconfig.so)

  meson install -C build --destdir "$pkgdir"

  # Handle conf.d using the hook to avoid overwriting the symlinks on upgrade
  mkdir -p "$pkgdir/usr/share/fontconfig/conf.default"
  for _f in "$pkgdir"/etc/fonts/conf.d/*.conf; do
    ln -sr "$pkgdir"/usr/share/fontconfig/conf.{avail,default}/"${_f##*/}"
    rm "$_f"
  done

  install -Dm644 fontconfig-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
