# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kbd
pkgver=2.2.0
pkgrel=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
groups=('sys-base')
url="http://www.kbd-project.org"
license=('GPL')
depends=('glibc' 'pam')
makedepends=('check')
source=(https://www.kernel.org/pub/linux/utils/${pkgname}/${pkgname}-${pkgver}.tar.gz
https://www.kernel.org/pub/linux/utils/kbd/${pkgname}-${pkgver}.tar.sign
        'fix-euro2.patch'
        'kbd-fix-loadkmap-compat.patch')
validpgpkeys=(
              '7F2A3D07298149A0793C9A4EA45ABA544CFFD434' #Alexey Gladkov 
             )

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	# rename keymap files with the same names
	# this is needed because when only name of keymap is specified
	# loadkeys loads the first keymap it can find, which is bad (see FS#13837)
	# this should be removed when upstream adopts the change
	mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
	mv data/keymaps/i386/olpc/es{,-olpc}.map
	mv data/keymaps/i386/olpc/pt{,-olpc}.map
	mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
	mv data/keymaps/i386/colemak/{en-latin9,colemak}.map

	# fix euro2 #28213
	patch -Np1 -i ../fix-euro2.patch
	patch -p1 -i ../kbd-fix-loadkmap-compat.patch # Restore compatibility with busybox loadkmap
	autoreconf -if
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--datadir=/usr/share/kbd \
	--mandir=/usr/share/man
	make ${MAKEFLAGS} KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

check() {
  	cd "${srcdir}/${pkgname}-${pkgver}"
	# This test is expected to fail since kbd-fix-loadkmap-compat.patch modifies the binary format
	sed -e 's|dumpkeys-bkeymap ||' -i tests/Makefile

	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install
}
