# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libxslt
pkgver=1.1.35
pkgrel=1
pkgdesc="XML stylesheet transformation library"
arch=('x86_64')
groups=('sys-base')
url="http://xmlsoft.org/XSLT/"
arch=(x86_64)
license=(custom)
depends=(libxml2 libgcrypt)
makedepends=(git python)
checkdepends=(docbook-xml)
optdepends=('python: Python bindings')
provides=(lib{,e}xslt.so)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.bz2")

prepare() {
  cd libxslt-v$pkgver

  # Make xsltproc output reproducible for packages which use it to generate documentation
  patch -Np1 -i ../0001-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libxslt-v$pkgver
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libxslt-v$pkgver
  make check
}

package() {
  cd libxslt-v$pkgver
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}