# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=libxslt
pkgname=(
  libxslt
  libxslt-docs
)
pkgver=1.1.39
pkgrel=1
pkgdesc="XML stylesheet transformation library"
arch=('x86_64')
groups=('sys-base')
url="http://xmlsoft.org/XSLT/"
arch=(x86_64)
license=(custom)
depends=(libxml2 libgcrypt)
makedepends=(git python)
checkdepends=(docbook-xml)
optdepends=('python: Python bindings')
provides=(lib{,e}xslt.so)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.bz2")

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --disable-static
    --with-python=/usr/bin/python
  )

  cd libxslt-$pkgver
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

}

check() {
  cd libxslt-$pkgver
  make check
}

package_libxslt() {
  optdepends=('python: Python bindings')
  provides=(lib{,e}xslt.so)

  cd libxslt-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p ../doc/usr/share
  mv "$pkgdir"/usr/share/{doc,gtk-doc} -t ../doc/usr/share

  install -Dm644 Copyright -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_libxslt-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"

  install -Dm644 libxslt-$pkgver/Copyright -t "$pkgdir/usr/share/licenses/$pkgname"
}
