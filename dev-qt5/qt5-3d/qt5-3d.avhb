# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt5-3d
_realname=${pkgname/5-/}
_basever=5.15.10
pkgver=5.15.10
pkgrel=1
_commit=01aa0a9cb22ce5ed2b7ead03ed9cbeb5f978e897
arch=('x86_64')
groups=('dev-qt')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='C++ and QML APIs for easy inclusion of 3D graphics'
depends=('qt5-declarative' 'assimp')
makedepends=('git' 'vulkan-headers')
source=(https://invent.kde.org/qt/qt/$_realname.git)
sha256sums=('SKIP')
options=(debug)

getsrc(){
  git clone https://invent.kde.org/qt/qt/$_realname.git
}

pkgver() {
  cd $_realname
  git checkout $_commit
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_realname}
  make ${MAKEFLAGS}
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}