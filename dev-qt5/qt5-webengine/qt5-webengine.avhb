# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt5-webengine
_realname=${pkgname/5-/}
_basever=5.15.10
pkgver=5.15.15
pkgrel=1
arch=('x86_64')
groups=('dev-qt')
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 'libxkbfile' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx' 'libxtst' 'ttf-font')
makedepends=('git' 'python' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler' 'pipewire' 'nodejs')
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
_catapult_commit=5eedfe23148a234211ba477f76fc2ea2e8529189
source=(https://code.qt.io/qt/qtwebengine.git
        https://code.qt.io/qt/qtwebengine-chromium.git
        https://chromium.googlesource.com/catapult.git
        qt5-webengine-python3.patch
        qt5-webengine-chromium-python3.patch
        qt5-webengine-ffmpeg5.patch
        qt5-webengine-pipewire-0.3.patch)
sha256sums=('SKIP'

            'SKIP'
            'SKIP'
            '398c996cb5b606695ac93645143df39e23fa67e768b09e0da6dbd37342a43f32'
            'fda4ff16790799fb285847918a677f4f3f7c0f513d4751f846ffc5aa5d873932'
            'c50d3019626183e753c53a997dc8a55938847543aa3178d4c51f377be741c693'
            '5e3a3c4711d964d5152a04059a2b5c1d14bb13dd29bce370120f60e85b476b6f')
options=(debug)

getsrc(){
  git clone ${source[0]}
  git clone ${source[1]}
  git clone ${source[2]}
}

pkgver() {
  cd $_realname
  git checkout v${pkgver}-lts
  echo `git describe`

  cd ../catapult
  git checkout $_catapult_commit
}

prepare() {
  mkdir -p build

  cd ${_realname}
  git submodule init
  git submodule set-url src/3rdparty "$srcdir"/qtwebengine-chromium
  git submodule set-branch --branch 87-based src/3rdparty
  git -c protocol.file.allow=always submodule update

  patch -p1 -i "$srcdir"/qt5-webengine-python3.patch # Fix build with Python 3
  patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-chromium-python3.patch

  # patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-ffmpeg5.patch # Fix build with ffmpeg 5
  patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-pipewire-0.3.patch # Port to pipewire 0.3

  # Update catapult for python3 compatibility
  rm -r src/3rdparty/chromium/third_party/catapult
  mv "$srcdir"/catapult src/3rdparty/chromium/third_party
}

build() {
  cd build
  qmake ../${_realname} CONFIG+=force_debug_info -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu \
    -webengine-kerberos \
    -webengine-webrtc-pipewire
  make ${MAKEFLAGS}
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_realname}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium

  # Fix cmake dependency versions
  sed -e "s|$pkgver\ |$_basever |" -i "$pkgdir"/usr/lib/cmake/*/*Config.cmake
}