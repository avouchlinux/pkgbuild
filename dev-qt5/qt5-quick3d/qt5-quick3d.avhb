# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt5-quick3d
_realname=${pkgname/5-/}
_basever=5.15.3
pkgver=5.15.3+kde+r2
pkgrel=1
_commit=1ede2ac20170357b3e8d7d9810e5474e08170827
arch=('x86_64')
groups=('dev-qt')
url='https://www.qt.io'
license=('GPL3')
pkgdesc='Qt module and API for defining 3D content in Qt Quick'
depends=('qt5-declarative')
makedepends=('git' 'assimp')
optdepends=('assimp: assimp import plugin')
groups=('qt' 'qt5')
_pkgfqn=${pkgname/5-/}
source=(https://invent.kde.org/qt/qt/$_realname.git
        qt5-quick3d-assimp-5.1.patch)
sha256sums=('SKIP'
            '57a3ac54c46f86d1f99ce20653bb34215cbd65fbd36f7ee802360336e88f268e')
options=(debug)

getsrc(){
  git clone ${source[0]}
}

pkgver() {
  cd $_realname
  git checkout $_commit
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit`
}

prepare() {
  mkdir -p build
  patch -d $_pkgfqn -p1 < qt5-quick3d-assimp-5.1.patch # Fix build with assimp 5.1
}

build() {
  cd build

  qmake ../${_realname}
  make ${MAKEFLAGS}
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}