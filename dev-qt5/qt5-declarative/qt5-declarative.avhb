# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt5-declarative
_realname=${pkgname/5-/}
_basever=5.15.3
pkgver=5.15.3+kde+r20
pkgrel=1
_commit=02105099301450c890e1caba977ef44efdc43da7
arch=('x86_64')
groups=('dev-qt')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QML and JavaScript languages'
depends=('qt5-base')
makedepends=('python' 'vulkan-headers' 'git')
conflicts=('qtchooser')
source=(https://invent.kde.org/qt/qt/$_realname.git)
sha256sums=('SKIP')
options=(debug)

getsrc(){
  git clone ${source[0]}
}

pkgver() {
  cd $_realname
  git checkout $_commit
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit`
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_realname} CONFIG+=fat-static-lto
  make ${MAKEFLAGS}
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Symlinks for backwards compatibility
  for b in "$pkgdir"/usr/bin/*; do
    ln -s $(basename $b) "$pkgdir"/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}