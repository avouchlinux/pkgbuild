# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt5-connectivity
_realname=${pkgname/5-/}
_basever=5.15.3
pkgver=5.15.3+kde+r6
pkgrel=1
_commit=aa2def2bdb76f8fd2454dc4515883fd7b5ddb521
arch=('x86_64')
groups=('dev-qt')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides access to Bluetooth hardware'
depends=('qt5-base' 'bluez-libs')
makedepends=('qt5-declarative' 'git')
optdepends=('qt5-declarative: QML bindings')
source=(git+https://invent.kde.org/qt/qt/$_realname#commit=$_commit)
sha256sums=('SKIP')
options=(debug)

getsrc(){
  git clone https://invent.kde.org/qt/qt/$_realname.git
}

pkgver() {
  cd $_realname
  git checkout $_commit
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit`
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_realname}
  make ${MAKEFLAGS}
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}