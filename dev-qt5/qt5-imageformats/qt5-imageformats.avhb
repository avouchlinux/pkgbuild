# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=qt5-imageformats
_realname=${pkgname/5-/}
_basever=5.15.10
pkgver=5.15.10+kde+r9
pkgrel=2
_commit=5aa33ec870977863c400103db94da452edbaf414
arch=('x86_64')
groups=('dev-qt')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=('qt5-base' 'jasper' 'libmng' 'libwebp')
makedepends=('git')
source=(https://invent.kde.org/qt/qt/$_realname.git)
sha256sums=('SKIP')  
options=(debug)

getsrc(){
  git clone ${source[0]}
}

pkgver() {
  cd $_realname
  git checkout $_commit
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_realname}
  make ${MAKEFLAGS}
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}